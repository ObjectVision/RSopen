container Overijssel
{
	unit<uint32> Read_Overijssel_gpkg_org
	:	StorageName     = "%PrivdataDir%/Plancapaciteit_2022/provincie/Overijssel/basis_shape_na_join_csv.gpkg"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "true"
	,	Source          = "Gintare Morkute-Lub- Provincie Overijssel - woensdag 23 november 2022 8:07"
						  "PBL bewerking: csv's joinen op ID en opslaan als nieuw GIS-bestand"
						  "conversie van shp naar (via gdb) gpkg in QGIS 3.22.3"
	{
		attribute<float64> IsoperimetrischQuotient := 4d * pi() * (Shape_Area / (Shape_Length * Shape_Length));//https://nl.wikipedia.org/wiki/Isoperimetrisch_quoti%C3%ABnt
		attribute<bool>    IsSymbol                := IsoperimetrischQuotient > 0.97;
	}
	
	unit<uint32> Read_Overijssel_gpkg:=subset(!Read_Overijssel_gpkg_org/IsSymbol)
	{
		attribute<rdc_meter> Geometry (poly)  := Read_Overijssel_gpkg_org/Geometry[Nr_OrgEntity][rdc_meter];
		attribute<rdc_meter> centroid_or_mid  := centroid_or_mid(Geometry);
		
		attribute<uint32>    Capaciteit_uint32      := Read_Overijssel_gpkg_org/totaalcap[Nr_OrgEntity][uint32];//58188
		attribute<uint32>    Sloop_uint32           := Read_Overijssel_gpkg_org/totaal_slp[Nr_OrgEntity][uint32];//6090
		
		attribute<uint32>    WT_EG_uint32      := Read_Overijssel_gpkg_org/Eengezins[Nr_OrgEntity][uint32];//8322
		attribute<uint32>    WT_MG_uint32      := Read_Overijssel_gpkg_org/Meergezins[Nr_OrgEntity][uint32];//5413
		//vul onbekend met reseterende capaciteit
		attribute<uint32>    WT_Onb_uint32    := (!IsDefined(WT_EG_uint32) && !IsDefined(WT_MG_uint32)) ? Capaciteit_uint32 : 0u/0u;//42782
		//sum WT:56517 (iets minder dan Bruto)
		
		attribute<uint32>    EG_H_uint32     := Read_Overijssel_gpkg_org/huurtotaal[Nr_OrgEntity][uint32];//3233
		attribute<uint32>    EG_K_uint32     := Read_Overijssel_gpkg_org/kooptotaal[Nr_OrgEntity][uint32];//6999
		attribute<uint32>    EG_Onb_uint32   := Read_Overijssel_gpkg_org/onb_kp_hr[Nr_OrgEntity][uint32];//47955
		//sum EG:58187 (is Bruto)
		
		attribute<string>    Plantype_str           := Read_Overijssel_gpkg_org/plantype[Nr_OrgEntity];
		
		attribute<bool>   IsHard   :=IsDefined(Geometry);//alle plannen in het bestand zijn hard
		attribute<string> Provnaam :=const("Overijssel",.);
	}
	
	unit<uint32>  Make_Overijssel := Read_Overijssel_gpkg
	, StorageName = "%PrivDataDir%/Plancapaciteit_2022/Overijssel.fss"
	{
		attribute<rdc_meter> Geometry (poly) := Read_Overijssel_gpkg/Geometry;
		attribute<uint32>    Capaciteit      := Read_Overijssel_gpkg/Capaciteit_uint32;
		attribute<uint32>    Sloop           := Read_Overijssel_gpkg/Sloop_uint32;
		
		attribute<uint32>    WT_EG           := Read_Overijssel_gpkg/WT_EG_uint32;
		attribute<uint32>    WT_MG           := Read_Overijssel_gpkg/WT_MG_uint32;
		attribute<uint32>    WT_ONB          := Read_Overijssel_gpkg/WT_Onb_uint32;
		
		attribute<uint32>    EG_H            := Read_Overijssel_gpkg/EG_H_uint32;
		attribute<uint32>    EG_K            := Read_Overijssel_gpkg/EG_K_uint32;
		attribute<uint32>    EG_ONB          := Read_Overijssel_gpkg/EG_Onb_uint32;
		
		attribute<bool>      IsHard          := Read_Overijssel_gpkg/IsHard;
		attribute<bool>      IsZacht         := !IsHard;
		
		attribute<string>    Plantype        := Read_Overijssel_gpkg/Plantype_str;
	}
}