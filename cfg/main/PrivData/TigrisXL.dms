container TigrisXL
{
	parameter<string> Write_All := Cases/Write_All;
	
	parameter<String> ClaimSrcRef: ['%PrivDataDir%/TXL_20221006'];
	parameter<String> ClaimSrcDir := ClaimSrcRef + '/@S@';
	parameter<string> SrcRegioRef: ['LMSSubzone'];
	
	unit<uint32> SrcRegio := ='/geography/RegioIndelingen/'+SrcRegioRef;
	
	unit<uint8> Table: NrOfRows = 2
	{
		attribute<string> name:     ['Labour', 'LandUse'];
		attribute<string> ModuleNm: ['Labour', 'LandUse'];
		
		container V := for_each_nedv(name, string(ID(.))+'[..]', void, .);
	}
	
	unit<uint8> TXLClaims: NrOfRows = 2
	{
		attribute<string> name: ['banen', 'woningen'];
		attribute<Classifications/TigrisXL/Table> TigrisXL_Table_rel := union_data(., Classifications/TigrisXL/Table/V/Labour, Classifications/TigrisXL/Table/V/LandUse);
		attribute<string> TigrisXL_Table_ref := Classifications/TigrisXL/Table/name[TigrisXL_Table_rel];
		attribute<string> TargetValUnitRef: ['NrJobs', 'NrWoningen'];
		attribute<string> Attr_ref: ['TXLJobs7', 'TXLHouseType'];
	}
	
	container Cases := for_each_ne(Classifications/AllCases/name, 'CaseT('+quote(Classifications/AllCases/Scenario_name)+','+quote(Classifications/AllCases/Variant_name)+')')
	{
		parameter<string> Write_All := 'Ready', ExplicitSuppliers = "=AsList(Classifications/AllCases/name+'/Tables/Write_All', ';')";
	}
	
	Template CaseT
	{
		//
		parameter<string> Scenario;
		parameter<string> Variant;
		//
		
		container Tables := for_each_ne(Table/name, 'TableT(Table/V/'+Table/name+','+quote(Scenario)+','+quote(Variant)+')')
		{
			parameter<string> Write_All := 'Ready', ExplicitSuppliers = "=AsList(Table/name+'/Zichtjaren/Write_All', ';')";
		}
	}
	
	Template TableT
	{
		//
		parameter<Table> table_id;
		parameter<string> Scenario;
		parameter<string> Variant;
		//
		
		container Zichtjaren := for_each_ne(Classifications/Time/Tigrisjaar/name, 'Templates/ReadFiles/Case(table_id, '+string(Classifications/Time/Tigrisjaar/jaar)+'[YearRange],'+quote(Scenario)+','+quote(Variant)+')')
		{
			parameter<string> Write_All := 'Ready', ExplicitSuppliers = "=AsList(Classifications/Time/Tigrisjaar/name+'/WritePerNVM', ';')";
		}
	}
}