////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Dit is RSOpen, de open source versie van het model RuimteScanner. Het scipt wordt uitgegeven onder GNU-GPL licentie.
//
// RSOpen is ontwikkeld door PBL Planbureau voor de Leefomgeving, i.s.m Object Vision B.V. en VU Vrije Universiteit Amsterdam.
// Opdrachtgever/ontwikkelaar PBL: Bart Rijken
// Contactpersoon/ontwikkelaar Object Vision B.V.: Jip Claassens (jclaassens@objectvision.nl)
// Contacpersoon PBL: Bas van Bemmel (Bas.vanBemmel@pbl.nl)
//
// Deze file bevat templates ofwel rekenschema's: stukken script die in verschillende contexten worden gebruikt
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

container Templates: using = "Units;Geography;SourceData/RegioIndelingen;Classifications;Classifications/Vastgoed;Classifications/Actor" 
{
	container Allocatie
	: Descr = "Alle templates voor de allocatie, inclusief iteratiestructuren en state preparatie."
	{
		#include<Allocatie/Zichtjaar_T.dms>
		#include<Allocatie/Sequence_T.dms>
		#include<Allocatie/SectorAllocRegio_T.dms>
		#include<Allocatie/Iter_T.dms>
		#include<Allocatie/Iter_Landbouw_T.dms>
		#include<Allocatie/IterSubsector_T.dms>
		#include<Allocatie/IterSubsector_T_Landbouw.dms>
		#include<Allocatie/IterSubsector_T_Wonen.dms>
		#include<Allocatie/IterSubsector_T_Wind.dms>
		#include<Allocatie/StatePrep_T.dms>
	}
	
	container Indicatoren
	{
		#include<Indicatoren.dms>
		
		Template BereikbaarheidGroen_Per_Regio_T 
		{
			attribute<bool>   IsRegio  (AdminDomain);
			attribute<Woning> Woningen (AdminDomain);
			container         Src;
			//
			
			container Afstanden := 
				for_each_ne(
					Classifications/Bereikbaarheid/AfstandGroen/name
					, 'Per_Afstand_T(
							'+string(id(Classifications/Bereikbaarheid/AfstandGroen))+'[Classifications/Bereikbaarheid/AfstandGroen]
							, IsRegio
							, Woningen
							, Src
						)'
				)
			{
				container InDezeStudie := Tot300m;
			}
		}
		
		Template BereikbaarGroen_Indicatoren_T
		{
			parameter<string> IndicatorExpr;
			parameter<string> ValuesUnit0;
			container         Src;
			//
			unit<float32> ValuesUnit :=  =ValuesUnit0;
			
			attribute<ValuesUnit>   Indicator        (AdminDomain) := =IndicatorExpr, Descr = "Berekende indicatorwaarde per cel volgens de gekozen definitie.";
			parameter<ValuesUnit>   Indicator_Aggregated           := mean(WoningenInThisStRegio > 0[Woning] || Indicator > 0[ValuesUnit] ? Indicator : null_f), Descr = "Gemiddelde positieve indicatorwaarde over het studiegebied.";
		}
	}
	
	container Suitabilities
	: Descr = "Templates voor het berekenen van geschiktheden voor verschillende sectoren."
	{
		#include<Suitabilities/Woningwaarde.dms>
		#include<Suitabilities/Woningwaarde_perWoningType.dms> //voor verwervingskosten
		#include<Suitabilities/Suitability_Zichtjaar_T.dms>
		#include<Suitabilities/Suitability_Wonen_perSubsector_T.dms>
		#include<Suitabilities/Suitability_Wonen_perOP_T.dms>
	}
	
	container Beschikbaarheden
	: Descr = "Templates voor het berekenen van beschikbaarheden voor verschillende sectoren."
	{
		#include<Beschikbaarheden/Wind_BepaalGeschikteCellenPerStempel_T.dms>
		#include<Beschikbaarheden/Zeef_T.dms>
		#include<Beschikbaarheden/Zeef_Basisjaar_T.dms>
		#include<Beschikbaarheden/Zeef_Wonen_perOP_T.dms>
		#include<Beschikbaarheden/Zeef_Wonen_perSubsector_T.dms>
		#include<Beschikbaarheden/Zeef_Zichtjaar_T.dms>
	}
	
	container Landgebruikskaart
	: Descr = "Templates voor het berekenen van de landgebruikskaart voor basisjaar en zichtjaar."
	{
		#include<Landgebruikskaart/GetNieuwGrouped_T.dms>
		#include<Landgebruikskaart/LU_Zichtjaar_T.dms>
		#include<Landgebruikskaart/Make_Landgebruikskaart_Basisjaar_T.dms>
	}
	
	#include<Landbouw.dms>
	#include<Claims.dms>
	#include<Trede.dms>
	#include<Densities.dms>
	#include<CaseClassifications.dms>
	#include<BereikbaarheidGroenBBG_T.dms> // RVNL methode
	#include<BereikbaarheidGroenBGT_T.dms> // Methode Jip obv BGT
	#include<SSM2017_Overstromingsschades.dms>
	#include<simplifyT.dms>
	#include<BAG_GebruiksdoelSets_T.dms> 
	#include<SOMERS_CO2_T.dms> 
	
	Template MaakRestrictieCombinaties_T
	: Descr = "Template voor het maken van restrictiecombinaties op basis van de restricties in de brondata en de gekozen restrictievariant."
	{
		parameter<String> Sector; 
		
		parameter<String> CombinatieRef := ='lowercase(VariantParameters/VariantK/RestrictiesVariant_'+Sector+'[Variant_rel])';
		container         src           := ='SourceData/Restricties/'+Sector;
		
		parameter<String> ZeerHardHardMiddelZacht := 'src/ZeerHard || src/Hard     || src/Middel   || src/Zacht';
		parameter<String> ZeerHardHardMiddel      := 'src/ZeerHard || src/Hard     || src/Middel';
		parameter<String> HardMiddelZacht         :=                 'src/Hard     || src/Middel   || src/Zacht';
		parameter<String> ZeerHardHard            := 'src/ZeerHard || src/Hard';
		parameter<String> HardMiddel              :=                 'src/Hard     || src/Middel';
		parameter<String> MiddelZacht             :=                                 'src/Middel   || src/Zacht';
		parameter<String> ZeerHard                := 'src/ZeerHard';
		parameter<String> Hard                    :=                 'src/Hard';
		parameter<String> Middel                  :=                                 'src/Middel';
		parameter<String> Zacht                   :=                                                 'src/Zacht';
		
		attribute<Bool>   Result (AdminDomain) := =switch(
													  case(CombinatieRef == lowercase('ZeerHardHardMiddelZacht'), ZeerHardHardMiddelZacht)
													, case(CombinatieRef == lowercase('ZeerHardHardMiddel')     , ZeerHardHardMiddel)
													, case(CombinatieRef == lowercase('ZeerHardHard')           , ZeerHardHard)
													, case(CombinatieRef == lowercase('ZeerHard')               , ZeerHard)
													, case(CombinatieRef == lowercase('HardMiddelZacht')        , HardMiddelZacht)
													, case(CombinatieRef == lowercase('HardMiddel')             , HardMiddel)
													, case(CombinatieRef == lowercase('Hard')                   , Hard)
													, case(CombinatieRef == lowercase('MiddelZacht')            , MiddelZacht)
													, case(CombinatieRef == lowercase('Middel')                 , Middel)
													, case(CombinatieRef == lowercase('Zacht')                  , Zacht)
													, 'const(FALSE, AdminDomain)'
												);
	}
	
	Template MaakStimuliCombinaties_T
	: Descr = "Template voor het maken van stimulicombinaties op basis van de stimuli in de brondata en de gekozen stimulivariant."
	{
		parameter<String> Sector;
		//
		parameter<String> CombinatieRef := ='lowercase(VariantParameters/VariantK/StimuliVariant_'+Sector+'[Variant_rel])';
		container         src           := ='SourceData/Stimuli/'+Sector;
		
		parameter<String> HardMiddelZacht         :=                 'src/Hard     || src/Middel   || src/Zacht';
		parameter<String> HardMiddel              :=                 'src/Hard     || src/Middel';
		parameter<String> MiddelZacht             :=                                 'src/Middel   || src/Zacht';
		parameter<String> Hard                    :=                 'src/Hard';
		parameter<String> Middel                  :=                                 'src/Middel';
		parameter<String> Zacht                   :=                                                 'src/Zacht';
		
		attribute<Bool>   Result (AdminDomain) := =switch(
													  case(CombinatieRef == lowercase('HardMiddelZacht')        , HardMiddelZacht)
													, case(CombinatieRef == lowercase('HardMiddel')             , HardMiddel)
													, case(CombinatieRef == lowercase('Hard')                   , Hard)
													, case(CombinatieRef == lowercase('MiddelZacht')            , MiddelZacht)
													, case(CombinatieRef == lowercase('Middel')                 , Middel)
													, case(CombinatieRef == lowercase('Zacht')                  , Zacht)
													, 'const(FALSE, AdminDomain)'
												);
	}
	
	Template PotentieleState_T
	: Descr = "Template voor het berekenen van de potentiele state per sector en subsector."
	{
		parameter<String> Zichtjaar_name;
		container Density;
		//
		
		container Per_SectorxSubsectoren := 
			for_each_ne(
				Sector/xSubsector/name
				, 'PotentieleState_SectorxSubsectoren_T('+string(id(Sector/xSubsector))+'[Sector/xSubsector], Zichtjaar_name, Density)'
			)
		{
			container Landbouw
			{
				container NonLandbouw 
				{
					attribute<Bool>    IsBeschikbaar_zichtjaar   (CompactedAdminDomain) := const(TRUE,CompactedAdminDomain);
					attribute<Float32> Zichtjaar                 (CompactedAdminDomain) := AdminDomain/NrHaPerCell * float32(IsBeschikbaar_zichtjaar);
				}
			}
		}
		container Per_Sector := for_each_ne(Sector/name, 'PotentieleState_Sector_T('+string(id(Sector))+'[Sector], Per_SectorxSubsectoren)');
	}
	
	Template PotentieleState_SectorxSubsectoren_T
	: Descr = "Template voor het berekenen van de potentiele state per subsector."
	{
		parameter<Sector/xSubsector> id;
		parameter<String> Zichtjaar_name;
		container Density;
		//
		
		attribute<Bool>    IsBeschikbaar_basisjaar   (CompactedAdminDomain) := = 'Zeef/Basisjaar/'+Sector/xSubsector/name[id]+'/IsBeschikbaar';
		attribute<Bool>    IsBeschikbaar_zichtjaar   (CompactedAdminDomain) := = 'Zeef/'+Zichtjaar_name+'/SectorxSubsectoren/'+Sector/xSubsector/name[id]+'/IsBeschikbaar';
		attribute<Float32> BAG_nieuwbouw             (CompactedAdminDomain) := = lowercase(Sector/xSubsector/Sector_name[id]) == lowercase('landbouw') 
																					? 'const(0f, CompactedAdminDomain)'
																					: 'BaseData/StartState/BAG_nieuwbouw/'+Sector/xSubsector/name[id]+'';
		
		attribute<Float32> Basisjaar     (CompactedAdminDomain) := ='StateBasisjaar/'+Sector/xSubsector/name[id]+ ' * float32(IsBeschikbaar_basisjaar)'
		{
			attribute<Float32> SubsectorShare (CompactedAllocDomain) := = 'sum(., CompactedAdminDomain/CompactedAllocDomain_rel) / sum(Per_Sector/'+Sector/xSubsector/Sector_name[id]+'/Basisjaar, CompactedAdminDomain/CompactedAllocDomain_rel)';
		}
		
		attribute<Float32> Zichtjaar                          (CompactedAdminDomain) := ='Density/Per_CompactedAllocDomain/'+Zichtjaar_name+'/SectorxSubsectoren/'+Sector/xSubsector/name[id]+'/Zichtjaar[CompactedAdminDomain/CompactedAllocDomain_rel] * AdminDomain/NrHaPerCell * float32(IsBeschikbaar_zichtjaar)';
		attribute<Float32> Zichtjaar_BeschikbaarheidBasisjaar (CompactedAdminDomain) := ='Density/Per_CompactedAllocDomain/'+Zichtjaar_name+'/SectorxSubsectoren/'+Sector/xSubsector/name[id]+'/Zichtjaar[CompactedAdminDomain/CompactedAllocDomain_rel] * AdminDomain/NrHaPerCell * float32(IsBeschikbaar_basisjaar)';
		attribute<Float32> Result_CompactedAdminDomain        (CompactedAdminDomain) := = 'Zichtjaar_BeschikbaarheidBasisjaar - Per_Sector/'+Sector/xSubsector/Sector_name[id]+'/Basisjaar';
		
		attribute<Float32> Result                      (CompactedAllocDomain) := sum(Result_CompactedAdminDomain, CompactedAdminDomain/CompactedAllocDomain_rel);
		attribute<Float32> TovStateVoorAllocatie       (CompactedAllocDomain) := = 'Result / sum(Per_Sector/'+Sector/xSubsector/Sector_name[id]+'/Basisjaar, CompactedAdminDomain/CompactedAllocDomain_rel)';
	}
	
	Template PotentieleState_Sector_T
	: Descr = "Template voor het berekenen van de potentiele state per sector."
	{
		parameter<Sector> id;
		container Per_SectorxSubsectoren;
		//
		
		unit<UInt8> Subsector := = Sector/Subsector_ref[id];
		
		attribute<Float32> Basisjaar (CompactedAdminDomain) := = 'add('+asItemList('Per_SectorxSubsectoren/'+Sector/name[id]+'/'+Subsector/name+'/Basisjaar')+')';
		attribute<Float32> Zichtjaar (CompactedAdminDomain) := = 'value(add('+asItemList('Per_SectorxSubsectoren/'+Sector/name[id]+'/'+Subsector/name+'/Zichtjaar')+'), float32)';
	}
	
	Template SectorxSubsector_T : using = "/Classifications/Vastgoed;Classifications/Actor"
	, Descr = "Template voor het definiëren van een subsector binnen een sector."
	{
		parameter<String> Sector_name;
		parameter<String> Subsector_name0;
		//
		unit<UInt8> CBSAggrKlasse := Grondgebruik/CBSKlasse/Aggr;
		unit<UInt8> Subsector := = Subsector_name0
		{
			attribute<String> Sector_name               := const(../Sector_name,.);
			attribute<String> Subsector_name            := =Subsector_name0+'/name';
			// attribute<String> Subsector_name_short      := =Subsector_name0+'/name_short';
			attribute<String> SrcRefGeschiktheid        := replace(Sector/SrcRefGeschiktheid[Sector_rel], '@SS@', Subsector_name);
			attribute<String> SrcRefRestricties         := Sector/SrcRefRestricties[Sector_rel];
			attribute<String> Subsector_ref             := Sector/Subsector_ref[Sector_rel];
			attribute<String> ValUnit_ref               := Sector/ValUnit_ref[Sector_rel];
			attribute<String> ValUnitDens_ref           := Sector/ValUnitDens_ref[Sector_rel];
			attribute<String> name                      := Sector_name+'/'+Subsector_name;
			// attribute<String> name_short                := Sector_name+'/'+Subsector_name_short;
			attribute<String> Label                     := Sector_name+'_'+Subsector_name;
			attribute<String> SectorAllocRegio_name     := rjoin(lowercase(Sector_name), lowercase(CaseClassifications/Actor/SectorAllocRegio/Sector_name), CaseClassifications/Actor/SectorAllocRegio/name);
			attribute<String> ClaimRegio_path           := rjoin(lowercase(Sector_name), lowercase(CaseClassifications/Actor/SectorAllocRegio/Sector_name), CaseClassifications/Actor/SectorAllocRegio/AllocRegio_path);
			attribute<Jobs6>  Jobs6_rel                 :=  =Subsector_name0+'/Jobs6_rel';
			
			attribute<String> StandVar_name             := replace(Sector/StandVar_name[Sector_rel], '@SS@', Subsector_name);
			attribute<String> StandVar_valueType        := Sector/StandVar_valueType[Sector_rel];
			attribute<String> StandVar_PF_name          := = lowercase(../Sector_name) == lowercase('Werken') ? 'replace(WerkenK/StandVar_PF_name[0[WerkenK]], ''@SS@'', Subsector_name)' : 'const(null_str,.)';
			attribute<String> StandVar_PF_path          := = lowercase(../Sector_name) == lowercase('Werken') ? '''PandFootprint/'' +Subsector_name' : 'const(null_str,.)';
			attribute<String> StandVar_PF_valueType     := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(''float32'',.)' : 'const(null_str,.)';
			attribute<String> StandVar_PF_unit          := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(''meter2'',.)' : 'const(null_str,.)';
			
			attribute<Bool>   HasClaims                 := Sector/HasClaims[Sector_rel];
			attribute<Bool>   HasAlloc                  := Sector/HasAlloc[Sector_rel];
			attribute<Bool>   IsVastgoed                := Sector/IsVastgoed[Sector_rel];
			attribute<Bool>   UseForLeegstandCorr       := Sector/UseForLeegstandCorr[Sector_rel];
			
			attribute<Bool>   HasClaims_PF              := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(WerkenK/HasClaims_PF[0[WerkenK]],.)' : 'const(FALSE,.)';
			attribute<Bool>   HasAlloc_PF               := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(WerkenK/HasAlloc_PF[0[WerkenK]],.)' : 'const(FALSE,.)';
			attribute<Bool>   IsVastgoed_PF             := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(WerkenK/IsVastgoed_PF[0[WerkenK]],.)' : 'const(FALSE,.)';
			attribute<Bool>   UseForLeegstandCorr_PF    := = lowercase(../Sector_name) == lowercase('Werken') ? 'const(WerkenK/UseForLeegstandCorr_PF[0[WerkenK]],.)' : 'const(FALSE,.)';
		}
	}
	
	Template Zonneladder_Union_T
	: Descr = "Template om de geometrie van de laddertreden te unionen en splitsen zodat er geen overlappingen meer zijn."
	{
		unit<UInt32> domain;
		attribute<rdc_meter> geometry (domain, poly);
		parameter<Classifications/Zonneladder/LadderTreden> Laddertrede_rel;
		//
	
		unit<UInt32> unioned_domain := geos_split_union_polygon(geometry)
		{
			attribute<Classifications/Zonneladder/LadderTreden> Laddertrede_rel := const(../Laddertrede_rel, ., Classifications/Zonneladder/LadderTreden);
			attribute<Bool> Defined_per_rdc_10m           (rdc_10m) := IsDefined(poly2grid(geometry, rdc_10m));
			attribute<Bool> Defined_Per_AllocDomain0 (AllocDomain) := sum_uint32(Defined_per_rdc_10m, /Geography/rdc_10m/AllocDomain_rel) > 0;
			attribute<Bool> Defined_Per_AllocDomain  (AllocDomain) := Defined_Per_AllocDomain0 && !BaseData/Omgeving/GrootWater1500mBuffer;
		}
		
		attribute<rdc_meter> unioned_geometry (unioned_domain, poly) := unioned_domain/geometry;
	}
	
	Template Zonneladder_Isoperimetrisch_quotient_T
	: Descr = "Template om de isoperimetrisch quotient te berekenen van een geometrie."
	{
		attribute<meter2> area (domain);
		attribute<meter> Length (domain);
		
		unit<UInt32> domain;
		parameter<Float32> paramPi := pi()[float32];
		
		attribute<Float32> Isoperimetrisch_quotient (domain) := (4f*paramPi*area)/sqr(Length), URL = "https://nl.wikipedia.org/wiki/Isoperimetrisch_quoti%C3%ABnt", Descr = "IQ={4pi area}/{arc_length^{2}";
	}
	
	template Zonneladder_ProcessTilesT
	: Descr = "Template om een target geometrie per tile te knippen en te controleren of de oppervlakte van de target geometrie binnen de tile overeenkomt met de oppervlakte van de target geometrie."
	{
		parameter<String> Tilename;
		unit<UInt32> TargetDomain;
		parameter<Float32> minimum_opp;
		// end case Parameters
		
		unit<UInt32> Tile_selection := select_with_org_rel(lowercase(Geography/TileDomain/name) == lowercase(Tilename))
		{
			attribute<rdc_meter>  geometry (poly) := Geography/tiledomain/geometry[org_rel];
			
			unit<UInt32> target_geom_in_tile := geos_overlay_polygon(geometry, TargetDomain/geometry)
			{
				parameter<meter2>    area := sum(area(geometry, meter2_f64)[meter2]);
				unit<UInt32> Multi_to_singlepart_polygons := geos_split_union_polygon(geometry)
				{
					parameter<Bool>   check_p := area <= target_geom_in_tile/area * 1.01f;
					parameter<Bool>   check_m := area >= target_geom_in_tile/area * 0.99f;
					parameter<meter2> area    := sum(area(geometry, meter2_f64)[meter2]);
					attribute<rdc_meter> geometry_checked (poly) := geometry, IntegrityCheck = "check_p && check_m", Descr = "Check om ervoor te zorgen areas kloppen en dus de geometrische operaties plausibele resultaten geven. Voornamelijk ingevoerd om kinderziektes uit geos/bg te detecteren." ;
				}
			}
		}
	}
	
	Template Buffer_gridcel_T // voor buffer points bepaling 
	: Descr = "Template om een buffer rondom punten te maken op basis van de gridcelgrootte."
	{
		attribute<Bool>    TeBufferen_src (CompactedAdminDomain);
		parameter<Float64> buffersize;
		///
		attribute<Bool>  TeBufferen (AdminDomain) := recollect_by_cond(AdminDomain/IsCompactedDomain, TeBufferen_src);
		parameter<Int16> gridbuffersize := roundup(buffersize / AdminDomain/gridsize)[int16];
		
		unit<SPoint> kernel_buffer  := range(spoint, point_yx(-(gridbuffersize -1s), -(gridbuffersize -1s)), point_yx(gridbuffersize,gridbuffersize))
		{
			attribute<UInt32>  dist     := dist2(point_yx(0s, 0s, .), uint32);
			attribute<Float32> distMatr := dist <= uint32(sqr(gridbuffersize)) ? 1f : 0f;
		}
		attribute<Float32> proximity_arg          (AdminDomain) := float32(TeBufferen), Descr = "1 als er iets te bufferen is, anders 0";
		parameter<Bool> IsErIetsTeBufferen                      := sum_uint32(TeBufferen) != 0, Descr = "Check of er uberhaupt iets te bufferen is, anders bufferberekening overslaan";
		attribute<Bool> proximity_res_AdminDomain (AdminDomain) := IsErIetsTeBufferen ? potential(proximity_arg, kernel_buffer/distMatr)  > 0.5f : const(FALSE, AdminDomain), Descr = "True als er iets te bufferen is en de potential kernel buffer > 0.5 is. Anders false";
		attribute<Bool> proximity_res    (CompactedAdminDomain) := collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, proximity_res_AdminDomain), Descr = "Resultaat in compacted domain";
	}
	
	Template OV_halte_bereikbaarheid_T
	: Descr = "Template om de bereikbaarheid van OV haltes te bepalen op basis van een raster met reistijden."
	{
		//
		parameter<Classifications/bereikbaarheid/OV_halte_bereikbaarheidK/CombiK> id;
		unit<IPoint> domain;
		//
		
		parameter<String> SrcRef := '%RSo_DataDir%/Omgeving/OV_halte_bereikbaarheid/OD_korste_@Maat@_@HHMod@_eerstehaltevan_@OVMod@_@OVInt@.tif';
		
		attribute<min> Resultaat0 (domain): StorageName =
			"= replace(SrcRef,
				'@Maat@',  Classifications/Bereikbaarheid/OV_halte_bereikbaarheidK/CombiK/MaatK_SrcRef[id],
				'@HHMod@', Classifications/Bereikbaarheid/OV_halte_bereikbaarheidK/CombiK/HhModK_SrcRef[id],
				'@OVMod@', Classifications/Bereikbaarheid/OV_halte_bereikbaarheidK/CombiK/OVModK_SrcRef[id],
				'@OVInt@', Classifications/Bereikbaarheid/OV_halte_bereikbaarheidK/CombiK/OVIntK_SrcRef[id]
			)";
		attribute<min> Resultaat (domain) := Resultaat0 == 0[min] ? 60[min] : Resultaat0;
	}
	
	#include <combine2bestanden.dms>
	#include <combine2bestanden_org.dms>
}