container Grondgebruik: using  = "Units;Geography;Classifications;Classifications/Grondgebruik"
{
	#include<BGT.dms>
	#include<BRT.dms>
	#include<LGN.dms>
	#include<IBIS.dms>
	#include<BBG.dms>
	#include<NVK.dms>
	
	container NWB 
	: StorageName = "%RSo_DataDir%/Grondgebruik/NWB/20251101/Wegvakken/Wegvakken.gpkg"
	, StorageType = "gdal.vect"
	, StorageReadOnly = "true"
	, DialogData = "rdc_meter"
	{
	
		unit<uint32> Stichtse_Rotonde := select_with_attr_by_cond(NWB/Wegvakken, NWB/Wegvakken/STT_NAAM == 'Stichtse Rotonde')
		, Descr = "hack om de Stichtse Rotondo nabij Amersfoort te identificeren. Zodat daarop niet gealloceerd mag worden. Buffer, dan dissolve, dan negatieve buffer."
		{
			attribute<rdc_meter> geometry_buff0           (poly) := bg_buffer_linestring(geometry, 100d, 16b);
			unit<uint32>         union                           := geos_split_union_polygon(geometry_buff0);
			attribute<rdc_meter> geometry_buff1    (poly, union) := geos_buffer_multi_polygon(union/geometry, -100d, 16b);
			
			attribute<bool>      Per_AdminDomain   (AdminDomain) := IsDefined(poly2grid(geometry_buff1, AdminDomain));
		}
	}
	
	container Imperviousness_Map
	{
		attribute<uint8>  Y2021_5m (rdc_5m)
		:	StorageName     = "%RSo_DataDir%/Grondgebruik/Imperviousness_Density_2021_010m_NL_rdc_uint8.tif"
		,	Source          = "Copernicus Land Monitoring Service, high resolution imperviousness map 2018"
		, 	URL             = "https://land.copernicus.eu/en/products/high-resolution-layer-imperviousness/imperviousness-density-2021"
		, 	Descr           = "Relevant tiles gecombineerd in QGIS, en dan gewarped naar RDC, en dan studiegebied NL genomen en naar uint8 gecast."
		, 	LazyCalculated  = "true";

		attribute<uint8>        Y2021_25m_write   (rdc_25m) := mean(Y2021_5m <= 100b ? Y2021_5m : null_b, rdc_5m/rdc_25m_rel),	StorageName     = "%RSo_DataDir%/Grondgebruik/Imperviousness_Density_2021_025m_NL_rdc_uint8.tif", StorageType = "gdalwrite.grid";
		attribute<uint8>        Y2021_25m         (rdc_25m) : StorageName     = "%RSo_DataDir%/Grondgebruik/Imperviousness_Density_2021_025m_NL_rdc_uint8.tif", StorageReadOnly = "true";
		
		attribute<Percent>       InProcent         (rdc_25m) := rdc_25m/IsStudyArea ? value(Y2021_25m, percent) : null_f;
		attribute<Percent>       InProcent_5m       (rdc_5m) := rdc_5m/IsStudyArea ? value(Y2021_5m, percent) : null_f;
	}
	
	container ABCD
	{
		unit<uint8> ABCD_K : nrofrows = 8
		{
			attribute<string> name   : ['Agroparken','Beleving','Drinkwater','Droogte','Natuurherstel','Productiviteit','Uitspoeling','Veen'];
			attribute<string> Label  := name;
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .); 
		}
		
		unit<uint8> ABCD_subK : nrofrows = 3
		{
			attribute<string> name   : ['A','B_C1', 'Overig'];
			attribute<string> Label  := name;
			attribute<UInt32> BrushColor :  [rgb(118, 159, 255),rgb(255, 180, 0),rgb(230, 230, 230)], DialogType = "BrushColor";
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .); 
		}
		
		attribute<int32> Agroparken        (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/agroparken.txt", StorageType = "gdal.grid";
		attribute<int32> Beleving          (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Beleving.txt", StorageType = "gdal.grid";
		attribute<int32> Drinkwater        (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Drinkwater.txt", StorageType = "gdal.grid";
		attribute<int32> Droogte           (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Droogte.txt", StorageType = "gdal.grid";
		attribute<int32> Natuurherstel     (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Natuurherstel.txt", StorageType = "gdal.grid";
		attribute<int32> Overlay           (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Overlay.txt", StorageType = "gdal.grid";
		attribute<int32> Overlay_gefilterd (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Overlay_gefilterd.txt", StorageType = "gdal.grid";
		attribute<int32> Productiviteit    (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Productiviteit.txt", StorageType = "gdal.grid";
		attribute<int32> Uitspoeling       (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Uitspoeling.txt", StorageType = "gdal.grid";
		attribute<int32> Veen              (rdc_100m) : StorageName = "%RSo_DataDir%/Grondgebruik/ABCD/Veen.txt", StorageType = "gdal.grid";
	
		attribute<ABCD_K> Combi            (rdc_100m) := switch(
															 case(Agroparken == 3i, ABCD_K/v/Agroparken)
															,case(Beleving == 1i, ABCD_K/v/Beleving)
															,case(Drinkwater == 1i, ABCD_K/v/Drinkwater)
															,case(Droogte == 2i, ABCD_K/v/Droogte)
															,case(Natuurherstel == 1i, ABCD_K/v/Natuurherstel)
															,case(Productiviteit == 2i, ABCD_K/v/Productiviteit)
															,case(Uitspoeling == 1i, ABCD_K/v/Uitspoeling)
															,case(Veen == 1i, ABCD_K/v/Veen)
															,null_b
														);
														
		attribute<ABCD_subK> ABCD_sub_rel (rdc_100m) :=  switch(
															 case(Overlay_gefilterd == 1i, ABCD_subK/v/a)
															,case(Overlay_gefilterd == 2i, ABCD_subK/v/B_C1)
															, ABCD_subK/v/Overig
														);
														
		attribute<ABCD_subK> ABCD_sub_rel_25m (rdc_25m) := ABCD_sub_rel[rdc_25m/rdc_100m_rel];
	}
}