container Bodem : using = "Classifications/Grondgebruik/Bodem"
{
	unit<uint32> BOFEK 
	: 	StorageName = "%RVF_DataDir%/landgebruik/BOFEK_2020.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	DialogData      = "rdc_meter"
	,	Descr = "Bodemfysische Eenhedenkaart"
	{
		attribute<BOFEK_K> BOFEK_rel := rlookup(uint32(BOFEK2020), BOFEK_K/EK);
	}
//	Bron: expert judgement van Gilbert Maas (WUR), verkregen (kaarten, rekenregels) via Arjen Koekoek (stichting CAS), voorjaar 2019. 
	container DraagkrachtKwel
	{
		parameter<string> SrcRef: ['%RSo_DataDir%/Omgeving/Bodem/risico_draagkracht_openformat'];
		
		attribute<Meter> DraagkrachtSrc (rdc_100m)
		:	StorageName     = "= SrcRef + '/DANK024a_draagkracht_totale_zetting1.tif'"
		,	StorageType     = "gdal.grid"
		,	StorageReadOnly = "True";
		
		attribute<Meter> Draagkracht (rdc_100m) := DraagkrachtSrc = -9999[meter] ? (0/0)[Meter] : DraagkrachtSrc;
		
		attribute<float32> KwelSrc (rdc_100m)
		:	StorageName     = "= SrcRef + '/PKS_Klimaateffecten_Wateroverlast_Kwel_en_infiltratie_Huidig_v15_t0_uit1.tif'"
		,	StorageType     = "gdal.grid"
		,	StorageReadOnly = "True";
		
		attribute<float32> Kwel (rdc_100m) := KwelSrc > -50f ? KwelSrc : (0/0)[float32];
		
		attribute<float32> RisicoklasseDikteVeenlaagFlevoland (rdc_100m)
		:	StorageName     = "= SrcRef + '/DikteVeenlaag1.tif'"
		,	StorageType     = "gdal.grid"
		,	StorageReadOnly = "True";
		
		attribute<float32> RisicoklassePleistoceenFlevoland (rdc_100m)
		:	StorageName     = "= SrcRef + '/TopPleistoceen.tif'"
		,	StorageType     = "gdal.grid"
		,	StorageReadOnly = "True";
		
		attribute<bool> AndersBouwen (rdc_100m) := 
			Draagkracht >= 0.5[Meter] ||
			Kwel <= -2f ||
			(RisicoklassePleistoceenFlevoland <= -10f ||
			RisicoklasseDikteVeenlaagFlevoland > 180f);
		
		attribute<bool> BouwenMetMaatregelen (rdc_100m) :=
			(Draagkracht >= 0.1[Meter] && Draagkracht < 0.5[Meter]) ||
			(Kwel > -2f && Kwel <= -0.5f) &&
			!AndersBouwen;
	}
	
	container Bodemdaling
	{
		attribute<cm> Bodemdaling_2016_2050_cm_1_org (rdc_100m)
		: StorageName     = "%RSo_DataDir%/Omgeving/Bodem/Bodemdaling/Bodemdaling 2016-2050 (cm)1.tif"
		, StorageReadOnly = "true"
		, StorageType     = "gdal.grid"
		, Source          = "Bodemdalingsvoorspellingskaart 2020-2050 (Gilles Erkens)";
		
		attribute<cm> Bodemdaling_2016_2050_cm_1       (rdc_100m) :=(Bodemdaling_2016_2050_cm_1_org <=-3.40282E+38f ? 0f/0f : Bodemdaling_2016_2050_cm_1_org)[cm];
		attribute<cm> Bodemdaling_2016_2050_cm_1_25mtr (rdc_25m) :=Bodemdaling_2016_2050_cm_1[Geography/rdc_25m/rdc_100m_rel];
	}
}