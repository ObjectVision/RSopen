container Plancapaciteit : using = "Units;Classifications;ModelParameters;VariantParameters;Geography"
, Descr = "Plancapaciteit data uit Plancapaciteit configuratie, ingelezen via TIF files per sector en hardheid"
{
	unit<UInt8> HardheidK: NrOfRows = 2
	, Descr = "Hardheid van de sector waarvoor plancapaciteiten beschikbaar zijn"
	{
		attribute<String> name:
		[
			'Hard',
			'Zacht'
		];
	}
	
	unit<UInt8> SectorK : NrOfRows = 4
	, Descr = "Sectoren waarvoor plancapaciteiten beschikbaar zijn"
	{
		attribute<String> name:
		[
			 'Wonen'
			,'Werken_AlgemeneBedrijfslocatie'
			,'Werken_Industrieterrein'
			,'Werken_Kantoor'
		];
		attribute<String> path:
		[
			 'Wonen'
			,'Werken/AlgemeneBedrijfslocatie'
			,'Werken/Industrieterrein'
			,'Werken/Kantoor'
		];
		attribute<String> sub:
		[
			 ''
			,'AlgemeneBedrijfslocatie'
			,'Industrieterrein'
			,'Kantoor'
		];
	}
	
	unit<uint8> WerkenHoofdKlasses : NrOfRows = 3
	{
		attribute<string> name : 
		[
			 'AlgemeneBedrijfslocatie'
			,'Industrieterrein'
			,'Kantoor'
		];
	}
	
	unit<UInt8> Sector_x_HardheidK := combine_uint8(SectorK, HardheidK)
	, Descr = "Combinatie van sector en hardheid"
	{
		attribute<String> Sector_name    := SectorK/name[first_rel];
		attribute<String> Sector_subname := SectorK/sub[first_rel];
		attribute<String> HardheidK_name := HardheidK/name[second_rel];
		attribute<String> path           := SectorK/path[first_rel]+'/'+HardheidK_name;
		attribute<String> name           := Sector_name+'_'+HardheidK_name;
	}
	
	container Read := 
		for_each_ne(
			Sector_x_HardheidK/path
			,'Read_PlanCapaciteit_Tifs_T('+quote(Sector_x_HardheidK/Sector_name)+', '+quote(Sector_x_HardheidK/HardheidK_name)+', '+quote(Sector_x_HardheidK/Sector_subname)+')'
		), Descr = "Inlezen van de plancapaciteit tifs per sector en hardheid"
	{
		container Landbouw
		: Descr = "Omdat het model gebruik maakt van templates voor alle te allocaren sectoren, moeten er waarden zijn voor de sectoren die wel gealloceerd worden, maar geen plannen voor zijn. Daarom is alles hier constant FALSE."
		{
			container Hard
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
			container Zacht
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
		}
		
		container Wind
		: Descr = "Omdat het model gebruik maakt van templates voor alle te allocaren sectoren, moeten er waarden zijn voor de sectoren die wel gealloceerd worden, maar geen plannen voor zijn. Daarom is alles hier constant FALSE."
		{
			container Hard
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
			container Zacht
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
		}
		
		container Zon
		: Descr = "Omdat het model gebruik maakt van templates voor alle te allocaren sectoren, moeten er waarden zijn voor de sectoren die wel gealloceerd worden, maar geen plannen voor zijn. Daarom is alles hier constant FALSE."
		{
			container Hard
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
			container Zacht
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
		}
		
		container Verblijfsrecreatie
		: Descr = "Omdat het model gebruik maakt van templates voor alle te allocaren sectoren, moeten er waarden zijn voor de sectoren die wel gealloceerd worden, maar geen plannen voor zijn. Daarom is alles hier constant FALSE."
		{
			container Hard
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
			container Zacht
			{
				attribute<Bool> Per_AdminDomain (AdminDomain) := const(FALSE, AdminDomain);
				attribute<Bool> Per_AllocDomain (AllocDomain) := const(FALSE, AllocDomain);
			}
		}
		
		container Werken
		{
			container Hard
			{
				container Per_Subsector_AdminDomain := 
					for_each_nedv(
						Sector/xSubsectoren/Werken/Subsector/Subsector_name
						, 'OR(
							 Sector/xSubsectoren/Werken/Subsector/KanOpIndustrieterrein[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Industrieterrein/Hard/Per_AdminDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpKantoor[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Kantoor/Hard/Per_AdminDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpAlgemeneBedrijfslocatie[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? AlgemeneBedrijfslocatie/Hard/Per_AdminDomain : FALSE
							)'
						, AdminDomain
						, bool
					);
				container Per_Subsector_AllocDomain := 
					for_each_nedv(
						Sector/xSubsectoren/Werken/Subsector/Subsector_name
						, 'OR(
							 Sector/xSubsectoren/Werken/Subsector/KanOpIndustrieterrein[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Industrieterrein/Hard/Per_AllocDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpKantoor[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Kantoor/Hard/Per_AllocDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpAlgemeneBedrijfslocatie[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? AlgemeneBedrijfslocatie/Hard/Per_AllocDomain : FALSE
							)'
						, AllocDomain
						, bool
					);
				attribute<Bool> Per_AdminDomain (AdminDomain) := 
					OR(
						 AlgemeneBedrijfslocatie/Hard/Per_AdminDomain
						,Industrieterrein/Hard/Per_AdminDomain
						,Kantoor/Hard/Per_AdminDomain
					);
				attribute<Bool> Per_AllocDomain (AllocDomain) := 
					OR(
						 AlgemeneBedrijfslocatie/Hard/Per_AllocDomain
						,Industrieterrein/Hard/Per_AllocDomain
						,Kantoor/Hard/Per_AllocDomain
					);
			}
			
			container Zacht
			{
				container Per_Subsector_AdminDomain := 
					for_each_nedv(
						Sector/xSubsectoren/Werken/Subsector/Subsector_name
						, 'OR(
							 Sector/xSubsectoren/Werken/Subsector/KanOpIndustrieterrein[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Industrieterrein/Zacht/Per_AdminDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpKantoor[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Kantoor/Zacht/Per_AdminDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpAlgemeneBedrijfslocatie[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? AlgemeneBedrijfslocatie/Zacht/Per_AdminDomain : FALSE
							)'
						, AdminDomain
						, bool
					);
				container Per_Subsector_AllocDomain := 
					for_each_nedv(
						Sector/xSubsectoren/Werken/Subsector/Subsector_name
						, 'OR(
							 Sector/xSubsectoren/Werken/Subsector/KanOpIndustrieterrein[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Industrieterrein/Zacht/Per_AllocDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpKantoor[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? Kantoor/Zacht/Per_AllocDomain : FALSE
							,Sector/xSubsectoren/Werken/Subsector/KanOpAlgemeneBedrijfslocatie[Sector/xSubsectoren/Werken/Subsector/v/'+Sector/xSubsectoren/Werken/Subsector/Subsector_name+'] ? AlgemeneBedrijfslocatie/Zacht/Per_AllocDomain : FALSE
							)'
						, AllocDomain
						, bool
					);
					
				attribute<Bool> Per_AdminDomain (AdminDomain) := 
					OR(
						 AlgemeneBedrijfslocatie/Zacht/Per_AdminDomain
						,Industrieterrein/Zacht/Per_AdminDomain
						,Kantoor/Zacht/Per_AdminDomain
					);
				attribute<Bool> Per_AllocDomain (AllocDomain) := 
					OR(
						 AlgemeneBedrijfslocatie/Zacht/Per_AllocDomain
						,Industrieterrein/Zacht/Per_AllocDomain
						,Kantoor/Zacht/Per_AllocDomain
					);
			}
		}
	}
	
	Template Read_PlanCapaciteit_Tifs_T
	: Descr = "Inlezen van de plancapaciteit tifs per sector en hardheid"
	{
		parameter<String> Sector_name0;
		parameter<String> HardheidK_name;
		parameter<String> Sector_subname;
		//
		
		parameter<string> Sector_name := Sector_subname == '' ? Sector_name0 : Sector_subname;
		
		attribute<Bool> Per_AdminDomain (AdminDomain)
		: StorageName = "=UsePrivPlanCapaciteit ? '%PrivDataDir%/PlanCapaciteit/'+ModelParameters/Plancapaciteit_filedate+'/PrivPlannen_'+Sector_name+'_'+HardheidK_name+'_rdc_25m.tif' : '%RSo_DataDir%/Beleid/Plancapaciteit/'+ModelParameters/Plancapaciteit_filedate+'/Plannen_'+Sector_name+'_'+HardheidK_name+'_rdc_25m.tif'"
		, StorageReadOnly = "TRUE";
		
		attribute<Bool> Per_AllocDomain (AllocDomain)
		: StorageName = "=UsePrivPlanCapaciteit ? '%PrivDataDir%/PlanCapaciteit/'+ModelParameters/Plancapaciteit_filedate+'/PrivPlannen_'+Sector_name+'_'+HardheidK_name+'_rdc_100m.tif' : '%RSo_DataDir%/Beleid/Plancapaciteit/'+ModelParameters/Plancapaciteit_filedate+'/Plannen_'+Sector_name+'_'+HardheidK_name+'_rdc_100m.tif'"
		, StorageReadOnly = "TRUE";
	}
}