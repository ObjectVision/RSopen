container RES10
{
	unit<UInt32> optionSet := range(uint32, 0, 1);
	
	attribute<String> GDAL_Options (optionSet) : [ 'AUTODETECT_TYPE=YES'];//https://gdal.org/drivers/vector/csv.html?highlight=csv
	
	unit<UInt32> Bod10
	: StorageName     = "%RSo_DataDir%/Energie/RES10/Tabel_2_3_Bod_RES_1_0_in_GWh_per_regio.csv"
	, StorageType     = "gdal.vect"
	, StorageReadOnly = "True"
	, Source          = "Tabel 2.3 https://www.pbl.nl/sites/default/files/downloads/pbl-2021-monitor-res-1.0-4509.pdf"
						"id2019 zelf toegevoegd om te kunnen koppelen met SourceData/Energie/Analysekaarten/RES_9_grenzen/grenzen_resregios_2019_RES_land/id"
	, URL             = "%RSo_DataDir%/Energie/RES10/pbl-2021-monitor-res-1.0-4509.pdf"
	, SyncMode        = "Attr"
	{
		attribute<GWh> Bod_Ondergrens_GWh :=Bod_Ondergrens_RES_1_0[GWh];
	}
	
	parameter<GWh> BodNL :=sum(Bod10/Bod_Ondergrens_GWh);
	
	parameter<Float32> MinimaalBodFractie:=35[TWh]/BodNL[TWh];
	
	parameter<GWh> BodWTNL :=sum(Bod10/Windmolens)[GWh];
	parameter<GWh> BodWTofZPVNL :=(sum(Bod10/Windmolens_of_zon_pv[GWh])*0.5f)[GWh];//aanname of--> half
	parameter<GWh> BodWTNL_Tot :=BodWTNL + BodWTofZPVNL;
	
	parameter<GWh> BodZPVNL :=sum(Bod10/Zon_pv_Totaal)[GWh];
	parameter<GWh> BodZPVNL_Tot :=BodZPVNL + BodWTofZPVNL;
	
	parameter<GW> BodWTNL_GW_Tot :=BodWTNL_Tot / /ModelParameters/Wind/Vollasturen_windturbine;
	parameter<GW> BodZPVNL_GW_Tot :=BodZPVNL_Tot / /ModelParameters/Zon/Vollasturen_zonnepaneel;
	
	unit<UInt32> RESRegio:= RES10_concept/Analysekaarten/RES_9_grenzen/grenzen_resregios_2019_RES_land
	{
		attribute<GWh> Bod :=rjoin(RES10_concept/Analysekaarten/RES_9_grenzen/grenzen_resregios_2019_RES_land/id[int32], Bod10/id2019, Bod10/Bod_Ondergrens_GWh);
		attribute<GWh> Doel :=MinimaalBodFractie * Bod;//Eventueel te gebruiken claim (absoluut) per RES-regio
	}
}