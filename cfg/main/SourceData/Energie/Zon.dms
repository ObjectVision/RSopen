container Zon
{
	unit<UInt32> Zonnevelden_polygonen := SourceData/Grondgebruik/BRT/Compleet/MaakLosseCat_functioneel_gebied/terrein_vlakken_zonnepark/combined_vlak
	, Descr = "Inlezen van de zonnevelden polygonen uit de BRT grondgebruikskaart. Deze zijn al gefilterd op functie terrein_vlak_zonnepark en samengevoegd tot één laag.";
	{
		attribute<rdc_meter> geometry (poly) := SourceData/Grondgebruik/BRT/Compleet/MaakLosseCat_functioneel_gebied/terrein_vlakken_zonnepark/combined_vlak/geometry;
		attribute<ha> area := convert(area(geometry, meter2_f64), ha);
		attribute<MW> Vermogen := area * ModelParameters/zon/Vermogen_ha, Descr = "Berekenen van het vermogen van elk zonneveld polygon op basis van de oppervlakte en de modelparameter voor vermogen per hectare.";
		
		attribute<.> Per_AllocDomain (AllocDomain) := poly2grid(geometry, AllocDomain);
		attribute<.> Per_AdminDomain (AdminDomain) := poly2grid(geometry, AdminDomain);
		
		unit<IPoint> AdminDomain := Geography/AdminDomain
		{
			attribute<MW>     VermogenProject    := ../Vermogen[../Per_AdminDomain];
			attribute<uint64> CountCellenProject := pcount(Per_AdminDomain)[../Per_AdminDomain];
			attribute<MW>     Vermogen           := MakeDefined(VermogenProject / CountCellenProject[float32], 0[MW]), Descr = "Toekennen van het vermogen van het zonneveld aan de cellen in het AdminDomain. Buiten het studiegebied is het vermogen 0.";
		}
		
		unit<IPoint> AllocDomain := Geography/AllocDomain
		{
			attribute<MW> VermogenProject := ../Vermogen[../Per_AllocDomain];
			attribute<uint64> CountCellenProject := pcount(Per_AllocDomain)[../Per_AllocDomain];
			attribute<MW> Vermogen := MakeDefined( VermogenProject / CountCellenProject[float32], 0[MW]), Descr = "Toekennen van het vermogen van het zonneveld aan de cellen in het AllocDomain. Buiten het studiegebied is het vermogen 0.";
		}
	}
}