container Vastgoed: using  = "Units;Geography;Classifications;RegioIndelingen"
, Descr = "Brondata voor vastgoed, zoals de BAG, logistieke panden, energielabels, eigendom en WOZ-waardes"
{
	#include<WOZ.dms>
	#include<EigendomStaat.dms>
	#include<BAG.dms>
	
	unit<UInt32> Logistiek_panden
	:	StorageName     = "%RSo_DataDir%/Vastgoed/LogistiekBuildings/buildings2021NL_rdc.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	DialogData      = "rdc_meter"
	,	url             = "%RSo_DataDir%/Vastgoed/LogistiekBuildings/buildings2021NL_meta.txt"
	,	SyncMode        = "none"
	, 	Descr           = "Bag panden geklassificeerd in logistiek functies door Merten Nefs (Nefs, M. (2022). Dutch Distribution Centres 2021 Geodata. Rotterdam: 4TU. ResearchData. DOI, 10(19361018), v1). Ontvangen van Eric Koomen op 28 maart 2022"
	{
		attribute<rdc_meter> geometry (poly);
		attribute<UInt32>    function_n;
		attribute<String>    function;
		attribute<String>    bag_id;
		attribute<uint64>    pand_bag_nr := uint64(bag_id);
		attribute<UInt32>    constr_yea;
		
		unit<UInt32> Logistics := select_with_attr_by_cond(., function_n != 3)
		{
		}
	}
	
	unit<UInt32> Energielabels
	:	StorageName     = "%RSo_DataDir%/Vastgoed/Energielabel/VestaStartanalyse2020/RVOtot.fss"
	,	StorageReadOnly = "True"
	,	DialogType      = "Map"
	,	DialogData      = "locatie"
	,	Source          = "export uit https://pbl.sliksvn.com/vesta/PD/branch/VestaStartanalyse2020_To_RSL rev 566, GeoDMS 7.239, open S1a_B_LuchtWP.dms activate /Invoer/RuimtelijkeData/energieLabel/RVOtot --> fss in LD, ongegeveer 300000 records bleken niet koppelbaar (voor vastgesteld en voorlopig dms geocoding (zijn niet opgenomen in fss bestand)"
	,	Descr           = "Alle energielabels van woningen en utiliteitsgebouwen in Nederland"
	{
		attribute<String>                                vbo_id;
		attribute<rdc_meter>                             locatie;
		attribute<UInt8>                                 SchilLabel_rel;
		attribute<String>                                pand_id;
		attribute<UInt32>                                Bron                : Descr = "Bron van het energielabel: 1=vastgesteld,2=voorlopig,3=utiliteit (is volledig)";
		attribute<Classifications/Vastgoed/Energielabel> Energielabel_rel    := SchilLabel_rel[Classifications/Vastgoed/Energielabel];
		attribute<AllocDomain>                           AllocDomain_rel     := locatie[AllocDomain];
		attribute<AdminDomain>                           AdminDomain_rel     := locatie[AdminDomain];
		attribute<Classifications/Vastgoed/Energielabel> Modus (AdminDomain) := Modus(Energielabel_rel, locatie[AdminDomain]); //, StorageName = "%LocalDataProjDir%/BaseData/Vastgoed/Energielabels.tif", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))";
	}
	
	container Rudifun_src
	// container Rudifun
	:	StorageName     = "%RSo_DataDir%/Vastgoed/Rudifun_2024_nl.gdb"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	DialogData      = "rdc_meter"
	,	Descr           = "De Ruimtelijke Dichtheden en Functiemenging in Nederland (RUDIFUN) dataset geeft een indicatie van de dichtheid en morfologie van bebouwd gebied."
	, 	URL             = "https://dataportaal.pbl.nl/RUDIFUN"
	,	SyncMode        = "AllTables"
	{
		
	}
	
	container Write_Rudifun
	:	StorageName     = "%RSo_DataDir%/Vastgoed/Rudifun_2024.mmd"
	,	Descr           = "De Ruimtelijke Dichtheden en Functiemenging in Nederland (RUDIFUN) dataset geeft een indicatie van de dichtheid en morfologie van bebouwd gebied."
	, 	URL             = "https://dataportaal.pbl.nl/RUDIFUN"
	{
		unit<uint32> Netto_Buurt := Rudifun_src/nl2_04_Netto_Buurt
		{
			attribute<rdc_meter> geometry (poly) := Rudifun_src/nl2_04_Netto_Buurt/geometry;
			attribute<string>    BU_CODE         := Rudifun_src/nl2_04_Netto_Buurt/BU_CODE;
		}
		unit<uint32> Bruto_Buurt := Rudifun_src/nl2_05_Bruto_Buurt
		{
			attribute<rdc_meter> geometry (poly) := Rudifun_src/nl2_05_Bruto_Buurt/geometry;
			attribute<string>    BU_CODE         := Rudifun_src/nl2_05_Bruto_Buurt/BU_CODE;
			attribute<string>    PV_CODE         := Rudifun_src/nl2_05_Bruto_Buurt/PV_CODE;
		}
	}
	
	container Rudifun
	:	StorageName     = "%RSo_DataDir%/Vastgoed/Rudifun_2024.mmd"
	,	StorageReadOnly = "True"
	,	Descr           = "De Ruimtelijke Dichtheden en Functiemenging in Nederland (RUDIFUN) dataset geeft een indicatie van de dichtheid en morfologie van bebouwd gebied."
	, 	URL             = "https://dataportaal.pbl.nl/RUDIFUN";
	
	container Funderingsschade
	{
		unit<uint32> per_Buurt
		:	StorageName     = "%RSo_DataDir%/Vastgoed/funderingskosten_buurten2020.csv"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		,	Descr           = "Funderingsschade ontvangen van Deltares d.d. 15-12-2025"
		{
			unit<uint32> Buurt := RegioIndelingen/CBS/Y2020/Buurt;
			
			attribute<Buurt>     Buurt_rel       := rlookup(buurtcode, Buurt/statcode);
			attribute<rdc_meter> geometry (poly) := Buurt/geometry[buurt_rel];
			attribute<uint32>    no_building     := num_buildings_buurt[uint32];
			attribute<float32>   cost_timber     := cost_timber_buurt[float32];
			attribute<float32>   cost_shallow    := cost_shallow_buurt[float32];
			attribute<float32>   total_cost      := total_cost_buurt[float32];
		}
		
		container Read_per_Object
		:	StorageName     = "%PrivDataDir%/Funderingsschade/buildings_pts_with_timber_shallow_costs.gpkg"
		,	StorageType     = "gdal.vect"
		,	StorageReadOnly = "True"
		,	Descr           = "Funderingsschade ontvangen van Deltares d.d. 15-12-2025"
		{
			unit<uint32> strong_climate_change
			{
				attribute<rdc_meter> geometry (arc);
				
				unit<uint32> s2p := sequence2points(geometry);
			}
		}
		
		unit<uint32> per_Object := Read_per_Object/strong_climate_change/s2p
		{
			attribute<rdc_meter>       geometry             := point;
			attribute<float32>         p_hout_fundering     := Read_per_Object/strong_climate_change/p_timber_pile[Read_per_Object/strong_climate_change/s2p/sequence_rel][float32], Descr = "";
			attribute<float32>         p_staal_fundering    := Read_per_Object/strong_climate_change/p_shallow_found[Read_per_Object/strong_climate_change/s2p/sequence_rel][float32], Descr = "";
			attribute<float32>         p_beton_fundering    := Read_per_Object/strong_climate_change/p_beton[Read_per_Object/strong_climate_change/s2p/sequence_rel][float32], Descr = "";
			
			attribute<float32>         cost_hout_fundering  := Read_per_Object/strong_climate_change/cost_timber_piles[Read_per_Object/strong_climate_change/s2p/sequence_rel][float32], Descr = "";
			attribute<float32>         cost_staal_fundering := Read_per_Object/strong_climate_change/cost_shallow_found[Read_per_Object/strong_climate_change/s2p/sequence_rel][float32], Descr = "";
			
			attribute<yr>              Bouwjaar             := per_Pand/bouwjaar_trunc[pand_rel], Descr = "";
			attribute<WP5>             t_i                  := per_Pand/WP5_rel[pand_rel], Descr = "Woningtype van pand i (bijvoorbeeld: rijwoning, hoekwoning, vrijstaand, appartement).";
			
			attribute<per_Pand>        pand_rel             := point_in_polygon(geometry, per_Pand/geometry), Descr = "";
			attribute<BouwjaarKlassen> c_i                  := Classify(Bouwjaar, BouwjaarKlassen/ClassBreaks), Descr = "Bouwjaarklasse van pand i (bijvoorbeeld: vóór 1940, 1940–1969, 1970–1989, ≥1990).";
			
			attribute<float32>         y_i                  := p_hout_fundering * cost_hout_fundering + p_staal_fundering * cost_staal_fundering, Descr = "Verwachte funderingsschade per pand, afgeleid uit private gegevens over funderingstype en herstelkosten.";
			attribute<float32>         s_i                  := per_Pand/Counts/woon_VBOs[pand_rel][float32], Descr = "Schaalfactor voor pand i: aantal woningen in het pand";
			attribute<float32>         w_i                  := ='switch(
																 '+AsList(
																 'case(t_i == WP5/v/'+WP_x_BJ/WP_name+' && c_i == BouwjaarKlassen/v/'+WP_x_BJ/BJ_name+', r_type/'+WP_x_BJ/WP_name+' * r_jaar/'+WP_x_BJ/BJ_name+' * s_i)', ',')+'
																, null_f
																)', Descr = "Publiek herleidbaar verdeelgewicht voor pand i binnen de buurt, opgebouwd uit bouwjaarklasse- en woningtypefactoren (r_jaar, r_type) en een schaalterm s_i (aantal woningen); gebruikt om buurtkosten proportioneel toe te rekenen (niet zelf een eurobedrag).";
		}
		
		container Write_r_jaar := 
			for_each_nedv(
				BouwjaarKlassen/name
				, 'mean(per_Object/c_i == '+string(id(BouwjaarKlassen))+'[BouwjaarKlassen] ? per_Object/y_i : null_f) / mean(per_Object/y_i)'
				, void
				, float32
			), StorageName = "%RSo_DataDir%/Vastgoed/funderingskosten_r_jaar.mmd", Descr = "Relatieve risicofactor voor funderingsschade voor bouwjaarklasse c. Afgeleid uit een private kalibratieset, maar gepubliceerd als geaggregeerd kental. Genormaliseerd zodat de gemiddelde of mediane waarde gelijk is aan 1.";
			
		container Write_r_type := 
			for_each_nedv(
				WP5/name
				, 'mean(per_Object/t_i == '+string(id(WP5))+'[WP5] ? per_Object/y_i : null_f) / mean(per_Object/y_i)'
				, void
				, float32
			), StorageName = "%RSo_DataDir%/Vastgoed/funderingskosten_r_type.mmd", Descr = "Relatieve risicofactor voor funderingsschade voor woningtype t. Afgeleid uit een private kalibratieset, maar gepubliceerd als geaggregeerd kental. Genormaliseerd zodat de gemiddelde of mediane waarde gelijk is aan 1.";
		
		container r_jaar : StorageName = "%RSo_DataDir%/Vastgoed/funderingskosten_r_jaar.mmd", StorageReadOnly = "true", Descr = "Relatieve risicofactor voor funderingsschade voor bouwjaarklasse c. Afgeleid uit een private kalibratieset, maar gepubliceerd als geaggregeerd kental. Genormaliseerd zodat de gemiddelde of mediane waarde gelijk is aan 1.";
		container r_type : StorageName = "%RSo_DataDir%/Vastgoed/funderingskosten_r_type.mmd", StorageReadOnly = "true", Descr = "Relatieve risicofactor voor funderingsschade voor woningtype t. Afgeleid uit een private kalibratieset, maar gepubliceerd als geaggregeerd kental. Genormaliseerd zodat de gemiddelde of mediane waarde gelijk is aan 1.";
		
		unit<uint32> per_Pand := ='/SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/BAG_RecentYear)+'/pand'
		{
			attribute<WP5>             t_i                       := WP5_rel, Descr = "Woningtype van pand i (bijvoorbeeld: rijwoning, hoekwoning, vrijstaand, appartement).";
			attribute<BouwjaarKlassen> c_i                       := Classify(Bouwjaar_trunc, BouwjaarKlassen/ClassBreaks), Descr = "Bouwjaarklasse van pand i (bijvoorbeeld: vóór 1940, 1940–1969, 1970–1989, ≥1990).";
			attribute<float32>         s_i                       := Counts/woon_VBOs[float32], Descr = "Schaalfactor voor pand i: aantal woningen in het pand";
			
			attribute<float32>         w_i                       := ='switch(
																	 '+AsList(
																	 'case(t_i == WP5/v/'+WP_x_BJ/WP_name+' && c_i == BouwjaarKlassen/v/'+WP_x_BJ/BJ_name+', r_type/'+WP_x_BJ/WP_name+' * r_jaar/'+WP_x_BJ/BJ_name+' * s_i)', ',')+'
																	, null_f
																	)', Descr = "Publiek herleidbaar verdeelgewicht voor pand i binnen de buurt, opgebouwd uit bouwjaarklasse- en woningtypefactoren (r_jaar, r_type) en een schaalterm s_i (aantal woningen); gebruikt om buurtkosten proportioneel toe te rekenen (niet zelf een eurobedrag).";
			
			attribute<float32>         k_i                       := C_b[buurt_rel] * (w_i / sum(w_i, buurt_rel)[buurt_rel]), Descr = "Toegerekend deel van de buurtkosten C_b aan pand i";
			
			attribute<per_Buurt>       buurt_rel                 := point_in_polygon(centroid_or_mid(geometry), per_Buurt/geometry), Descr = "";
			attribute<float32>         C_b           (per_Buurt) := per_Buurt/total_cost, Descr = "Totale geschatte funderingsschade (euro) in buurt b.";
			
			attribute<Eur_woning>      k_i_censored                    := min_elem(k_i, ModelParameters/Wonen/Funderingsschade_P95);
			attribute<Eur>             k        (CompactedAdminDomain) := sum(k_i_censored[ToedelingsMatrix/Pand_rel] * ToedelingsMatrix/ToedelingPand, ToedelingsMatrix/CompactedAdminDomain_rel)[Eur]; // eindresultaat
			attribute<Eur_woning>      Verschil                        := k_i - k_i_censored;
		}
		
		unit<uint8> WP_x_BJ := combine_uint8(WP5, BouwjaarKlassen)
		{
			attribute<string> WP_name := WP5/name[first_rel];
			attribute<string> BJ_name := BouwjaarKlassen/name[second_rel];
		}
		
		unit<uint8> BouwjaarKlassen := cat_range(0b, 4b)
		{
			attribute<string> label          : ['<1940', '1940-1969', '1970-1989', '>=1990'];
			attribute<string> name           := AsItemName(label);
			attribute<yr>     ClassBreaks   : [0, 1940, 1970, 1990], DialogType = "Classification";
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
	}
	
	
}