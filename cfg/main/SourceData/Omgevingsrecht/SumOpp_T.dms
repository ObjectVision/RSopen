template SumOppT
{
	parameter<string> subpad;
	parameter<string> Filename;
	
	unit<uint32> Data
	:	StorageName   = "='%RSo_DataDir%/Beleid/Ruimte/' + subpad + '/' + Filename + '.dbf'"
	, StorageType     = "gdal.vect"
	, DialogType      = "Map"
	, StorageReadOnly = "True"
	
	{
		attribute<rdc_meter>   geometry(poly) : StorageName   = "='%RSo_DataDir%/Beleid/Ruimte/' + subpad + '/' + Filename + '.shp'";
		attribute<naamunique>  naam_Rel       := rlookup(naam, naamunique/values);
		attribute<themaunique> thema_Rel      := rlookup(thema, themaunique/values);
		
		attribute<meter2>    oppm2      := area(geometry, meter2);
		attribute<ha>        oppha      := oppm2[ha];
		attribute<float32>   aandeelprc := (oppha / totoppha) *100f;
	}
	
	parameter<ha> totoppha := sum(Data/oppha);
	
	unit<uint32> naamunique := unique(Data/naam)
	{
		attribute<uint32>  aantal  := pcount(Data/naam_Rel);
		attribute<float32> oppha   := sum(Data/oppha,Data/naam_Rel);
		attribute<float32> aandeel := (oppha / totoppha) * 100f;
	}
	
	unit<uint32> themaunique := unique(Data/thema)
	{
		attribute<uint32> aantal   := pcount(Data/thema_Rel);
		attribute<float32> oppha   := sum(Data/oppha,Data/thema_Rel);
		attribute<float32> aandeel := (oppha / totoppha) * 100f;
	}
}
