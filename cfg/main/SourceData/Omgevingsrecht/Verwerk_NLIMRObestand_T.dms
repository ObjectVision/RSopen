template verwerk_NLIMRObestand_T
{
	// begin case Parameters
	parameter<string> bestandsnaam;
	parameter<string> subpad;
	// end case Parameters
	
	parameter<string> Bestandsloc: = '%RSo_DataDir%/Beleid/Ruimte/'+ subpad + '/'+ bestandsnaam + '.gpkg';
	
	unit<uint32> NLIMRObestand: StorageName = "=Bestandsloc", StorageType = "gdal.vect", StorageReadOnly = "True"
	{
		attribute<rdc_meter> geometry (poly);
	}
	
	unit<uint32> laag_naam := unique(NLIMRObestand/naam) //, StorageName = "='%LocalDataProjDir%/' + subpad + '/' + bestandsnaam + '.csv'", StorageType = "gdalwrite.vect"//, StorageReadOnly = "FALSE"
	{
		attribute<uint2>  InSelectie  := const(0u2,.);
		attribute<string> ti_naam := AsItemname(values);
	}
	
	unit<uint32> NLIMROSelectiebestand: StorageName = "='%RSo_DataDir%/beleid/ruimte/' + subpad + '/' + bestandsnaam + '.csv'", StorageType = "gdal.vect", StorageReadOnly = "True"
	{
		attribute<bool> selectie:= InSelectie == '1';
	}
	
	unit<uint32> laag_naam_selectie := select_with_org_rel(rjoin(laag_naam/values, NLIMROSelectiebestand/values, NLIMROSelectiebestand/selectie))
	{
		attribute<string> values  := laag_naam/values[org_rel];
		attribute<string> ti_naam := laag_naam/ti_naam[org_rel];
	}
	
	container Laag_namen := for_each_ne(laag_naam/ti_naam, 'SelectieEnVergrid(NLIMRObestand,' + quote(laag_naam/values) + ')');
	
	container Laag_namen_selectie := for_each_ne(laag_naam_selectie/ti_naam, 'SelectieEnVergrid(NLIMRObestand,' + quote(laag_naam_selectie/values) + ')');
}