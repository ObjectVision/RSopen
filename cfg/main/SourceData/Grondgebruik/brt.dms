container BRT
{
	parameter<String> filedate := ModelParameters/BRT_file_date;
	
	unit<UInt32> IsZee := select_with_org_rel(lowercase(Compleet/top10nl_waterdeel_vlak/Label) == lowercase('Water vlak - droogvallend') || lowercase(Compleet/top10nl_waterdeel_vlak/Label) == lowercase('Water vlak - zee'))
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := geos_buffer_multi_polygon(geometry, 1d, 16b);
	}
	unit<UInt32> IsWaterweg := select_with_org_rel(lowercase(Compleet/top10nl_waterdeel_vlak/Label) == lowercase('Water vlak - waterloop hoofd') && lowercase(Compleet/top10nl_waterdeel_vlak/hoofdafwatering) == lowercase('ja'))
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := geos_buffer_multi_polygon(geometry, 1d, 16b);
	}
	
	unit<UInt32> IsHoofdweg := select_with_org_rel(Compleet/top10nl_wegdeel_vlak/awegnummer != '' || Compleet/top10nl_wegdeel_vlak/ewegnummer != '' || Compleet/top10nl_wegdeel_vlak/nwegnummer != '' || Compleet/top10nl_wegdeel_vlak/swegnummer != '')
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_wegdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := geos_buffer_multi_polygon(geometry, 1d, 16b);
	}
	unit<UInt32> IsSpoor := select_with_org_rel(lowercase(Compleet/top10nl_spoorbaandeel_lijn/typespoorbaan) == lowercase('trein'))
	{
		attribute<rdc_meter> geometry (arc) := Compleet/top10nl_spoorbaandeel_lijn/geometry[org_rel];
		attribute<Bool>      IsElectrisch   := (lowercase(Compleet/top10nl_spoorbaandeel_lijn/elektrificatie) == lowercase('ja'))[org_rel];
	}
	
	unit<UInt32> IsDierentuinOpenluchtMuseum := union_unit(
													Compleet/MaakLosseCat_functioneel_gebied/Terrein_vlakken_dierentuin_safaripark/combined_vlak
													,Compleet/MaakLosseCat_functioneel_gebied/Terrein_vlakken_openluchtmuseum/combined_vlak
												)
	{
		attribute<rdc_meter> geometry (poly)                 := union_data(., Compleet/MaakLosseCat_functioneel_gebied/Terrein_vlakken_dierentuin_safaripark/combined_vlak/geometry
																				,Compleet/MaakLosseCat_functioneel_gebied/Terrein_vlakken_openluchtmuseum/combined_vlak/geometry);
		attribute<bool>      Per_AdminDomain   (AdminDomain) := IsDefined(poly2grid(geometry, AdminDomain));
	}
	
	
	container Compleet
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/top10nl_Compleet-'+filedate+'.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<UInt32> top10nl_Wegdeel_vlak
		{
			attribute<String>                       Label                := BRT_Classifications/Label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/Label_uq> Label_uq_rel         := rlookup(lowercase(Label), lowercase(BRT_Classifications/Label_uq/values));
			attribute<Bool>                         IsStudyArea           := IsDefined(point_in_polygon(centroid_or_mid(geometry_simpl), rdc_meter/BoundingBoxPlus/polygon));
		}
		
		unit<UInt32> top10nl_Waterdeel_vlak
		{
			attribute<String>                       Label                := BRT_Classifications/Label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/Label_uq> Label_uq_rel         := rlookup(lowercase(Label), lowercase(BRT_Classifications/Label_uq/values));
			attribute<Bool>                         IsStudyArea           := IsDefined(point_in_polygon(centroid_or_mid(geometry_simpl), rdc_meter/BoundingBoxPlus/polygon));
		}
		
		unit<UInt32> top10nl_terrein_vlak
		{
			attribute<String>                       Label                 := BRT_Classifications/Label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel  := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/Label_uq> Label_uq_rel          := rlookup(lowercase(Label), lowercase(BRT_Classifications/Label_uq/values));
			attribute<rdc_meter>                    geometry_simpl (poly) := geos_buffer_multi_polygon(geometry, 1d, 16b);
			attribute<Bool>                         IsStudyArea           := IsDefined(point_in_polygon(centroid_or_mid(geometry_simpl), rdc_meter/BoundingBoxPlus/polygon));
		}
		container MaakLosseCat_functioneel_gebied :=
			for_each_ne(
				BRT_Classifications/Label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/Label_uq))+ ',''functioneel_gebied'')'
			);
		container MaakLosseCat_plaats :=
			for_each_ne(
				BRT_Classifications/Label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/Label_uq))+ ',''plaats'')'
			);
		container MaakLosseCat_gebouw :=
			for_each_ne(
				BRT_Classifications/Label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/Label_uq))+ ',''gebouw'')'
			);
	}

	unit<UInt32> BRT_Classifications := Classifications/Grondgebruik/BRT/visualisatiecodes;
	
	Template BRT_groepen_T
	{
		parameter<BRT_Classifications/Label_uq> Label_rel;
		parameter<String> Type;
		//
		
	
		unit<UInt32> vlak := ='select_with_org_rel(rlookup(lowercase(BRT_Classifications/Label)[rlookup(int32(top10nl_'+Type+'_vlak/visualisatiecode), BRT_Classifications/code)], lowercase(BRT_Classifications/Label_uq/values)) == Label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_vlak/geometry[org_rel]';
			// attribute<UInt32>    aantal_inwoners := ='top10nl_'+Type+'_vlak/aantalinwoners[uint32][org_rel]';
			// attribute<String>    plaatsnaam      := ='top10nl_'+Type+'_vlak/naamnl[org_rel]';
		} 
		unit<UInt32> multi_vlak := ='select_with_org_rel(rlookup(lowercase(BRT_Classifications/Label)[rlookup(int32(top10nl_'+Type+'_multivlak/visualisatiecode), BRT_Classifications/code)], lowercase(BRT_Classifications/Label_uq/values)) == Label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_multivlak/geometry[org_rel]';
			// attribute<UInt32>    aantal_inwoners := ='top10nl_'+Type+'_multivlak/aantalinwoners[uint32][org_rel]';
			// attribute<String>    plaatsnaam      := ='top10nl_'+Type+'_multivlak/naamnl[org_rel]';
		} 
		
		unit<UInt32> combined_vlak := union_unit(vlak, multi_vlak)
		{
			attribute<rdc_meter> geometry (poly) := union_data(., vlak/geometry, multi_vlak/geometry);
			// attribute<UInt32>    aantal_inwoners := union_data(., vlak/aantal_inwoners, multi_vlak/aantal_inwoners);
			// attribute<String>    plaatsnaam      := union_data(., vlak/plaatsnaam, multi_vlak/plaatsnaam);
		} 
	}
}