container BRT
{
	parameter<string> filedate := ModelParameters/BRT_file_date;
	
	unit<uint32> IsZee := select_with_org_rel(Compleet/top10nl_waterdeel_vlak/label == 'Water vlak - droogvallend' || Compleet/top10nl_waterdeel_vlak/label == 'Water vlak - zee')
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	unit<uint32> IsWaterweg := select_with_org_rel(Compleet/top10nl_waterdeel_vlak/label == 'Water vlak - waterloop hoofd' && Compleet/top10nl_waterdeel_vlak/hoofdafwatering == 'ja')
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	
	unit<uint32> IsHoofdweg := select_with_org_rel(Compleet/top10nl_wegdeel_vlak/awegnummer != '' || Compleet/top10nl_wegdeel_vlak/ewegnummer != '' || Compleet/top10nl_wegdeel_vlak/nwegnummer != '' || Compleet/top10nl_wegdeel_vlak/swegnummer != '')
	{
		attribute<rdc_meter> geometry (poly) := Compleet/top10nl_wegdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	
	container Compleet
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/top10nl_Compleet-'+filedate+'.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_Wegdeel_vlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		unit<uint32> top10nl_Waterdeel_vlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		unit<uint32> top10nl_terrein_vlak
		{
			attribute<string>                       label                 := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel  := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel          := rlookup(label, BRT_Classifications/label_uq/values);
			attribute<rdc_meter>                    geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
		}
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/label_uq))+ ',''plaats'')'
			);
	}

	unit<uint32> BRT_Classifications := Classifications/Grondgebruik/BRT/visualisatiecodes;
	
	Template BRT_groepen_T
	{
		parameter<BRT_Classifications/label_uq> label_rel;
		parameter<string> Type;
		//
		
	
		unit<uint32> vlak := ='select_with_org_rel(rlookup(BRT_Classifications/label[rlookup(int32(top10nl_'+Type+'_vlak/visualisatiecode), BRT_Classifications/code)], BRT_Classifications/label_uq/values) == label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_vlak/geometry[org_rel]';
			attribute<uint32>    aantal_inwoners := ='top10nl_'+Type+'_vlak/aantalinwoners[uint32][org_rel]';
			attribute<string>    plaatsnaam      := ='top10nl_'+Type+'_vlak/naamnl[org_rel]';
		} 
		unit<uint32> multi_vlak := ='select_with_org_rel(rlookup(BRT_Classifications/label[rlookup(int32(top10nl_'+Type+'_multivlak/visualisatiecode), BRT_Classifications/code)], BRT_Classifications/label_uq/values) == label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_multivlak/geometry[org_rel]';
			attribute<uint32>    aantal_inwoners := ='top10nl_'+Type+'_multivlak/aantalinwoners[uint32][org_rel]';
			attribute<string>    plaatsnaam      := ='top10nl_'+Type+'_multivlak/naamnl[org_rel]';
		} 
		
		unit<uint32> combined_vlak := union_unit(vlak, multi_vlak)
		{
			attribute<rdc_meter> geometry (poly) := union_data(., vlak/geometry, multi_vlak/geometry);
			attribute<uint32>    aantal_inwoners := union_data(., vlak/aantal_inwoners, multi_vlak/aantal_inwoners);
			attribute<string>    plaatsnaam      := union_data(., vlak/plaatsnaam, multi_vlak/plaatsnaam);
		} 
	}
}