container BRT
{
	parameter<string> filedate := ModelParameters/BRT_file_date;
	
	unit<uint32> IsZee := select_with_org_rel(waterdeel/top10nl_waterdeel_vlak/label == 'Water vlak - droogvallend' || waterdeel/top10nl_waterdeel_vlak/label == 'Water vlak - zee')
	{
		attribute<rdc_meter> geometry (poly) := waterdeel/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	unit<uint32> IsWaterweg := select_with_org_rel(waterdeel/top10nl_waterdeel_vlak/label == 'Water vlak - waterloop hoofd' && waterdeel/top10nl_waterdeel_vlak/hoofdafwatering == 'ja')
	{
		attribute<rdc_meter> geometry (poly) := waterdeel/top10nl_waterdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	
	unit<uint32> IsHoofdweg := select_with_org_rel(Wegdeel/top10nl_wegdeel_vlak/awegnummer != '' || Wegdeel/top10nl_wegdeel_vlak/ewegnummer != '' || Wegdeel/top10nl_wegdeel_vlak/nwegnummer != '' || Wegdeel/top10nl_wegdeel_vlak/swegnummer != '')
	{
		attribute<rdc_meter> geometry (poly) := Wegdeel/top10nl_wegdeel_vlak/geometry[org_rel];
		attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
	}
	
	container Compleet
	:	StorageName     = "='%RSLDataDir%/Grondgebruik/BRT/BRT_20240130.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_plaats_vlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		unit<uint32> top10nl_plaats_multivlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/label_uq))+ ',''plaats'')'
			);
	}
	
	
	container Spoorbaandeel
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/'+filedate+'/top10nl_Spoorbaandeel.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_spoorbaandeel_lijn 
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
			attribute<bool>                         IsElectrisch         := elektrificatie == 'ja';
		}
		
		container MaakLosseCat := for_each_ne(BRT_Classifications/label_uq/name, 'BRT_groepen_T(' +string(id(BRT_Classifications/label_uq))+ ',''spoorbaandeel'')');
	}
	
	container Wegdeel
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/'+filedate+'/top10nl_wegdeel.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_wegdeel_vlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/label_uq))+ ',''wegdeel'')'
			);
	}
	
	container Waterdeel
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/'+filedate+'/top10nl_Waterdeel.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_Waterdeel_vlak
		{
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(' +string(id(BRT_Classifications/label_uq))+ ',''waterdeel'')'
			);
	}
	
	container Terreinvlakken
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/'+filedate+'/top10nl_Terrein.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables" //AllTables None
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_terrein_vlak
		{
			attribute<rdc_meter> geometry (poly);
			attribute<rdc_meter> geometry_simpl (poly) := bg_simplify_multi_polygon(Geometry, 1d);
			attribute<int64> visualisatiecode;
			attribute<string> typelandgebruik;
			attribute<string> voorkomen;
			
			attribute<string>                       label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>          visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq> label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
			
			// attribute<RegioIndelingen/MRA8> MRA_rel := point_in_polygon(centroid_or_mid(geometry), /geography/RegioIndelingen/MRA8/geometry);
			
			// unit<uint32> Amsterdam := select_with_org_rel(IsDefined(MRA_rel)) 
			// {
				// attribute<rdc_meter>                    geometry (poly)      := ../geometry[org_rel];
				// attribute<string>                       label                := ../label[org_rel];
				// attribute<BRT_Classifications>          visualisatiecode_rel := ../visualisatiecode_rel[org_rel];
				// attribute<BRT_Classifications/label_uq> label_uq_rel         := ../label_uq_rel[org_rel];
				
				// attribute<string> typelandgebruik := ../typelandgebruik[org_rel];
				// attribute<string> voorkomen       := ../voorkomen[org_rel];
			// }
		}
		
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(BRT_Classifications/label_uq/V/' + BRT_Classifications/label_uq/name + ', ''terrein'')'
			);
	}
	
	container FunctioneelGebied
	:	StorageName     = "='%RSo_DataDir%/Grondgebruik/BRT/'+filedate+'/top10nl_FunctioneelGebied.gpkg'"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	DialogData      = "rdc_meter"
	, 	SyncMode        = "AllTables"
	, 	Descr           = "Download from PDOK"
	{
		unit<uint32> top10nl_functioneel_gebied_vlak 
		{
			attribute<string>                         label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>            visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq>   label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		unit<uint32> top10nl_functioneel_gebied_multivlak  
		{
			attribute<string>                         label                := BRT_Classifications/label[visualisatiecode_rel];
			attribute<BRT_Classifications>            visualisatiecode_rel := rlookup(int32(visualisatiecode), BRT_Classifications/code);
			attribute<BRT_Classifications/label_uq>   label_uq_rel         := rlookup(label, BRT_Classifications/label_uq/values);
		}
		
		container MaakLosseCat :=
			for_each_ne(
				BRT_Classifications/label_uq/name
				, 'BRT_groepen_T(BRT_Classifications/label_uq/V/' + BRT_Classifications/label_uq/name + ', ''functioneel_gebied'')'
			);
			
	}
	Template BRT_groepen_T
	{
		parameter<BRT_Classifications/label_uq> label_rel;
		parameter<string> Type;
		//
		
		unit<uint32> vlak := ='select_with_org_rel(top10nl_'+Type+'_vlak/label_uq_rel == label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_vlak/geometry[org_rel]';
			attribute<uint32>    aantal_inwoners := ='top10nl_'+Type+'_vlak/aantalinwoners[uint32][org_rel]';
			attribute<string>    plaatsnaam      := ='top10nl_'+Type+'_vlak/naamnl[org_rel]';
		} 
		unit<uint32> multi_vlak := ='select_with_org_rel(top10nl_'+Type+'_multivlak/label_uq_rel == label_rel)'
		{
			attribute<rdc_meter> geometry (poly) := ='top10nl_'+Type+'_multivlak/geometry[org_rel]';
			attribute<uint32>    aantal_inwoners := ='top10nl_'+Type+'_multivlak/aantalinwoners[uint32][org_rel]';
			attribute<string>    plaatsnaam      := ='top10nl_'+Type+'_multivlak/naamnl[org_rel]';
		} 
		
		unit<uint32> combined_vlak := union_unit(vlak, multi_vlak)
		{
			attribute<rdc_meter> geometry (poly) := union_data(., vlak/geometry, multi_vlak/geometry);
			attribute<uint32>    aantal_inwoners := union_data(., vlak/aantal_inwoners, multi_vlak/aantal_inwoners);
			attribute<string>    plaatsnaam      := union_data(., vlak/plaatsnaam, multi_vlak/plaatsnaam);
		} 
	}
	
	unit<uint32> BRT_Classifications := Classifications/Grondgebruik/BRT/visualisatiecodes;
}