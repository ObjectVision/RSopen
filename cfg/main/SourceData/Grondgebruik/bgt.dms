container BGT: using = "Units;Classifications;Classifications/Grondgebruik/BGT;Geography;RegioIndelingen"
, Descr = "Basisregistratie Grootschalige Topografie"
{
	parameter<String> filedate := ModelParameters/BGT_file_date;
	
	unit<UInt32> Read
	:	StorageName = "='%RSo_DataDir%/Grondgebruik/BGT/'+filedate+'.mmd'"
	,	StorageReadOnly = "True" 
	, 	Descr = "Generated in BGT-Tools project, available from Object Vision Github repository"
	,	URL = "https://github.com/ObjectVision/BGT-Tools"
	{
		// attribute<rdc_meter>                           geometry_dpoint (poly); //is al gesimplified op 1 meter in de BGT-Tools. 
		// attribute<rdc_meter>                           geometry (poly) := geometry_dpoint;
		// attribute<String>                              Type_name;
		// attribute<String>                              plus_Type;
		// attribute<String>                              bron_bestand;
		attribute<Classes>                             visualisatiecode_rel           := rlookup(lowercase(Class_name), lowercase(Classes/Class_Label));
		attribute<Classes>                             visualisatiecode_rel2          := rlookup(lowercase(Class_name2), lowercase(Classes/Class_Label));
		attribute<String>                              Class_name                     := lowercase(bron_bestand) == 'pand' ? 'pand_waardeOnbekend' : bron_bestand + '_' + Type_name;
		attribute<String>                              Class_name2                    := bron_bestand + '_' + plus_Type;
		attribute<km2>                                 oppervlakkm2                   := area(geometry,meter2_f64)[km2];
	}
	
	unit<UInt32> IsDefinedAndNotNoData := select_with_attr_by_cond(Read, IsDefined(Read/visualisatiecode_rel)) 
	{
		attribute<Bool> IsStudyArea := IsDefined(point_in_polygon(centroid_or_mid(geometry), rdc_meter/BoundingBoxPlus/polygon));
		
		container MaakLosseCat :=
			for_each_ne(
				Classes/Class_name
				, 'MaakLosse_BGT_groepen_T('+string(id(Classes))+', IsDefinedAndNotNoData)'
			);
		
		attribute<Bool> EvidentBenut    := ='VariantParameters/EvidentBenut/BGT/'+VariantParameters/VariantK/EvidentBenutVariant_BGT[/variant_rel]+'[visualisatiecode_rel]';
		attribute<Bool> EvidentBenutZon := visualisatiecode_rel == Classes/V/waterdeel_waterloop ? False : EvidentBenut;//ook waterloop EvidentBenut tbv vermijden Zon in havens Amsetrdan en Rotterdam, ARK bij Utrecht
		
		attribute<Percent> FractieGroen   := Classes/Fractie_Groen[visualisatiecode_rel];
		attribute<Percent> FractieBlauw   := Classes/Fractie_Blauw[visualisatiecode_rel];
		attribute<Percent> FractieBruin   := Classes/Fractie_Bruin[visualisatiecode_rel];
		attribute<Percent> FractieVerhard := Classes/Fractie_Verhard[visualisatiecode_rel];
		
		attribute<Percent> FractieHalfverhardingCapaciteit := Classes/FractieWaterretentieCapaciteit/Halfverharding[visualisatiecode_rel];
		attribute<Percent> FractieHolleWegCapaciteit       := Classes/FractieWaterretentieCapaciteit/HolleWeg[visualisatiecode_rel];
		attribute<Percent> FractieWadiCapaciteit           := Classes/FractieWaterretentieCapaciteit/Wadi[visualisatiecode_rel];
		
		unit<uint64> per_2_5_all := poly2allgrids_uint64(geometry, rdc_2_5m)
		{
			attribute<float32> FractieGroen                      := IsDefinedAndNotNoData/FractieGroen[polygon_rel][ratio];
			attribute<float32> FractieBlauw                      := IsDefinedAndNotNoData/FractieBlauw[polygon_rel][ratio];
			attribute<float32> FractieBruin                      := IsDefinedAndNotNoData/FractieBruin[polygon_rel][ratio];
			attribute<float32> FractieVerhard                    := IsDefinedAndNotNoData/FractieVerhard[polygon_rel][ratio];
			attribute<float32> FractieGroen_2_5m      (rdc_2_5m) := max_elem(max(FractieGroen, grid_rel), 0f);
			attribute<float32> FractieBlauw_2_5m      (rdc_2_5m) := max_elem(max(FractieBlauw, grid_rel), 0f);
			attribute<float32> FractieBruin_2_5m      (rdc_2_5m) := max_elem(max(FractieBruin, grid_rel), 0f);
			attribute<float32> FractieVerhard_2_5m    (rdc_2_5m) := max_elem(max(FractieVerhard, grid_rel), 0f);
			attribute<meter2>  Groen_m2_2_5m          (rdc_2_5m) := FractieGroen_2_5m * rdc_2_5m/NrMeter2PerCell;
			attribute<meter2>  Blauw_m2_2_5m          (rdc_2_5m) := FractieBlauw_2_5m * rdc_2_5m/NrMeter2PerCell;
			attribute<meter2>  Bruin_m2_2_5m          (rdc_2_5m) := FractieBruin_2_5m * rdc_2_5m/NrMeter2PerCell;
			attribute<meter2>  Verhard_m2_2_5m        (rdc_2_5m) := FractieVerhard_2_5m * rdc_2_5m/NrMeter2PerCell;
			
			attribute<float32> FractieHalfverhardingCapaciteit                 := IsDefinedAndNotNoData/FractieHalfverhardingCapaciteit[polygon_rel][ratio];
			attribute<float32> FractieHolleWegCapaciteit                       := IsDefinedAndNotNoData/FractieHolleWegCapaciteit[polygon_rel][ratio];
			attribute<float32> FractieWadiCapaciteit                           := IsDefinedAndNotNoData/FractieWadiCapaciteit[polygon_rel][ratio];
			attribute<float32> FractieHalfverhardingCapaciteit_2_5m (rdc_2_5m) := max_elem(max(FractieHalfverhardingCapaciteit, grid_rel), 0f);
			attribute<float32> FractieHolleWegCapaciteit_2_5m       (rdc_2_5m) := max_elem(max(FractieHolleWegCapaciteit, grid_rel), 0f);
			attribute<float32> FractieWadiCapaciteit_2_5m           (rdc_2_5m) := max_elem(max(FractieWadiCapaciteit, grid_rel), 0f);
			attribute<meter2>  HalfverhardingCapaciteit_m2_2_5m     (rdc_2_5m) := FractieHalfverhardingCapaciteit_2_5m * rdc_2_5m/NrMeter2PerCell;
			attribute<meter2>  HolleWegCapaciteit_m2_2_5m           (rdc_2_5m) := FractieHolleWegCapaciteit_2_5m * rdc_2_5m/NrMeter2PerCell;
			attribute<meter2>  WadiCapaciteit_m2_2_5m               (rdc_2_5m) := FractieWadiCapaciteit_2_5m * rdc_2_5m/NrMeter2PerCell;
			
			attribute<Bool>    IsEvidentBenut                    := IsDefinedAndNotNoData/EvidentBenut[polygon_rel];
			attribute<Bool>    IsEvidentBenutZon                 := IsDefinedAndNotNoData/EvidentBenutZon[polygon_rel];
			attribute<Bool>    IsEvidentBenut_2_5m    (rdc_2_5m) := any(IsEvidentBenut, grid_rel);
			attribute<Bool>    IsEvidentBenutZon_2_5m (rdc_2_5m) := any(IsEvidentBenutZon, grid_rel);
		}
		
		attribute<meter2>   Make_GroenOpp_AdminDomain      (AdminDomain) := sum(per_2_5_all/Groen_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/GroenOpp_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   Make_BlauwOpp_AdminDomain      (AdminDomain) := sum(per_2_5_all/Blauw_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/BlauwOpp_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   Make_BruinOpp_AdminDomain      (AdminDomain) := sum(per_2_5_all/Bruin_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/BruinOpp_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   Make_VerhardOpp_AdminDomain    (AdminDomain) := sum(per_2_5_all/Verhard_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/VerhardOpp_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   GroenOpp_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_GroenOpp_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   BlauwOpp_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_BlauwOpp_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   BruinOpp_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_BruinOpp_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   VerhardOpp0_AdminDomain        (AdminDomain) : StorageName = "=PropValue(Make_VerhardOpp_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   VerhardOpp_AdminDomain         (AdminDomain) := max_elem(AdminDomain/NrMeter2PerCell - GroenOpp_AdminDomain - BlauwOpp_AdminDomain - BruinOpp_AdminDomain, 0f); // @Jip, moeten we nog iets met het verschil tussen dit item en het item erboven?
	
		attribute<meter2>   Make_HalfverhardingCapaciteit_AdminDomain (AdminDomain) := sum(per_2_5_all/HalfverhardingCapaciteit_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/HalfverhardingCapaciteit_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   Make_HolleWegCapaciteit_AdminDomain       (AdminDomain) := sum(per_2_5_all/HolleWegCapaciteit_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/HolleWegCapaciteit_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   Make_WadiCapaciteit_AdminDomain           (AdminDomain) := sum(per_2_5_all/WadiCapaciteit_m2_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/WadiCapaciteit_'+/ModelParameters/BGT_file_date+'_Per_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageType = "gdalwrite.grid";
		attribute<meter2>   HalfverhardingCapaciteit_AdminDomain      (AdminDomain) : StorageName = "=PropValue(Make_HalfverhardingCapaciteit_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   HolleWegCapaciteit_AdminDomain            (AdminDomain) : StorageName = "=PropValue(Make_HolleWegCapaciteit_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		attribute<meter2>   WadiCapaciteit_AdminDomain                (AdminDomain) : StorageName = "=PropValue(Make_WadiCapaciteit_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "gdal.grid";
		
		attribute<Bool>   Make_IsEvidentBenut_AdminDomain      (AdminDomain) := Modus(per_2_5_all/IsEvidentBenut_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/EvidentBenut_'+/ModelParameters/BGT_file_date+'_Modus_Per_AdminDomain_'+/ModelParameters/StudyArea+'_'+VariantParameters/VariantK/EvidentBenutVariant_BGT[/variant_rel]+'.tif'", StorageType = "tif";
		attribute<Bool>   IsEvidentBenut_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_IsEvidentBenut_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
		attribute<Bool>   Make_IsEvidentBenutZon_AdminDomain   (AdminDomain) := Modus(per_2_5_all/IsEvidentBenutZon_2_5m, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/EvidentBenutZon_'+/ModelParameters/BGT_file_date+'_Modus_Per_AdminDomain_'+/ModelParameters/StudyArea+'_'+VariantParameters/VariantK/EvidentBenutVariant_BGT[/variant_rel]+'.tif'", StorageType = "tif";
		// attribute<Bool>   IsEvidentBenutZon_AdminDomain        (AdminDomain) : StorageName = "=PropValue(Make_IsEvidentBenutZon_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
		
		unit<UInt32> IsGroenVoorziening := select_with_org_rel(visualisatiecode_rel == Classes/V/begroeidterreindeel_groenVoorziening)
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[org_rel];
		}
		unit<UInt32> IsSpoorbaan := select_with_org_rel(visualisatiecode_rel == Classes/V/wegdeel_Spoorbaan)
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[org_rel];
			
			parameter<Float64> BufferSizeSnelwegenVoorWindRestrictie := ModelParameters/Wind/BufferSizeSnelwegenVoorWindRestrictie;
			
			attribute<rdc_meter> WindBuffer (poly) := geos_buffer_multi_polygon(geometry, BufferSizeSnelwegenVoorWindRestrictie, 16b);
			
			attribute<Bool>   Make_Per_AdminDomain      (AdminDomain) := IsDefined(poly2grid(WindBuffer, AdminDomain)), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/IsSpoorwegBuffer_'+string(BufferSizeSnelwegenVoorWindRestrictie)+'m_'+/ModelParameters/BGT_file_date+'_'+/ModelParameters/StudyArea+'.tif'", StorageType = "tif";
			attribute<Bool>   Per_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_Per_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
		}
		unit<UInt32> IsWaterweg := select_with_org_rel(visualisatiecode_rel == Classes/V/waterdeel_waterloop && (plus_Type == 'kanaal' || plus_Type == 'rivier' || plus_Type == 'waardeOnbekend'))
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[org_rel];
			attribute<String>    plus_Type       := ../plus_Type[org_rel];
		}
	
		unit<UInt32> IsAutoweg := select_with_org_rel(visualisatiecode_rel == Classes/V/wegdeel_rijbaan_autosnelweg )
		{
			attribute<rdc_meter> geometry       (poly) := ../geometry[org_rel];
			attribute<String>    plus_Type             := ../plus_Type[org_rel];
			attribute<Classes>   visualisatiecode_rel  := ../visualisatiecode_rel[org_rel];
			
			parameter<Float64> BufferSizeSnelwegenVoorWindRestrictie := ModelParameters/Wind/BufferSizeSnelwegenVoorWindRestrictie;
			
			attribute<rdc_meter> WindBuffer       (poly) := geos_buffer_multi_polygon(geometry, BufferSizeSnelwegenVoorWindRestrictie, 16b);
			
			unit<UInt32> FixKlaverbladen := .
			{
				attribute<rdc_meter> KlaverbladBufferPlus        (poly) := geos_buffer_multi_polygon(geometry,  100d, 16b);
				unit<UInt32>         union                              := geos_split_union_polygon(KlaverbladBufferPlus, visualisatiecode_rel);
				attribute<rdc_meter> KlaverbladBufferMin  (union, poly) := geos_buffer_multi_polygon(union/geometry, -100d, 16b);
				attribute<Bool>      Make_Per_AdminDomain (AdminDomain) := IsDefined(poly2grid(KlaverbladBufferMin, AdminDomain)), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/IsAutoweg_KlaverbladFix_'+ModelParameters/BGT_file_date+'_'+/ModelParameters/StudyArea+'.tif'", StorageType = "tif";
				attribute<Bool>      Per_AdminDomain      (AdminDomain) : StorageName = "=PropValue(Make_Per_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
			}
			attribute<Bool>   Make_Per_AdminDomain      (AdminDomain) := IsDefined(poly2grid(WindBuffer, AdminDomain)), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/IsAutowegBuffer_'+string(BufferSizeSnelwegenVoorWindRestrictie)+'m_'+ModelParameters/BGT_file_date+'_'+/ModelParameters/StudyArea+'.tif'", StorageType = "tif";
			attribute<Bool>   Per_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_Per_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
		
			//adhoc
			attribute<rdc_meter> SnelwegBufferWoningSloop                         (poly) := geos_buffer_multi_polygon(geometry, 200d, 16b);
			attribute<Bool>      Make_SnelwegBufferWoningSloop_AdminDomain      (AdminDomain) := IsDefined(poly2grid(SnelwegBufferWoningSloop, AdminDomain)), StorageName = "='%LocalDataProjDir%/BaseData/Grondgebruik/BGT/IsAutowegBuffer_200m_'+ModelParameters/BGT_file_date+'_'+/ModelParameters/StudyArea+'.tif'", StorageType = "tif";
			attribute<Bool>      SnelwegBufferWoningSloop_AdminDomain           (AdminDomain) : StorageName = "=PropValue(Make_SnelwegBufferWoningSloop_AdminDomain, 'StorageName')", StorageReadOnly = "TRUE", StorageType = "tif";
		
		}
		
		unit<UInt32> IsWindturbine := select_with_org_rel(visualisatiecode_rel == Classes/V/overigbouwwerk_windturbine)
		{
			attribute<rdc_meter> geometry       (poly) := ../geometry[org_rel];
			attribute<String>    plus_Type       := ../plus_Type[org_rel];
		}
	}
	
	Template MaakLosse_BGT_groepen_T
	{
		//
		parameter<Classes> visualisatiecode_rel_base;
		unit<UInt32> src;
		//
		
		container Source 
		{
			parameter<Classes> vc_rel := visualisatiecode_rel_base;
		}
		container phase := phasecontainer(source, 'MaakLosse_BGT '+string(visualisatiecode_rel_base));
		parameter<Classes> visualisatiecode_rel := phase/vc_rel;
		
		unit<UInt32> vlak_org := select_with_org_rel(src/IsStudyArea && src/visualisatiecode_rel == visualisatiecode_rel)
		{
			attribute<rdc_meter>      geometry               (poly) := src/geometry[org_rel];
			// parameter<rdc_meter>      geometry_union         (poly) := geos_union_polygon(geometry);
			// attribute<.>              per_2_5m           (rdc_2_5m) := poly2grid(geometry, rdc_2_5m);
			// attribute<bool>           per_5m               (rdc_5m) := poly2grid(geometry_union, rdc_5m);
			// attribute<Bool>           Per_AdminDomain (AdminDomain) := Modus(IsDefined(per_2_5m), rdc_2_5m/rdc_25m_rel);
			attribute<Classes>        visualisatiecode_rel          := src/visualisatiecode_rel[org_rel];
			
			
			// container verharding
			// {
				// attribute<Percent>        Avg_Verharding         (bool) := mean(/SourceData/Grondgebruik/Imperviousness_Map/InProcent_5m, per_5m), Descr = "Gemiddelde verharding in procenten per BGT vlak van dit type.";
				// parameter<Percent>        Avg_Verharding_Total          := Avg_Verharding[TRUE];
			// }
		}
		
		unit<UInt32> vlak_alt := select_with_org_rel(src/IsStudyArea && src/visualisatiecode_rel2 == visualisatiecode_rel)
		{
			attribute<rdc_meter>      geometry               (poly) := src/geometry[org_rel];
			// parameter<rdc_meter>      geometry_union         (poly) := geos_union_polygon(geometry);
			attribute<Classes>        visualisatiecode_rel          := src/visualisatiecode_rel2[org_rel];
			// attribute<bool>           per_5m               (rdc_5m) := poly2grid(geometry_union, rdc_5m);
		
			// container verharding
			// {
				// attribute<Percent>        Avg_Verharding         (bool) := mean(/SourceData/Grondgebruik/Imperviousness_Map/InProcent_5m, per_5m), Descr = "Gemiddelde verharding in procenten per BGT vlak van dit type.";
				// parameter<Percent>        Avg_Verharding_Total          := Avg_Verharding[TRUE];
			// }
		}
		
		unit<uint32> combined := union_unit(vlak_org, vlak_alt)
		{
			attribute<rdc_meter>      geometry               (poly) := union_data(., vlak_org/geometry, vlak_alt/geometry);
			attribute<Classes>        visualisatiecode_rel          := union_data(., vlak_org/visualisatiecode_rel, vlak_alt/visualisatiecode_rel);
		
			attribute<.>              per_2_5m           (rdc_2_5m) := poly2grid(geometry, rdc_2_5m);
			// attribute<bool>           per_5m               (rdc_5m) := poly2grid(geometry_union, rdc_5m);
			attribute<Bool>           Per_AdminDomain (AdminDomain) := Modus(IsDefined(per_2_5m), rdc_2_5m/rdc_25m_rel);
			
			attribute<provincie>      provincie_rel                 := MakeDefined(point_in_polygon(centroid_or_mid(geometry), provincie/geometry), Provincie/v/Zeeland);
		}
		
		container DetermineSoilSealing_PerProv :=
			for_each_ne(
				Provincie/name
				, 'DetermineSoilSealingPerType_T('+string(id(Provincie))+')'
			)
		{
			attribute<Percent> Avg_Verharding (combined) := ='union_data(combined, '+AsList(Provincie/name+'/subset/Avg_Verharding', ',')+')';
			parameter<Percent> Avg_Verharding_Total := mean(Avg_Verharding);
		}
	}
	
	Template DetermineSoilSealingPerType_T
	{
		//
		parameter<provincie> prov_id;
		//
		
		unit<UInt32> subset := select_with_org_rel(combined/provincie_rel = prov_id)
		{
			attribute<rdc_meter>      geometry               (poly) := combined/geometry[org_rel];
			attribute<.>              per_5m               (rdc_5m) := poly2grid(geometry, rdc_5m);
			
			attribute<Percent>        Avg_Verharding                := mean(/SourceData/Grondgebruik/Imperviousness_Map/InProcent_5m, per_5m), Descr = "Gemiddelde verharding in procenten per BGT vlak van dit type.";
			// parameter<Percent>        Avg_Verharding_Total          := mean(Avg_Verharding);
			
		}
	}
}