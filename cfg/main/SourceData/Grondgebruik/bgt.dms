container BGT: using = "Units;Classifications;Classifications/Grondgebruik/BGT;Geography"
{
	parameter<string> filedate := ModelParameters/BGT_file_date;
	
	unit<uint32> ReadCombine: StorageName = "='%RSLDataDir%/Grondgebruik/BGT/'+filedate+'.fss'", StorageReadOnly = "True"
	, Descr = "Download from PDOK in gml-files, conversion to shp via ogr2ogr. Then read the individual shp-files and combined and stored in fss"
	{
		attribute<rdc_meter>                           geometry (poly);
		attribute<LandUsePreparation/CBS2010KlasseEK>  bbg_code;
		attribute<uint32>                              code;
		attribute<string>                              plus_type;
		attribute<LandUsePreparation/BGT/poly_type>    visualisatiecode_rel                  := rlookup(code, LandUsePreparation/BGT/poly_type/code);
		attribute<LandUsePreparation/CBSKlasse>        CBSKlasse_rel                         := rlookup(bbg_code, LandUsePreparation/CBSKlasse/EK2010);
		attribute<string>                              label                                 := LandUsePreparation/BGT/poly_type/label[visualisatiecode_rel];
		
		attribute<bool>                                EvidentBenut_BuitenBBG                := BGT/poly_type/EvidentBenut_BuitenBBG[rlookup(code, BGT/poly_type/code)];
		attribute<bool>                                EvidentBenut_BinnenBBG                := BGT/poly_type/EvidentBenut_BinnenBBG[rlookup(code, BGT/poly_type/code)];
		attribute<uint32>                              per_2_5                    (rdc_2_5m) := poly2grid(geometry,  rdc_2_5m);
		
		attribute<bool>                                EvidentBenut_BuitenBBG_2_5 (rdc_2_5m) := EvidentBenut_BuitenBBG[per_2_5];
		attribute<bool>                                EvidentBenut_BinnenBBG_2_5 (rdc_2_5m) := EvidentBenut_BinnenBBG[per_2_5];
		
		attribute<bool>                                Make_BuitenBBG_per_AdminDomain (AdminDomain) := modus(EvidentBenut_BuitenBBG_2_5, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BGT/ReadCombine/EvidentBenut_'+/ModelParameters/BGT_file_date+'_BuitenBBG_Modus_per_AdminDomain.tif'";
		attribute<bool>                                BuitenBBG_per_AdminDomain      (AdminDomain) : StorageName = "=PropValue(Make_BuitenBBG_per_AdminDomain, 'StorageName')", StorageReadOnly = "true";

		attribute<bool>                                Make_BinnenBBG_per_AdminDomain (AdminDomain) := modus(EvidentBenut_BinnenBBG_2_5, rdc_2_5m/rdc_25m_rel), StorageName = "='%LocalDataProjDir%/BGT/ReadCombine/EvidentBenut_'+/ModelParameters/BGT_file_date+'_BinnenBBG_Modus_per_AdminDomain.tif'";
		attribute<bool>                                BinnenBBG_per_AdminDomain      (AdminDomain) : StorageName = "=PropValue(Make_BinnenBBG_per_AdminDomain, 'StorageName')", StorageReadOnly = "true";

		unit<uint32> IsGroenvoorziening := subset(visualisatiecode_rel == /Classifications/Grondgebruik/BGT/poly_type/V/begroeidterreindeel_groenvoorziening)
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[nr_OrgEntity];
		}
		unit<uint32> IsSpoorbaan := subset(visualisatiecode_rel == /Classifications/Grondgebruik/BGT/poly_type/V/wegdeel_spoorbaan)
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[nr_OrgEntity];
		}
		unit<uint32> IsWaterweg := subset(visualisatiecode_rel == /Classifications/Grondgebruik/BGT/poly_type/V/waterdeel_waterloop && (plus_type == 'kanaal' || plus_type == 'rivier' || plus_type == 'waardeOnbekend'))
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[nr_OrgEntity];
			attribute<string>    plus_type       := ../plus_type[nr_OrgEntity];
		
		}
		unit<uint32> IsWindturbine := subset(visualisatiecode_rel == /Classifications/Grondgebruik/BGT/poly_type/V/bouwwerk_overig_windturbine)
		{
			attribute<rdc_meter> geometry (poly) := ../geometry[nr_OrgEntity];
			attribute<string>    plus_type       := ../plus_type[nr_OrgEntity];
		}
		
		// attribute<RegioIndelingen/MRA8> MRA_rel := point_in_polygon(centroid_or_mid(geometry), /geography/RegioIndelingen/MRA8/geometry);
	}
}