container NVK : using = "classifications/grondgebruik/nvk"
{
	attribute<LU_ModelType>      HDB2050_100m_diff              (rdc_100m) := HDB/count_diff_25m_cellen >= ModelParameters/MinimumCountNieuwNatuur ? HDB/HDB2050_100m_LU_ModelType : (0b/0b)[LU_ModelType];
	attribute<LU_ModelType>      BAU2050_100m_diff              (rdc_100m) := BAU/count_diff_25m_cellen >= ModelParameters/MinimumCountNieuwNatuur ? BAU/BAU2050_100m_LU_ModelType : (0b/0b)[LU_ModelType];
	
	attribute<LU_ModelType>      HDB2050_25m_diff                (rdc_25m) := HDB/HDB2050_25m_diff;
	attribute<LU_ModelType>      BAU2050_25m_diff                (rdc_25m) := BAU/BAU2050_25m_diff;
 
	container Huidig
	{
		attribute<HDB_NVKKlasse> Read_Huidig_2_5m                (rdc_2_5m) 
		: StorageName = "%RSo_DataDir%/Grondgebruik/NVK/nvk_huidig-20201126-100144_newval.tif"
		, StorageType = "gdal.grid"
		, LazyCalculated = "true"
		, Source = "inez woltjer - WUR 20220216 SURFfilesender: basiskaarten NatuurVerkenning scenarios";
		///
		attribute<NVK_LHM_Tabel> Huidig_2_5m_NVK_LHM_Tabel       (rdc_2_5m) := invert(NVK_LHM_Tabel/HDB_NVKKlasse_rel)[Read_Huidig_2_5m];
		attribute<LHMType>       Huidig_2_5m_LHMType             (rdc_2_5m) := rjoin(Huidig_2_5m_NVK_LHM_Tabel, id(NVK_LHM_Tabel), NVK_LHM_Tabel/LHMType_rel);
		attribute<LU_ModelType>  Huidig_2_5m_LU_ModelType        (rdc_2_5m) := LHMType/LU_ModelType_rel[Huidig_2_5m_LHMType];
		///
		attribute<LU_ModelType>  Write_Huidig_25m_LU_ModelType    (rdc_25m) := modus(Huidig_2_5m_LU_ModelType, rdc_2_5m/rdc_25m_rel), StorageName = "%LocalDataProjDir%/NVK/nvk_huidig_25m_LU_ModelType.tif", StorageType = "gdalwrite.grid";
		attribute<LU_ModelType>  Huidig_25m_LU_ModelType         (rdc_25m) : StorageName = "%LocalDataProjDir%/NVK/nvk_huidig_25m_LU_ModelType.tif",	StorageType = "gdal.grid", 	StorageReadOnly = "true";
		///
		attribute<LU_ModelType>  Huidig_100m_LU_ModelType        (rdc_100m) := modus(Huidig_25m_LU_ModelType, rdc_25m/rdc_100m_rel);
	}

	container HDB
	{
		attribute<HDB_NVKKlasse> Read_HDB2050_2_5m               (rdc_2_5m) 
		: StorageName = "%RSo_DataDir%/Grondgebruik/NVK/hdb2050_run2021_t9xlsx_20220124-191401_newval.tif"
		, StorageType = "gdal.grid"
		, LazyCalculated = "true"
		, Source = "Rogier Pouwels - WUR 20230704 SURFfilesender: NatuurVerkenning scenarios";
		///
		attribute<NVK_LHM_Tabel> HDB2050_2_5m_NVK_LHM_Tabel      (rdc_2_5m) := invert(NVK_LHM_Tabel/HDB_NVKKlasse_rel)[Read_HDB2050_2_5m];
		attribute<LHMType>       HDB2050_2_5m_LHMType            (rdc_2_5m) := rjoin(HDB2050_2_5m_NVK_LHM_Tabel, id(NVK_LHM_Tabel), NVK_LHM_Tabel/LHMType_rel);
		attribute<LU_ModelType>  HDB2050_2_5m_LU_ModelType       (rdc_2_5m) := LHMType/LU_ModelType_rel[HDB2050_2_5m_LHMType];
		///
		attribute<LU_ModelType>  Write_HDB2050_25m_LU_ModelType   (rdc_25m) := modus(HDB2050_2_5m_LU_ModelType, rdc_2_5m/rdc_25m_rel), StorageName = "%LocalDataProjDir%/NVK/nvk_hdb_25m_LU_ModelType.tif", StorageType = "gdalwrite.grid";
		attribute<LU_ModelType>  HDB2050_25m_LU_ModelType        (rdc_25m) : StorageName = "%LocalDataProjDir%/NVK/nvk_hdb_25m_LU_ModelType.tif",	StorageType = "gdal.grid", 	StorageReadOnly = "true";
		///
		attribute<LU_ModelType>  HDB2050_100m_LU_ModelType       (rdc_100m) := modus(HDB2050_25m_LU_ModelType, rdc_25m/rdc_100m_rel);
		///
		attribute<LU_ModelType>  HDB2050_25m_diff                 (rdc_25m) := HDB2050_25m_LU_ModelType != Huidig/Huidig_25m_LU_ModelType ? HDB2050_25m_LU_ModelType : (0b/0b)[LU_ModelType];
		attribute<uint8>         count_diff_25m_cellen           (rdc_100m) := sum_uint8(IsDefined(HDB2050_25m_diff), rdc_25m/rdc_100m_rel);
	}

	container BAU
	{
		attribute<uint32> BaUdiff_BT (rdc_25m)
		:	StorageName     = "%RSo_DataDir%/Grondgebruik/NVK/BAU/20230901_Sandy_van_Tol/BaUdiff_BT.tif"
		,	Source          = "Sandy van Tol - NLG - PBL 20220530 Y:\Kennisbasis\natuur\data_natuur\LULUCF_KEV\FGDB\LULUCF_natuur.gdb BaUdiff_BT conversie naar tif lzw in ArcGIS 10.8.1"
		,	url             = "%RSo_DataDir%/Grondgebruik/LULUCF/220421_Nat25-KEV.pdf"
		, LazyCalculated = "true"
		,	StorageType     = "gdal.grid"
		,	StorageReadOnly = "True"
		{
			attribute<uint8>                 data_org          (rdc_25m);
			attribute<string>                BT_naam           (rdc_25m) := rjoin(data_org, uint8(BAU_Diff_K/GIS_BaUdiff_BT/VALUE), BAU_Diff_K/GIS_BaUdiff_BT/naam);
			attribute<NVK_LHM_Tabel>         NVK_LHM_Tabel_rel (rdc_25m) := rlookup(trim(lowercase(BT_naam)), trim(lowercase(NVK_LHM_Tabel/Uitleg_NVK))); 
		}
		
		attribute<LHMType>       LHMType_rel                      (rdc_25m) := NVK_LHM_Tabel/LHMType_rel[BaUdiff_BT/NVK_LHM_Tabel_rel]; 
		attribute<LU_ModelType>  LU_ModelType_rel                 (rdc_25m) := LHMType/LU_ModelType_rel[LHMType_rel];
		///
		attribute<LU_ModelType>  BAU2050_100m_LU_ModelType       (rdc_100m) := modus(BAU2050_25m_diff, rdc_25m/rdc_100m_rel);
		///
		attribute<LU_ModelType>  BAU2050_25m_diff                 (rdc_25m) := LU_ModelType_rel;
		attribute<uint8>         count_diff_25m_cellen           (rdc_100m) := sum_uint8(IsDefined(BAU2050_25m_diff), rdc_25m/rdc_100m_rel);
		
		container BAU_Diff_K 
		: StorageName = "%RSo_DataDir%/Grondgebruik/NVK/BAU/20230901_Sandy_van_Tol/RC_BT2LUCF_bewerk_20230920.xlsx"
		, StorageType = "gdal.vect"
		, StorageReadOnly = "true"
		, SyncMode = "AllTables";
	}
}