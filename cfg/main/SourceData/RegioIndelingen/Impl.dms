container Impl
: Descr = "Inlezen van verschillende regio indelingen die gebruikt worden binnen de RuimteScanner."
{
	unit<UInt32> Read_GemeenteRES_2018_gdb
	:	StorageName     = "%RSo_DataDir%/RegioIndelingen/Gemeentegrenzen_2018_A1_PBL_RES.gdb"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	{
		attribute<rdc_meter>          geometry (poly);
		attribute<CBS/Y2018/gemeente> gemeente_rel := rlookup(uint32(gemcode), CBS/Y2018/gemeente/gem_nr);
		// attribute<RESRegio>           RESregio_rel := rlookup(RESnaam, RESRegio/name);
	}
	
	unit<UInt32> Read_NVM_2011_COROP2022Begrenzing_shp
	:	StorageName     = "%RSo_DataDir%/RegioIndelingen/NVM_2011_COROP2022Begrenzing.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	, 	SyncMode        = "None"
	, 	Source          = "ArcGIS 10.8.1 bewerking 15 april 2024 Y:\Project\M500567_Gesprekstools\M500567_RuimteScanner\BronData\NVM_COROP"
						  "Stap 1: feature to point LMS_2011_versie_2012_12_13.shp inside"
						  "Stap 2: stap2_SJ_NVM_LMS_2011_versie_2012_12_13_feature_to_point_centroid.shp Spatial Join nvm_2011.shp"
						  "Stap 3: join --> stap3_LMS_2011_versie_2012_12_13_join_stap2_fid_fidnvm.shp"
						  "Stap 4: stap4_dissolve_nvmregio.shp dissolve op nvmregio"
						  "Stap 5: handmatig toekennen LMS-subzones aan een nvm-regio binnen een COROP-begrenzing"
						  "Stap 6: dissolve subzones met de nieuwe toekenning"
						  "Stap 7: dissolve met hulp=1, 1 grote polygoon"
						  "Stap 8: erase nvm_2011.shp met stap7"
						  "Stap 9: merge stap 8 en stap 6"
						  "Stap 9: dissolve stap 8 met nvmregio" 
						  "Stap 10: Multipart to Single Part stap 9" 
						  "Stap 11: Samenvoegen polygonen o.a. bij IJmuiden, Noord-Noord-Holland kust" 
						  "Stap 12: dissolve stap 11 met nvmregio" 
	{
		attribute<rdc_meter>    geometry (poly);
		attribute<String>       nvmnaam;
		attribute<String>       name  := AsItemName(nvmnaam);
		
		unit<UInt32> ZonderBuitengebied := select_with_org_rel(id(.) > 0)
		, Descr = "Selectie van alle NVM 2011 COROP gebieden zonder het buitengebied (id = 0)"
		{
			attribute<rdc_meter>    geometry (poly)               := ../geometry[org_rel];
			attribute<String>       name                          := ../name[org_rel];
			attribute<String>       Label                         := name;
			attribute<.>            per_rdc_100m (rdc_100m)       := poly2grid(geometry, rdc_100m);
			attribute<.>            per_rdc_25m   (rdc_25m)       := poly2grid(geometry, rdc_25m);
			attribute<String>       NVM_2011_2_name               := lowercase(name) == lowercase('Almere') ? 'Amsterdam' : name;
			attribute<NVM_2011_2>   NVM_2011_2_rel                := rlookup(lowercase(NVM_2011_2_name), lowercase(NVM_2011_2/name));
			
			unit<UInt32> NVM_2011_2 := unique(NVM_2011_2_name)
			{
				attribute<String>    name := values;
				attribute<String>    Label := name, DialogType = "LabelText";
				attribute<rdc_meter> geometry  (polygon) := geos_union_polygon(../geometry, ../NVM_2011_2_rel);
			}
		}
	}

	unit<UInt32> Read_LMSSubzone_shp
	:	StorageName     = "%RSo_DataDir%/RegioIndelingen/LMS_2011_versie_2012_12_13.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	KeepData        = "True"
	,	Descr           = "LMS subzones 2011, regio indelingen gebruikt in het Landelijk Model Systeem. TIGRIS claims worden hierin aangeleverd."
	{
		attribute<rdc_meter_base> geometry(poly);
		attribute<rdc_meter_base> geometry_base (poly) := geometry;
		attribute<Int32>     ZONE_id;
		attribute<Int32>     GEM2011;
		attribute<UInt32>    LMSSUB;
		attribute<UInt32>    LMSSUB2004;
		attribute<UInt8>     LAND;
	}
	
	unit<UInt32> Read_Grootstedelijke_agglomeraties_shp
	:	StorageName     = "%RSo_DataDir%/RegioIndelingen/CBS_grootstedelijke_agglomeratie_2012.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	Descr           = "Inlezen grootstedelijke agglomeraties van CBS uit 2012."
	{
		attribute<rdc_meter>  geometry  (poly);
		attribute<String>     Label := statnaam;
	}
	
	unit<UInt32> Read_Bevolkingskern_2011_shp
	:	StorageName     = "%RSo_DataDir%/RegioIndelingen/bevolkingskern_2011.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	Descr           = "Inlezen van de bevolkingskernen 2011, met bijbehorende populatie aantallen."
	{
		attribute<rdc_meter> geometry (poly);
		attribute<Float64>   Populatie        := float64(Read_Bevolkingskern_2011_shp/BEV11TOT);
	}
}