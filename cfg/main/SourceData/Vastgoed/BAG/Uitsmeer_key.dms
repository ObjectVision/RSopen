container Uitsmeer_key
: Descr = "Verdeling van panden over 5m gridcellen, en daarmee over AdminDomain en AllocDomain. Dit is nodig om de uitsmeerberekeningen van woningen en werkplekken te kunnen doen."
{
	unit<UInt32> pand_domain := ..
	{
		attribute<Bool>                      IsPand_niet_in_grid_pand_tabel     := RasterCount_per_pand_raw == 0, Descr = "als een pand niet in de tabel zit, dan wordt deze niet vergrid via poly2allgrids. In dat geval moeten we deze handmatig toevoegen.";
		attribute<UInt32>                    RasterCount_per_pand_raw           := pcount(pand_grid_5m_tabel_zonder_missers/Pand_rel), Descr = "aantal 5m gridcellen waar een pand aan bijdraagt (zonder de panden die niet in de tabel zitten)";
		attribute<Float32>                   RasterCount_per_pand               := pcount(pand_grid_5m_tabel/Pand_rel)[float32], Descr = "aantal 5m gridcellen waar een pand aan bijdraagt (inclusief de panden die niet in de tabel zaten)";
	}
	
	unit<UInt32> pand_grid_5m_tabel_zonder_missers := poly2allgrids(pand_domain/geometry, rdc_5m) //fka grid_centroid_in_all_panden_rel
	, Descr = "vergrid alle panden naar een 5m grid" 
	{
		attribute<rdc_5m>                    rdc_5m_rel                         := grid_rel;
		attribute<pand_domain>               Pand_rel                           := polygon_rel;
		
		attribute<Float32> ToedelingPand  := 1f / float32(pand_domain/RasterCount_per_pand_raw)[Pand_rel]
		{
			attribute<Float32>               per_pand             (pand_domain) := sum(., Pand_rel);
			attribute<Float32>               per_5m                    (rdc_5m) := sum(., rdc_5m_rel);
		}
	}
	
	unit<UInt32> Panden_niet_in_pand_grid_5m_tabel := select_with_org_rel(pand_domain/IsPand_niet_in_grid_pand_tabel) //Panden_niet_in_grid_pand_tabel
	, Descr = "als een pand niet matched aan een linkerbovenhoek/centerpoint van een cel, dan wordt deze niet vergrid via poly2allgrids. In dat geval moeten we deze handmatig toevoegen."
	{
		attribute<rdc_5m>                    rdc_5m_rel                         := centroid_or_mid(pand_domain/geometry[Pand_rel])[rdc_5m];
		attribute<pand_domain>               Pand_rel                           := org_rel;
		
		attribute<Float32> ToedelingPand  := const(1f, .);
	}
	
	
	unit<UInt32> pand_grid_5m_tabel := union_unit(pand_grid_5m_tabel_zonder_missers, Panden_niet_in_pand_grid_5m_tabel) //fka grid_centroid_in_all_panden_rel_unioned
	, Descr = "append panden die niet via poly2allgrids werden vergrid."
	{
		attribute<rdc_5m>                    rdc_5m_rel                         := union_data(., pand_grid_5m_tabel_zonder_missers/rdc_5m_rel   , Panden_niet_in_pand_grid_5m_tabel/rdc_5m_rel), Descr = "bepaal de 5m gridcel waarin de 5m gridcel ligt";
		attribute<pand_domain>               Pand_rel                           := union_data(., pand_grid_5m_tabel_zonder_missers/Pand_rel     , Panden_niet_in_pand_grid_5m_tabel/Pand_rel), Descr = "bepaal het pand dat bij de 5m gridcel hoort";
		attribute<Float32>                   ToedelingPand                      := union_data(., pand_grid_5m_tabel_zonder_missers/ToedelingPand, Panden_niet_in_pand_grid_5m_tabel/ToedelingPand), Descr = "bepaal de toedeling van het pand aan de 5m gridcel";
		
		attribute<AdminDomain>               AdminDomain_rel                    := rdc_5m_rel[AdminDomain], Descr = "bepaal de AdminDomain cel waarin de 5m gridcel ligt";
		attribute<AdminDomain/id_key>        AdminDomain_id_rel                 := value(uint32(PointRow(AdminDomain_rel)) * AdminDomain/nr_cols + uint32(PointCol(AdminDomain_rel)), AdminDomain/id_key), Descr = "bepaal de id van de AdminDomain cel waarin de 5m gridcel ligt";
		
		attribute<AllocDomain>               AllocDomain_rel                    := rdc_5m_rel[AllocDomain], Descr = "bepaal de AllocDomain cel waarin de 5m gridcel ligt";
		attribute<AllocDomain/id_key>        AllocDomain_id_rel                 := value(uint32(PointRow(AllocDomain_rel)) * AllocDomain/nr_cols + uint32(PointCol(AllocDomain_rel)), AllocDomain/id_key), Descr = "bepaal de id van de AllocDomain cel waarin de 5m gridcel ligt";
		
		//om te bepalen hoeveel kleine blokjes (5m) in grote blokken (25m) liggen moeten we eerst de set van pand-groteblok-combinaties bepalen.
		unit<uint64> pandAdmin_keyset := combine_unit_uint64(pand_domain, AdminDomain), Descr = "genereer een unieke keyset van pand x Admindomain";
		unit<uint64> pandAlloc_keyset := combine_unit_uint64(pand_domain, AllocDomain), Descr = "genereer een unieke keyset van pand x Allocdomain";
		
		attribute<pandAdmin_keyset>          pandAdmin_keyset_rel               := combine_Data(pandAdmin_keyset, Pand_rel, AdminDomain_id_rel), Descr = "genereer de pand x Admindomain keyset door de combine van pand_rel en Admindomain_id_rel";
		attribute<AdminDomain_in_all_panden> AdminDomain_in_all_panden_rel      := rlookup(pandAdmin_keyset_rel, AdminDomain_in_all_panden/pandAdmin_keyset_rel), Descr = "bepaal voor elke rij in de pand_grid_5m_tabel tot welke unieke combinatie van pand x Admindomain deze rij behoort";
		
		attribute<pandAlloc_keyset>          pandAlloc_keyset_rel               := combine_Data(pandAlloc_keyset, Pand_rel, AllocDomain_id_rel), Descr = "genereer de pand x Allocdomain keyset door de combine van pand_rel en Allocdomain_id_rel";
		attribute<AllocDomain_in_all_panden> AllocDomain_in_all_panden_rel      := rlookup(pandAlloc_keyset_rel, AllocDomain_in_all_panden/pandAlloc_keyset_rel), Descr = "bepaal voor elke rij in de pand_grid_5m_tabel tot welke unieke combinatie van pand x Allocdomain deze rij behoort";
	}
	
	unit<UInt32> AdminDomain_in_all_panden := unique(pand_grid_5m_tabel/pandAdmin_keyset_rel)
	, Descr = "per pand, per gridcel, de fractie van dat pand dat in die gridcel ligt"
	{
		attribute<pand_domain>                         Pand_rel                            := value(values / uint64(#AdminDomain), pand_domain), Descr = "bepaal het pand dat bij de 5m gridcel hoort";
		attribute<uint64>                              pand_bag_nr                         := pand_domain/pand_bag_nr[Pand_rel], Descr = "bepaal het bag nummer van het pand dat bij de 5m gridcel hoort";
		attribute<AdminDomain/id_key>                  AdminDomain_id_rel                  := value(values % uint64(#AdminDomain), AdminDomain/id_key), Descr = "bepaal de id van de AdminDomain cel waarin de 5m gridcel ligt";
		
		// truc om na de unique de bijborende AdminDomain_rel op te zoeken.
		attribute<AdminDomain>                         AdminDomain_rel                     := point_yx(int32(AdminDomain_id_rel / AdminDomain/nr_cols), int32(AdminDomain_id_rel % AdminDomain/nr_cols), AdminDomain), Descr = "bepaal de AdminDomain cel waarin de 5m gridcel ligt";
		attribute<CompactedAdminDomain>                CompactedAdminDomain_rel            := CompactedAdminDomain/Per_AdminDomain[AdminDomain_rel], Descr = "bepaal de CompactedAdminDomain cel waarin de AdminDomain cel ligt";
		attribute<pand_grid_5m_tabel/pandAdmin_keyset> pandAdmin_keyset_rel                := combine_Data(pand_grid_5m_tabel/pandAdmin_keyset, Pand_rel, AdminDomain_id_rel), Descr = "genereer de pand x Admindomain keyset door de combine van pand_rel en Admindomain_id_rel";
		
		attribute<Float32>                             ToedelingPand                       := sum(pand_grid_5m_tabel/ToedelingPand, pand_grid_5m_tabel/AdminDomain_in_all_panden_rel)
		, Descr = "bepaal de toedeling van het pand aan de 5m gridcel, gesommeerd over alle 5m gridcellen die in dezelfde AdminDomain cel liggen"
		{
			attribute<Float32>              per_pand              (pand_domain) := sum(., Pand_rel), Descr = "sommeer de toedeling van het pand aan de 5m gridcel over alle 5m gridcellen die bij dat pand horen";
			attribute<Float32>              Per_AD                (AdminDomain) := sum(., AdminDomain_id_rel)[AdminDomain/id_rel], Descr = "sommeer de toedeling van het pand aan de 5m gridcel over alle 5m gridcellen die in dezelfde AdminDomain cel liggen";
		}
		
		attribute<Bool>                                met_vbo_selection_condition                 := pand_domain/met_vbo_selection_condition[Pand_rel], Descr = "bepaal of het pand voldoet aan de vbo selectie (geen vbo-loos pand met woonfunctie)";
		attribute<Bool>                                Omit_VBOlozePanden_metWoonfunctie_condition := NOT(pand_domain/BepalingGebruiksdoelInOmgeving/VBOloosPand_metWoonFunctie)[Pand_rel], Descr = "bepaal of het pand een vbo-loos pand met woonfunctie is (en dus niet mee moet doen in de uitsmeerberekeningen)";
	}
	
	unit<UInt32> AllocDomain_in_all_panden := unique(pand_grid_5m_tabel/pandAlloc_keyset_rel)
	, Descr = "per pand, per gridcel, de fractie van dat pand dat in die gridcel ligt"
	{
		attribute<pand_domain>                         Pand_rel                            := value(values / uint64(#AllocDomain), pand_domain), Descr = "bepaal het pand dat bij de 5m gridcel hoort";
		attribute<uint64>                              pand_bag_nr                         := pand_domain/pand_bag_nr[Pand_rel], Descr = "bepaal het bag nummer van het pand dat bij de 5m gridcel hoort";
		attribute<AllocDomain/id_key>                  AllocDomain_id_rel                  := value(values % uint64(#AllocDomain), AllocDomain/id_key), Descr = "bepaal de id van de AllocDomain cel waarin de 5m gridcel ligt";
		
		// truc om na de unique de bijborende AdminDomain_rel op te zoeken.
		attribute<AllocDomain>                         AllocDomain_rel                     := point_yx(int32(AllocDomain_id_rel / AllocDomain/nr_cols), int32(AllocDomain_id_rel % AllocDomain/nr_cols), AllocDomain), Descr = "bepaal de AllocDomain cel waarin de 5m gridcel ligt";
		attribute<pand_grid_5m_tabel/pandAlloc_keyset> pandAlloc_keyset_rel                := combine_Data(pand_grid_5m_tabel/pandAlloc_keyset, Pand_rel, AllocDomain_id_rel), Descr = "genereer de pand x Allocdomain keyset door de combine van pand_rel en Allocdomain_id_rel";
		
		attribute<Float32>                             ToedelingPand                       := sum(pand_grid_5m_tabel/ToedelingPand, pand_grid_5m_tabel/AllocDomain_in_all_panden_rel), Descr = "bepaal de toedeling van het pand aan de 5m gridcel, gesommeerd over alle 5m gridcellen die in dezelfde AllocDomain cel liggen"
		{
			attribute<Float32>              per_pand              (pand_domain) := sum(., Pand_rel), Descr = "sommeer de toedeling van het pand aan de 5m gridcel over alle 5m gridcellen die bij dat pand horen";
			attribute<Float32>              Per_AD                (AllocDomain) := sum(., AllocDomain_id_rel)[AllocDomain/id_rel], Descr = "sommeer de toedeling van het pand aan de 5m gridcel over alle 5m gridcellen die in dezelfde AllocDomain cel liggen";
		}
		
		attribute<Bool>                                met_vbo_selection_condition := pand_domain/met_vbo_selection_condition[Pand_rel], Descr = "bepaal of het pand voldoet aan de vbo selectie (geen vbo-loos pand met woonfunctie)";
	}
}
