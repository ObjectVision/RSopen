unit<uint32> VBO := select_with_attr_by_cond(src, src/VBO_selection)
{
	parameter<meter2>              total_sum_opp_trunc          := sum(oppervlakte_trunc) * 1[verblijfsobject];
	attribute<adres_domein>        adres_rel                    := rlookup(nummeraanduiding_id, adres_domein/nummeraanduiding_id);
	attribute<m2PandFootprint>     footprint                    := pand_domein/footprint[pand_rel];
	attribute<pand_met_vbo_domein> pand_vbo_rel                 := rlookup(pand_id, pand_met_vbo_domein/identificatie); //pand_rel
	attribute<uint32>              count_vbo_alle (AdminDomain) := count(geometry, AdminDomain_rel);
	unit<uint32>                   Verblijfsrecreatie           := SourceData/Grondgebruik/VerblijfsrecreatieBuffer;
	attribute<bool>                LigtInCBSVerblijfsrecreatie  := IsDefined(point_in_polygon(geometry, Verblijfsrecreatie/geometry_rd));
	
	container gebruiksdoelen := collect_attr_by_cond(src/gebruiksdoelen, ., src/VBO_selection)
	{
		container gebruiksdoelen_str :=
			for_each_nedv(
				vbo_gebruiksdoel/name
				,'gebruiksdoelen/'+vbo_gebruiksdoel/name+' ? '+quote(vbo_gebruiksdoel/name)+' : '''''
				, VBO
				, string
			);
		
		attribute<string> doelen_str    (..) := =AsList('gebruiksdoelen_str/'+vbo_gebruiksdoel/name,'+'',''+');
		attribute<string> doelen_str0   (..) := replace(doelen_str, ',,', ',', ',,,', ',', ',,,', ',', ',,', ',');
		attribute<string> doelen_str00  (..) := substr(doelen_str0, strlen(doelen_str0) - 1, strlen(doelen_str0)) == ',' ? substr(doelen_str0, 0, strlen(doelen_str0) - 1) : doelen_str0; 
		attribute<string> doelen_str000 (..) := substr(doelen_str00, 0,1) == ',' ? substr(doelen_str00, 1, strlen(doelen_str00)) : doelen_str00; 
		
		attribute<uq_doelencombis> uq_rel (..) := rlookup(doelen_str000, uq_doelencombis/values);
		
		unit<uint32> uq_doelencombis := unique(doelen_str000)
		{
			attribute<string> label  := values, DialogType = "LabelText";
			attribute<uint32> count  := count(uq_rel, uq_rel);
			attribute<int32>  vbo_opp := sum(oppervlakte_trunc, uq_rel)[int32];
		}
	}
	
	container gebruiksdoelen_cbs := collect_attr_by_cond(src/gebruiksdoelen_cbs, ., src/VBO_selection);
	
	container LigtInCBSVerblijfsrecreatie_perdoel := 
		for_each_nedv(
			vbo_gebruiksdoel/name
			, 'LigtInCBSVerblijfsrecreatie && gebruiksdoelen/'+vbo_gebruiksdoel/name
			,VBO
			, bool
		)
	{
		parameter<uint32> Sum  := ='add('+AsList('sum('+vbo_gebruiksdoel/name+'[uint32])',',')+')';
	}
}