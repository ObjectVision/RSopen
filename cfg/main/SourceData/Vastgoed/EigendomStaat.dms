// code van: https://github.com/RuudvandenWijngaart/VestaDV/blob/SAWEC-WP5/model/RuimtelijkeData/EigendomStaat.dms  (Hestia) 15 oktober 2021 a1ef8a9cbd37bc189e5302ba129b020f3313e223
// aanpassingen aan Units en verwijzing naar BAG, opnemen metaData en met gdal2.vec inlezen
// wegsterren niet relevante code maar nog wel laten staan

container EigendomStaat: url= "%RSo_DataDir%/Vastgoed/EigendomStaat/20210915_documentatie_eigendom.pdf"
{
	// inlezen puntenbestand Woningcorporaties
	unit<uint32> Corporatiebezit_Data
	:   StorageName     = "%RSo_DataDir%/Vastgoed/EigendomStaat/CorporatieBezit.shp"
	,   StorageType     = "gdal.vect"
	,	DialogType      = "Map"
	,	StorageReadOnly = "True"
	,	Source          = "https://pbl.sliksvn.com/vesta/SD51/EigendomStaat revision 640"
	{
		attribute<string> Label := 'C_'+string(OBJECTid), DialogType = "LabelText";
		attribute<rdc_meter> geometry;
		attribute<Eigendom> Eigendom_rel := const(Eigendom/V/WoonCorp,.); //eigendomstatus als attribuut
	}
	
	// inlezen puntenbestand particuliere huur
	unit<uint32> Particulierehuur_Data
	:   StorageName     = "%RSo_DataDir%/Vastgoed/EigendomStaat/ParticuliereHuur.shp"
	,   StorageType     = "gdal.vect"
	,	DialogType      = "Map"
	,	StorageReadOnly = "True"
	,	Source          = "https://pbl.sliksvn.com/vesta/SD51/EigendomStaat revision 640"
	{
		attribute<string>                            Label := 'H_'+string(OBJECTid);
		attribute<rdc_meter>                         geometry;
		attribute<Eigendom> Eigendom_rel := const(Eigendom/V/PartHuur,.); //eigendomstatus als attribuut
	}
	
	unit<uint32> EigendomStaat_Data := union_unit(Corporatiebezit_Data, Particulierehuur_Data)
	, Descr = "samenvoegen bovenstaande puntenbestanden tot gezamenlijke entiteit"
	{
		attribute<rdc_meter>                         geometry     := union_data(., Corporatiebezit_Data/geometry, Particulierehuur_Data/geometry);
		attribute<string>                            Label        := union_data(., Corporatiebezit_Data/Label, Particulierehuur_Data/Label);
		attribute<Eigendom> Eigendom_rel := union_data(., Corporatiebezit_Data/Eigendom_rel, Particulierehuur_Data/Eigendom_rel);
		attribute<pand>                              Pand_rel     := point_in_polygon(geometry, pand/geometry), Descr = "ruimtelijke matching om bijbehorend BAG pand per punt te bepalen";
	}
	
	unit<uint32> pand := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand'
	, Descr = "afgeleide attributen vastgesteld per BAG pand"
	{
		attribute<rdc_meter> geometry                (poly) := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/geometry';
		attribute<uint32>    count_vbo_pand_woon            := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/counts/woon_VBOs';
		attribute<meter2>    Footprint                      := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Footprint';
		attribute<meter2>    VBOopp_inPand_Eengezins        := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP2/Eengezins';
		attribute<meter2>    VBOopp_inPand_Meergezins       := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP2/Meergezins';
		attribute<meter2>    VBOopp_inPand_Vrijstaand       := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP4/Vrijstaand';
		attribute<meter2>    VBOopp_inPand_Twee_onder_1_kap := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP4/Twee_onder_1_kap';
		attribute<meter2>    VBOopp_inPand_Rijtjeswoning    := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP4/Rijtjeswoning';
		attribute<meter2>    VBOopp_inPand_Appartement      := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/Oppervlaktes/VBOopp_perWP4/Appartement';
		attribute<WP2>       WP2_rel                        := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/WP2_rel';
		attribute<WP4>       WP4_rel                        := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand/WP4_rel';
		attribute<Eigendom>  Eigendom_rel                   := MakeDefined(Modus(EigendomStaat_Data/Eigendom_rel, EigendomStaat_Data/Pand_rel), Eigendom/V/Koop), Descr = "eigendomstatus wordt afgeleid van gekoppelde puntattributen, uitgaande van meest voorkomende status. Indien onbekend wordt koop verondersteld.";
		attribute<bool>      Is_VS_pand                     := Eigendom_rel != Eigendom/V/WoonCorp;
	}
}