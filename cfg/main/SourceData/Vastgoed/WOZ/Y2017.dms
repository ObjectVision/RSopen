container Y2017 // Bron: CBS. Peildatum 1 jan 2016 (bestand is 1 jan 2017)
{
	container Gemeente := T('Gem'  , 'GM', RegioIndelingen/CBS/Y2016/Gemeente);
	container Wijk     := T('Wijk' , 'WK', RegioIndelingen/CBS/Y2016/wijk);
	container Buurt    := T('Buurt', 'BU', RegioIndelingen/CBS/Y2016/buurt);
	
	container per_WOZBb :=
		for_each_nedv(
			WOZBBG/name,
			'MakeDefined(MakeDefined(Buurt/domain/Nul2Undefined/'+WOZBBG/name+', Wijk/domain/Nul2Undefined/'+WOZBBG/name+'), Gemeente/domain/Nul2Undefined/'+WOZBBG/name+')',
			AdminDomain, Eur_m2
		)
	{
		attribute<Eur_m2> max_elem (AdminDomain) := = 'max_elem('+asItemList(WOZBBG/name)+')';
	}
	
	attribute<Eur_m2> Gegeven_BBG (AdminDomain) := =
		'merge('
			'value(CBS2010Klasse/WOZBBG_rel[SourceData/Grondgebruik/BBG/dominant/Per_AdminDomain/Y2017], UInt16),'
			'Eur_m2,'+
			asItemList('per_WOZBb/'+WOZBBG/name)+
		')';
	
	container BAG := SourceData/Vastgoed/BAG/PerJaar/Y2018;
	
	attribute<Eur> Make_Per_vob (BAG/VBO) := Gegeven_BBG[BAG/VBO/AdminDomain_rel] * (BAG/VBO/oppervlakte_trunc * 1[verblijfsobject]), StorageName = '%RSo_DataDir%/Vastgoed/WOZ/WOZ2017.fss';
	attribute<Eur> Per_vob (BAG/VBO) : StorageName = '%RSo_DataDir%/Vastgoed/WOZ/WOZ2017.fss';
	attribute<Eur> Mean_AdminDomain (AdminDomain) := mean(Per_vob, BAG/VBO/AdminDomain_rel);
	attribute<Classifications/Vastgoed/WOZ> PerWOZKlasse (AdminDomain) := Classify(Mean_AdminDomain, Classifications/Vastgoed/WOZ/ClassBreaks);
	
	container Niet_Woningen
	{
		attribute<Eur> Per_vob        (BAG/VBO/Per_Gebruiksdoel/niet_woon/select) := Gegeven_BBG[BAG/VBO/Per_Gebruiksdoel/niet_woon/select/AdminDomain_rel] * (BAG/VBO/Per_Gebruiksdoel/niet_woon/select/oppervlakte_trunc * 1[verblijfsobject]);
		attribute<Eur> Per_AdminDomain (AdminDomain) := sum(Per_vob, BAG/VBO/Per_Gebruiksdoel/niet_woon/select/AdminDomain_rel);
		attribute<Eur> Per_AllocDomain (AllocDomain) := sum(Per_vob, BAG/VBO/Per_Gebruiksdoel/niet_woon/select/AllocDomain_rel);
	}
	
	Template T
	{
		//
		parameter<String> SrcRef;
		parameter<String> CodeRef;
		unit<UInt32> RegionK;
		//
		
		unit<UInt32> Domain
		:  StorageName = "= '%RSo_DataDir%/Vastgoed/WOZ/190215_'+ SrcRef +'_woz.csv'"
		,  StorageType = "gdal.vect"
		,  StorageReadOnly = "True"
		{
			attribute<String> r2017;
			attribute<String> bg2015_groep;
			attribute<String> wozm2_mean;
			attribute<String> wozm2_sd;
			attribute<String> aantalobjecten;
			
			attribute<String> R_CODE := CodeRef+r2017;
			
			// attribute<RegionK> Region_rel := = 'rjoin(R_CODE, RegionK/'+CodeRef+'_CODE, id(RegionK))';
			attribute<RegionK> Region_rel := = 'rjoin(R_CODE, RegionK/statcode, id(RegionK))';
			attribute<WOZBBG> WOZBBG_rel := rjoin(uint8(bg2015_groep), WOZBBG/EK_rel, id(WOZBBG));
			
			attribute<Eur_m2> mean := wozm2_mean[Eur_m2];
			attribute<Eur_m2> sd := wozm2_sd[Eur_m2];
			attribute<Int32> n := aantalobjecten[int32];
			
			container per_WOZBb :=
				for_each_nedv(
					WOZBBG/name,
					'sum('
						'(Domain/mean + Domain/sd) * '+string(WOZBBG/growth_factor)+'[float32] * float32(Domain/WOZBBG_rel == '+string(id(WOZBBG))+'[WOZBBG]),'
						'Domain/Region_rel'
					')[RegionK/Per_AdminDomain]',
					Geography/AdminDomain, Eur_m2
				)
			{
				attribute<Eur_m2> max_elem (AdminDomain) := = 'max_elem('+asItemList(WOZBBG/name)+')';
			}
			
			container Nul2Undefined :=
				for_each_nedv(
					WOZBBG/name,
					'per_WOZBb/'+WOZBBG/name+' == 0[Eur_m2] ? (0/0)[Eur_m2] : per_WOZBb/'+WOZBBG/name,
					Geography/AdminDomain, Eur_m2
				);
		}
	}
}