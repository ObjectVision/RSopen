container BAG : using = "Classifications/Vastgoed;Classifications"
{
	unit<uint8> WP5 := Classifications/Vastgoed/WP5;
	unit<uint8> WP4 := Classifications/Vastgoed/WP4;
	unit<uint8> WP3 := Classifications/Vastgoed/WP3;
	unit<uint8> WP2 := Classifications/Vastgoed/WP2;
	unit<uint8> pand_status := Classifications/Vastgoed/pand_status;
	unit<uint8> vbo_status := Classifications/Vastgoed/vbo_status;
	
	parameter<string> SnapshotDir2024 := '%RSo_DataDir%/Vastgoed/20240101_RS';
	
	unit<uint32> BAG_jaren := range(uint32, 2012, uint32(right(string(ModelParameters/BAG_RecentYear),4)) + 1)
	{
		attribute<uint32> Jaar           := id(.);
		attribute<string> name           := 'Y' + string(jaar);
		attribute<string> prev_Jaar_name := name[prev_jaar]; 
		attribute<uint32> prev_Jaar      := Jaar - 1;
		attribute<bool>   IsFirstYear    := jaar == first(Jaar);
		attribute<string> Label          := name, DialogType = "LabelText";
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		
		unit<uint32> minusFirstYear := select_with_org_rel(!IsFirstYear)
		{
			attribute<uint32> Jaar  := ../jaar[org_rel];
			attribute<string> name  := ../name[org_rel];
			attribute<string> Label := name, DialogType = "LabelText";
		}
	}
	
	container PerJaar := for_each_ne(BAG_jaren/name, 'PerJaar_T('+quote(string(BAG_jaren/jaar))+')');
	
	Template PerJaar_T
	{
		parameter<string> JaarStr;
		///
		attribute<bool> pand_selection_condition (VolledigeBAG/panden/pand) := VolledigeBAG/panden/pand/IsVoorraad 
															&& VolledigeBAG/panden/pand/Begindatum < int32(JaarStr+'0101') 
															&& VolledigeBAG/panden/pand/Einddatum >= int32(JaarStr+'0101');
		unit<uint32> pand := select_with_attr_by_cond(VolledigeBAG/panden/pand, pand_selection_condition)
		, Descr = "set met panden op 1 januari van elk jaar die voorraad zijn volgens de CBS definitie."
		{
			parameter<string> Selection_string      := 'Voorraad';
			
			//deze ontkoppeling is alleen voor debugging. Mocht je hier tegenaan lopen. Commentarieer de komende twee regels uit, en de derde aan. 
			// attribute<WP5>            WP5_rel                          := AfleidingPandType/results/WP5_rel, StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/BAG/'+Selection_string+'_'+JaarStr+'_'+/ModelParameters/StudyArea+'/WP5_rel.fss'";
			// attribute<WP5>            WP5_rel                          : StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/BAG/'+Selection_string+'_'+JaarStr+'_'+/ModelParameters/StudyArea+'/WP5_rel.fss'", StorageReadOnly = "TRUE";
			attribute<WP5>            WP5_rel                          := AfleidingPandType/results/WP5_rel;
			
			attribute<WP4>            WP4_rel                          := WP5/WP4_rel[WP5_rel];
			attribute<WP3>            WP3_rel                          := WP5/WP3_rel[WP5_rel];
			attribute<WP2>            WP2_rel                          := WP5/WP2_rel[WP5_rel];
			attribute<WP1>            WP1_rel                          := WP5/WP1_rel[WP5_rel];
			
			attribute<bool>           pand_selection_condition (VolledigeBAG/panden/pand) := ../pand_selection_condition;
			attribute<bool>           Is_VrijeSector                                      := rjoin(pand_bag_nr, SourceData/Vastgoed/EigendomStaat/pand/pand_bag_nr, SourceData/Vastgoed/EigendomStaat/pand/Is_VS_pand);
		
			attribute<../pand_hoogte> PHN_rel                          := rlookup(pand_bag_nr, ../pand_hoogte/pand_bag_nr);
			attribute<cm>             pand_hoogte                      := ../pand_hoogte/hoogte_cm[PHN_rel];
			attribute<cm>             pand_hoogte_maaiveld             := ../pand_hoogte/hoogte_maaiveld[PHN_rel];

			unit<uint32> ToedelingsMatrix := Uitsmeer_key/AdminDomain_in_all_panden;
			unit<uint32> ToedelingsMatrix_AllocDomain := Uitsmeer_key/AllocDomain_in_all_panden;
			#include<Uitsmeer_key.dms>

			container Counts
			{
				attribute<uint32> VBOs       (..) := pcount(vbo/Pand_rel);
				attribute<uint32> woon_VBOs  (..) := pcount(VBO/Per_gebruiksdoel/woon/select/Pand_rel);
				
				attribute<float32> Per_AllocDomain (AllocDomain) := sum(ToedelingsMatrix_AllocDomain/ToedelingPand, ToedelingsMatrix_AllocDomain/AllocDomain_rel);
				attribute<float32> Per_AdminDomain (AdminDomain) := sum(ToedelingsMatrix/ToedelingPand, ToedelingsMatrix/AdminDomain_rel);
				
				container Vbos_perGebruiksdoel :=
					for_each_nedv(
						vbo_gebruiksdoel_plus/name
						, 'sum_uint32(vbo/Impl/gebruiksdoelen_cbs/'+vbo_gebruiksdoel_plus/name+', vbo/Pand_rel)'
						, pand
						, uint32
					)
				{
					attribute<uint32> Totaal (pand) := ='add('+AsList(vbo_gebruiksdoel_plus/name,',')+')';
				}
			}
			
			container Oppervlaktes
			{
				attribute<m2_pand>         vbo_opp            (..) := sum(vbo/oppervlakte_trunc, vbo/Pand_rel) * 1[verblijfsobject] / 1[Units/PandUnit];
				attribute<m2_pand>         vbo_opp_niet_woon0 (..) := sum(vbo/Impl/gebruiksdoelen_cbs/niet_woon ? vbo/oppervlakte_trunc : 0[m2_vbo], vbo/Pand_rel) == 0[m2_vbo]
																		? (0/0)[m2_pand]
																		: sum(vbo/Impl/gebruiksdoelen_cbs/niet_woon ? vbo/oppervlakte_trunc : 0[m2_vbo], vbo/Pand_rel) * 1[verblijfsobject] < Footprint[meter2] 
																			? Footprint / 1[m2PandFootprint] * 1[m2_pand]
																			: sum(vbo/Impl/gebruiksdoelen_cbs/niet_woon ? vbo/oppervlakte_trunc : 0[m2_vbo], vbo/Pand_rel) * 1[verblijfsobject / Units/PandUnit];
				
				attribute<float32>         Ratio_vbo_opp_niet_woon_Footprint (..) := (vbo_opp_niet_woon0 / 1[m2_pand]) / (Footprint / 1[m2PandFootprint]);
				attribute<m2_pand>         vbo_opp_niet_woon                 (..) := Ratio_vbo_opp_niet_woon_Footprint >= 100f ? (0/0)[m2_pand] : vbo_opp_niet_woon0;
				
				
				container VBOopp_perWP2 :=
					for_each_nedv(
						WP2/name
						,'sum(vbo/WP2_rel == WP2/V/'+WP2/name+'[WP2] ? vbo/oppervlakte_trunc : 0[m2_vbo], vbo/Pand_rel) * 1[Units/verblijfsobject]'
						, pand
						, meter2
					), Descr = "wordt oa gebruikt in EigendomStaat tbv bepaling VS/SH verdeling.";
					
				container VBOopp_perWP4 :=
					for_each_nedv(
						WP4/name
						,'sum(vbo/WP4_rel == WP4/V/'+WP4/name+'[WP4] ? vbo/oppervlakte_trunc : 0[m2_vbo], vbo/Pand_rel) * 1[Units/verblijfsobject]'
						, pand
						, meter2
					), Descr = "wordt oa gebruikt in EigendomStaat tbv bepaling VS/SH verdeling.";
					
				container VBOopp_perGebruiksdoel :=
					for_each_nedv(
						vbo_gebruiksdoel_plus/name
						, 'sum(vbo/Impl/gebruiksdoelen_cbs/'+vbo_gebruiksdoel_plus/name+' ? vbo/oppervlakte_trunc : (0/0)[m2_Vbo], vbo/Pand_rel) * 1[verblijfsobject]'
						, pand
						, meter2
					), Descr = "per gebruiksdoel de vbo oppervlakte in een pand";
				
				container Footprint_perGebruiksdoel :=
					for_each_nedv(
						vbo_gebruiksdoel_plus/name
						, quote(vbo_gebruiksdoel_plus/name)+' == ''Logistiek'' && IsLogistiekFunctie 
							? Footprint 
							: '+quote(vbo_gebruiksdoel_plus/name)+' == ''Logistiek'' && NOT(IsLogistiekFunctie)
								? 0[m2PandFootprint]
								: IsLogistiekFunctie 
									? 0[m2PandFootprint] 
									: (VBOopp_perGebruiksdoel/'+vbo_gebruiksdoel_plus/name+' / (vbo_opp * 1[Units/PandUnit])) * Footprint'
						, pand
						, m2PandFootprint
					), Descr = "per gebruiksdoel de pand Footprint obv de share van vbo Oppervlaktes in het pand"
				{
					attribute<vbo_gebruiksdoel_plus> ArgMax    (pand) := ='!IsDefined(bijeenkomst) ? (0/0)[vbo_gebruiksdoel_plus] : ArgMax('+AsItemList(vbo_gebruiksdoel_plus/name)+')[vbo_gebruiksdoel_plus]';
				}
				
				container Fractie_VBOOpp_perGebruiksdoel :=
					for_each_nedv(
						vbo_gebruiksdoel_plus/name
						,'(sum(vbo/Impl/gebruiksdoelen_cbs/'+vbo_gebruiksdoel_plus/name+' ? vbo/oppervlakte_trunc : (0/0)[m2_Vbo], vbo/Pand_rel) * 1[verblijfsobject]) / (vbo_opp * 1[Units/PandUnit])'
						, pand
						, float32
					)
				{
					attribute<vbo_gebruiksdoel_plus> ArgMax (...) := ='ArgMax_alldefined('+AsItemList(vbo_gebruiksdoel_plus/name)+')[vbo_gebruiksdoel_plus]';
				}
				
			}
			
			attribute<bool> met_vbo_selection_condition := counts/VBOs > 0;
			unit<uint32>    met_vbo := select_with_attr_by_cond(., met_vbo_selection_condition)
			{
				container Counts
				{
					container Vbos_perGebruiksdoel      := collect_attr_by_cond(Pand/Counts/Vbos_perGebruiksdoel, met_vbo, pand/met_vbo_selection_condition);
					attribute<float32>  Monumentaal_Per_AllocDomain (AllocDomain) := sum(float32(Bouwjaar_trunc <= 1900[yr])[ToedelingsMatrix_AllocDomain/Pand_rel] * ToedelingsMatrix_AllocDomain/ToedelingPand, ToedelingsMatrix_AllocDomain/AllocDomain_rel);
					attribute<float32>  Per_AllocDomain             (AllocDomain) := sum(ToedelingsMatrix_AllocDomain/ToedelingPand, ToedelingsMatrix_AllocDomain/AllocDomain_rel);
				}
				container Oppervlaktes
				{
					container Footprint_perGebruiksdoel      := collect_attr_by_cond(Pand/Oppervlaktes/Footprint_perGebruiksdoel, met_vbo, pand/met_vbo_selection_condition);
					container VBOopp_perGebruiksdoel         := collect_attr_by_cond(Pand/Oppervlaktes/VBOopp_perGebruiksdoel, met_vbo, pand/met_vbo_selection_condition);
					container Fractie_VBOOpp_perGebruiksdoel := collect_attr_by_cond(Pand/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel, met_vbo, pand/met_vbo_selection_condition);
					attribute<m2_pand> vbo_opp (..) := collect_by_cond(met_vbo, pand/met_vbo_selection_condition, Pand/Oppervlaktes/vbo_opp);
				}
				
				unit<uint32> ToedelingsMatrix := select_with_org_rel(pand/ToedelingsMatrix/met_vbo_selection_condition)
				{
					attribute<uint64>      pand_bag_nr      := org_rel -> pand_bag_nr;
					attribute<AdminDomain> AdminDomain_rel  := org_rel -> AdminDomain_rel;
					attribute<float32>     ToedelingPand    := org_rel -> ToedelingPand;
					attribute<..>          Pand_rel         := rlookup(pand_bag_nr, ../pand_bag_nr);
				}
				unit<uint32> ToedelingsMatrix_AllocDomain := select_with_org_rel(pand/ToedelingsMatrix_AllocDomain/met_vbo_selection_condition)
				{
					attribute<uint64>      pand_bag_nr      := org_rel -> pand_bag_nr;
					attribute<AllocDomain> AllocDomain_rel  := org_rel -> AllocDomain_rel;
					attribute<float32>     ToedelingPand    := org_rel -> ToedelingPand;
					attribute<..>          Pand_rel         := rlookup(pand_bag_nr, ../pand_bag_nr);
				}
			}
			
			#include<AfleidingPandType.dms>
		}
		
		unit<uint32> vbo  := select_with_attr_by_cond(VolledigeBAG/VBOs/vbo
														, VolledigeBAG/VBOs/vbo/IsVoorraad 
															&& VolledigeBAG/VBOs/vbo/Begindatum < int32(JaarStr+'0101') 
															&& VolledigeBAG/VBOs/vbo/Einddatum >= int32(JaarStr+'0101'))
		, Descr = "set met verblijfsobjecten op 1 januari van elk jaar die voorraad zijn volgens de CBS definitie."
		{
			parameter<string>                Selection_string      := 'Voorraad';
			attribute<vbo_gebruiksdoel_plus> gebruiksdoel_plus_rel := Impl/gebruiksdoel_plus_rel;
			attribute<pand>                  Pand_rel              := rlookup(pand_bag_nr, pand/pand_bag_nr);
			attribute<pand_alle_statussen>   pand_alle_statussen_rel := rlookup(pand_bag_nr, pand_alle_statussen/pand_bag_nr);
			attribute<yr>                    Bouwjaar_trunc        := Pand_rel -> Bouwjaar_trunc;
			attribute<WP5>                   WP5_rel               := Pand_rel -> WP5_rel;
			attribute<WP4>                   WP4_rel               := WP5/WP4_rel[WP5_rel];
			attribute<WP3>                   WP3_rel               := WP5/WP3_rel[WP5_rel];
			attribute<WP2>                   WP2_rel               := WP5/WP2_rel[WP5_rel];
			attribute<WP1>                   WP1_rel               := WP5/WP1_rel[WP5_rel];
			
			attribute<AllocDomain>           AllocDomain_rel       := geometry[AllocDomain];
			attribute<AdminDomain>           AdminDomain_rel       := geometry[AdminDomain];
			
			container Per_Gebruiksdoel := for_each_ne(vbo_gebruiksdoel_plus/name, 'Templates/BAG_GebruiksdoelSets_T(' + quote(vbo_gebruiksdoel_plus/name) +',..)');
			
			container Oppervlaktes
			{
				container per_WP2_AllocDomain :=
					for_each_nedv(
						WP2/name,
						'mean(WP2_rel == ' + string(id(WP2)) + '[WP2] ? oppervlakte_trunc : (0/0)[m2_vbo], geometry[AllocDomain])',
						AllocDomain, m2_vbo
					);
					
				container per_WP2_AdminDomain :=
					for_each_nedv(
						WP2/name,
						'mean(WP2_rel == ' + string(id(WP2)) + '[WP2] ? oppervlakte_trunc : (0/0)[m2_vbo], geometry[AdminDomain])',
						AdminDomain, m2_vbo
					);
				
				container per_WP4_AllocDomain :=
					for_each_nedv(
						WP4/name,
						'mean(WP4_rel == ' + string(id(WP4)) + '[WP4] ? oppervlakte_trunc : (0/0)[m2_vbo], geometry[AllocDomain])',
						AllocDomain, m2_vbo
					);
				
				container per_WP4_AdminDomain :=
					for_each_nedv(
						WP4/name,
						'mean(WP4_rel == ' + string(id(WP4)) + '[WP4] ? oppervlakte_trunc : (0/0)[m2_vbo], geometry[AdminDomain])',
						AdminDomain, m2_vbo
					);
			}
			container Counts
			{
				container per_WP1_AllocDomain :=
					for_each_nedv(
						WP1/name
						, 'sum_uint32(WP1_rel == ' + string(id(WP1)) + '[WP1], geometry[AllocDomain])'
						, AllocDomain, uint32
					)
				{
					// attribute<uint32> Totaal (AllocDomain) := ='add(' + AsItemList(WP1/name) + ')';
				}
				
				container per_WP2_AllocDomain :=
					for_each_nedv(
						WP2/name 
						, 'sum_uint32(WP2_rel == ' + string(id(WP2)) + '[WP2], geometry[AllocDomain])'
						, AllocDomain, uint32
					)
				{
					attribute<uint32> Totaal (AllocDomain) := ='add(' + AsItemList(WP2/name) + ')';
				}
				
				container per_WP2_AdminDomain :=
					for_each_nedv(
						WP2/name
						, 'sum_uint32(WP2_rel == ' + string(id(WP2)) + '[WP2], geometry[AdminDomain])'
						, AdminDomain, uint32
					)
				{
					attribute<uint32> Totaal (AdminDomain) := ='add(' + AsItemList(WP2/name) + ')';
				}
				
				container per_WP4_AllocDomain :=
					for_each_nedv(
						WP4/name 
						, 'sum_uint32(WP4_rel == ' + string(id(WP4)) + '[WP4], geometry[AllocDomain])'
						, AllocDomain, uint32
					)
				{
					attribute<uint32> Totaal (AllocDomain) := ='add(' + AsItemList(WP4/name) + ')';
				}
				
				container per_WP4_AdminDomain :=
					for_each_nedv(
						WP4/name 
						, 'sum_uint32(WP4_rel == ' + string(id(WP4)) + '[WP4], geometry[AdminDomain])'
						, AdminDomain, uint32
					)
				{
					attribute<uint32> Totaal (AdminDomain) := ='add(' + AsItemList(WP4/name) + ')';
				}
			}
			
			container Impl
			{
				container gebruiksdoelen :=
					for_each_nedv(
						vbo_gebruiksdoel/name
						, 'bitand(functie_code,vbo_gebruiksdoel/V/'+vbo_gebruiksdoel/name+'->functie_code) > 0w' 
						, vbo
						, bool
					);
				
				container gebruiksdoelen_cbs
				{
					attribute<bool>   utiliteit         (...) := ='('+AsList('gebruiksdoelen/'+vbo_gebruiksdoel/Utiliteiten/name, ' || ')+') && !woon';
					attribute<uint32> Count_Utiliteiten (...) := =AsList('gebruiksdoelen/'+vbo_gebruiksdoel/Utiliteiten/name+'[uint32]', ' + ');
					
					attribute<bool>   bijeenkomst       (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/bijeenkomst;
					attribute<bool>   cel               (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/cel;
					attribute<bool>   Gezondheidszorg   (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/Gezondheidszorg;
					attribute<bool>   industrie         (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/industrie;
					attribute<bool>   Kantoor           (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/Kantoor;
					attribute<bool>   logies            (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/logies;
					attribute<bool>   onderwijs         (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/onderwijs;
					attribute<bool>   overige_gebruiks  (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/overige_gebruiks;
					attribute<bool>   sport             (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/sport;
					attribute<bool>   winkel            (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/winkel;
					attribute<bool>   woon              (...) := gebruiksdoelen/woon;
					attribute<bool>   utiliteit_combi   (...) := !woon && Count_Utiliteiten > 1;
					attribute<bool>   Logistiek         (...) := pand/IsLogistiekFunctie[Pand_rel];
					attribute<bool>   niet_woon         (...) := !woon;
				}
				
				attribute<vbo_gebruiksdoel_plus> gebruiksdoel_plus_rel (..) := switch(
														 case(gebruiksdoelen_cbs/woon, vbo_gebruiksdoel_plus/v/woon)
														, case(gebruiksdoelen_cbs/bijeenkomst, vbo_gebruiksdoel_plus/v/bijeenkomst)
														, case(gebruiksdoelen_cbs/cel, vbo_gebruiksdoel_plus/v/cel)
														, case(gebruiksdoelen_cbs/Gezondheidszorg, vbo_gebruiksdoel_plus/v/Gezondheidszorg)
														, case(gebruiksdoelen_cbs/industrie, vbo_gebruiksdoel_plus/v/industrie)
														, case(gebruiksdoelen_cbs/Kantoor, vbo_gebruiksdoel_plus/v/Kantoor)
														, case(gebruiksdoelen_cbs/logies, vbo_gebruiksdoel_plus/v/logies)
														, case(gebruiksdoelen_cbs/onderwijs, vbo_gebruiksdoel_plus/v/onderwijs)
														, case(gebruiksdoelen_cbs/overige_gebruiks, vbo_gebruiksdoel_plus/v/overige_gebruiks)
														, case(gebruiksdoelen_cbs/sport, vbo_gebruiksdoel_plus/v/sport)
														, case(gebruiksdoelen_cbs/winkel, vbo_gebruiksdoel_plus/v/winkel)
														, vbo_gebruiksdoel_plus/v/utiliteit_combi
														);
			}
		}
		
		unit<uint32> pand_alle_statussen := select_with_attr_by_cond(VolledigeBAG/panden/pand
														, VolledigeBAG/panden/pand/Begindatum < int32(JaarStr+'0101') 
															&& VolledigeBAG/panden/pand/Einddatum >= int32(JaarStr+'0101'))
		, Descr = "set met panden op 1 januari van elk jaar met alle statussen. Tbv BAG Nieuwbouw identificatie"
		{
			parameter<string> Selection_string             := 'AlleStatussen';

			//deze ontkoppeling is alleen voor debugging. Mocht je hier tegenaan lopen. Commentarieer de komende twee regels uit, en de derde aan. 
			// attribute<WP5>            WP5_rel                          := AfleidingPandType/results/WP5_rel, StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/BAG/'+Selection_string+'_'+JaarStr+'_'+/ModelParameters/StudyArea+'/WP5_rel.fss'";
			// attribute<WP5>            WP5_rel                          : StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/BAG/'+Selection_string+'_'+JaarStr+'_'+/ModelParameters/StudyArea+'/WP5_rel.fss'", StorageReadOnly = "TRUE";
			attribute<WP5>            WP5_rel                          := AfleidingPandType/results/WP5_rel;
			
			attribute<WP4>    WP4_rel                      := WP5/WP4_rel[WP5_rel];
			attribute<WP3>    WP3_rel                      := WP5/WP3_rel[WP5_rel];
			attribute<WP2>    WP2_rel                      := WP5/WP2_rel[WP5_rel];
			attribute<WP1>    WP1_rel                      := WP5/WP1_rel[WP5_rel];
			
			unit<uint32> ToedelingsMatrix := = 'Uitsmeer_key/AdminDomain_in_all_panden';
			#include<Uitsmeer_key.dms>
			
			container Counts
			{
				attribute<uint32> VBOs             (..) := pcount(vbo_alle_statussen/pand_alle_statussen_rel);
				attribute<uint32> woon_VBOs        (..) := pcount(vbo_alle_statussen/Per_gebruiksdoel/woon/select/pand_alle_statussen_rel);
			}
			#include<AfleidingPandType.dms>
		}
		
		unit<uint32> vbo_alle_statussen  := select_with_attr_by_cond(VolledigeBAG/VBOs/vbo
														, VolledigeBAG/VBOs/vbo/Begindatum < int32(JaarStr+'0101') 
															&& VolledigeBAG/VBOs/vbo/Einddatum >= int32(JaarStr+'0101'))
		, Descr = "set met verblijfsobjecten op 1 januari van elk jaar met alle statussen. Tbv BAG Nieuwbouw identificatie"
		{
			parameter<string> Selection_string             := 'AlleStatussen';
			attribute<vbo_gebruiksdoel_plus> gebruiksdoel_plus_rel := Impl/gebruiksdoel_plus_rel;
			attribute<pand>                                 Pand_rel              := rlookup(pand_bag_nr, pand/pand_bag_nr);
			attribute<pand_alle_statussen>                  pand_alle_statussen_rel              := rlookup(pand_bag_nr, pand_alle_statussen/pand_bag_nr);
			attribute<yr>                    Bouwjaar_trunc        := Pand_rel -> Bouwjaar_trunc;

			container Per_Gebruiksdoel := for_each_ne(vbo_gebruiksdoel_plus/name, 'Templates/BAG_GebruiksdoelSets_T(' + quote(vbo_gebruiksdoel_plus/name) +', ..)');
			container Impl
			{
				container gebruiksdoelen :=
					for_each_nedv(
						vbo_gebruiksdoel/name
						, 'bitand(functie_code, vbo_gebruiksdoel/V/'+vbo_gebruiksdoel/name+' -> functie_code) > 0w' 
						, vbo_alle_statussen
						, bool
					);
				
				container gebruiksdoelen_cbs
				{
					attribute<bool>   utiliteit         (...) := ='('+AsList('gebruiksdoelen/'+vbo_gebruiksdoel/Utiliteiten/name, ' || ')+') && !woon';
					attribute<uint32> Count_Utiliteiten (...) := =AsList('gebruiksdoelen/'+vbo_gebruiksdoel/Utiliteiten/name+'[uint32]', ' + ');
					
					attribute<bool>   bijeenkomst       (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/bijeenkomst;
					attribute<bool>   cel               (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/cel;
					attribute<bool>   Gezondheidszorg   (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/Gezondheidszorg;
					attribute<bool>   industrie         (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/industrie;
					attribute<bool>   Kantoor           (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/Kantoor;
					attribute<bool>   logies            (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/logies;
					attribute<bool>   onderwijs         (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/onderwijs;
					attribute<bool>   overige_gebruiks  (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/overige_gebruiks;
					attribute<bool>   sport             (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/sport;
					attribute<bool>   winkel            (...) := !woon && Count_Utiliteiten == 1 && gebruiksdoelen/winkel;
					attribute<bool>   woon              (...) := gebruiksdoelen/woon;
					attribute<bool>   utiliteit_combi   (...) := !woon && Count_Utiliteiten > 1;
					attribute<bool>   Logistiek         (...) := pand_alle_statussen/IsLogistiekFunctie[pand_alle_statussen_rel];
					attribute<bool>   niet_woon         (...) := !woon;
				}
				
				attribute<vbo_gebruiksdoel_plus> gebruiksdoel_plus_rel (..) := switch(
														 case(gebruiksdoelen_cbs/woon, vbo_gebruiksdoel_plus/v/woon)
														, case(gebruiksdoelen_cbs/bijeenkomst, vbo_gebruiksdoel_plus/v/bijeenkomst)
														, case(gebruiksdoelen_cbs/cel, vbo_gebruiksdoel_plus/v/cel)
														, case(gebruiksdoelen_cbs/Gezondheidszorg, vbo_gebruiksdoel_plus/v/Gezondheidszorg)
														, case(gebruiksdoelen_cbs/industrie, vbo_gebruiksdoel_plus/v/industrie)
														, case(gebruiksdoelen_cbs/Kantoor, vbo_gebruiksdoel_plus/v/Kantoor)
														, case(gebruiksdoelen_cbs/logies, vbo_gebruiksdoel_plus/v/logies)
														, case(gebruiksdoelen_cbs/onderwijs, vbo_gebruiksdoel_plus/v/onderwijs)
														, case(gebruiksdoelen_cbs/overige_gebruiks, vbo_gebruiksdoel_plus/v/overige_gebruiks)
														, case(gebruiksdoelen_cbs/sport, vbo_gebruiksdoel_plus/v/sport)
														, case(gebruiksdoelen_cbs/winkel, vbo_gebruiksdoel_plus/v/winkel)
														, vbo_gebruiksdoel_plus/v/utiliteit_combi
														);
			}
		}
		
		parameter<string> pand_hoogte_jaar := uint32(JaarStr) >= 2020 ? string(JaarStr) : string(2020);
		
		unit<uint32> pand_hoogte
		:	StorageName     = "='%RSo_DataDir%/Vastgoed/PHN_'+pand_hoogte_jaar+'0101.fss'"
		,	StorageReadOnly = "True"
		{
			attribute<rdc_meter> geometry (poly) := rjoin(pand_bag_nr, pand/pand_bag_nr, pand/geometry);
			attribute<string>    identificatie;
			attribute<uint64>    pand_bag_nr                  := uint64(identificatie);
			attribute<uint16>    hoogte; 
			attribute<cm>        hoogte_cm       := float32(hoogte)[cm]; 
			attribute<cm>        hoogte_maaiveld; 
			attribute<int16>     AHN_inwinjaar; 
		}
	}

	container VolledigeBAG
	{ 
		parameter<string>  Snapshot_Date_Input := ModelParameters/BAG_file_date;
		parameter<string>  Result_fss_dir      := '%RSo_DataDir%/Vastgoed/VolledigeTabel_' + Snapshot_Date_Input;
		parameter<int32>   MaxDatum            := 20500101i;
		parameter<string>  Bouwjaar_truncation_Expr := 'Bouwjaar <= 1005w || Bouwjaar > @YYYY@[yr] ? null_w : Bouwjaar < 1500w ? 1500w : Bouwjaar';
		parameter<string>  max_Bouwjaar             := string(ModelParameters/BAG_RecentYear);
		
		container panden
		{
			unit<uint32> vbo_domein := VBOs/vbo;
			
			unit<uint32> Import
			:	StorageName = "= Result_fss_dir + '/pand.fss'"
			,	StorageReadOnly = "True"
			{
				attribute<rdc_mm>                       geometry_mm            (poly);
				attribute<rdc_mm>                       geometry_mm_simpl      (poly) := geos_buffer_multi_polygon(geometry_mm, 100d, 16b);
				attribute<rdc_meter>                    geometry               (poly) := geometry_mm[rdc_meter], DisableStorage = "TRUE";
				attribute<rdc_meter>                    geometry_sImpl         (poly) := geometry_mm_simpl[rdc_meter], DisableStorage = "TRUE";
				attribute<rdc_meter>                    geometry_inflated_20cm_dpoint (poly) : Descr = "tbv pand typering"; //use the geometry from src that is corrected for inverted winding order. 
				// attribute<rdc_meter>                    geometry_inflated_20cm_dpoint (poly) := geos_buffer_multi_polygon(src/geometry, 0.2d, 4b), Descr = "tbv pand typering"; //use the geometry from src that is corrected for inverted winding order. 
				attribute<rdc_meter>                    geometry_inflated_20cm (poly) := geometry_inflated_20cm_dpoint;
				
				attribute<string>                       identificatie;
				attribute<yr>                           Bouwjaar;
				attribute<bool>                         IsNederland; //                  := IsDefined(point_in_polygon(centroid_or_mid(geometry), /Geography/StudyAreas/rdc_Nederland/geometry));
				attribute<bool>                         IsFriesland; //                  := IsDefined(point_in_polygon(centroid_or_mid(geometry), /Geography/StudyAreas/rdc_Friesland/geometry));
				attribute<bool>                         IsUtrecht; //                    := IsDefined(point_in_polygon(centroid_or_mid(geometry), /Geography/StudyAreas/rdc_Utrecht/geometry));
				attribute<bool>                         IsNoord_Holland; //              := IsDefined(point_in_polygon(centroid_or_mid(geometry), /Geography/StudyAreas/rdc_Noord_Holland/geometry));

				container meta := meta_src_template(., pand_status);
			}
			
			unit<uint32> src := Import
			{
				attribute<rdc_meter>                    geometry_org (poly)          := Import/geometry;
				attribute<rdc_meter>                    geometry_simpl_org (poly)    := Import/geometry_sImpl;
				attribute<uint64>                       pand_bag_nr                  := uint64(identificatie);
				attribute<pand_status>                  status_rel                   := Import/meta/status_rel;
				attribute<int32>                        Begindatum                   := Impl/Begindatum0 < 19600101i ? Impl/TIJDSTIPREGISTRATIE : Impl/Begindatum0;
				attribute<int32>                        Einddatum                    := Impl/Einddatum0 < 19600101i ? Impl/EINDREGISTRATIE : Impl/Einddatum0;
				attribute<yr>                           Bouwjaar                     := Import/Bouwjaar;
				attribute<yr>                           Bouwjaar_trunc               := =replace(Bouwjaar_truncation_Expr, '@YYYY@', max_Bouwjaar);
				attribute<m2PandFootprint>              Footprint                    := area(geometry, meter2_f64)[m2PandFootprint];
				
				// attribute<bool>                         LigtInCBSVerblijfsrecreatie  := IsDefined(point_in_polygon(centroid_or_mid(geometry), SourceData/Grondgebruik/VerblijfsrecreatieBuffer/geometry_rd));
				attribute<bool>                         LigtInCBSVerblijfsrecreatie  := IsDefined(point_in_polygon(centroid_or_mid(geometry), /SourceData/Grondgebruik/BBG/Dominant/IsVerblijfsRecreatieSub/union_jaren/split_union_buffer/geometry));
				attribute<bool>                         IsLogistiekFunctie           := IsDefined(rlookup(pand_bag_nr, SourceData/Vastgoed/Logistiek_panden/Logistics/pand_bag_nr));
				attribute<bool>                         IsVoorraad                   := pand_status/isVoorraad[status_rel];
				attribute<bool>                         IsStudyArea                  := ='Is'+ModelParameters/StudyArea;
				
				attribute<rdc_meter>                    geometry_inflated_20cm (poly):= Import/geometry_inflated_20cm;
				attribute<rdc_meter>                    geometry (poly)              := recollect_by_cond(Impl/HasNegativearea, Impl/select_reversed_winding_order/geometry_reversed, geometry_org);
				attribute<rdc_meter>                    geometry_simpl (poly)        := recollect_by_cond(Impl/HasNegativearea, Impl/select_reversed_winding_order/geometry_reversed, geometry_sImpl_org);
				
				unit<uint32> Impl := .
				{
					//script to flip negative winding orders
					attribute<bool>                     HasNegativearea              := area(geometry_org, meter2_f64) < 0[meter2_f64];
					unit<uint32> select_reversed_winding_order := select_with_org_rel(HasNegativearea)
					{
						attribute<rdc_meter>                geometry (poly)          := src/geometry_org[org_rel];
						attribute<int32>                    Begindatum0              := Impl/Begindatum0[org_rel];
						unit<uint32> seq_points := sequence2points(geometry)
						{
							attribute<rdc_meter> points_reversed       := reverse(point);
							attribute<uint32>    Sequence_rel_Reversed := reverse(Sequence_rel);
						}
						
						attribute<meter2>    area_reversed            := area(geometry_reversed, meter2);
						attribute<rdc_meter> geometry_reversed (poly) := points2sequence(seq_points/points_reversed, seq_points/Sequence_rel_Reversed);
					}
				
					attribute<int32>                        TIJDSTIPREGISTRATIE          := int32(replace(substr(Import/meta/TIJDSTIPREGISTRATIE,0,10),'-',''));
					attribute<int32>                        EINDREGISTRATIE              := int32(replace(substr(Import/meta/EINDREGISTRATIE,0,10),'-',''));
					attribute<int32>                        Begindatum0                  := Import/meta/Begindatum == 0i ? MaxDatum : Import/meta/Begindatum;
					attribute<int32>                        Einddatum0                   := Import/meta/Einddatum == 0i ? MaxDatum : Import/meta/Einddatum;
					attribute<rdc_meter>                    NW                           := lower_bound(geometry);
					attribute<rdc_meter>                    SE                           := upper_bound(geometry);
					attribute<float64>                      X_ext                        := sub_or_null(PointCol(SE),PointCol(NW));
					attribute<float64>                      Y_ext                        := sub_or_null(PointRow(SE),PointRow(NW));
					
					attribute<float64>                      lower_x := PointCol(NW);
					attribute<float64>                      upper_x := PointCol(SE);
					attribute<float64>                      lower_y := PointRow(NW);
					attribute<float64>                      upper_y := PointRow(SE);
					
					attribute<bool>                         pand_selection_condition     := ='X_ext < 1000d && Y_ext < 1000d' //panden met een grotere x/y range dan 1km eruit
																							'&& lower_x < 300000d && lower_x > 0d'
																							'&& upper_x < 300000d && upper_x > 0d'
																							'&& lower_y < 620000d && lower_y > 300000d'
																							'&& upper_y < 620000d && upper_y > 300000d'//; //binnen NL
																							'&& IsStudyArea';
																							
				}
			}  
				
			unit<uint32> pand := select_with_attr_by_org_rel(src, src/Impl/pand_selection_condition);
		}
		
		container VBOs
		{
			//Import the source BAG files
			unit<uint32> Import
			:	StorageName = "= Result_fss_dir + '/vbo.fss'"
			,	StorageReadOnly = "True"
			{
				attribute<rdc_mm>                     geometry_mm;
				attribute<rdc_meter>                  geometry  := geometry_mm[rdc_meter], DisableStorage = "TRUE";
				attribute<string>                     identificatie;
				attribute<int32>                      oppervlakte;
				attribute<string>                     nummeraanduiding_id;
				attribute<bool>                       IsNederland;//                  := IsDefined(point_in_polygon(geometry, /Geography/StudyAreas/rdc_Nederland/geometry));
				attribute<bool>                       IsFriesland;//                  := IsDefined(point_in_polygon(geometry, /Geography/StudyAreas/rdc_Friesland/geometry));
				attribute<bool>                       IsUtrecht  ;//                  := IsDefined(point_in_polygon(geometry, /Geography/StudyAreas/rdc_Utrecht/geometry));
				attribute<bool>                       IsNoord_Holland;//                    := IsDefined(point_in_polygon(geometry, /Geography/StudyAreas/rdc_Noord_Holland/geometry));
				
				unit<uint32> gerelateerdPand
				{
					attribute<string> identificatie;
					attribute<string> pand_id;
				}
				
				container gebruiksdoelen :=
					for_each_ndv(
						vbo_gebruiksdoel/name
						, Import
						, bool
					);
				
				container meta := meta_src_template(., vbo_status);
			}
			
			//enrich the source attributes, and prepare selection criteria.
			unit<uint32> src := Import
			{
				attribute<rdc_meter>                  geometry                  := Import/geometry;
				attribute<uint64>                     vbo_bag_nr                := uint64(identificatie);
				attribute<uint64>                     pand_bag_nr               := rjoin(vbo_bag_nr, uint64(Import/gerelateerdPand/identificatie), uint64(Import/gerelateerdPand/pand_id));
				attribute<int32>                      oppervlakte               := Import/oppervlakte;
				attribute<m2_Vbo>                     oppervlakte_trunc         := oppervlakte[m2_Vbo] < 500000[m2_Vbo]  ? oppervlakte[m2_Vbo] : (0/0)[m2_Vbo];
				attribute<vbo_status>                 status_rel                := Import/meta/status_rel;
				attribute<int32>                      Begindatum                := Import/meta/Begindatum == 0i ? MaxDatum : Import/meta/Begindatum;
				attribute<int32>                      Einddatum                 := Import/meta/Einddatum == 0i ? MaxDatum : Import/meta/Einddatum;
				attribute<yr>                         pand_Bouwjaar_trunc       := rjoin(pand_bag_nr, panden/src/pand_bag_nr, panden/src/Bouwjaar_trunc);
				attribute<bool>                       IsVoorraad                := vbo_status/isVoorraad[status_rel];
				attribute<uint16>                     functie_code              := Impl/gebruiksdoelen/functie_code;
				// attribute<bool>                       LigtInCBSVerblijfsrecreatie  := IsDefined(point_in_polygon(geometry, SourceData/Grondgebruik/VerblijfsrecreatieBuffer/geometry_rd));
				attribute<bool>                       LigtInCBSVerblijfsrecreatie  := IsDefined(point_in_polygon(geometry, SourceData/Grondgebruik/BBG/Dominant/IsVerblijfsRecreatieSub/union_jaren/split_union_buffer/geometry));
				attribute<bool>                       IsStudyArea                  := ='Is'+ModelParameters/StudyArea;
				
				unit<uint32> Impl := .
				{
					container gebruiksdoelen :=
						for_each_nedv(
							vbo_gebruiksdoel/name
							, 'Import/gebruiksdoelen/'+vbo_gebruiksdoel/name
							, src
							, bool
						)
					{
						attribute<uint16> functie_code (src) := ='add('+AsItemList(vbo_gebruiksdoel/name+'? '+string(vbo_gebruiksdoel/functie_code)+'w : 0w')+')';
					}

					attribute<float64>    x                         := PointCol(geometry);
					attribute<float64>    y                         := PointRow(geometry);
					attribute<bool>       vbo_selection_condition   := x < 300000d && x > 0d && y < 620000d && y > 300000d && IsStudyArea; //binnen NL
				}
			}

			// We only select valid records. We omit for example all records that have a geometry outside of the bounding box of Netherlands 
			unit<uint32> vbo := select_with_attr_by_cond(src, src/Impl/vbo_selection_condition);
			// unit<uint32> vbo := select_with_attr_by_org_rel(src, const(TRUE,src));
		}
		
		template meta_src_template // Voor het inlezen van de generieke BAG attributen
		{
			unit<uint32> domain;
			unit<uint8>  statusType;
			// end case Parameters

			attribute<string>     Status                 (domain);
			attribute<statusType> status_rel             (domain) := rlookup(LowerCase(Status), LowerCase(statusType/Label));

			attribute<int32>      Begindatum             (domain);
			attribute<int32>      Einddatum              (domain);
			attribute<uint32>     VOORKOMENidENTIFICATIE (domain); //een identificatienummer kan meerdere keren voorkomen. Dit is een volgnummer dat de versie weergeeft.

			attribute<string>     TIJDSTIPREGISTRATIE    (domain);
			attribute<string>     EINDREGISTRATIE        (domain);

			attribute<int32>      DOCUMENTDATUM          (domain);
			attribute<string>     DOCUMENTNUMMER         (domain);

			attribute<bool>       IS_GECONSTATEERD       (domain);
		}
	}
	
	#include<MaakVolledigeBAG.dms>
}