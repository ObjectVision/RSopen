container Restricties : using = "units;classifications;modelparameters;VariantParameters;geography"
{
	unit<uint8> Sector := Actor/Sector;
	unit<uint8> HardheidK: NrOfRows = 4
	{
		attribute<string> name:
		[
			'ZeerHard',
			'Hard',
			'Middel',
			'Zacht'
		];
		attribute<string> label := name;
	}

	unit<uint32> Sector_x_HardheidK := combine(Sector,HardheidK)
	{
		attribute<string> Sector_name     := Sector/name[first_rel];
		attribute<string> HardheidK_name  := HardheidK/name[second_rel];
		attribute<string> name            := Sector_name+'_'+HardheidK_name;
		attribute<string> label           := Sector_name+'/'+HardheidK_name;
	}
	
	
	container MaakCombinaties := 
		for_each_ne(
			Sector/name
			,'MaakCombinaties_T('+quote(Sector/name)+')'
		);
	
	container Read_Restrictie_Tifs := 
		for_each_ne(
			Sector_x_HardheidK/label
			,'Read_Restrictie_Tifs_T('+quote(Sector_x_HardheidK/name)+')'
		);
	
	Template MaakCombinaties_T
	{
		parameter<string> Sector;
		//
		parameter<string> CombinatieRef := ='VariantParameters/VariantK/RestrictiesVariant_'+Sector+'[Variant_rel]';
		container         src           := ='Read_Restrictie_Tifs/'+Sector;
		attribute<bool>   Result (AdminDomain) := switch(
													  case(CombinatieRef == 'ZeerHardHardMiddelZacht', src/ZeerHard/tif || src/Hard/tif || src/Middel/tif || src/Zacht/tif)
													, case(CombinatieRef == 'ZeerHardHardMiddel', src/ZeerHard/tif || src/Hard/tif || src/Middel/tif)
													, case(CombinatieRef == 'ZeerHardHard', src/ZeerHard/tif || src/Hard/tif)
													, case(CombinatieRef == 'ZeerHard', src/ZeerHard/tif)
													, case(CombinatieRef == 'HardMiddelZacht', src/Hard/tif || src/Middel/tif || src/Zacht/tif)
													, case(CombinatieRef == 'HardMiddel', src/Hard/tif || src/Middel/tif)
													, case(CombinatieRef == 'Hard', src/Hard/tif || src/Middel/tif)
													, case(CombinatieRef == 'MiddelZacht', src/Middel/tif || src/Zacht/tif)
													, case(CombinatieRef == 'Middel', src/Middel/tif)
													, case(CombinatieRef == 'Zacht', src/Zacht/tif)
													, const(FALSE, AdminDomain)
												);
	}
	
	Template Read_Restrictie_Tifs_T
	{
		parameter<string> name;
		//
		
		attribute<bool> Tif (AdminDomain)
		: StorageName = "='%RSo_DataDir%/Beleid/Restricties/'+ModelParameters/Restricties_filedate+'/Restricties_'+name+'.tif'"
		, StorageReadOnly = "true";
	
	}
}