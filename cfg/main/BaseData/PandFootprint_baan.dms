container PandFootprint_baan
{
	parameter<String> Year                := ModelParameters/werken/PandFootprint_per_job_calc_yr, Descr = "Jaar van de BAG data die gebruikt wordt voor de berekening van de pandfootprint per baan. Standaard is dit het jaar dat ook gebruikt wordt voor de startstaat (ModelParameters/Werken/StartYear). In geval van een analyse waarbij de dichtheid van pandfootprint per baan in een ander jaar bepaald moet worden, kan dit hier aangepast worden.";
	parameter<String> distance_decay_Type := 'rev_dist_scaled', Descr = "Type afstandsverval functie die gebruikt wordt in de potentiaal berekening. Standaard is dit rev_dist_scaled, wat een negatieve exponentiele functie is met schaal 1 op 5000m.";
	
	container LISA            := SourceData/Actoren/Banen/Per_TXLJobs6_AdminDomain;
	unit<UInt32> Pand         := ='BaseData/StartState/PandFootprint/PerYear_AdminDomain/'+string(Year)+'/pand_met_vbo';
	container PandFootprint   := Pand/Footprint_perTXLJobs6_AdminDomain;
	// container Opp_inPand      := Pand/Opp_perTXLJobs6;
	container PerGebruiksdoel := Pand/PerGebruiksdoel_corr;
	
	unit<UInt32> RegioUnit := SourceData/RegioIndelingen/NVM;
	
	container Banen_perTXLJobs6_per_grid :=
		for_each_nedv(
			Actor/TXLJobs6/name
			, 'sum(LISA/'+Actor/TXLJobs6/name+', AdminDomain/AllocDomain_rel)'
			, AllocDomain
			, Job
		), Descr = "Agregeer banen per TXLJobs6 subsector naar allocdomain";
	
	container Smoothed
	{
		container Potential_Banen :=
			for_each_nedv(
				Actor/TXLJobs6/name,
				'IsDefined(SourceData/RegioIndelingen/CBS/Last/Buurt/Per_AllocDomain) ? max_elem(potential(Banen_perTXLJobs6_per_grid/'+Actor/TXLJobs6/name+', Distmatrices/Impl_100m/pot5000m/PotRange/'+distance_decay_Type+'), ModelParameters/Werken/Job_Ondergrens) : (0f/0f)[Job]',
				AllocDomain, Job
			), Descr = "Bepaal de potentiaal van banen per TXLJobs6 subsector op basis van een afstandsverval functie. Dit is nodig om te voorkomen dat in dunbevolkte gebieden met weinig banen, de pandfootprint per baan te hoog wordt.";
		
		container Potential_m2PandFootprint :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'IsDefined(SourceData/RegioIndelingen/CBS/Last/Buurt/Per_AllocDomain) ? max_elem(potential(sum(PandFootprint/'+Actor/TXLJobs6/name+',AdminDomain/AllocDomain_rel), Distmatrices/Impl_100m/pot5000m/PotRange/'+distance_decay_Type+'), ModelParameters/Werken/PandFootprint_Ondergrens) : (0f/0f)[m2PandFootprint]'
				, AllocDomain
				, m2PandFootprint
			), Descr = "Bepaal de potentiaal van pandfootprint per TXLJobs6 subsector op basis van een afstandsverval functie. Dit is nodig om te voorkomen dat in dunbevolkte gebieden met weinig banen, de pandfootprint per baan te hoog wordt.";
		
		container Result :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'Potential_m2PandFootprint/'+Actor/TXLJobs6/name+' / Potential_Banen/'+Actor/TXLJobs6/name
				, AllocDomain
				, m2PandFootprint_job
			), Descr = "Bepaal de pandfootprint per baan op basis van de gesmoothede potentiaal waarden.";
	}
	
	container PerRegio : using = "Classifications"
	{
		container Banen_perRegio :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'sum(Banen_perTXLJobs6_per_grid/'+Actor/TXLJobs6/name+', RegioUnit/Per_AllocDomain)'
				, RegioUnit
				, Job
			), Descr = "Agregeer banen per TXLJobs6 subsector naar regio niveau";
		
		container m2PandFootprint_perRegio :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'sum(PandFootprint/'+Actor/TXLJobs6/name+', RegioUnit/Per_AdminDomain)'
				, RegioUnit
				, m2PandFootprint
			), Descr = "Agregeer pandfootprint per TXLJobs6 subsector naar regio niveau";
		
		container Opp_perRegio :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'sum(Opp_inPand/'+Actor/TXLJobs6/name+', RegioUnit/Per_AdminDomain)'
				, RegioUnit
				, meter2
			), Descr = "Agregeer vbo opp in pand per TXLJobs6 subsector naar regio niveau";
		
		// container Fractie_Opp_inTXLJobs6_perVBOfuctie :=
			// for_each_nedv(
				// Vastgoed/vbo_gebruiksdoel_plus/name
				// , 'sum(PerGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'/Opp, pand/nvm_rel)  / sum(Pand/Opp_perTXLJobs6_perPand/'+Actor/TXLJobs6/name[Vastgoed/vbo_gebruiksdoel_plus/TXLJobs6_rel]+', pand/nvm_rel)'
				// , RegioUnit
				// , float32
			// ), Descr = "Bepaal per vbo gebruiksdoel het aandeel van de vbo opp dat toe te schrijven is aan een bepaalde TXLJobs6 subsector, op basis van de nvm relatie.";
		
		container Ratio_Footprint_Opp_perAdminDomain :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, '(Opp_perRegio/'+Actor/TXLJobs6/name+' / m2PandFootprint_perRegio/'+Actor/TXLJobs6/name+')[RegioUnit/Per_AdminDomain]'
				, AdminDomain
				, float32
			), Descr = "Bepaal per admin domain de ratio tussen vbo opp en pandfootprint, op basis van de regio waarden.";
		
		container Result_PerRegio :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'm2PandFootprint_perRegio/'+Actor/TXLJobs6/name+' / Banen_perRegio/'+Actor/TXLJobs6/name
				, RegioUnit
				, m2PandFootprint_job
			), Descr = "Bepaal de pandfootprint per baan op basis van de regio waarden.";
		
		container Result :=
			for_each_nedv(
				Actor/TXLJobs6/name
				, 'Result_PerRegio/'+Actor/TXLJobs6/name+'[RegioUnit/Per_AllocDomain]'
				, AllocDomain
				, m2PandFootprint_job
			), Descr = "Breng de regio waarden terug naar allocdomain niveau.";
	}

	container Result := = ModelParameters/Werken/Type_m2perBaan+'/Result', Descr = "Kies hier welk resultaat gebruikt moet worden. Standaard is dit de smoothed versie, maar in sommige gevallen kan het wenselijk zijn om de op regio gebaseerde versie te gebruiken. used in VariantData/dichtheid; baseData/bag_nieuwbouw; templates/indicatoren; templates/Overstromingschades";
}