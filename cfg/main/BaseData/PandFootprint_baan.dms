container PandFootprint_baan
{
	parameter<string> Year                := ModelParameters/pandfootprint_per_baan_berekenings_jaar;
	parameter<string> distance_decay_type := 'rev_dist_scaled';
	
	container LISA            := ='SourceData/Actoren/Banen/'+string(Year)+'/Per_TXLJobs7_AdminDomain';
	container Pand            := ='BaseData/PandFootprint/PerYear_AdminDomain/'+string(year)+'/pand_met_vbo';   
	container PandFootprint   := Pand/footprint_perJobs6_AdminDomain;
	container Opp_inPand      := Pand/Opp_perJobs6;
	container PerGebruiksdoel := Pand/PerGebruiksdoel_corr;
	
	unit<uint32> RegioUnit := SourceData/RegioIndelingen/NVM;
	
	container Banen_perJobs6_per_grid :=
		for_each_nedv(
			Actor/Jobs6/name
			, 'sum(LISA/'+Actor/Jobs6/name+', AdminDomain/AllocDomain_rel)'
			, AllocDomain
			, Job
		);
	
	container Smoothed
	{
		container Potential_Banen :=
			for_each_nedv(
				Actor/Jobs6/name,
				'IsDefined(SourceData/RegioIndelingen/CBS/Last/Buurt/per_AllocDomain) ? max_elem(potential(Banen_perJobs6_per_grid/'+Actor/Jobs6/name+', Distmatrices/Impl_100m/pot5000m/potrange/'+distance_decay_type+'), ModelParameters/Job_Ondergrens) : (0f/0f)[Job]',
				AllocDomain, Job
			);
		
		container Potential_m2PandFootprint :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'IsDefined(SourceData/RegioIndelingen/CBS/Last/Buurt/per_AllocDomain) ? max_elem(potential(sum(PandFootprint/'+Actor/Jobs6/name+',AdminDomain/AllocDomain_rel), Distmatrices/Impl_100m/pot5000m/potrange/'+distance_decay_type+'), ModelParameters/PandFootprint_Ondergrens) : (0f/0f)[m2PandFootprint]'
				, AllocDomain
				, m2PandFootprint
			);
		
		container Result :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'Potential_m2PandFootprint/'+Actor/Jobs6/name+' / Potential_Banen/'+Actor/Jobs6/name
				, AllocDomain
				, m2PandFootprint_job
			);
	}
	
	container PerRegio
	{
		container Banen_perRegio :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'sum(Banen_perJobs6_per_grid/'+Actor/Jobs6/name+', RegioUnit/per_AllocDomain)'
				, RegioUnit
				, Job
			);
		
		container m2PandFootprint_perRegio :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'sum(PandFootprint/'+Actor/Jobs6/name+', RegioUnit/per_AdminDomain)'
				, RegioUnit
				, m2PandFootprint
			);
		
		container Opp_perRegio :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'sum(Opp_inPand/'+Actor/Jobs6/name+', RegioUnit/per_AdminDomain)'
				, RegioUnit
				, meter2
			);
		
		container Fractie_Opp_inJobs6_perVBOfuctie :=
			for_each_nedv(
				vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum(PerGebruiksdoel/'+vastgoed/vbo_gebruiksdoel_plus/name+'/Opp, pand/nvm_rel)  / sum(Pand/Opp_perJobs6_perPand/'+Actor/Jobs6/name[vastgoed/vbo_gebruiksdoel_plus/jobs6_rel]+',pand/nvm_rel)'
				, RegioUnit
				, float32
			);
		
		container Ratio_Footprint_Opp_perAdminDomain :=
			for_each_nedv(
				Actor/Jobs6/name
				, '(Opp_perRegio/'+Actor/Jobs6/name+' / m2PandFootprint_perRegio/'+Actor/Jobs6/name+')[RegioUnit/per_AdminDomain]'
				, AdminDomain
				, float32
			);
		
		container Result_PerRegio :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'm2PandFootprint_perRegio/'+Actor/Jobs6/name+' / Banen_perRegio/'+Actor/Jobs6/name
				, RegioUnit
				, m2PandFootprint_job
			);
		
		container Result :=
			for_each_nedv(
				Actor/Jobs6/name
				, 'Result_PerRegio/'+Actor/Jobs6/name+'[RegioUnit/per_AllocDomain]'
				, AllocDomain
				, m2PandFootprint_job
			); 
	}
	
	container Result := = ModelParameters/Type_m2perBaan+'/Result'; //used in VariantData/dichtheid; basedata/bag_nieuwbouw; templates/indicatoren; templates/overstromingschades
}