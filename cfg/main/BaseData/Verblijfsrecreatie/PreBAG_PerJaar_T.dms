Template PreBAG_PerJaar_T
{
	//
	parameter<uint32> JaarValue;
	unit<uint32> RegioUnit;
	//
	
	unit<uint32> Panden := SourceData/Vastgoed/BAG/PerJaar/Y2012/panden/pand/pand_met_vbo;
	unit<uint32> VBOS   := SourceData/Vastgoed/BAG/PerJaar/Y2012/vbos/vbo;
	
	unit<uint32> preBAG_Pand_inCBS := ='subset(Panden/bouwjaar_trunc < '+string(JaarValue)+'[int16] && Panden/LigtInCBSVerblijfsrecreatie)'
	{
		attribute<rdc_meter> Geometry (poly) := Panden/Geometry[nr_OrgEntity];
		attribute<uint32>    pand_rel (VBOS) := point_in_polygon(VBOS/geometry, Geometry);
		attribute<RegioUnit> RegioUnit_rel   := point_in_polygon(centroid_or_mid(geometry), RegioUnit/geometry);
		
		container footprint_per_vbo_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum(Panden/Footprint_VBOOpp_per_vbo_functie/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+'[nr_OrgEntity], RegioUnit_rel)'
				, RegioUnit
				, meter2
			);
		
	}
	
	unit<uint32> preBAG_Pand := ='subset(Panden/bouwjaar_trunc < '+string(JaarValue)+'[int16])'
	{
		attribute<rdc_meter> Geometry (poly) := Panden/Geometry[nr_OrgEntity];
		attribute<uint32>    pand_rel (VBOS) := point_in_polygon(VBOS/geometry, Geometry);
		attribute<RegioUnit> RegioUnit_rel   := point_in_polygon(centroid_or_mid(geometry), RegioUnit/geometry);
		
		container footprint_per_vbo_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum(Panden/Footprint_VBOOpp_per_vbo_functie/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+'[nr_OrgEntity], RegioUnit_rel) / 1[m2PandFootprint] * 1[meter2]'
				, RegioUnit
				, meter2
			);
	}
	
	unit<uint32> preBAG_VBO_inCBS := subset(IsDefined(preBAG_Pand_inCBS/pand_rel))
	{
		attribute<rdc_meter> Geometry := VBOS/geometry[nr_OrgEntity];
		attribute<m2_vbo>    opp      := VBOS/oppervlakte_trunc[nr_OrgEntity];
		
		container gebruiksdoelen := for_each_nedv(Classifications/Vastgoed/vbo_gebruiksdoel/name, 'VBOS/gebruiksdoelen/'+Classifications/vastgoed/vbo_gebruiksdoel/name+'[nr_OrgEntity]' ,., bool);
		
		attribute<uint32> pand_rel := preBAG_Pand_inCBS/pand_rel[nr_OrgEntity];
		attribute<RegioUnit> RegioUnit_rel := point_in_polygon(geometry, RegioUnit/geometry);
		
		container totale_count_per_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum(gebruiksdoelen/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+' ? 1 : 0, RegioUnit_rel)'
				, RegioUnit
				, uint32
			);
	}
	
	unit<uint32> preBAG_VBO := subset(IsDefined(preBAG_Pand/pand_rel))
	{
		attribute<rdc_meter> Geometry := VBOS/geometry[nr_OrgEntity];
		attribute<m2_vbo>    opp      := VBOS/oppervlakte_trunc[nr_OrgEntity];
		
		attribute<uint32>    pand_rel := preBAG_Pand/pand_rel[nr_OrgEntity];
		attribute<RegioUnit> RegioUnit_rel := point_in_polygon(geometry, RegioUnit/geometry);
	}
}