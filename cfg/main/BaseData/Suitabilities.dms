container Suitabilities
{
	container Wonen
	{
		container Woningwaarde_perWoningType := 
			for_each_ne(
				Classifications/Vastgoed/WP4/name
				, 'Templates/Suitabilities/Woningwaarde_perWoningType_T('+string(id(Classifications/Vastgoed/WP4))+'[Classifications/Vastgoed/WP4])'
			);
		
		#include<RegionalAvgCharacteristics.dms>
		#include<UAI.dms>
		#include<PrijsIndex.dms>
		#include<Grondproductiekosten.dms>
		#include<Verwervingskosten.dms>
	}
	
	#include<Geschiktheden_Werken.dms>
	
	container Verblijfsrecreatie : using = "SourceData/Actoren/Banen;Classifications/Grondgebruik;Classifications/Actor"
	{
		container ProbabilityMap_Verblijfsrecreatie
		{
			container Variant_Dist_1000m_Neighbourhood   := MakeProbabilityMapT_Verblijfsrec('Verblijfsrecreatie','Dist_1000m_Neighbourhood', '1000');
		}
		
		container ReadCoefficients_Verblijfsrecreatie 
		{
			container Variant_Dist_100m_Neighbourhood  := ReadCoefficientsT('Verblijfsrecreatie','20211119','_Dist_100m_Neighbourhood');
			container Variant_Dist_1000m_Neighbourhood := ReadCoefficientsT('Verblijfsrecreatie','20211213','_Dist_1000m_Neighbourhood');
			container Variant_LnDist_100m_Neighbourhood := ReadCoefficientsT('Verblijfsrecreatie','20211119','_LnDist_100m_Neighbourhood');
			container Variant_LnDist_1000m_Neighbourhood := ReadCoefficientsT('Verblijfsrecreatie','20211119','_LnDist_1000m_Neighbourhood');
		}
		
		Template MakeProbabilityMapT_Verblijfsrec
		{
			parameter<string> Sectorname;
			parameter<string> Variantname;
			parameter<string> Radius;
			
			attribute<float32> raw (AllocDomain) := ='MakeDefined(
				ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/Constant
				+ (Diversen/Per_AllocDomain/DistTo_Airports_2019 / 1[km])                       * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_airport_km_2019
				+ (Diversen/Per_AllocDomain/DistTo_Highway_acces_exit_2018 / 1[km])             * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_highway_km_2018 
				+ (Diversen/Per_AllocDomain/DistTo_Trainstation_2019 / 1[km])                   * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_train_km_2019
				+ (Diversen/Per_AllocDomain/DistTo_Urban_Contour_2000 / 1[km])                  * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_urbancontour_km_2000
				// + (Diversen/Per_AllocDomain/DistTo_EHS_1990 / 1[km])                         * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_ehs_km_1990
				+ log(Diversen/Per_AllocDomain/tt_100kinhab_min_2017 / 1[min])                  * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/ln_tt_100kinhab_min_2017 
				+ (Diversen/Per_AllocDomain/Hedonic_landPrice_2007 / 1[Eur_m2])                 * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/landPrice_2007
				+ (Diversen/Per_AllocDomain/BelevingLandschap/NoMissing2[float32])              * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/landscape_quality
				+ (Diversen/Per_AllocDomain/DistTo_Natura2000_2020 / 1[km])                     * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/dist_natura2000_km_2020
				
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_agriculture    * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_a
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_infrastructure * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_i
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_forest         * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_f
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_othernature    * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_o
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_residential    * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_r
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_water          * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_w
				+ Diversen/Per_AllocDomain/Avg_shr_LUType_neigh_1000r_diversity      * ReadCoefficients_Verblijfsrecreatie/Variant_'+Variantname+'/Result/avg_shr_luType_1996_neigh_'+Radius+'r_d
				,0f
			)';
			
			attribute<float32> exponentiated (AllocDomain):= AllocDomain/IsStudyArea ? exp(raw) / (1f + exp(raw)) : 0f; //value met float32 om raw heen om de metriek eraf te halen. Want na log of exp klopt de metriek toch niet.
		}
		
		Template ReadCoefficientsT
		{
			parameter<string> Sectorname;
			parameter<string> Datum;
			parameter<string> Variantname;
			
			unit<uint32> AdminDomain
			:  StorageName = "='%RSo_DataDir%/Vastgoed/logitcoeff_Has'+Sectorname+'_'+Datum+''+Variantname+'.csv'"
			,  StorageType = "gdal.vect"
			,  StorageReadOnly = "True" 
			{
				attribute<string> parm;
				attribute<string> name := replace(parm, 'construction', 'buildingsite', '_cons', 'Constant', 'buildingsite', 'construction', 'o.', '', ' ', '_','-','');
			}
			
			container Result := for_each_nedv(AdminDomain/name, 'rjoin(rlookup('+quote(lowercase(AdminDomain/name))+', lowercase(AdminDomain/name)), id(AdminDomain), AdminDomain/estimate)[float32]', void, float32);
		}
	}
}