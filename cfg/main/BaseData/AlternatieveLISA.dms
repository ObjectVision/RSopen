container AlternatieveLISA
{
	//
	parameter<uint32> LISA_jaar := ModelParameters/LISA_StartYear;
	//
	
	parameter<string> LISA_jaar_str := 'Y'+string(LISA_jaar);
	unit<uint32> Gemeente := ='SourceData/RegioIndelingen/CBS/'+string(LISA_jaar_str)+'/Gemeente';
	unit<uint32> LISA := ='SourceData/Actoren/Banen/'+LISA_jaar_str;
	
	container Prepare_LISA_data_AdminDomain := Prepare_LISA_data_T('AdminDomain');
	container Prepare_LISA_data_AllocDomain := Prepare_LISA_data_T('AllocDomain');
	
	Template Prepare_LISA_data_T
	{
		//
		parameter<string> domain_str;
		//
		
		unit<ipoint> domain := =domain_str;
		
		container LISA_gem :=
			for_each_nedv(
				Actor/TXLJobs7/name
				, 'LISA/per_TXLJobs7/'+Actor/TXLJobs7/name
				, Gemeente
				, Job
			);
		
		container VBO_opp := 
			for_each_nedv(
				Actor/Jobs6/name
				,'BaseData/StartState/PandFootprint/PerYear_AdminDomain/'+LISA_jaar_str+'/pand_met_vbo/vboopp_perJobs6_'+domain_str+'/'+Actor/Jobs6/name
				, domain
				, meter2
			);
		
		container Desaggregatie_per_Jobs6_vboopp :=
			for_each_nedv(
				Actor/Jobs6/name
				,	'scalesum(
						VBO_opp/'+Actor/Jobs6/name+'
						,Gemeente/per_'+domain_str+'
						,LISA_gem/'+Actor/Jobs6/name+'
					)'
				, domain
				, Job
			);
	}
	
	container impl
	{
		unit<uint8> domains : nrofrows = 2
		{
			attribute<string> name    : ['AdminDomain', 'AllocDomain'];
		}
		unit<uint8> potentials : nrofrows = 6
		{
			attribute<uint32> range: [0,25,50,100,200,500];
			attribute<string> name := 'Pot'+string(range)+'m';
		}
	}
	
	unit<uint8> Variants := combine_uint8(Actor/Jobs6, impl/domains, impl/potentials)
	{
		attribute<string> job_name        := Actor/Jobs6/name[first_rel]; 
		attribute<string> domain_name     := impl/domains/name[second_rel]; 
		attribute<string> potential_name  := impl/potentials/name[third_rel]; 
		attribute<uint32> potential_value := impl/potentials/range[third_rel]; 
		
		attribute<string> name := job_name +'_'+ domain_name +'_'+ potential_name;
		attribute<string> label := name;
	}
	
	container PerVariant := for_each_ne(Variants/name, 'PerVariant_T('+quote(Variants/job_name)+', '+quote(Variants/domain_name)+', '+string(Variants/potential_value)+'[uint32])');
	
	Template PerVariant_T
	{
		//
		parameter<string> sector_name;
		parameter<string> domain_str;
		parameter<uint32> potential_value;
		//
		
		parameter<string> Domain_ref_short := = domain_str+'_ref_short';
		parameter<string> j := sector_name;
		
		unit<ipoint> domain := = domain_str;
		
		attribute<job> Estimate_potential (domain) := ='potential(
			Prepare_LISA_data_'+domain_str+'/Desaggregatie_per_Jobs6_vboopp/'+sector_name+'
			, geography/Distmatrices_'+Domain_ref_short+'/pot'+string(potential_value)+'m/Flat2
		)';
	}
}