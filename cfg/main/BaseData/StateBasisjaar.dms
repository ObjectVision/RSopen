container StateBasisjaar : using = "classifications/modellering"
{
	container State := =ModelParameters/BaseDataOntkoppeld ? 'Read' : 'Write';
	
	container Read_UNCHECKED :=
		for_each_ndvna(
			Standvar_Prep/path
			, AdminDomain
			, Standvar_Prep
			, Standvar_Prep/unit_name
			, '%LocalDataProjDir%/BaseData/StandBasisjaar/'+Standvar_Prep/path+'_'+/ModelParameters/StudyArea+'.tif'
		), StorageReadOnly = "true"
	{
		container Wonen
		{
			attribute<meter2> Footprint (AdminDomain) : StorageName = "='%LocalDataProjDir%/BaseData/StandBasisjaar/Wonen/Footprint_'+/ModelParameters/StudyArea+'.tif'";
		}
		container Verblijfsrecreatie
		{
			attribute<meter2> Footprint (AdminDomain) : StorageName = "='%LocalDataProjDir%/BaseData/StandBasisjaar/Verblijfsrecreatie/Footprint_'+/ModelParameters/StudyArea+'.tif'";
		}
	}
	
	container Read :=
		for_each_neidvn(
			Standvar_Prep/path
			, 'Read_UNCHECKED/'+Standvar_Prep/path
			, 'not(all(IsNull(this)))'
			, AdminDomain
			, Standvar_Prep
			, Standvar_Prep/unit_name
		)
	{
		container Wonen
		{
			attribute<Woning> Aantal_Woningen_Totaal (AdminDomain) := = 'add('+AsItemList(Classifications/Vastgoed/WP2xVSSH/name)+')';
			attribute<meter2> Footprint              (AdminDomain) := Read_UNCHECKED/Wonen/Footprint, IntegrityCheck = "not(all(IsNull(this)))";
		}
		
		container Verblijfsrecreatie
		{
			attribute<meter2> Footprint              (AdminDomain) := Read_UNCHECKED/Verblijfsrecreatie/Footprint, IntegrityCheck = "not(all(IsNull(this)))";
		}
		
		container PandFootprint
		{
			attribute<meter2> Totaal                 (AdminDomain) := ='add('+AsItemList(Actor/Jobs6/name)+')';
		}
	}
	
	container Write :=
		for_each_neidvna(
			Standvar_Prep/path
			, 'Src/'+Standvar_Prep/path+'' 
			, 'not(all(IsNull(this)))'
			, AdminDomain
			, Standvar_Prep
			, Standvar_Prep/unit_name
			, '%LocalDataProjDir%/BaseData/StandBasisjaar/'+Standvar_Prep/path+'_'+/ModelParameters/StudyArea+'.tif'
		)
	{
		parameter<string> Generate := 'Ready', ExplicitSuppliers = "=AsList(Standvar_Prep/path, ';')+';Wonen/Footprint;Verblijfsrecreatie/Footprint'";
		
		container Wonen
		{
			attribute<meter2>     Footprint (AdminDomain) := Src/Wonen/footprint, StorageName = "='%LocalDataProjDir%/BaseData/StandBasisjaar/Wonen/Footprint_'+/ModelParameters/StudyArea+'.tif'";
		}
		container Verblijfsrecreatie
		{
			attribute<meter2>     Footprint (AdminDomain) := Src/Verblijfsrecreatie/footprint, StorageName = "='%LocalDataProjDir%/BaseData/StandBasisjaar/Verblijfsrecreatie/Footprint_'+/ModelParameters/StudyArea+'.tif'";
		}
	}
	
	container Src
	{
		container Wonen := 
			for_each_nedv(
				Vastgoed/WP2xVSSH/name
				, 'Src0/Wonen/WP2xVSSHs/'+Vastgoed/WP2xVSSH/name
				, AdminDomain
				, Woning
			)
		{
			attribute<Woning> Totaal    (AdminDomain) := = 'add('+AsItemList(Vastgoed/WP2xVSSH/name)+')';
			attribute<meter2> Footprint (AdminDomain) := Src0/Wonen/footprint;
		}
		
		container Werken := 
			for_each_nedv(
				Actor/Jobs6/name
				, 'Src0/Werken/'+Actor/Jobs6/name
				, AdminDomain
				, job
			);
		
		container Wind
		{
			attribute<MW> Totaal                (AdminDomain) := Src0/Wind/Totaal;
			attribute<Yr> jaartal_ingebruikname (AdminDomain) := Src0/Wind/jaartal_ingebruikname;
		}
		
		container Zon
		{
			attribute<MW> Totaal (AdminDomain) := Src0/Zon/Totaal;
		}
		
		container Verblijfsrecreatie
		{
			attribute<Verblijfsobject> Totaal    (AdminDomain) := Src0/Verblijfsrecreatie/Totaal;
			attribute<meter2>          Footprint (AdminDomain) := Src0/Verblijfsrecreatie/footprint;
		}
		
		container PandFootprint := 
			for_each_nedv(
				Actor/Jobs6/name
				,'Basedata/StartState/PandFootprint/PerYear_AdminDomain/Y'+string(ModelParameters/Model_StartYear)+'/pand_met_vbo/footprint_perJobs6_AdminDomain/'+Actor/Jobs6/name
				, AdminDomain
				, m2PandFootprint
			);
			
		container Landbouw := 
			for_each_nedv(
				Actor/LandbouwK/name
				,'BaseData/StartState/Grondgebruik/per_AdminDomain/gg_Landbouw == rlookup(Actor/LandbouwK/V/'+Actor/LandbouwK/name+', Actor/LU_ModelType/LandbouwK_rel) ? AdminDomain/NrHaPerCell : 0[Ha]'
				, AdminDomain
				, Ha
			);
	}
	
	container Src0
	{
		#include<Wonen.dms>
		
		container Werken := 
			for_each_nedv(
				Actor/Jobs6/name
				, ModelParameters/Use_AltLISA 
						? 'AlternatieveLISA/PerVariant/'+Actor/Jobs6/name+'_'+ModelParameters/AltLISA_cluster_variant+'_'+ModelParameters/AltLISA_potential_variant+'/Estimate_potential'
						: 'WritePrivData/LISA/FSS/Per_TXLJobs7/'+Actor/Jobs6/name
				, AdminDomain
				, job
			);
		
		container Wind
		{
			attribute<MW> Totaal                (AdminDomain) := SourceData/Energie/Wind/Veldsituatie_2022/AdminDomain/Vermogen_enkelecellen / float32(rdc_meter/IsStudyArea_AdminDomain); 
			attribute<Yr> jaartal_ingebruikname (AdminDomain) := SourceData/Energie/Wind/Veldsituatie_2022/AdminDomain/jaartal_ingebruikname / uint16(rdc_meter/IsStudyArea_AdminDomain); 
		}
		
		container Zon
		{
			attribute<MW> Totaal (AdminDomain) := SourceData/Energie/Zon/Zonnevelden_polygonen/AdminDomain/Vermogen / float32(rdc_meter/IsStudyArea_AdminDomain);
		}
		
		container Verblijfsrecreatie
		{
			attribute<verblijfsobject> Totaal    (AdminDomain) := ='PandFootprint/PerYear_AdminDomain/Y'+string(ModelParameters/Model_StartYear)+'/pand_met_vbo/PerGebruiksdoel_corr/logies/count_per_AdminDomain[verblijfsobject]';  
			attribute<meter2>          Footprint (AdminDomain) := ='PandFootprint/PerYear_AdminDomain/Y'+string(ModelParameters/Model_StartYear)+'/pand_met_vbo/PerGebruiksdoel_corr/logies/Footprint_per_AdminDomain';  
		}
	}
}