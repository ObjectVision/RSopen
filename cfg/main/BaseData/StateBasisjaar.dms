container StateBasisjaar : using = "classifications/modellering"
{
	container State := =ModelParameters/BasedataOntkoppeld ? 'Read' : 'Write';
	
	container Read :=
		for_each_ndvna(
			Standvar_Prep/path
			, AdminDomain
			, Standvar_Prep
			, Standvar_Prep/unit_name
			, '%LocalDataDir%/RSopen/BaseData/StandBasisjaar/'+Standvar_Prep/path+'.tif'
		), StorageReadOnly = "true"
	{
		container Wonen
		{
			attribute<Woning> Aantal_Woningen_Totaal (AdminDomain) := = 'add('+AsItemList(Classifications/Vastgoed/WP2xVSSH/name)+')';
		}
		container PandFootprint
		{
			attribute<m2PandFootprint> m2PandFootprint_Totaal (AdminDomain) := = 'add('+AsItemList(Classifications/Actor/Jobs6/name)+')';
		}
	}
	
	container Write :=
		for_each_nedvna(
			Standvar_Prep/path
			, 'Src/'+Standvar_Prep/path+'' 
			, AdminDomain
			, Standvar_Prep
			, Standvar_Prep/unit_name
			, '%LocalDataDir%/RSopen/BaseData/StandBasisjaar/'+Standvar_Prep/path+'.tif'
		)
	{
		parameter<string> Generate := 'Ready', ExplicitSuppliers = "=AsList(Standvar_Prep/path, ';')";
	}
	
	container Src
	{
		container Wonen := 
			for_each_nedv(
				Vastgoed/WP2xVSSH/name
				, 'Src0/Wonen/WP2xVSSHs/'+Vastgoed/WP2xVSSH/name
				, AdminDomain
				, Woning
			)
		{
			attribute<Woning> Totaal (AdminDomain) := = 'add('+AsItemList(Vastgoed/WP2xVSSH/name)+')';
		}
		
		container Werken := 
			for_each_nedv(
				Actor/Jobs6/name
				, 'Src0/Werken/'+Actor/Jobs6/name
				, AdminDomain
				, job
			);
		
		container Wind
		{
			attribute<MW> Totaal                (AdminDomain) := Src0/Wind/Totaal;
			attribute<Yr> jaartal_ingebruikname (AdminDomain) := Src0/Wind/jaartal_ingebruikname;
		}
		
		container Zon
		{
			attribute<MW> Totaal (AdminDomain) := Src0/Zon/Totaal;
		}
		
		container Verblijfsrecreatie
		{
			attribute<Verblijfsobject> Totaal (AdminDomain) := Src0/Verblijfsrecreatie/Totaal;
		}
		
		container PandFootprint := 
			for_each_nedv(
				Actor/Jobs6/name
				,'BaseData/PandFootprint/PerYear_AdminDomain/Y'+string(ModelParameters/Model_StartYear)+'/pand_met_vbo/footprint_perJobs6_AdminDomain/'+Actor/Jobs6/name
				, AdminDomain
				, m2PandFootprint
			);
	}
	
	container Src0
	{
		#include<Wonen.dms>
		
		container Werken := //inlezen alternatieve LISA
			for_each_nedv(
				Actor/Jobs6/name
				, 'BaseData/AlternatieveLISA/PerVariant/'+Actor/Jobs6/name+'_'+ModelParameters/AltLISA_cluster_variant+'_'+ModelParameters/AltLISA_potential_variant+'/Estimate_potential'
				, AdminDomain
				, job
			);
		
		container Wind
		{
			attribute<MW> Totaal                (AdminDomain) := /SourceData/Energie/Wind/Veldsituatie_2022/AdminDomain/Vermogen_enkelecellen / float32(StudieGebied_AdminDomain); 
			attribute<Yr> jaartal_ingebruikname (AdminDomain) := /SourceData/Energie/Wind/Veldsituatie_2022/AdminDomain/jaartal_ingebruikname / uint16(StudieGebied_AdminDomain); 
		}
		
		container Zon
		{
			attribute<MW> Totaal (AdminDomain) := /SourceData/Energie/Zon/Zonnevelden_polygonen/AdminDomain/Vermogen / float32(StudieGebied_AdminDomain);
		}
		
		container Verblijfsrecreatie
		{
			attribute<verblijfsobject> Totaal (AdminDomain) := ='BaseData/PandFootprint/PerYear_AdminDomain/Y'+string(ModelParameters/Model_StartYear)+'/pand_met_vbo/PerGebruiksdoel_corr/logies/count_per_AdminDomain[verblijfsobject]';  
		}
	}
}