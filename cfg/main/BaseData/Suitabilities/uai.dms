container UrbanAttractivityIndex
{
	attribute<Float32> R_5000m_AbsWeight (AllocDomain) := C_5000m_AbsWeight/Resultaat;
	
	container C_5000m_AbsWeight := CalculateUAI(5000, 'AbsWeight');
	
	Template CalculateUAI
	{
		parameter<UInt32> potentiaal;
		parameter<String> distance_decay;
		
		attribute<Float32> Resultaat    (AllocDomain)        := Detailhandel/norm + BijeenkomstLogies/norm + Monumentaal/norm;
		attribute<Float32> Resultaat_ln (AllocDomain)        := log(log(log(log(resultaat + 1f) + 1f) + 1f) +1f);
		parameter<Float32> max                               := max(resultaat_ln);
		attribute<Float32> Resultaat_ln_scaled (AllocDomain) := Resultaat_ln / max;
		
		container Detailhandel      := T(Count/Detailhandel, potentiaal, distance_decay);
		container BijeenkomstLogies := T(Count/BijeenkomstLogies, potentiaal, distance_decay);
		container Monumentaal       := T(Count/Monumentaal, potentiaal, distance_decay);
		
		container Count 
		{
			attribute<Float32> Detailhandel      (AllocDomain) := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(BAG_RecentYear)+'/vbo/Per_Gebruiksdoel/winkel/select/counts/Per_AllocDomain[float32]';
			attribute<Float32> BijeenkomstLogies (AllocDomain) := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(BAG_RecentYear)+'/vbo/Per_Gebruiksdoel/bijeenkomst/select/counts/Per_AllocDomain[float32] + SourceData/Vastgoed/BAG/PerJaar/Y'+string(BAG_RecentYear)+'/vbo/Per_Gebruiksdoel/logies/select/counts/Per_AllocDomain[float32]';
			attribute<Float32> Monumentaal       (AllocDomain) := ='MakeDefined(SourceData/Vastgoed/BAG/PerJaar/Y'+string(BAG_RecentYear)+'/pand/met_vbo/Counts/Monumentaal_Per_AllocDomain[float32], 0f)';
		}
		
		Template T
		{
			//
			attribute<Float32> Count (AllocDomain);
			parameter<UInt32> potentiaal;
			parameter<String> distance_decay;
			//
			
			attribute<Float32> Potential (AllocDomain) := ='potential(Count, Distmatrices_100m/pot'+string(potentiaal)+'m/'+distance_decay+')'; 
			
			parameter<Float32> Max                     := max(Potential);
			attribute<Float32> Max_regio (AllocDomain) := max(Potential, SourceData/RegioIndelingen/CBS/Last/Gemeente/Per_AllocDomain)[SourceData/RegioIndelingen/CBS/Last/Gemeente/Per_AllocDomain];
			
			attribute<Float32> norm (AllocDomain) := (Potential / Max) / 3f;
		}
	}
}