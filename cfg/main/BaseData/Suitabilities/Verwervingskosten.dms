container Verwervingskosten
{
	container BAG               := SourceData/Vastgoed/BAG/PerJaar;
	container BAG_ModelJaar     := = 'BAG/Y'+string(ModelParameters/Model_StartYear);
	container BAG_RecentsteJaar := = 'BAG/Y'+string(ModelParameters/Model_StartYear);
	
	unit<uint32> vbo_startjaar  := BAG_RecentsteJaar/VBO/per_Gebruiksdoel/woon/Select;
	unit<uint32> pand_startjaar := BAG_RecentsteJaar/pand;
	
	parameter<string> Generate := 'Ready', ExplicitSuppliers = "Woningen/Write;Niet_Woningen/Write";
	
	container Woningen
	{
		container PerWP4PerWoning := for_each_nedv(
			Classifications/Vastgoed/WP4/name,
			'Woningwaarde_perWoningType/'+Classifications/Vastgoed/WP4/name+'/Basisjaar',  
			AdminDomain, Eur_Woning
		);
		
		attribute<Eur> PerVBO (vbo_startjaar) :=
			= 'max_elem(
					merge(
						vbo_startjaar/WP4_rel[uint16],
						Eur_Woning ,'+
						AsItemList('PerWP4PerWoning/'+Classifications/Vastgoed/WP4/name+'[vbo_startjaar/AdminDomain_rel]')+
					'),
					0[Eur_Woning]
				) * 1[Woning]';
		
		attribute<Eur> Calc   (AdminDomain) := sum(PerVBO, vbo_startjaar/AdminDomain_rel);
		attribute<Eur> Write  (AdminDomain) := Calc, StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/Verwervingskosten_Woningen_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))", StorageType = "gdalwrite.grid", Descr = "If this IntegrityCheck fails, go back and investigate why the calc is already failing. Do not continue without fixing this! All following results will be invalid!";
		attribute<Eur> Read_UNCHECKED   (AdminDomain) : StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/Verwervingskosten_Woningen_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageReadOnly = "True", Descr = "This tif file could potentially contain only zeros or undefined values. To check for this eventuality, the container following this one checks for this.", StorageType = "tif";
		attribute<Eur> Read             (AdminDomain) := Read_UNCHECKED, IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))", Descr = "If this IntegrityCheck fails, go back and investigate why the calc/write is already failing. Do not continue without fixing this! All following results will be invalid!";
		attribute<Eur> Totaal (AdminDomain) := = ModelParameters/BaseDataOntkoppeld ? 'Read' : 'Write';
	}
	
	container Niet_Woningen
	{
		parameter<Eur_m2>  prijs                             := ModelParameters/Wonen/Verwervingskosten/NietWoningen;
		attribute<eur>     WOZ_Totaal_2017     (AdminDomain) := SourceData/Vastgoed/WOZ/Y2017/Niet_Woningen/Per_AdminDomain;
		attribute<m2_pand> oppervlakte      (pand_startjaar) := pand_startjaar/Oppervlaktes/vbo_opp_niet_woon;
		attribute<uint32>  per_rdc_10m             (rdc_10m) := poly2grid(pand_startjaar/geometry, rdc_10m);
		attribute<float32> count_10m_per_pand      (rdc_10m) := (pcount(per_rdc_10m)[per_rdc_10m])[float32];
		attribute<meter2>  opp_uitgesmeerd_op_grid (rdc_10m) := oppervlakte[per_rdc_10m] / count_10m_per_pand * 1[PandUnit];
		attribute<meter2>  opp_per_grid        (AdminDomain) := sum(opp_uitgesmeerd_op_grid, rdc_10m/AdminDomain_rel)[meter2];
		attribute<Eur>     kental_Totaal       (AdminDomain) := opp_per_grid * prijs;
		attribute<Eur>     Per_AdminDomain     (AdminDomain) := max_elem(WOZ_Totaal_2017, kental_Totaal);
		
		attribute<Eur>     Calc             (AdminDomain) := Per_AdminDomain;
		attribute<Eur>     Write            (AdminDomain) := Calc, StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/Verwervingskosten_Niet_Woningen_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))", StorageType = "gdalwrite.grid", Descr = "Check om ervoor te zorgen dat niet de hele kaart undefined of zero is. Is dit wel zo, ga dan terug tracen naar waar het probleem zit.";
		attribute<Eur>     Read_UNCHECKED   (AdminDomain) : StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/Verwervingskosten_Niet_Woningen_AdminDomain_'+/ModelParameters/StudyArea+'.tif'", StorageReadOnly = "True", Descr = "This tif file could potentially contain only zeros or undefined values. To check for this eventuality, the container following this one checks for this.", StorageType = "tif";
		attribute<Eur>     Read             (AdminDomain) := Read_UNCHECKED, IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))";
		attribute<Eur>     Totaal           (AdminDomain) := = ModelParameters/BaseDataOntkoppeld ? 'Read' : 'Write';
	}
	
	attribute<Eur> Totaal (AdminDomain) := Woningen/Totaal + Niet_Woningen/Totaal;
}