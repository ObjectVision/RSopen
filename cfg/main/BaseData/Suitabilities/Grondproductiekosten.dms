container Grondproductiekosten : using = "Geography"
{
	parameter<Float32> Significatie_threshold := 0.11f;
	
	#include<C.dms>
	
	container ReadData := for_each_ne(C/GrondproductieModelVarianten/name, 'T/ReadData('+string(id(C/GrondproductieModelVarianten))+'[C/GrondproductieModelVarianten])')
	{
		container LocatieVariabelen
		{
			attribute<Float32> AndersBouwen         (AdminDomain) := SourceData/Bodem/DraagkrachtKwel/AndersBouwen[AdminDomain/rdc_100m_rel][float32];
			attribute<Float32> BouwenMetMaatregelen (AdminDomain) := SourceData/Bodem/DraagkrachtKwel/BouwenMetMaatregelen[AdminDomain/rdc_100m_rel][float32];
			attribute<Float32> AfstandLocatus       (AdminDomain) := (SourceData/Vastgoed/AfstandLocatus_src = 0f ? 1f : SourceData/Vastgoed/AfstandLocatus_src < 0f ? 50000f : SourceData/Vastgoed/AfstandLocatus_src)[AdminDomain/rdc_100m_rel];
			attribute<Float32> Kaveldichtheid       (AdminDomain) := SourceData/Diversen/Per_AdminDomain/Kaveldichtheid[float32]; 
			attribute<sec>     Reistijd5kInw        (AdminDomain) := SourceData/Diversen/Per_AdminDomain/Reistijd5kInw;
		}
	}
	
	container Calculations := 
		for_each_ne(
			C/Combi/name,
			'T/CalculateCosts('+
				string(id(C/Combi))+'[C/Combi],
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/estimate,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/stderror,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/pvalue,'+
				quote(C/Onzekerheid1/CalcStr[C/Onzekerheid/first_rel[C/Combi/second_rel]])+','+
				quote(C/Onzekerheid2/name[C/Onzekerheid/second_rel[C/Combi/second_rel]])+
			')'
		)
	{
		container Totaal :=
			for_each_ind(
				'nedv',
				C/Onzekerheid/name,
				replace(
					'add('+asItemList(C/GrondproductieModelVarianten/name+'/@V@/Kosten')+')',
					'@V@', C/Onzekerheid/name
				),
				AdminDomain, Eur_Ha
			);
	}
	
	container Results :=
		for_each_ind(
			'nedvc',
			C/Combi/name,
			'Calculations/'+C/Combi/name+'/Kosten',
			AdminDomain, Eur_Ha,
			'Classified/'+C/Combi/name+'/Target_9K/ClassBreaks'
		)
	{
		container Statistics := for_each_ne(C/Combi/name, 'T/Statistics('+quote(C/Combi/GrondproductieModelVarianten_name)+','+string(C/Combi/second_rel)+'[C/Onzekerheid])');
		
		container Totaal :=
			for_each_ind(
				'nedv'
				,C/Onzekerheid/name
				,replace(
					'add('+asItemList(C/GrondproductieModelVarianten/name+'/@V@')+')'
					,'@V@', C/Onzekerheid/name
				)
				,AdminDomain
				, Eur_Ha
			);
	}
	
	#include<T.dms>
}