container Grondproductiekosten : using = "Geography"
, Descr = "Schatting van de grondproductiekosten voor verschillende bouwtypen op basis van een set van modelvarianten."
{
	parameter<Float32> Significatie_threshold := ModelParameters/Werken/Grondproductie_significatie_threshold, Descr = "Drempelwaarde voor de p-waarde waaronder een coëfficiënt als significant wordt beschouwd.";
	
	#include<C.dms>
	
	container ReadData := for_each_ne(C/GrondproductieModelVarianten/name, 'T/ReadData('+string(id(C/GrondproductieModelVarianten))+'[C/GrondproductieModelVarianten])')
	{
		container LocatieVariabelen
		{
			attribute<Float32> AndersBouwen         (AdminDomain) := SourceData/Bodem/DraagkrachtKwel/AndersBouwen[AdminDomain/rdc_100m_rel][float32], Descr = "1 als er anders gebouwd moet worden ivm draagkracht of kwel, anders 0";
			attribute<Float32> BouwenMetMaatregelen (AdminDomain) := SourceData/Bodem/DraagkrachtKwel/BouwenMetMaatregelen[AdminDomain/rdc_100m_rel][float32], Descr = "1 als er met maatregelen gebouwd moet worden ivm draagkracht of kwel, anders 0";
			attribute<Float32> AfstandLocatus       (AdminDomain) := (SourceData/Vastgoed/AfstandLocatus_src = 0f ? 1f : SourceData/Vastgoed/AfstandLocatus_src < 0f ? 50000f : SourceData/Vastgoed/AfstandLocatus_src)[AdminDomain/rdc_100m_rel], Descr = "Afstand in meters tot de dichtstbijzijnde Locatus winkel (minimaal 1m, maximaal 50km)";
			attribute<Float32> Kaveldichtheid       (AdminDomain) := SourceData/Diversen/Per_AdminDomain/Kaveldichtheid[float32], Descr = "Aantal kavels per hectare in de buurt (1km) van de cel";
			attribute<sec>     Reistijd5kInw        (AdminDomain) := SourceData/Diversen/Per_AdminDomain/Reistijd5kInw, Descr = "Reistijd in seconden naar 5.000 inwoners";
		}
	}
	
	container Calculations := 
		for_each_ne(
			C/Combi/name,
			'T/CalculateCosts('+
				string(id(C/Combi))+'[C/Combi],
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/estimate,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/stderror,
				ReadData/'+C/Combi/GrondproductieModelVarianten_name+'/ModelVariant/pvalue,'+
				quote(C/Onzekerheid1/CalcStr[C/Onzekerheid/first_rel[C/Combi/second_rel]])+','+
				quote(C/Onzekerheid2/name[C/Onzekerheid/second_rel[C/Combi/second_rel]])+
			')'
		), Descr = "Berekening van de grondproductiekosten voor elke combinatie van bouwtype en onzekerheidsscenario."
	{
		container Totaal :=
			for_each_ind(
				'nedv',
				C/Onzekerheid/name,
				replace(
					'add('+asItemList(C/GrondproductieModelVarianten/name+'/@V@/Kosten')+')',
					'@V@', C/Onzekerheid/name
				),
				AdminDomain, Eur_Ha
			), Descr = "Totaal van de geschatte kosten over alle modelvarianten voor een combinatie van bouwtype en onzekerheidsscenario.";
	}
	
	container Results :=
		for_each_ind(
			'nedvc'
			,C/Combi/name
			, 'Calculations/'+C/Combi/name+'/Kosten * ModelParameters/Wonen/PrijsIndex_Bouwkosten_2017_naar_'+string(ModelParameters/Model_StartYear)
			, AdminDomain
			, Eur_Ha
			, 'Classified/'+C/Combi/name+'/Target_9K/ClassBreaks'
		), Descr = "Schatting is dat kosten 2017 waren, dus opslag naar dat jaar."
	{
		container Statistics := for_each_ne(C/Combi/name, 'T/Statistics('+quote(C/Combi/GrondproductieModelVarianten_name)+','+string(C/Combi/second_rel)+'[C/Onzekerheid])'), Descr = "Berekening van de statistieken (mean, p10, p90) van de geschatte kosten per combinatie van bouwtype en onzekerheidsscenario.";
		
		container Totaal :=
			for_each_ind(
				'nedv'
				,C/Onzekerheid/name
				,replace(
					'add('+asItemList(C/GrondproductieModelVarianten/name+'/@V@')+')'
					,'@V@', C/Onzekerheid/name
				)
				,AdminDomain
				, Eur_Ha
			), Descr = "Totaal van de geschatte kosten over alle modelvarianten voor een onzekerheidsscenario.";
	}
	
	#include<T.dms>
}