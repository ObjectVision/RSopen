container T
{
	Template ReadData
	{
		//
		parameter<C/GrondproductieModelVarianten> id;
		//
		
		parameter<string> StorageStr := '%RSo_DataDir%/Vastgoed/'+C/GrondproductieModelVarianten/name[id]+'SchattingDec19.csv';
		
		unit<uint32> ModelVariant
		:  StorageName = "=StorageStr"
		,  StorageType = "gdal.vect"
		,  StorageReadOnly = "True"
		{
			attribute<C/GrondproductieVariabelen> GrondproductieVariabelen_rel := rlookup(./Variabele, C/GrondproductieVariabelen/name);
		}
	}
	
	Template CalculateCosts : using = "ReadData" 
	{
		//
		parameter<C/Combi> id;
		unit<uint32> domain;
		attribute<string> Coefficients (domain);
		attribute<string> Stderror (domain);
		attribute<string> Pvalue (domain);
		parameter<string> CalcStr0;
		parameter<string> Onzekerheid2_name;
		//
		
		parameter<string> GenericStr := 'MakeDefined(float32(@C@), 0f)';
		parameter<string> CoefficientStr := replace(GenericStr, '@C@', 'Coefficients');
		parameter<string> StderrorStr    := replace(GenericStr, '@C@', 'Stderror');
		parameter<string> CalcStr := = CalcStr0;
		
		attribute<bool> IsSignificant                   (domain) := float32(Pvalue) <= Significatie_threshold;
		attribute<bool> IsControleVar                   (domain) := C/GrondproductieVariabelen/IsControleVar[domain/GrondproductieVariabelen_rel];
		attribute<bool> IsOnzekereControleVar           (domain) := C/GrondproductieVariabelen/IsOnzekereControleVar[domain/GrondproductieVariabelen_rel];
		attribute<bool> IsOnzekereControleVarEnNegatief (domain) := IsOnzekereControleVar && ParameterValueIsNegatief;
		attribute<bool> IsOnzekereControleVarEnPositief (domain) := IsOnzekereControleVar && !ParameterValueIsNegatief;
		attribute<bool> IsIntercept                     (domain) := domain/GrondproductieVariabelen_rel == C/GrondproductieVariabelen/V/Intercept;
		
		attribute<float32> ParameterValue0    (domain) := = '('+CalcStr+')';
		attribute<bool> ParameterValueIsNegatief (domain) := ParameterValue0 < 0f;
		attribute<float32> ParameterValueSig  (domain) := = 'float32(IsSignificant) * ('+CalcStr+')';
		attribute<float32> ParameterValue     (domain) := IsIntercept || IsControleVar ? parameterValue0: ParameterValueSig;
		
		attribute<string> VariableValue_ref (domain) :=
		//Onzekere controlevariabelen geven we een zo hoog mogelijke waarde in de High Variant en een zo laag mogelijke waarde in de Low Variant
			Onzekerheid2_name == 'Low'  && IsOnzekereControleVarEnNegatief ? '1f' :
			Onzekerheid2_name == 'Low'  && IsOnzekereControleVarEnPositief ? '0f' :
			Onzekerheid2_name == 'High' && IsOnzekereControleVarEnNegatief ? '0f' :
			Onzekerheid2_name == 'High' && IsOnzekereControleVarEnPositief ? '1f':
			C/GrondproductieVariabelen/DefaultWaarde[domain/GrondproductieVariabelen_rel];
		
		attribute<string> List (domain) := 'value('+VariableValue_ref+', float32) * '+string(ParameterValue);
		
		attribute<Eur_Ha> Kosten0 (AdminDomain) := = 'exp(add('+AsItemList(List+'[float32]')+')) + const(0[Eur_ha], AdminDomain)';
		attribute<Eur_Ha> Kosten  (AdminDomain) := MakeDefined(Kosten0, max(Kosten0));
	}
	
	Template Statistics
	{
		//
		parameter<string> SrcStr;
		parameter<C/Onzekerheid> id;
		//
		
		attribute<Eur_Ha> Src (AdminDomain) := = 'Results/'+SrcStr+'/'+C/Onzekerheid/name[id];
		
		attribute<bool> BBG (AdminDomain) := SourceData/Diversen/Per_AdminDomain/begrenzing_bebouwd_gebied;
		
		container Result := for_each_ne(C/Extent/name, 'BBGT('+C/Extent/CalcStr+')');
		
		Template BBGT
		{
			//
			attribute<bool> IsExtent (AdminDomain);
			//
			
			attribute<Eur_ha> InExtent (AdminDomain) := Src / float32(IsExtent);
			
			parameter<Eur_ha> mean    := mean(InExtent);
			parameter<Eur_ha> sd      := sd(InExtent);
			
			parameter<Eur_ha> min     := min(InExtent);
			parameter<Eur_ha> rth_element_20 := rth_element(InExtent, 0.20f);
			parameter<Eur_ha> rth_element_50 := rth_element(InExtent, 0.50f);
			parameter<Eur_ha> rth_element_80 := rth_element(InExtent, 0.80f);
			parameter<Eur_ha> max     := max(InExtent);
		}
	}
	
	Template Export
	{
		//
		parameter<string> SrcStr;
		//
		
		container Results :=
			for_each_nedv(
				C/Onzekerheid/name,
				replace(
					'union_data(C/ExtentxIndicator,'+AsItemList(replace(SrcStr, '@E@', C/ExtentxIndicator/Extent_name, '@I@', C/ExtentxIndicator/Indicator_name))+')',
					'@O@', C/Onzekerheid/name
				),
				C/ExtentxIndicator, Eur_Ha
			)
		{
			attribute<string> Extent_name    (C/ExtentxIndicator) := C/ExtentxIndicator/Extent_name;
			attribute<string> Indicator_name (C/ExtentxIndicator) := C/ExtentxIndicator/Indicator_name;
		}
	}
}