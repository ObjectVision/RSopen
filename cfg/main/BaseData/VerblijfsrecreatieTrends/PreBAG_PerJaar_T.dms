Template PreBAG_PerJaar_T
: Descr = "Template om BAG data te onttrekken voor jaren voordat de bag volledig was, i.e. pre 2012."
{
	parameter<UInt32> JaarValue;
	unit<UInt32> RegioUnit;
	//
	
	unit<UInt32> Panden := SourceData/Vastgoed/BAG/PerJaar/Y2012/pand/met_vbo;
	unit<UInt32> VBOS   := SourceData/Vastgoed/BAG/PerJaar/Y2012/vbo;
	
	unit<UInt32> preBAG_Pand_inCBS := ='select_with_org_rel(Panden/Bouwjaar_trunc < '+string(JaarValue)+'[UInt16] && Panden/LigtInCBSVerblijfsrecreatie)'
	, Descr = "Select panden als het bouwjaar voor BAG jaar ligt en binnen BBG-verblijfsrecreatie"
	{
		attribute<rdc_meter> geometry (poly) := Panden/geometry[org_rel];
		attribute<UInt32>    Pand_rel (VBOS) := point_in_polygon(VBOS/geometry, geometry);
		attribute<RegioUnit> RegioUnit_rel   := point_in_polygon(centroid_or_mid(geometry), RegioUnit/geometry);
		
		container Footprint_per_vbo_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum(Panden/Oppervlaktes/Footprint_perGebruiksdoel/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+'[org_rel], RegioUnit_rel)'
				, RegioUnit
				, meter2
			), Descr = "sommeer footprint per gebruiksdoel per regio";
		
	}
	
	unit<UInt32> preBAG_Pand := ='select_with_org_rel(Panden/Bouwjaar_trunc < '+string(JaarValue)+'[UInt16])'
	, Descr = "Select panden als het bouwjaar voor BAG jaar ligt"
	{
		attribute<rdc_meter> geometry (poly) := Panden/geometry[org_rel];
		attribute<UInt32>    Pand_rel (VBOS) := point_in_polygon(VBOS/geometry, geometry);
		attribute<RegioUnit> RegioUnit_rel   := point_in_polygon(centroid_or_mid(geometry), RegioUnit/geometry);
		
		container Footprint_per_vbo_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum(Panden/Oppervlaktes/Footprint_perGebruiksdoel/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+'[org_rel], RegioUnit_rel) / 1[m2PandFootprint] * 1[meter2]'
				, RegioUnit
				, meter2
			), Descr = "sommeer footprint per gebruiksdoel per regio";
	}
	
	unit<UInt32> preBAG_VBO_inCBS := select_with_org_rel(IsDefined(preBAG_Pand_inCBS/Pand_rel))
	, Descr = "Select vbos als het gerelateerde pand bouwjaar voor select-jaar ligt en binnen BBG-verblijfsrecreatie."
	{
		attribute<rdc_meter> geometry := VBOS/geometry[org_rel];
		attribute<m2_vbo>    opp      := VBOS/oppervlakte_trunc[org_rel];
		
		container gebruiksdoelen := for_each_nedv(Classifications/Vastgoed/vbo_gebruiksdoel/name, 'VBOS/Impl/gebruiksdoelen/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+'[org_rel]' ,., bool);
		
		attribute<UInt32> Pand_rel := preBAG_Pand_inCBS/Pand_rel[org_rel];
		attribute<RegioUnit> RegioUnit_rel := point_in_polygon(geometry, RegioUnit/geometry);
		
		container totale_count_per_functie_perRegio :=
			for_each_nedv(
				Classifications/Vastgoed/vbo_gebruiksdoel/name
				, 'sum_uint32(gebruiksdoelen/'+Classifications/Vastgoed/vbo_gebruiksdoel/name+', RegioUnit_rel)'
				, RegioUnit
				, uint32
			), Descr = "sommeer aantal vbos per gebruiksdoel per regio";
	}
	
	unit<UInt32> preBAG_VBO := select_with_org_rel(IsDefined(preBAG_Pand/Pand_rel))
	, Descr = "Select vbos als het gerelateerde pand bouwjaar voor select-jaar ligt"
	{
		attribute<rdc_meter> geometry := VBOS/geometry[org_rel];
		attribute<m2_vbo>    opp      := VBOS/oppervlakte_trunc[org_rel];
		
		attribute<UInt32>    Pand_rel := preBAG_Pand/Pand_rel[org_rel];
		attribute<RegioUnit> RegioUnit_rel := point_in_polygon(geometry, RegioUnit/geometry);
	}
}