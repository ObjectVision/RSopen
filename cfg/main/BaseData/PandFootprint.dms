container PandFootprint
: Descr = "tbv bepaling pandFootprint per cell voor StateBasisjaar, voor de dichtheid van pandFootprint per baan"
{
	container PerYear_AdminDomain := for_each_ne(SourceData/Vastgoed/BAG/BAG_jaren/name, 'PerYear_T('+string(SourceData/Vastgoed/BAG/BAG_jaren/jaar)+','+quote('AdminDomain')+')');
	
	Template PerYear_T
	{
		parameter<uint32> Year;
		parameter<string> domain_ref;
		//
		
		unit<ipoint> domain := = domain_ref;
		unit<uint32> pand_met_vbo := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(Year)+'/pand/met_vbo'
		{
			attribute<SourceData/RegioIndelingen/NVM> nvm_rel := point_in_polygon(centroid_or_mid(geometry), SourceData/RegioIndelingen/NVM/geometry);
			container PerGebruiksdoel_corr := for_each_ne(Classifications/Vastgoed/vbo_gebruiksdoel_plus/name, 'PerGebruiksdoel_T('+quote(Classifications/Vastgoed/vbo_gebruiksdoel_plus/name)+')');
			
			container BAG_functie_Jobs6_Ratio :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/Footprint_Per_AdminDomain')
					, domain
					, m2PandFootprint
				);
			
			container Footprint_perJobs6_AllocDomain :=
				for_each_nedv(
					Actor/Jobs6/name
					, 'sum(Footprint_perJobs6_AdminDomain/'+Actor/Jobs6/name+', domain/AllocDomain_rel)'
					, AllocDomain
					, m2PandFootprint
				)
			{
				attribute<m2PandFootprint> Totaal (AllocDomain) := = 'min_elem(add('+AsItemList(Actor/Jobs6/name)+'), AllocDomain/NrHaPerCell[m2PandFootprint])';
				
				container Share := for_each_nedv(Actor/Jobs6/name, 'Footprint_perJobs6_AllocDomain/'+Actor/Jobs6/name+'[float32] / Totaal[float32]', AllocDomain, float32);
			}
			
			container Footprint_perJobs6_AdminDomain :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/Footprint_per_AdminDomain')
					, domain
					, m2PandFootprint
				)
			{
				attribute<m2PandFootprint> Totaal (domain) := = 'min_elem(add('+AsItemList(Actor/Jobs6/name)+'), domain/NrHaPerCell[m2PandFootprint])';
				
				container Share := for_each_nedv(Actor/Jobs6/name, 'Footprint_perJobs6_AdminDomain/'+Actor/Jobs6/name+'[float32] / Totaal[float32]', domain, float32);
			}
			
			container vboopp_perJobs6_AdminDomain :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/opp_Per_AdminDomain')
					, domain
					, meter2
				)
			{
				attribute<meter2> Totaal (domain) := = 'min_elem(add('+AsItemList(Actor/Jobs6/name)+'), domain/NrHaPerCell[meter2])';
				
				container Share := for_each_nedv(Actor/Jobs6/name, 'vboopp_perJobs6_AdminDomain/'+Actor/Jobs6/name+'[float32] / Totaal[float32]', domain, float32);
			}
			
			container vboopp_perJobs6_AllocDomain :=
				for_each_nedv(
					Actor/Jobs6/name
					, 'sum(vboopp_perJobs6_AdminDomain/'+Actor/Jobs6/name+', domain/AllocDomain_rel)'
					, AllocDomain
					, meter2
				)
			{
				attribute<meter2> Totaal (AllocDomain) := = 'min_elem(add('+AsItemList(Actor/Jobs6/name)+'), AllocDomain/NrHaPerCell[meter2])';
				
				container Share := for_each_nedv(Actor/Jobs6/name, 'vboopp_perJobs6_AllocDomain/'+Actor/Jobs6/name+'[float32] / Totaal[float32]', AllocDomain, float32);
			}
			
			container Opp_perJobs6 :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/opp_Per_AdminDomain')
					, domain
					, meter2
				)
			{
				attribute<meter2> Totaal (domain) := = 'add('+AsItemList(Actor/Jobs6/name)+')';
			}
			
			container Opp_perJobs6_perPand :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/opp')
					, pand_met_vbo
					, meter2
				);
			
			container CountObj_perJobs6 :=
				for_each_nedv(
					Actor/Jobs6/name
					, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel_corr/','#', '/count_Per_AdminDomain')
					, domain
					, float32
				)
			{
				attribute<float32> Totaal (domain) := = 'add('+AsItemList(Actor/Jobs6/name)+')';
			}
			
			Template PerGebruiksdoel_T
			{
				//
				parameter<string> name;
				//
				
				attribute<m2PandFootprint> Footprint                 (pand_met_vbo) := ='pand_met_vbo/Oppervlaktes/Footprint_perGebruiksdoel/'+name;
				attribute<meter2>          opp                       (pand_met_vbo) := ='pand_met_vbo/Oppervlaktes/VboOpp_perGebruiksdoel/'+name;
				attribute<float32>         count                     (pand_met_vbo) := ='pand_met_vbo/Counts/Vbos_perGebruiksdoel/'+name+'[float32]';
				
				attribute<m2PandFootprint> Footprint_Per_AdminDomain       (domain) := sum(Footprint[pand_met_vbo/ToedelingsMatrix/Pand_rel] * pand_met_vbo/ToedelingsMatrix/ToedelingPand, pand_met_vbo/ToedelingsMatrix/AdminDomain_rel); 
				attribute<meter2>          opp_Per_AdminDomain             (domain) := sum(opp[pand_met_vbo/ToedelingsMatrix/Pand_rel] * pand_met_vbo/ToedelingsMatrix/ToedelingPand, pand_met_vbo/ToedelingsMatrix/AdminDomain_rel); 
				attribute<float32>         count_Per_AdminDomain           (domain) := sum(count[pand_met_vbo/ToedelingsMatrix/Pand_rel] * pand_met_vbo/ToedelingsMatrix/ToedelingPand, pand_met_vbo/ToedelingsMatrix/AdminDomain_rel); 
			} 
		}
	}
}