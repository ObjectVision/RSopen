container Stimuli
{
	container SrcSrc := /SourceData/Omgevingsrecht;
	
	unit<uint8> Sector := Actor/Sector;
	
	unit<uint8> CombiK: NrOfRows = 4
	{
		attribute<string> name:
		[
			'Hard',
			'HardMiddel',
			'HardMiddelZacht',
			'Geen'
		];
		
		attribute<string> trede_name:
		[
			'BinnenStimuliHard',
			'BinnenStimuliMiddel',
			'BinnenStimuliZacht',
			'BinnenStimuliGeen'
		];
		
		attribute<string> label := name, DialogType = "LabelText";
		
		unit<uint32> XSector := combine(., Sector)
		{
			attribute<string> Combi_name := ../name[first_rel];
			attribute<string> Sector_name := Sector/name[second_rel];
			attribute<string> name := Combi_name+'_'+Sector_name;
			attribute<string> label := Combi_name+'/'+Sector_name, DialogType = "LabelText";
		}
	}
	
	container Totaal:= = iif(ModelParameters/BaseDataOntkoppeld, 'Read', 'Write')+'Totaal';
	container ReadTotaal_UNCHECKED := 
		for_each_ndva(
			CombiK/XSector/label
			, AdminDomain
			, bool
			, '%LocalDataDir%/RSopen/BaseData/Beleid/Stimuli/'+CombiK/XSector/name+'.tif'
		), StorageReadOnly = "true";
	
	container ReadTotaal := 
		for_each_neidv(
			CombiK/XSector/label
			, 'ReadTotaal_UNCHECKED/'+CombiK/XSector/label
			, 'not(all(IsNull(this)))'
			, AdminDomain
			, bool
		), StorageReadOnly = "true";
		
	container WriteTotaal := 
		for_each_neidva(
			CombiK/XSector/label
			,replace(
				'max_elem(
					AMVB/@C@,
					Structuurvisie/@C@,'+
					AsItemList('PRV/'+SourceData/RegioIndelingen/CBS/y2022/Provincie/name+'/@C@')+
				')',
				'@C@', CombiK/XSector/Sector_name+'/Combis/'+CombiK/XSector/Combi_name
			)
			, 'not(all(IsNull(this)))'
			, AdminDomain
			, bool
			, '%LocalDataDir%/RSopen/BaseData/Beleid/Stimuli/'+CombiK/XSector/name+'.tif'
	);
	
	#include<AMVB.dms>
	#include<Structuurvisie.dms>
	#include<PRV.dms>
}