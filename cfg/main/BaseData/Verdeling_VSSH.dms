container Verdeling_VSSH
{
	unit<uint32> pand_volledig_domain := /SourceData/Vastgoed/BAG/VolledigeBAG/panden/pand;
	unit<uint32> pand_domain     := SourceData/Vastgoed/EigendomStaat/pand
	{
		attribute<bool> panden_SocialeHuur_selection_condition := Eigendom_rel == /Classifications/vastgoed/Eigendom/V/WoonCorp;
		attribute<bool> panden_VrijeSector_selection_condition := Eigendom_rel != /Classifications/vastgoed/Eigendom/V/WoonCorp;
	}
	
	container per_AdminDomain :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Uitsmeren_WP2_'+Classifications/Vastgoed/WP2xVSSH/VSSH_name+'/'+Classifications/Vastgoed/WP2xVSSH/WP_name+'/Aantal_Woningen_per_CompactedAdminDomain[woning]'
			, CompactedAdminDomain
			, woning
		)
	{
		attribute<float32> Totaal (CompactedAdminDomain) := =AsList(Classifications/Vastgoed/WP2xVSSH/name, ' + ');
	}
	
	container Footprint_per_CompactedAdminDomain :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Uitsmeren_WP2_'+Classifications/Vastgoed/WP2xVSSH/VSSH_name+'/'+Classifications/Vastgoed/WP2xVSSH/WP_name+'/Footprint_per_CompactedAdminDomain[meter2]'
			, CompactedAdminDomain
			, meter2
		)
	{
		attribute<meter2> Totaal (CompactedAdminDomain) := =AsList(Classifications/Vastgoed/WP2xVSSH/name, ' + ');
	}
	
	unit<uint32> per_Gemeente := ='SourceData/RegioIndelingen/cbs/Y'+string(ModelParameters/Model_Startyear)+'/gemeente'
	{
		attribute<uint32>  Aantal_SH := sum(panden_SocialeHuur/count_vbo_pand_woon, point_in_polygon(centroid_or_mid(panden_SocialeHuur/geometry), geometry));
		attribute<uint32>  Aantal_VS := sum(panden_VrijeSector/count_vbo_pand_woon, point_in_polygon(centroid_or_mid(panden_VrijeSector/geometry), geometry));
		
		attribute<float32> P_SH      := Aantal_SH[float32] / (Aantal_SH + Aantal_VS)[float32];
		attribute<float32> P_SH_perCompactedAdminDomain (CompactedAdminDomain) := P_SH[per_CompactedAdminDomain];
	}
	
	unit<uint32> panden_SocialeHuur := select_with_attr_by_org_rel(SourceData/Vastgoed/EigendomStaat/pand, pand_domain/panden_SocialeHuur_selection_condition);
	unit<uint32> panden_VrijeSector := select_with_attr_by_org_rel(SourceData/Vastgoed/EigendomStaat/pand, pand_domain/panden_VrijeSector_selection_condition);
	
	container Uitsmeren_WP2_SocialeHuur := for_each_ne(Classifications/Vastgoed/WP2/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP2/name)+', ''WP2'', panden_SocialeHuur, pand_domain/panden_SocialeHuur_selection_condition)');
	container Uitsmeren_WP2_VrijeSector := for_each_ne(Classifications/Vastgoed/WP2/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP2/name)+', ''WP2'', panden_VrijeSector, pand_domain/panden_VrijeSector_selection_condition)');
	
	container Uitsmeren_WP4_SocialeHuur := for_each_ne(Classifications/Vastgoed/WP4/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP4/name)+', ''WP4'', panden_SocialeHuur, pand_domain/panden_SocialeHuur_selection_condition)');
	container Uitsmeren_WP4_VrijeSector := for_each_ne(Classifications/Vastgoed/WP4/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP4/name)+', ''WP4'', panden_VrijeSector, pand_domain/panden_VrijeSector_selection_condition)');
	
	unit<uint8>          AllocRegio       := /Classifications/Modellering/AllocRegiosK;

	container Per_AllocRegios := 
		for_each_ne(
			AllocRegio/name
			, 'MakeProxy_perAllocRegio_T('+string(id(AllocRegio))+'[AllocRegio])'
		)
	{
		parameter<string> Generate_Wonen              := 'Ready', ExplicitSuppliers = "=AsList(AllocRegio/name+'/Write_Wonen_WP2xVSSH_FSS', ';')";
	}
	
	Template MakeProxy_perAllocRegio_T
	{
		parameter<AllocRegio> AllocRegio_rel;
		///
		parameter<string> AllocRegio_name := AllocRegio/name[AllocRegio_rel];
		parameter<string> AllocRegio_path := AllocRegio/path[AllocRegio_rel];

		unit<uint32> RegioUnit := ='/SourceData/'+AllocRegio_path;
		
		container Write_Wonen_WP2xVSSH_FSS
		: StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/WP2xVSSH_Proxy/'+AllocRegio_name+'/'+/ModelParameters/StudyArea+'.fss'"
		, Descr = "deze proxy's worden gebruikt om de claims te verdelen tusen een/meergezins naar VS/SH."
		{
			attribute<Woning> eengezins_VrijeSector_Proxy  (RegioUnit) := sum(Verdeling_VSSH/per_AdminDomain/eengezins_VrijeSector, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> eengezins_SocialeHuur_Proxy  (RegioUnit) := sum(Verdeling_VSSH/per_AdminDomain/eengezins_SocialeHuur, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> meergezins_VrijeSector_Proxy (RegioUnit) := sum(Verdeling_VSSH/per_AdminDomain/meergezins_VrijeSector, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> meergezins_SocialeHuur_Proxy (RegioUnit) := sum(Verdeling_VSSH/per_AdminDomain/meergezins_SocialeHuur, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> eengezins_Proxy              (RegioUnit) := eengezins_VrijeSector_Proxy  + eengezins_SocialeHuur_Proxy;
			attribute<Woning> meergezins_Proxy             (RegioUnit) := meergezins_VrijeSector_Proxy + meergezins_SocialeHuur_Proxy;
		}
	}	
	
	Template Uitsmeren_T
	{
		//
		parameter<string> WP_str;
		parameter<string> WP_type;
		unit<uint32>      pand_perVSSH_domain;
		attribute<bool>   VSSH_cond (pand_domain);
		//
		
		attribute<meter2> footprint_pand_perVSSH_domain           (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_type+'_rel == Classifications/Vastgoed/'+WP_type+'/V/'+WP_str+' ? pand_perVSSH_domain/footprint : 0[meter2]';
		attribute<meter2> footprint_per_pand_domain               (pand_domain) := recollect_by_cond(VSSH_cond, footprint_pand_perVSSH_domain, 0[meter2]);
		attribute<meter2> footprint_per_CompactedAdminDomain      (CompactedAdminDomain) := sum(footprint_per_pand_domain[pand_domain/ToedelingsMatrix/pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel); 
		
		attribute<meter2> VBOopp_per_pand_perVSSH_domain          (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_type+'_rel == Classifications/Vastgoed/'+WP_type+'/V/'+WP_str+' ? pand_perVSSH_domain/VBOopp_inPand_'+WP_str+' : 0[meter2]';
		attribute<meter2> VBOopp_per_pand_domain                  (pand_domain) := recollect_by_cond(VSSH_cond, VBOopp_per_pand_perVSSH_domain, 0[meter2]);
		attribute<meter2> VBOopp_per_CompactedAdminDomain         (CompactedAdminDomain) := sum(VBOopp_per_pand_domain[pand_domain/ToedelingsMatrix/pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel); 
		
		attribute<Woning> Aantal_woningen_per_pand_perVSSH_domain (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_type+'_rel == Classifications/Vastgoed/'+WP_type+'/V/'+WP_str+' ? pand_perVSSH_domain/count_vbo_pand_woon[Woning] : 0[Woning]';
		attribute<Woning> Aantal_woningen_per_pand_domain         (pand_domain) := recollect_by_cond(VSSH_cond, Aantal_woningen_per_pand_perVSSH_domain, 0[Woning]);
		attribute<Woning> Aantal_woningen_per_CompactedAdminDomain(CompactedAdminDomain) := sum(Aantal_woningen_per_pand_domain[pand_domain/ToedelingsMatrix/pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel); 
	}
}