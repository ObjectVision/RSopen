container Verdeling_VSSH
: Descr = "Bereking van de verdeling van woningen over sociale huur en vrije sector, per woningtype (een of meergezins) en per administratief gebied (CompactedAdminDomain). Het heeft deze indeling voor woon subsectoren, maar aanlevering van clamis is op een en meergezins indeling. Dus een vertaalslag moet hiervoor gemaakt worden. Dit doen we via een eigendomslaag icm de BAG."
{
	unit<UInt32> pand_volledig_domain := /SourceData/Vastgoed/BAG/VolledigeBAG/panden/pand;
	unit<UInt32> pand_domain          := SourceData/Vastgoed/EigendomStaat/pand
	, Descr = "Link naar eigendomstaat per pand"
	{
		attribute<Bool> panden_SocialeHuur_selection_condition := Eigendom_rel == /Classifications/Vastgoed/Eigendom/V/WoonCorp, Descr = "sociale huur is als eigendom een woningcorporatie is";
		attribute<Bool> panden_VrijeSector_selection_condition := Eigendom_rel != /Classifications/Vastgoed/Eigendom/V/WoonCorp, Descr = "vrije sector is al het andere";
	}
	
	container Per_AdminDomain :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Uitsmeren_WP2_'+Classifications/Vastgoed/WP2xVSSH/VSSH_name+'/'+Classifications/Vastgoed/WP2xVSSH/WP_name+'/Aantal_Woningen_Per_CompactedAdminDomain[Woning]'
			, CompactedAdminDomain
			, Woning
		), Descr = "ophalen van de uitgesmeerde aantallen woningen per compactedAdminDomain per WP2xVSSH"
	{
		attribute<Float32> Totaal (CompactedAdminDomain) := =asList(Classifications/Vastgoed/WP2xVSSH/name, ' + ');
	}
	
	container Footprint_Per_CompactedAdminDomain :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Uitsmeren_WP2_'+Classifications/Vastgoed/WP2xVSSH/VSSH_name+'/'+Classifications/Vastgoed/WP2xVSSH/WP_name+'/Footprint_Per_CompactedAdminDomain[meter2]'
			, CompactedAdminDomain
			, meter2
		), Descr = "ophalen van de uitgesmeerde footprint per compactedAdminDomain per WP2xVSSH"
	{
		attribute<meter2> Totaal (CompactedAdminDomain) := =asList(Classifications/Vastgoed/WP2xVSSH/name, ' + ');
	}
	
	unit<UInt32> per_Gemeente := ='SourceData/RegioIndelingen/cbs/Y'+string(ModelParameters/Model_StartYear)+'/gemeente'
	{
		attribute<UInt32>  Aantal_SH := sum(panden_SocialeHuur/count_vbo_pand_woon, point_in_polygon(centroid_or_mid(panden_SocialeHuur/geometry), geometry)), Descr = "Bereken aantal woningen die in sociale huur panden liggen per gemeente";
		attribute<UInt32>  Aantal_VS := sum(panden_VrijeSector/count_vbo_pand_woon, point_in_polygon(centroid_or_mid(panden_VrijeSector/geometry), geometry)), Descr = "bereken aantal woningen die in vrije sector panden liggen per gemeente";
		
		attribute<Float32> P_SH      := Aantal_SH[float32] / (Aantal_SH + Aantal_VS)[float32], Descr = "Bereken per gemeente het aandeel sociale huur";
		attribute<Float32> P_SH_perCompactedAdminDomain (CompactedAdminDomain) := P_SH[Per_CompactedAdminDomain], Descr = "Geef elke cel in de gemeente die dat aandeel als constante.";
	}
	
	unit<UInt32> panden_SocialeHuur := select_with_attr_by_org_rel(SourceData/Vastgoed/EigendomStaat/pand, pand_domain/panden_SocialeHuur_selection_condition);
	unit<UInt32> panden_VrijeSector := select_with_attr_by_org_rel(SourceData/Vastgoed/EigendomStaat/pand, pand_domain/panden_VrijeSector_selection_condition);

	container Uitsmeren_WP2_SocialeHuur := for_each_ne(Classifications/Vastgoed/WP2/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP2/name)+', ''WP2'', panden_SocialeHuur, pand_domain/panden_SocialeHuur_selection_condition)'), Descr = "Smeer sociale huur woningen uit over panden per WP2";
	container Uitsmeren_WP2_VrijeSector := for_each_ne(Classifications/Vastgoed/WP2/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP2/name)+', ''WP2'', panden_VrijeSector, pand_domain/panden_VrijeSector_selection_condition)'), Descr = "Smeer vrije sector woningen uit over panden per WP2";

	container Uitsmeren_WP4_SocialeHuur := for_each_ne(Classifications/Vastgoed/WP4/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP4/name)+', ''WP4'', panden_SocialeHuur, pand_domain/panden_SocialeHuur_selection_condition)'), Descr = "Smeer sociale huur woningen uit over panden per WP4";
	container Uitsmeren_WP4_VrijeSector := for_each_ne(Classifications/Vastgoed/WP4/name, 'Uitsmeren_T('+quote(Classifications/Vastgoed/WP4/name)+', ''WP4'', panden_VrijeSector, pand_domain/panden_VrijeSector_selection_condition)'), Descr = "Smeer vrije sector woningen uit over panden per WP4";

	unit<UInt8>          AllocRegio       := /Classifications/Modellering/AllocRegiosK;

	container Per_AllocRegios := 
		for_each_ne(
			AllocRegio/name
			, 'MakeProxy_perAllocRegio_T('+string(id(AllocRegio))+'[AllocRegio])'
		)
	{
		parameter<String> Generate_Wonen              := 'Ready', ExplicitSuppliers = "=asList(AllocRegio/name+'/Write_Wonen_WP2xVSSH_fss', ';')";
	}
	
	Template MakeProxy_perAllocRegio_T
	{
		parameter<AllocRegio> AllocRegio_rel;
		///
		parameter<String> AllocRegio_name := AllocRegio/name[AllocRegio_rel];
		parameter<String> AllocRegio_path := AllocRegio/path[AllocRegio_rel];

		unit<UInt32> RegioUnit := ='/SourceData/'+AllocRegio_path;
		
		container Write_Wonen_WP2xVSSH_fss
		: StorageName = "='%LocalDataProjDir%/BaseData/Vastgoed/WP2xVSSH_Proxy/'+AllocRegio_name+'/'+/ModelParameters/StudyArea+'.fss'"
		, Descr = "deze proxy's worden gebruikt om de claims te verdelen tusen een/meergezins naar VS/SH."
		{
			attribute<Woning> eengezins_VrijeSector_Proxy  (RegioUnit) := sum(Verdeling_VSSH/Per_AdminDomain/eengezins_VrijeSector, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> eengezins_SocialeHuur_Proxy  (RegioUnit) := sum(Verdeling_VSSH/Per_AdminDomain/eengezins_SocialeHuur, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> meergezins_VrijeSector_Proxy (RegioUnit) := sum(Verdeling_VSSH/Per_AdminDomain/meergezins_VrijeSector, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> meergezins_SocialeHuur_Proxy (RegioUnit) := sum(Verdeling_VSSH/Per_AdminDomain/meergezins_SocialeHuur, RegioUnit/Per_CompactedAdminDomain);
			attribute<Woning> eengezins_Proxy              (RegioUnit) := eengezins_VrijeSector_Proxy  + eengezins_SocialeHuur_Proxy;
			attribute<Woning> meergezins_Proxy             (RegioUnit) := meergezins_VrijeSector_Proxy + meergezins_SocialeHuur_Proxy;
		}
	}	
	
	Template Uitsmeren_T
	: Descr = ""
	{
		parameter<String> WP_str;
		parameter<String> WP_Type;
		unit<UInt32>      pand_perVSSH_domain;
		attribute<Bool>   VSSH_cond (pand_domain);
		//
		
		attribute<meter2> Footprint_pand_perVSSH_domain            (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_Type+'_rel == Classifications/Vastgoed/'+WP_Type+'/V/'+WP_str+' ? pand_perVSSH_domain/Footprint : 0[meter2]', Descr = "Selecteer footprint als wp-type gelijk is, anders 0.";
		attribute<meter2> Footprint_per_pand_domain                        (pand_domain) := recollect_by_cond(VSSH_cond, Footprint_pand_perVSSH_domain, 0[meter2]), Descr = "Breng dat resultaat van pand_perVSSH_domain naar het pand_domain.";
		attribute<meter2> Footprint_Per_CompactedAdminDomain      (CompactedAdminDomain) := sum(Footprint_per_pand_domain[pand_domain/ToedelingsMatrix/Pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel), Descr = "sommeer de pandfootprint over CAD cellen, gebaseerd op de uitsmeer-pand-relatie. Zodat grote panden netjes over meerdere cellen worden verdeeld."; 
		
		attribute<meter2> VBOopp_per_pand_perVSSH_domain           (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_Type+'_rel == Classifications/Vastgoed/'+WP_Type+'/V/'+WP_str+' ? pand_perVSSH_domain/VBOopp_inPand_'+WP_str+' : 0[meter2]', Descr = "Selecteer vbo opp als wp-type gelijk is, anders 0";
		attribute<meter2> VBOopp_per_pand_domain                           (pand_domain) := recollect_by_cond(VSSH_cond, VBOopp_per_pand_perVSSH_domain, 0[meter2]), Descr = "Breng dat resultaat van pand_perVSSH_domain naar het pand_domain.";
		attribute<meter2> VBOopp_Per_CompactedAdminDomain         (CompactedAdminDomain) := sum(VBOopp_per_pand_domain[pand_domain/ToedelingsMatrix/Pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel), Descr = "sommeer de pandvboopp over CAD cellen, gebaseerd op de uitsmeer-pand-relatie. Zodat grote panden netjes over meerdere cellen worden verdeeld.";
		
		attribute<Woning> Aantal_Woningen_per_pand_perVSSH_domain  (pand_perVSSH_domain) := ='pand_perVSSH_domain/'+WP_Type+'_rel == Classifications/Vastgoed/'+WP_Type+'/V/'+WP_str+' ? pand_perVSSH_domain/count_vbo_pand_woon[Woning] : 0[Woning]', Descr = "Selecteer aantal woningen als wp-type gelijk is, anders 0";
		attribute<Woning> Aantal_Woningen_per_pand_domain                  (pand_domain) := recollect_by_cond(VSSH_cond, Aantal_Woningen_per_pand_perVSSH_domain, 0[Woning]), Descr = "Breng dat resultaat van pand_perVSSH_domain naar het pand_domain.";
		attribute<Woning> Aantal_Woningen_Per_CompactedAdminDomain(CompactedAdminDomain) := sum(Aantal_Woningen_per_pand_domain[pand_domain/ToedelingsMatrix/Pand_rel] * pand_domain/ToedelingsMatrix/ToedelingPand, pand_domain/ToedelingsMatrix/CompactedAdminDomain_rel), Descr = "sommeer het aantal woningen over CAD cellen, gebaseerd op de uitsmeer-pand-relatie. Zodat grote panden netjes over meerdere cellen worden verdeeld.";
	}
}