container BAG_nieuwbouw : using = "Classifications"
, Descr = "Aangezien het startjaar van de simulaties in het model vaak niet samenvalt met de meest actuele invoerdata die voorhanden is bij bronhouders, is er meer informatie over de meest recente ontwikkelingen nodig. 
			Voor de stand van wonen, werken en verblijfsrecreatie in het basisjaar wordt de BAG gebruikt, echter deze dataset wordt dagelijks ge√ºpdatet. Oftewel, recente ontwikkelingen tussen startjaar en de 
			datum van de meest recente BAG-versie in het model zijn al beschikbaar. Daarom worden tussen het startjaar en het eerste gemodelleerde zichtjaar deze recente bouwontwikkelingen meegenomen en 
			hoeven daarmee niet meer gealloceerd te worden in het model. Daarnaast zit er in BAG ook al status-informatie voor ontwikkelingen in de nabije toekomst, panden die waarvoor een bouwvergunning 
			verleend is, of de bouw al gestart is. Hiervan kan met grote zekerheid worden aangenomen dat deze gerealiseerd worden. Eveneens zijn er panden ofwel objecten die gesloopt zullen gaan worden. 
			Een pand waarvoor een sloopvergunning verleend is zal zeer waarschijnlijk in de nabije toekomst gesloopt gaan worden en deze ontwikkeling wordt dan ook meegenomen in het eerstvolgende zichtjaar. 
			Al deze informatie wordt toegepast in het eerste zichtjaar."
{
	container BAG               := SourceData/Vastgoed/BAG/PerJaar;
	container BAG_ModelJaar     := = 'BAG/Y'+string(ModelParameters/Model_StartYear);
	container BAG_RecentsteJaar := = 'BAG/Y'+string(ModelParameters/BAG_RecentYear);
	
	// model start jaar
	unit<UInt32> BAG_ModelJaar_vbo_voorraad  := BAG_ModelJaar/vbo{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_ModelJaar_pand_voorraad := BAG_ModelJaar/pand{parameter<Bool> Issubset := FALSE;}
	
	// meest recente BAG
	unit<UInt32> BAG_RecentsteJaar_vbo           := BAG_RecentsteJaar/vbo_alle_statussen{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_RecentsteJaar_vbo_voorraad  := BAG_RecentsteJaar/vbo{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_RecentsteJaar_vbo_toekomst  := BAG_RecentsteJaar/vbo_alle_statussen_toekomst{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_RecentsteJaar_pand          := BAG_RecentsteJaar/pand_alle_statussen{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_RecentsteJaar_pand_voorraad := BAG_RecentsteJaar/pand{parameter<Bool> Issubset := FALSE;}
	unit<UInt32> BAG_RecentsteJaar_pand_toekomst := BAG_RecentsteJaar/pand_alle_statussen_toekomst{parameter<Bool> Issubset := FALSE;}

	
	attribute<Bool> IsNieuwVBO_TussenModelenRecentJaar (BAG_RecentsteJaar_vbo_voorraad) := !IsDefined(rlookup(BAG_RecentsteJaar_vbo_voorraad/vbo_bag_nr, BAG_ModelJaar_vbo_voorraad/vbo_bag_nr)), Descr = "";
	attribute<Bool> IsWegVBO_TussenModelenRecentJaar       (BAG_ModelJaar_vbo_voorraad) := !IsDefined(rlookup(BAG_ModelJaar_vbo_voorraad/vbo_bag_nr, BAG_RecentsteJaar_vbo_voorraad/vbo_bag_nr)), Descr = "";
	attribute<Bool> IsNieuwVBO_NaRecentJaarWelActief            (BAG_RecentsteJaar_vbo) := BAG_RecentsteJaar_vbo/status_rel == vbo_status/V/Verblijfsobject_gevormd, Descr = "";
	attribute<Bool> IsNieuwVBO_NaRecentJaar            (BAG_RecentsteJaar_vbo_toekomst) := !IsDefined(rlookup(BAG_RecentsteJaar_vbo_toekomst/vbo_bag_nr, BAG_RecentsteJaar_vbo_voorraad/vbo_bag_nr)), Descr = "Alle objecten in toekomstset die nog niet in de voorraad set zaten, moeten wel nieuw toegevoegd zijn. Later kijken wat daarvan de status is.";
	attribute<Bool> IsWegVBO_NaRecentJaar              (BAG_RecentsteJaar_vbo_toekomst) := IsDefined(rlookup(BAG_RecentsteJaar_vbo_toekomst/vbo_bag_nr, BAG_RecentsteJaar_vbo_voorraad/vbo_bag_nr)) && vbo_status/IsIngetrokken[BAG_RecentsteJaar_vbo_toekomst/status_rel], Descr = "Alle objecten die in toekomstset zitten met onttrekkingstatus, en voorraad waren.";
	
	attribute<Bool> IsNieuwPand_NaRecentJaarWelActief          (BAG_RecentsteJaar_pand) := pand_status/WordtGebouwd[BAG_RecentsteJaar_pand/status_rel], Descr = "alle panden, die op meest recente datum de status bouw gestart of vergunning verleend hebben, en dus binnenkort voorraad gaan worden.";
	attribute<Bool> IsNieuwPand_NaRecentJaar          (BAG_RecentsteJaar_pand_toekomst) := !IsDefined(rlookup(BAG_RecentsteJaar_pand_toekomst/pand_bag_nr, BAG_RecentsteJaar_pand_voorraad/pand_bag_nr)), Descr = "alle panden, die op meest recente datum de status bouw gestart of vergunning verleend hebben, en dus binnenkort voorraad gaan worden.";
	attribute<Bool> IsWegPand_NaRecentJaar            (BAG_RecentsteJaar_pand_toekomst) := pand_status/IsIngetrokken[BAG_RecentsteJaar_pand_toekomst/status_rel], Descr = "alle panden, die op meest recente datum de status sloop vergunning verleend of gesloopt hebben, en dus binnenkort uit de voorraad gaan.";
	
	
	
	unit<UInt32> NieuwVBO_TussenModelenRecentJaar := select_with_org_rel(IsNieuwVBO_TussenModelenRecentJaar) //fka NieuweVBOsTussenModelenRecentJaar
	, Descr = "alle vbo's die in BAG_RecentsteJaar (e.g. 2026) er wel zijn, maar nog niet in BAG_ModelJaar (e.g. 2023) aanwezig waren."
	{
		attribute<rdc_meter>                        geometry               := org_rel -> geometry;
		attribute<m2_vbo>                           oppervlakte_trunc      := org_rel -> oppervlakte_trunc;
		attribute<Vastgoed/WP2>                     WP2_rel                := org_rel -> WP2_rel;
		attribute<Vastgoed/WP4>                     WP4_rel                := org_rel -> WP4_rel;
		attribute<uint64>                           vbo_bag_nr             := org_rel -> vbo_bag_nr;
		attribute<uint64>                           pand_bag_nr            := org_rel -> pand_bag_nr;
		attribute<BAG_RecentsteJaar_pand_voorraad>  pand_rel               := rlookup(pand_bag_nr, BAG_RecentsteJaar_pand_voorraad/pand_bag_nr);
		// attribute<Pand_met_nieuw_vbo>               pand_met_nieuw_vbo_rel := rlookup(pand_bag_nr, Pand_met_nieuw_vbo/pand_bag_nr);
		
		container gebruiksdoelen :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'org_rel -> Impl/gebruiksdoelen_cbs/'+Vastgoed/vbo_gebruiksdoel_plus/name+''
				, .
				, bool
			), Descr = "Ophalen gebruiksdoelen voor deze subset";
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'BAG_RecentsteJaar_pand_voorraad/Oppervlaktes/Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[pand_rel]'
				, .
				, m2PandFootprint
			), Descr = "Ophalen footprint per functie uit pandenset naar deze vbo subset.";
			
		container Vbos_InPand_perGebruiksdoel:=  
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum_uint32(gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+', Pand_rel)[float32][(Pand_rel)]'
				, .
				, float32
			), Descr = "Count aantal vbo's per gebruiksdoel in een pand vertaalt naar het vbo";
	}
	
	unit<UInt32> WegVBO_TussenModelenRecentJaar := select_with_org_rel(IsWegVBO_TussenModelenRecentJaar) //fka VBOsWegTussenModelenRecentJaar
	, Descr = "alle vbo's die in BAG_ModelJaar (e.g. 2021) aanwezig waren, maar niet meer in BAG_RecentsteJaar (e.g. 2023)."
	{
		attribute<rdc_meter>    geometry          := org_rel -> geometry;
		attribute<m2_vbo>       oppervlakte_trunc := org_rel -> oppervlakte_trunc;
		attribute<Vastgoed/WP2> WP2_rel           := org_rel -> WP2_rel; //deze komt oud modeljaar
		attribute<Vastgoed/WP4> WP4_rel           := org_rel -> WP4_rel;
		attribute<uint64>       vbo_bag_nr        := org_rel -> vbo_bag_nr;
		attribute<uint64>       pand_bag_nr       := org_rel -> pand_bag_nr;
		attribute<BAG_ModelJaar_pand_voorraad> Pand_rel := rlookup(pand_bag_nr, BAG_ModelJaar_pand_voorraad/pand_bag_nr);
		
		container gebruiksdoelen :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'org_rel -> Impl/gebruiksdoelen_cbs/'+Vastgoed/vbo_gebruiksdoel_plus/name+''
				, .
				, bool
			), Descr = "Ophalen gebruiksdoelen voor deze subset";
			
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'BAG_ModelJaar_pand_voorraad/Oppervlaktes/Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[Pand_rel]'
				, .
				, m2PandFootprint
			), Descr = "Ophalen footprint per functie uit pandenset naar deze vbo subset.";
			
		container Vbos_InPand_perGebruiksdoel:=  
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum_uint32(gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+', Pand_rel)[float32][(Pand_rel)]'
				, .
				, float32
			), Descr = "Count aantal vbo's per gebruiksdoel in een pand vertaalt naar het vbo";
	}
	
	
	unit<UInt32> NieuwVBO_NaRecentJaarWelActief := select_with_org_rel(IsNieuwVBO_NaRecentJaarWelActief) //fka Vbo_nieuwbouw
	, Descr = "alle VBO's, die op meest recente datum de status gevormd hebben, en dus binnenkort voorraad gaan worden."
	{
		attribute<rdc_meter>      geometry           := org_rel -> geometry;
		attribute<m2_vbo>         oppervlakte_trunc  := org_rel -> oppervlakte_trunc;
		attribute<uint64>         vbo_bag_nr         := org_rel -> vbo_bag_nr;
		attribute<uint64>         pand_bag_nr        := org_rel -> pand_bag_nr;
		attribute<NieuwPand_NaRecentJaarWelActief>  pand_rel := rlookup(pand_bag_nr, NieuwPand_NaRecentJaarWelActief/pand_bag_nr);
		attribute<Vastgoed/WP2>   WP2_rel            := NieuwPand_NaRecentJaarWelActief/WP2_rel[pand_rel]; //deze en volgende moeten op basis zijn van wpTyperingen terwijl de bestaande/geloopte op nieuwe snapshots moeten gebeuren.
		attribute<Vastgoed/WP4>   WP4_rel            := NieuwPand_NaRecentJaarWelActief/WP4_rel[pand_rel];
		
		container gebruiksdoelen :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'org_rel -> Impl/gebruiksdoelen_cbs/'+Vastgoed/vbo_gebruiksdoel_plus/name
				, .
				, bool
			), Descr = "Ophalen gebruiksdoelen voor deze subset";
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'NieuwPand_NaRecentJaarWelActief/Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[pand_rel]'
				, .
				, m2PandFootprint
			), Descr = "Ophalen footprint per functie uit pandenset naar deze vbo subset.";
		
		container Vbos_InPand_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum(gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[uint32], pand_rel)[float32][(pand_rel)]'
				, .
				, float32
			), Descr = "Count aantal vbo's per gebruiksdoel in een pand vertaalt naar het vbo";
	}
	
	unit<UInt32> NieuwVBO_NaRecentJaar := select_with_org_rel(IsNieuwVBO_NaRecentJaar)
	, Descr = "VBO's nog niet bestonden in de meest recente BAG, maar wel in toekomst-BAG."
	{
		attribute<rdc_meter>                  geometry               := org_rel -> geometry;
		attribute<m2_vbo>                     oppervlakte_trunc      := org_rel -> oppervlakte_trunc;
		attribute<uint64>                     pand_bag_nr            := org_rel -> pand_bag_nr;
		attribute<vbo_status>                 status_rel             := org_rel -> status_rel;
		attribute<int32>                      Begindatum             := org_rel -> Begindatum;
		attribute<int32>                      Einddatum              := org_rel -> Einddatum;
		attribute<Int32>                      oppervlakte            := org_rel -> oppervlakte;
		attribute<NieuwPand_NaRecentJaar>     pand_rel               := rlookup(pand_bag_nr, NieuwPand_NaRecentJaar/pand_bag_nr);
		attribute<Vastgoed/WP2>               WP2_rel                := NieuwPand_NaRecentJaar/WP2_rel[pand_rel]; //deze en volgende moeten op basis zijn van wpTyperingen terwijl de bestaande/geloopte op nieuwe snapshots moeten gebeuren.
		attribute<Vastgoed/WP4>               WP4_rel                := NieuwPand_NaRecentJaar/WP4_rel[pand_rel];

		container gebruiksdoelen :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'org_rel -> Impl/gebruiksdoelen_cbs/'+Vastgoed/vbo_gebruiksdoel_plus/name+''
				, .
				, bool
			), Descr = "Ophalen gebruiksdoelen voor deze subset";
			
			
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'NieuwPand_NaRecentJaar/Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[Pand_rel]'
				, .
				, m2PandFootprint
			), Descr = "Ophalen footprint per functie uit pandenset naar deze vbo subset.";
			
		container Vbos_InPand_perGebruiksdoel:=  
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum_uint32(gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+', Pand_rel)[float32][(Pand_rel)]'
				, .
				, float32
			), Descr = "Count aantal vbo's per gebruiksdoel in een pand vertaalt naar het vbo";
	}
	
	unit<UInt32> NieuwPand_NaRecentJaarWelActief := select_with_org_rel(IsNieuwPand_NaRecentJaarWelActief) //fka Pand_nieuwbouw
	{
		parameter<Bool>            Issubset        := TRUE;
		
		attribute<rdc_meter>       geometry (poly) := org_rel -> geometry;
		attribute<yr>              Bouwjaar        := org_rel -> Bouwjaar;
		attribute<uint64>          pand_bag_nr     := org_rel -> pand_bag_nr;
		attribute<m2PandFootprint> Footprint       := area(geometry, meter2_f64)[m2PandFootprint];
		attribute<m2_pand>         pand_vbo_opp    := (sum(NieuwVBO_NaRecentJaarWelActief/oppervlakte_trunc, NieuwVBO_NaRecentJaarWelActief/pand_rel) * 1[verblijfsobject]) / 1[Units/PandUnit];
		
		attribute<UInt32>          count_woon_VBOs := sum_uint32(NieuwVBO_NaRecentJaarWelActief/gebruiksdoelen/woon, NieuwVBO_NaRecentJaarWelActief/pand_rel);
		attribute<Vastgoed/WP2>    WP2_rel         := count_woon_VBOs == 0 ? (0/0)[Vastgoed/WP2] : count_woon_VBOs >  1 ? Vastgoed/WP2/V/meergezins : Vastgoed/WP2/V/eengezins;
		attribute<Vastgoed/WP4>    WP4_rel         := org_rel -> WP4_rel;
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				,'((sum(NieuwVBO_NaRecentJaarWelActief/gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+' ? NieuwVBO_NaRecentJaarWelActief/oppervlakte_trunc : 0[m2_vbo], NieuwVBO_NaRecentJaarWelActief/pand_rel) * 1[Units/verblijfsobject]) / (pand_vbo_opp * 1[Units/PandUnit])) * Footprint'
				, .
				, m2PandFootprint
			), Descr = "per gebruiksdoel sommeer het vbo-oppervlak per pand, deel dat door het totale vbo-oppervlak in dat pand. Dit geeft de fractie van elke gebruiksdoel in een pand obv vbo-oppervlak. Dit wordt vermenigvuldigd met de pand-footprint om zo de pand-footprint per gebruiksdoel te krijgen.";
			
		// container Footprint_perGebruiksdoel_perGridcell := //fka PerGebruiksdoel
			// for_each_nedv(
				// Vastgoed/vbo_gebruiksdoel_plus/name
				// , 	'sum(
						// recollect_by_cond(IsNieuwPand_NaRecentJaarWelActief, Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+', 0f)
							// [BAG_RecentsteJaar_pand/ToedelingsMatrix/Pand_rel] * BAG_RecentsteJaar_pand/ToedelingsMatrix/ToedelingPand, BAG_RecentsteJaar_pand/ToedelingsMatrix/CompactedAdminDomain_rel
					// )[m2PandFootprint]'
				// , CompactedAdminDomain
				// , m2PandFootprint
			// ), Descr = "per gebruiksdoel de hoeveelheid m2 pandfootpritn per CompactedAdminDomain";
		
		// container Footprint_perJob6_perGridcell :=
			// for_each_nedv(
				// Actor/Jobs6/name
				// , replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'Footprint_perGebruiksdoel_perGridcell/', '#', '')
				// , CompactedAdminDomain
				// , m2PandFootprint
			// ), Descr = "Vertaling/aggregatie van BAG gebruiksdoelen naar Jobs6";
	}
	
	unit<UInt32> NieuwPand_NaRecentJaar := select_with_org_rel(IsNieuwPand_NaRecentJaar) //fka Pand_nieuwbouw
	{
		parameter<Bool>            Issubset        := TRUE;
		
		attribute<rdc_meter>       geometry (poly) := org_rel -> geometry;
		attribute<yr>              Bouwjaar        := org_rel -> Bouwjaar;
		attribute<uint64>          pand_bag_nr     := org_rel -> pand_bag_nr;
		attribute<m2PandFootprint> Footprint       := area(geometry, meter2_f64)[m2PandFootprint];
		attribute<m2_pand>         pand_vbo_opp    := (sum(NieuwVBO_NaRecentJaar/oppervlakte_trunc, NieuwVBO_NaRecentJaar/pand_rel) * 1[verblijfsobject]) / 1[Units/PandUnit];
		
		attribute<UInt32>          count_woon_VBOs := sum_uint32(NieuwVBO_NaRecentJaar/gebruiksdoelen/woon, NieuwVBO_NaRecentJaar/pand_rel);
		attribute<Vastgoed/WP2>    WP2_rel         := count_woon_VBOs == 0 ? (0/0)[Vastgoed/WP2] : count_woon_VBOs >  1 ? Vastgoed/WP2/V/meergezins : Vastgoed/WP2/V/eengezins;
		attribute<Vastgoed/WP4>    WP4_rel         := org_rel -> WP4_rel;
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				,'((sum(NieuwVBO_NaRecentJaar/gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+' ? NieuwVBO_NaRecentJaar/oppervlakte_trunc : 0[m2_vbo], NieuwVBO_NaRecentJaar/pand_rel) * 1[Units/verblijfsobject]) / (pand_vbo_opp * 1[Units/PandUnit])) * Footprint'
				, .
				, m2PandFootprint
			), Descr = "per gebruiksdoel sommeer het vbo-oppervlak per pand, deel dat door het totale vbo-oppervlak in dat pand. Dit geeft de fractie van elke gebruiksdoel in een pand obv vbo-oppervlak. Dit wordt vermenigvuldigd met de pand-footprint om zo de pand-footprint per gebruiksdoel te krijgen.";
			
		container Footprint_perGebruiksdoel_perGridcell := //fka PerGebruiksdoel
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 	'sum(
						recollect_by_cond(IsNieuwPand_NaRecentJaarWelActief, Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+', 0f)
							[BAG_RecentsteJaar_pand/ToedelingsMatrix/Pand_rel] * BAG_RecentsteJaar_pand/ToedelingsMatrix/ToedelingPand, BAG_RecentsteJaar_pand/ToedelingsMatrix/CompactedAdminDomain_rel
					)[m2PandFootprint]'
				, CompactedAdminDomain
				, m2PandFootprint
			), Descr = "per gebruiksdoel de hoeveelheid m2 pandfootpritn per CompactedAdminDomain";
		
		container Footprint_perJob6_perGridcell :=
			for_each_nedv(
				Actor/Jobs6/name
				, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'Footprint_perGebruiksdoel_perGridcell/', '#', '')
				, CompactedAdminDomain
				, m2PandFootprint
			), Descr = "Vertaling/aggregatie van BAG gebruiksdoelen naar Jobs6";
	}
	
	unit<UInt32> WegVBO_NaRecentJaar := select_with_org_rel(IsWegVBO_NaRecentJaar)
	, Descr = "alle VBO's, die in BAG-toekomst nieuw de status ingetrokken hebben, en dus binnenkort uit de voorraad gaan."
	{
		attribute<rdc_meter>            geometry           := org_rel -> geometry;
		attribute<m2_vbo>               oppervlakte_trunc  := org_rel -> oppervlakte_trunc;
		attribute<uint64>               vbo_bag_nr         := org_rel -> vbo_bag_nr;
		attribute<uint64>               pand_bag_nr        := org_rel -> pand_bag_nr;
		attribute<WegPand_NaRecentJaar> Pand_rel           := rlookup(pand_bag_nr, WegPand_NaRecentJaar/pand_bag_nr);
		attribute<Vastgoed/WP2>         WP2_rel            := WegPand_NaRecentJaar/WP2_rel[Pand_rel]; //deze en volgende moeten op basis zijn van wpTyperingen terwijl de bestaande/geloopte op nieuwe snapshots moeten gebeuren.
		attribute<Vastgoed/WP4>         WP4_rel            := WegPand_NaRecentJaar/WP4_rel[Pand_rel];
		
		container gebruiksdoelen :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'org_rel -> Impl/gebruiksdoelen_cbs/'+Vastgoed/vbo_gebruiksdoel_plus/name
				, .
				, bool
			), Descr = "Ophalen gebruiksdoelen voor deze subset";
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'WegPand_NaRecentJaar/Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[Pand_rel]'
				, .
				, m2PandFootprint
			), Descr = "Ophalen footprint per functie uit pandenset naar deze vbo subset.";
		
		container Vbos_InPand_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 'sum(gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+'[uint32], Pand_rel)[float32][(Pand_rel)]'
				, .
				, float32
			), Descr = "Count aantal vbo's per gebruiksdoel in een pand vertaalt naar het vbo";
	}
	
	
	unit<UInt32> WegPand_NaRecentJaar := select_with_org_rel(IsWegPand_NaRecentJaar)
	{
		parameter<Bool>            Issubset        := TRUE;
		
		attribute<rdc_meter>       geometry (poly) := org_rel -> geometry;
		attribute<yr>              Bouwjaar        := org_rel -> Bouwjaar;
		attribute<uint64>          pand_bag_nr     := org_rel -> pand_bag_nr;
		attribute<m2PandFootprint> Footprint       := area(geometry, meter2_f64)[m2PandFootprint];
		attribute<m2_pand>         pand_vbo_opp    := (sum(WegVBO_NaRecentJaar/oppervlakte_trunc, WegVBO_NaRecentJaar/Pand_rel) * 1[verblijfsobject]) / 1[Units/PandUnit];
		
		attribute<UInt32>          count_woon_VBOs := sum_uint32(WegVBO_NaRecentJaar/gebruiksdoelen/woon, WegVBO_NaRecentJaar/Pand_rel);
		attribute<Vastgoed/WP2>    WP2_rel         := count_woon_VBOs == 0 ? (0/0)[Vastgoed/WP2] : count_woon_VBOs >  1 ? Vastgoed/WP2/V/meergezins : Vastgoed/WP2/V/eengezins;
		attribute<Vastgoed/WP4>    WP4_rel         := org_rel -> WP4_rel;
		
		container Footprint_perGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				,'((sum(WegVBO_NaRecentJaar/gebruiksdoelen/'+Vastgoed/vbo_gebruiksdoel_plus/name+' ? WegVBO_NaRecentJaar/oppervlakte_trunc : 0[m2_vbo], WegVBO_NaRecentJaar/Pand_rel) * 1[Units/verblijfsobject]) / (pand_vbo_opp * 1[Units/PandUnit])) * Footprint'
				, .
				, m2PandFootprint
			), Descr = "per gebruiksdoel sommeer het vbo-oppervlak per pand, deel dat door het totale vbo-oppervlak in dat pand. Dit geeft de fractie van elke gebruiksdoel in een pand obv vbo-oppervlak. Dit wordt vermenigvuldigd met de pand-footprint om zo de pand-footprint per gebruiksdoel te krijgen.";
			
		container PerGebruiksdoel :=
			for_each_nedv(
				Vastgoed/vbo_gebruiksdoel_plus/name
				, 	'sum(
						recollect_by_cond(Pand_Nieuwbouw_Selection_condition, Footprint_perGebruiksdoel/'+Vastgoed/vbo_gebruiksdoel_plus/name+', 0f)
							[BAG_RecentsteJaar_pand/ToedelingsMatrix/Pand_rel] * BAG_RecentsteJaar_pand/ToedelingsMatrix/ToedelingPand, BAG_RecentsteJaar_pand/ToedelingsMatrix/CompactedAdminDomain_rel
					)[m2PandFootprint]'
				, CompactedAdminDomain
				, m2PandFootprint
			), Descr = "per gebruiksdoel de hoeveelheid m2 pandfootpritn per CompactedAdminDomain";
		
		container PerJobs6 :=
			for_each_nedv(
				Actor/Jobs6/name
				, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'PerGebruiksdoel/', '#', '')
				, CompactedAdminDomain
				, m2PandFootprint
			), Descr = "Vertaling/aggregatie van BAG gebruiksdoelen naar Jobs6";
	}
	
	container CalcNieuwbouw_perDoel := for_each_ne(vbo_gebruiksdoel_plus/name, 'CalcNieuwbouw_perDoel_T('+quote(vbo_gebruiksdoel_plus/name)+')');
	
	#include<CalcNieuwbouw_perDoel_T.dms>
	#include<Uitsmeren_T.dms>
	
// =================================================================== //
// ============================= RESULTS ============================= //
// =================================================================== //
	
	container Wonen :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'value('
				'(CalcNieuwbouw_perDoel/woon/NieuwVBO_TussenModelenRecentJaar_PerDoel/count_Per_CompactedAdminDomain_WP2/'+Classifications/Vastgoed/WP2xVSSH/WP_name+' + '                  // nieuwe voorraad vbo's tussen model/recent Year 
				 'CalcNieuwbouw_perDoel/woon/NieuwVBO_NaRecentJaarWelActief_PerDoel/count_Per_CompactedAdminDomain_WP2/'+Classifications/Vastgoed/WP2xVSSH/WP_name+' + '                    // nieuwe VBOs na recent Year die toen wel al actief waren
				 'CalcNieuwbouw_perDoel/woon/NieuwVBO_NaRecentJaar_PerDoel/count_Per_CompactedAdminDomain_WP2/'+Classifications/Vastgoed/WP2xVSSH/WP_name+')'                               // nieuwe VBOs na recent Year die ook nog niet actief waren
				'* '+(Classifications/Vastgoed/WP2xVSSH/VSSH_rel = Classifications/Vastgoed/VSSH/V/SocialeHuur ? 'BaseData/StartState/Verdeling_VSSH/per_Gemeente/P_SH_perCompactedAdminDomain' : '(1f - BaseData/StartState/Verdeling_VSSH/per_Gemeente/P_SH_perCompactedAdminDomain)')+'[float32],'
				'Woning'
			')'
			'- CalcNieuwbouw_perDoel/woon/WegVBO_TussenModelenRecentJaar_PerDoel/count_Per_CompactedAdminDomain_WP2xVSSH/'+Classifications/Vastgoed/WP2xVSSH/name+   // gesloopte vbo's tussen model/recent Year
			'- CalcNieuwbouw_perDoel/woon/WegVBO_NaRecentJaar_PerDoel/count_Per_CompactedAdminDomain_WP2xVSSH/'+Classifications/Vastgoed/WP2xVSSH/name               // gesloopte vbo's na recent Year
			, CompactedAdminDomain
			, Woning
		), Descr = "tel nieuwe woon vbo's in nieuwbouw panden na recent jaar, plus nieuwe voorraad vbo's tussen model en recent jaar, uitgesplitst van WP2 naar WP2xVSSH, minus gesloopte vbo's tussen model en recent jaar per WP2xVSSH, per CompactedAdminDomain"
	{	
		attribute<woning> Totaal (CompactedAdminDomain) := ='add('+AsItemList(Classifications/Vastgoed/WP2xVSSH/name)+')';
	}

	container Werken :=
		for_each_nedv(
			Actor/Jobs6/name
			, 'Pandfootprint/'+Actor/Jobs6/name+' / BaseData/Densities/PandFootprint_baan/RESULT/'+Actor/Jobs6/name+'[CompactedAdminDomain/AllocDomain_rel]'
			, CompactedAdminDomain
			, Job
		), Descr = "tel nieuwe pandfooprint in nieuwbouw panden na recent jaar, plus nieuwe voorraad pandfooprint tussen model en recent jaar, minus gesloopte pandfooprint tussen model en recent jaar, per gebruiksdoel, per CompactedAdminDomain gedeeld door de pandfootprint per baan om zo tot een aantal banen per cel te komen door BAG-nieuwbouw toevoegingen en onttrekkingen."
	{	
		attribute<Job> Totaal (CompactedAdminDomain) := ='add('+AsItemList(Actor/Jobs6/name)+')';
	}
	
	container Pandfootprint :=
		for_each_nedv(
			Actor/Jobs6/name
			, '('+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaarWelActief_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
				') < 0[m2PandFootprint]'
			  ' ? max_elem('+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaarWelActief_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
				', -AdminDomain/NrMeter2PerCell[m2PandFootprint])'
			  ' : min_elem('+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaarWelActief_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'+ '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref, '@', 'CalcNieuwbouw_perDoel/', '#', '/NieuwVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
					'- '+replace(Actor/Jobs6/vbo_gebruiksdoel_ref_bagnieuwbouwsloop, '@', 'CalcNieuwbouw_perDoel/', '#', '/WegVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Footprint_Per_CompactedAdminDomain')+
				', AdminDomain/NrMeter2PerCell[m2PandFootprint])'
			, CompactedAdminDomain
			, m2PandFootprint
		), Descr = "tel nieuwe pandfooprint in nieuwbouw panden na recent jaar, plus nieuwe voorraad pandfooprint tussen model en recent jaar, minus gesloopte pandfooprint tussen model en recent jaar, en na recentjaar per gebruiksdoel
		, per CompactedAdminDomain op. En corrigeer voor max cell size. Dit kan groter zijn door overlappende panden.";
	
	container Verblijfsrecreatie
	{
		attribute<verblijfsobject> Totaal (CompactedAdminDomain) := 
			value(CalcNieuwbouw_perDoel/logies/NieuwVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Aantal_Vbos_Per_CompactedAdminDomain 
				+ CalcNieuwbouw_perDoel/logies/NieuwVBO_NaRecentJaarWelActief_PerDoel/Uitsmeren_Totaal/Aantal_Vbos_Per_CompactedAdminDomain 
				+ CalcNieuwbouw_perDoel/logies/NieuwVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Aantal_Vbos_Per_CompactedAdminDomain 
				- CalcNieuwbouw_perDoel/logies/WegVBO_TussenModelenRecentJaar_PerDoel/Uitsmeren_Totaal/Aantal_Vbos_Per_CompactedAdminDomain
				- CalcNieuwbouw_perDoel/logies/WegVBO_NaRecentJaar_PerDoel/Uitsmeren_Totaal/Aantal_Vbos_Per_CompactedAdminDomain
				, verblijfsobject);
	}
	
	container Wind
	{
		attribute<MW>    Totaal                (CompactedAdminDomain) := const(0[MW], CompactedAdminDomain);
		attribute<Yr>    jaartal_ingebruikname (CompactedAdminDomain) := Totaal > 0[MW] ? BAG_RecentYear[Yr] : (0/0)[Yr];
		attribute<Meter> MaxWerpAfstandNom     (CompactedAdminDomain) := const(null_f, CompactedAdminDomain, Meter);
		attribute<Meter> Ashoogte              (CompactedAdminDomain) := const(null_f, CompactedAdminDomain, Meter);
		attribute<Meter> Rotordiameter         (CompactedAdminDomain) := const(null_f, CompactedAdminDomain, Meter);
	}
	
	container Zon
	{
		attribute<MW> Totaal (CompactedAdminDomain) := const(0[MW], CompactedAdminDomain);
	}
	
}