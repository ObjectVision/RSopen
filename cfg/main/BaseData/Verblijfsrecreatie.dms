container Verblijfsrecreatie
{
	parameter<string> BAG_RecentYear := /ModelParameters/BAG_RecentYear; //Vanaf recentyear ipv basisjaar omdat we tussen basisjaar en recentyear uitgaan van de bag nieuwbouw en sloop
	parameter<uint32> BAG_RecentYear_value := substr(BAG_RecentYear,1,5)[uint32];
	parameter<string> BBG_Year := /ModelParameters/BBG_Year;
	parameter<uint32> BBG_Year_value := BBG_Year[uint32];
	
	unit<uint32> jaren96_11 := range(uint32, 1996, 2012)
	{
		attribute<string> label := name , DialogType = "LabelText";
		attribute<string> name  := 'Y' + string(id(.));
		attribute<uint32> value := id(.);
		container V := for_each_nedv(name, String(ID(.))+'[..]', void, .);
	}
	
	unit<uint8> Jaren := union_unit_uint8(jaren96_11, SourceData/Vastgoed/BAG/BAG_jaren)
	{
		attribute<string>  name  := union_data(., jaren96_11/name, SourceData/Vastgoed/BAG/BAG_jaren/name);
		attribute<string>  label := name, DialogType = "LabelText";
		attribute<float32> value := union_data(., jaren96_11/value[float32], SourceData/Vastgoed/BAG/BAG_jaren/Jaar[float32]);
		
		container V := for_each_nedv(name, String(ID(.))+'[..]', void, .);
	}
	
	container Per_AllocRegio := for_each_ne(VariantParameters/ThisVariantSectorAllocRegio/uq_AllocRegios/AllocRegio_name, 'PerRegioUnit_T('+quote(VariantParameters/ThisVariantSectorAllocRegio/uq_AllocRegios/AllocRegio_name)+')');
	
	#include<PerRegioUnit_T.dms>
	#include<ReadData_BAG_T.dms>
	#include<PreBAG_PerJaar_T.dms>
	
	Template ReadData_BBG_T
	{
		//
		parameter<uint32> Year;
		unit<uint32> RegioUnit;
		//
		
		attribute<Ha> BrutoOpp          (AdminDomain):= ='SourceData/Grondgebruik/BBG/Dominant/per_AdminDomain_perKlasse/J'+string(Year)+'/PerKlasse/Verblijfsrecreatie && AdminDomain/IsNederland ? AdminDomain/NrHaPerCell : 0[ha]';
		attribute<Ha> BrutoOpp_perRegio (RegioUnit)  := sum(BrutoOpp, RegioUnit/Per_AdminDomain);
		
		parameter<Ha> Sum_BrutoOpp := sum(BrutoOpp);
	}
	
	Template ZichtjaarT
	{
		//
		parameter<string> Zichtjaar_name;
		parameter<string> RegioUnit_str;
		//
		
		unit<uint32> PerRegioUnit := =ModelParameters/VariantDataOntkoppeld ? 'Read_PerRegioUnit' : 'Make_PerRegioUnit';
		
		unit<uint32> Read_PerRegioUnit := RegioUnit, StorageName = "='%LocalDataDir%/RSopen/VariantData/Verblijfsrecreatie_Claims/'+RegioUnit_str+'_'+/Variant_name+'.fss'", StorageReadOnly = "true"
		{
			attribute<verblijfsobject> Zichtjaar_Objecten;
			attribute<vbo_ha>          Zichtjaar_Object_perHa;
		}
		
		unit<uint32> Make_PerRegioUnit := Calc_PerRegioUnit, StorageName = "='%LocalDataDir%/RSopen/VariantData/Verblijfsrecreatie_Claims/'+RegioUnit_str+'_'+/Variant_name+'.fss'"
		{
			attribute<verblijfsobject> Zichtjaar_Objecten     := Calc_PerRegioUnit/Zichtjaar_Objecten;
			attribute<vbo_ha>          Zichtjaar_Object_perHa := Calc_PerRegioUnit/Zichtjaar_Object_perHa;
		}
		
		unit<uint32> Calc_PerRegioUnit := RegioUnit
		{
			attribute<verblijfsobject> Zichtjaar_Objecten     := = 'Claims/PerRegio_Objecten/'+Zichtjaar_name+'/objects';
			attribute<Ha>              Zichtjaar_BrutoOpp     := = 'Claims/PerRegio_BrutoOpp/'+Zichtjaar_name+'/BrutoOpp';
			attribute<vbo_ha>          Zichtjaar_Object_perHa := Zichtjaar_Objecten / Zichtjaar_BrutoOpp;
		}
	}
}