container Wonen  // Woningen
{
	container WP2xVSSHs :=
		for_each_nedv(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Verdeling_VSSH/Per_AdminDomain/'+Classifications/Vastgoed/WP2xVSSH/name
			, CompactedAdminDomain
			, Woning
		), Descr = "Ophalen resultaat uit Verdeling_VSSH container. Per CAD aantal woningen per WP2xVSSH";
	
	container WP4 :=
		for_each_nedv(
			Classifications/Vastgoed/WP4/name
			, replace(
				asList('Verdeling_VSSH/Uitsmeren_WP4_'+Classifications/Vastgoed/VSSH/name +'/@WP@/Aantal_Woningen_Per_CompactedAdminDomain',' + ')
				, '@WP@'
				, Classifications/Vastgoed/WP4/name)
			, CompactedAdminDomain
			, Woning
		), Descr = "Ophalen resultaat uit Verdeling_VSSH container. Per CAD aantal woningen per WP4"
	{
		attribute<Woning> Totaal (CompactedAdminDomain) := ='Add('+asItemList(Classifications/Vastgoed/WP4/name)+')';
	}
	
	container WP2_opp :=
		for_each_nedv(
			Classifications/Vastgoed/WP2/name
			, replace(
				asList('Verdeling_VSSH/Uitsmeren_WP2_'+Classifications/Vastgoed/VSSH/name +'/@WP@/VBOopp_Per_CompactedAdminDomain ',' + ')
				, '@WP@'
				, Classifications/Vastgoed/WP2/name)
			, CompactedAdminDomain
			, meter2
		), Descr = "Ophalen resultaat uit Verdeling_VSSH container. Per CAD oppervlakte per WP2";
		
	container WP4_opp :=
		for_each_nedv(
			Classifications/Vastgoed/WP4/name
			, replace(
				asList('Verdeling_VSSH/Uitsmeren_WP4_'+Classifications/Vastgoed/VSSH/name +'/@WP@/VBOopp_Per_CompactedAdminDomain ',' + ')
				, '@WP@'
				, Classifications/Vastgoed/WP4/name)
			, CompactedAdminDomain
			, meter2
		), Descr = "Ophalen resultaat uit Verdeling_VSSH container. Per CAD oppervlakte per WP4";
	
	container WP1s :=
		for_each_neidv(
			Classifications/Vastgoed/WP1/name
			, 'WP4/Totaal'
			, 'not(all(IsNull(this)))'
			, CompactedAdminDomain
			, Woning
		), Descr = "Totaal alle woningen per CAD. Met integritycheck om ervoor te zorgen dat deze set niet volledig undefined is. In het verleden gebeurde dit nog wel eens door een fout elders. Faalt dit, dan zul je moeten gaan tracen waar het vandaan komt."
	{
		attribute<Woning> Per_CompactedAllocDomain (CompactedAllocDomain) := sum(Totaal, CompactedAdminDomain/CompactedAllocDomain_rel);
	}
	
	//
	attribute<meter2> Footprint (CompactedAdminDomain) := Verdeling_VSSH/Footprint_Per_CompactedAdminDomain/Totaal, Descr = "Ophalen resultaat uit Verdeling_VSSH container. Nodig voor lu map";
}