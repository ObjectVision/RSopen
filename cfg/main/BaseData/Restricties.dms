container Restricties
{
	container SrcSrc := SourceData/Omgevingsrecht;
	
	unit<uint8> Sector := Actor/Sector;
	
	unit<uint8> CombiK: NrOfRows = 5
	{
		attribute<string> name:
		[
			'Geen',
			'ZeerHard',
			'ZeerHardHard',
			'ZeerHardHardMiddel',
			'ZeerHardHardMiddelZacht'
		];
		
		attribute<string> label := name, DialogType = "LabelText";
		
		unit<uint32> XSector := combine(., Sector)
		{
			attribute<string> Combi_name := ../name[first_rel];
			attribute<string> Sector_name := Sector/name[second_rel];
			attribute<string> name := Combi_name+'_'+Sector_name;
			attribute<string> label := Combi_name+'/'+Sector_name, DialogType = "LabelText";
		}
	}
	
	container Totaal := = iif(ModelParameters/BaseDataOntkoppeld, 'Read', 'Write')+'Totaal';
	
	container ReadTotaal :=
		for_each_ndva(
			CombiK/XSector/label
			, AdminDomain
			, bool
			, '%LocalDataDir%/RSopen/BaseData/Beleid/Restricties/'+CombiK/XSector/name+'.tif'
		), StorageReadOnly = "true";
	
	container WriteTotaal :=
		for_each_nedva(
			CombiK/XSector/label
			, 'CalcTotaal/'+CombiK/XSector/label
			, AdminDomain
			, bool
			, '%LocalDataDir%/RSopen/BaseData/Beleid/Restricties/'+CombiK/XSector/name+'.tif'
		);
	
	container CalcTotaal :=
		for_each_nedv(
			CombiK/XSector/label
			,replace(
				'max_elem(
					EU/@C@,
					AMVB/@C@,
					Bevi/@C@,'+
					AsItemList('PRV/'+RegioIndelingen/CBS/y2022/Provincie/name+'/@C@')+','+
					AsItemList('PMV/'+RegioIndelingen/CBS/y2022/Provincie/name+'/@C@')+','+
					'Overigen/@C@
				)',
				'@C@', CombiK/XSector/Sector_name+'/Combis/'+CombiK/XSector/Combi_name
			)
			, AdminDomain
			, bool
		);
	
	#include<EU.dms>
	#include<AMVB.dms>
	#include<Bevi.dms>
	#include<PRV.dms>
	#include<PMV.dms>
	#include<Overigen.dms>
}