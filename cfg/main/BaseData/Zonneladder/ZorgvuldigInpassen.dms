container ZorgvuldigInpassen
{
	container LangsInfraWerken
	: Descr = "wegbermen, dijktaluds"
	{
		// TODO: met dynapoint en point_in_polygon relevante bermen selecteren.
		unit<uint32> UitBGT_bermen := select_with_org_rel(/SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/IsStudyArea
																	&& (SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/visualisatiecode_rel == /Classifications/Grondgebruik/BGT/Classes/V/ondersteunendwegdeel_berm 
																	|| SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/visualisatiecode_rel == /Classifications/Grondgebruik/BGT/Classes/V/ondersteunendwegdeel_verkeerseiland
																	))
		{
			attribute<rdc_meter> geometry (poly) := SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/geometry[org_rel];
			
			parameter<LadderTreden> Laddertrede_rel := LadderTreden/V/wegbermen[LadderTreden];
			parameter<float32> fractie := LadderTreden/fractie[float32][LadderTreden/V/wegbermen];
			attribute<meter2>    area               := area(geometry, meter2_f64)[meter2];
			attribute<rdc_meter> Centroid           := centroid_or_mid(geometry);
			attribute<uint32>    bebouwde_kom_rel   := point_in_polygon(centroid, SourceData/RegioIndelingen/Bevolkingskern_2011/geometry);
			
			unit<uint32> buitenbebouwdekom := select_with_org_rel(!IsDefined(bebouwde_kom_rel) && area >= minimale_opp_berm)
			{
				attribute<rdc_meter> geometry (poly) := ../geometry[org_rel];
			}
				
			container per_tile := for_each_ne(
				Geography/TileDomain/name,
				'Templates/Zonneladder_ProcessTilesT('+quote(Geography/TileDomain/name)+', UitBGT_bermen/buitenbebouwdekom, minimale_opp_berm)'
			);
			
			unit<uint32> union_domain := ='union_unit('+AsItemList('per_tile/'+Geography/TileDomain/name+'/Tile_selection/target_geom_in_tile/Multi_to_singlepart_polygons')+')'
			{
				attribute<rdc_meter> geometry (poly) := ='union_data(.,'+AsItemList('per_tile/'+Geography/TileDomain/name+'/Tile_selection/target_geom_in_tile/Multi_to_singlepart_polygons/geometry[rdc_meter]')+')';
				
				container Union := Templates/Zonneladder_Union_T(., geometry, Laddertrede_rel);
				
				attribute<meter2_f64> NrMeter2_max := area(geometry, meter2_f64);
				attribute<meter2>     NrMeter2 := NrMeter2_max[meter2] * fractie;
			}
		}
		
		parameter<meter2> Bermen_opp := sum(UitBGT_bermen/union_domain/NrMeter2);
		parameter<meter2> Totaal_opp := Bermen_opp ; //+ Taluds_opp + Groen_rond_wegen_opp + Gras_rond_wegen_opp;
		
		parameter<GW> Potentieel_gegenereerd_Vermogen_laag := (Totaal_opp / 10000[meter2 / ha]) * Vermogen_ha_laag / 1000[MW / GW];
		parameter<GW> Potentieel_gegenereerd_Vermogen_hoog := (Totaal_opp / 10000[meter2 / ha]) * Vermogen_ha_hoog / 1000[MW / GW];
	}
	
	container IndustrielePlassen 
	: Descr = "bijvoorbeeld operatieve zandwinplassen"
	{
		parameter<LadderTreden> Laddertrede_rel := LadderTreden/V/IndustrielePlassen[LadderTreden];
		parameter<float32> fractie := LadderTreden/fractie[float32][LadderTreden/V/IndustrielePlassen];
		
		unit<uint32>  UitBBG_watermetdelfstoffen := ='select_with_org_rel(SourceData/Grondgebruik/BBG/'+BBG_Bron+'/CBSKlasse_rel_'+ModelParameters/BBG_Year+' == /Classifications/Grondgebruik/CBSKlasse/V/water_met_delfstofwinningsfunctie)'
		{
			attribute<rdc_meter> geometry_rd (poly) := ='SourceData/Grondgebruik/BBG/'+BBG_Bron+'/geometry_simpl[org_rel]';
			
			container Union := Templates/Zonneladder_Union_T(., geometry_rd, Laddertrede_rel);
			
			attribute<meter2_f64> NrMeter2_max (union/unioned_domain) := area(Union/unioned_geometry, meter2_f64);
			attribute<meter2> NrMeter2 (union/unioned_domain) := NrMeter2_max[meter2] * fractie;
		}
		
		parameter<meter2> UitBBG_watermetdelfstoffen_opp := sum(UitBBG_watermetdelfstoffen/NrMeter2);
		parameter<meter2> Totaal_opp := UitBBG_watermetdelfstoffen_opp;
		
		parameter<GW> Potentieel_gegenereerd_Vermogen_laag := (Totaal_opp / 10000[meter2 / ha]) * Vermogen_ha_laag / 1000[MW / GW];
		parameter<GW> Potentieel_gegenereerd_Vermogen_hoog := (Totaal_opp / 10000[meter2 / ha]) * Vermogen_ha_hoog / 1000[MW / GW];
	}
	
	parameter<GW> Potentieel_gegenereerd_Vermogen_laag := LangsInfraWerken/Potentieel_gegenereerd_Vermogen_laag + IndustrielePlassen/Potentieel_gegenereerd_Vermogen_laag;
	parameter<GW> Potentieel_gegenereerd_Vermogen_hoog := LangsInfraWerken/Potentieel_gegenereerd_Vermogen_hoog + IndustrielePlassen/Potentieel_gegenereerd_Vermogen_hoog;
}