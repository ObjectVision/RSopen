container Claims
{
	container WonenEnWerken // Bron: Berekeningen van Significance B.V. in opdracht van PBL in het kader van het project Ruimtelijke Verkenningen 2023. Zie https://www.pbl.nl/publicaties/ruimtelijke-verkenning-2023-vier-scenarios-voor-de-inrichting-van-nederland-in-2050
	{
		parameter<String> ClaimSrcRef: ['%RSLDataDir%/Claims/TigrisXLClaimsPerNVM'];
		parameter<String> ClaimSrcDir := ClaimSrcRef+'/@T@/@S@/@V@/@F@';
		parameter<string> SrcRegioRef: ['NVM'];
		
		unit<uint32> SrcRegio := ='/geography/RegioIndelingen/'+SrcRegioRef;
		
		container Cases := for_each_ne(Modellering/AllCases/name, 'CaseT('+quote(Modellering/AllCases/Scenario_name)+','+quote(Modellering/AllCases/Variant_name)+')');
		
		Template CaseT
		{
			//
			parameter<string> Scenario;
			parameter<string> Variant;
			//
			
			container Tables := for_each_ne(TigrisXL/Table/name, 'DatasetT(TigrisXL/Table/V/'+TigrisXL/Table/name+','+quote(Scenario)+','+quote(Variant)+')');
		}
		
		Template DatasetT
		{
			//
			parameter<TigrisXL/Table> table_id;
			parameter<string> Scenario;
			parameter<string> Variant;
			//
			
			container Zichtjaren := for_each_ne(Time/Tigrisjaar/name, 'T(table_id, '+string(Time/Tigrisjaar/jaar)+'[YearRange],'+quote(Scenario)+','+quote(Variant)+')');
		}
		
		Template T
		{
			//
			parameter<TigrisXL/Table> table_id;
			parameter<YearRange> Jaar;
			parameter<string> Scenario;
			parameter<string> Variant;
			//
			
			parameter<string> FileName_bare := TigrisXL/Table/name[table_id];
			
			parameter<string> File := replace(ClaimSrcDir, '@T@', string(jaar), '@S@', Scenario, '@V@', Variant, '@F@', FileName_bare)+'.csv';
			
			unit<uint32> NVM: StorageName = "= File", StorageType = "gdal.vect", StorageReadOnly = "True";
		}
	}
}