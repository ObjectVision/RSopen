container Distmatrices // := for_each_nex(Matrix/name, 'Impl/'+Matrix/name+'/PotRange', spoint)
{
	unit<UInt32> Dist2Range;
	
	template PotRange
	{
		parameter<meter>   gridgrootte;
		parameter<meter>   afkapstraal;
		parameter<Float32> straal  := afkapstraal / gridgrootte;
		parameter<Float32> oppervlakte  := pi()[float32] * straal^2[float32];
		
		unit<SPoint> tmpRange:= range(spoint, point_yx(-int16(straal), -int16(straal)), point_yx(int16(straal) + 1s, int16(straal) + 1s));
		unit<SPoint> PotRange:= tmpRange // TODO 2017-09-26: remove indirection & clean-up
		{
			attribute<Dist2Range> distMatr := dist2(point_yx(0s, 0s, tmpRange), Dist2Range);
			attribute<Float32> dist := sqrt(float32(distMatr));
			attribute<Float32> dist_scaled := dist / straal;
			
			attribute<Float32> rev_dist        := max_elem(straal - dist, 0f);
			parameter<Float32> rev_dist_max    := max(rev_dist);
			attribute<Float32> rev_dist_scaled := rev_dist / rev_dist_max;
			attribute<Float32> AbsWeight :=
				distMatr[float32] <= (straal * straal) ?
					distMatr[float32] != 0f ?
						1f / (distMatr[float32] ^ 2f) :
						1f :
					0f;
			
			attribute<Float32> AbsWeight_ln :=
				distMatr[float32] <= (straal * straal) ?
					distMatr[float32] != 0f ?
						1f / (distMatr[float32] / straal) :
						1f :
					0f;
			
			attribute<Float32> RelWeight         := scalesum(     AbsWeight , 1.0f );
			attribute<Float32> RelWeightSqrt     := scalesum(Sqrt(AbsWeight), 1.0f );
			attribute<Float32> Flat2             := float32(distMatr <= uint32(straal * straal));
			attribute<Float32> Flat_Visualisatie := dist <= 1.5f ? 1f : 0f;
			attribute<Float32> neighbourhood     := distMatr > 0 ? 1f : 0f;
			parameter<Float32> oppervlakte     := sum(Flat2);
			attribute<Float32> binair          := distMatr <= 100 ? 1f : 0f;
		}
	}
	
	container Impl_5m   := for_each_ne(Matrix/name, 'PotRange(5[meter],'+string(Matrix/size)+'[meter])');
	container Impl_10m  := for_each_ne(Matrix/name, 'PotRange(10[meter],'+string(Matrix/size)+'[meter])');
	container Impl_25m  := for_each_ne(Matrix/name, 'PotRange(25[meter],'+string(Matrix/size)+'[meter])');
	container Impl_100m := for_each_ne(Matrix/name, 'PotRange(100[meter],'+string(Matrix/size)+'[meter])');
}