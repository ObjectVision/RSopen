Template Create_GridDomain_T
{
	parameter<Meter> gridsize;
	parameter<int32> nr_cols := int32((x_max - x_min) / gridsize);
	parameter<int32> nr_rows := int32((y_max - y_min) / gridsize);
	
	unit<ipoint> domain :=
		range(
			 gridset(
				 rdc_meter
				,point_yx(-gridsize, gridsize, rdc_meter)
				,point_yx(y_max, x_min, rdc_meter)
				,ipoint
			)
			,point_yx(0i, 0i)
			,point_yx(nr_rows, nr_cols)
		),	DialogData = "rdc_meter"
	{
		parameter<Meter>                gridsize        := ../gridsize;
		parameter<int32>                nr_cols         := ../nr_cols;
		attribute<rdc_meter>            point           := mapping(., rdc_meter) + point_yx(-0.5f * gridsize, 0.5f * gridsize, rdc_meter);
		unit<uint32> id_key := range(uint32, 0, #domain);
		attribute<uint32>               id_rel   := value(uint32(pointrow(id(.))) * uint32(nr_cols) + uint32(pointcol(id(.))), id_key);
		
		attribute<rdc_5m>               rdc_5m_rel      := mapping(., rdc_5m);
		attribute<rdc_10m>              rdc_10m_rel     := mapping(., rdc_10m);
		attribute<rdc_25m>              rdc_25m_rel     := mapping(., rdc_25m);
		attribute<rdc_100m>             rdc_100m_rel    := mapping(., rdc_100m);
		attribute<rdc_250m>             rdc_250m_rel    := mapping(., rdc_250m);
		attribute<rdc_500m>             rdc_500m_rel    := mapping(., rdc_500m);
		attribute<AdminDomain>          AdminDomain_rel := mapping(., AdminDomain);
		attribute<AllocDomain>          AllocDomain_rel := mapping(., AllocDomain);
		
		parameter<ha>                   NrHaPerCell     := convert(gridsize * gridsize, ha);
		parameter<km2>                  NrKm2PerCell    := NrHaPerCell[km2];
		attribute<bool>                 IsNederland     := IsDefined(poly2grid(SourceData/RegioIndelingen/NL/geometry, .));
	}
	
	unit<uint64> points := select_with_org_rel(isDefined(domain/point) && domain/IsNederland)
	{
		attribute<rdc_meter>            geometry  := org_rel -> point;
		attribute<float32>              x         := PointCol(geometry);
		attribute<float32>              y         := PointRow(geometry);
		attribute<domain>               grid_rel  := geometry[domain];	
	}
}