container Bereikbaarheid
{
	unit<UInt8> AfstandGroen: NrOfRows = 3
	, Descr = "Classificatie van afstand tot groen"
	{
		attribute<meter> Bovengrens: [300, 500, 1000];
		attribute<String> name := 'Tot'+string(Bovengrens)+'m';
		attribute<String> Label := name, DialogType = "LabelText";
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
	}
	
	container OV_halte_bereikbaarheidK 
	: Descr = "Classificatie van bereikbaarheid van OV haltes"
	{
		unit<UInt8> MaatK: NrOfRows = 1
		, Descr = "Classificatie van afstand of reistijd naar dichtstbijzijnde OV halte"
		{
			// attribute<String> SrcRef   : ['afstand', 'reistijd'];
			attribute<String> SrcRef   : ['reistijd'];
			attribute<String> name     := SrcRef;
			attribute<String> Label    := name, DialogType = "LabelText";
		}
		
		unit<UInt8> HhModK: NrOfRows = 1
		, Descr = "Classificatie van het vervoermiddel waarmee de OV halte wordt benaderd"
		{
			// attribute<String> SrcRef: ['walking', 'car',    'bike'];
			// attribute<String> name:   ['voetganger', 'auto',  'fiets'];
			attribute<String> SrcRef: ['walking'];
			attribute<String> name:   ['voetganger'];
			attribute<String> Label := name, DialogType = "LabelText";
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
		
		unit<UInt8> OVModK: NrOfRows = 3
		, Descr = "Classificatie van het OV vervoermiddel van de dichtstbijzijnde OV halte"
		{
			// attribute<String> SrcRef: ['Bus', 'Metro', 'Rail',  'Tram', 'Ferry'];
			// attribute<String> name:   ['Bus', 'Metro', 'Trein', 'Tram', 'Ferry'];
			// attribute<Bool> IsRail:   [FALSE,    TRUE,    TRUE,   TRUE,   FALSE];
			attribute<String> SrcRef: ['Metro', 'Rail',  'Tram'];
			attribute<String> name:   ['Metro', 'Trein', 'Tram'];
			attribute<Bool> IsRail:   [ TRUE,    TRUE,   TRUE];
			attribute<String> Label := name, DialogType = "LabelText";
		}
		
		unit<UInt8> RailModK := select_with_org_rel(OVModK/IsRail)
		, Descr = "Selectie van de OVModK die rail als vervoermiddel heeft"
		{
			attribute<String> SrcRef := OVModK/SrcRef[org_rel];
			attribute<String> name := OVModK/name[org_rel];
			attribute<String> Label := name, DialogType = "LabelText";
		}
		
		unit<UInt8> OVIntK: NrOfRows = 3
		, Descr = "Classificatie van de frequentie van het OV vervoer bij de dichtstbijzijnde OV halte"
		{
			attribute<String> SrcRef: ['Weinig', 'Gemiddeld', 'Veel'];
			attribute<String> name:   ['Laag', 'Midden',      'Hoog'];
			attribute<String> Label := name, DialogType = "LabelText";
		}
		
		unit<UInt32> RailModKXOVIntK := combine(RailModK, OVIntK)
		, Descr = "Combinatie (Carthesiaans product) van RailModK en OVIntK"
		{
			attribute<String> RailModK_name := RailModK/name[first_rel];
			attribute<String> OVIntK_name := OVIntK/name[second_rel];
			attribute<String> name := RailModK_name+'/'+OVIntK_name;
			attribute<String> Label := replace(name, '/', '_'), DialogType = "LabelText";
		}
		
		unit<UInt32> CombiK0 := combine(OVModK, OVIntK)
		, Descr = "Combinatie (Carthesiaans product) van OVModK en OVIntK"
		{
			attribute<String> OVModK_name := OVModK/name[first_rel];
			attribute<String> OVIntK_name := OVIntK/name[second_rel];
			attribute<String> name := OVModK_name+'/'+OVIntK_name;
			attribute<String> Label := replace(name, '/', '_'), DialogType = "LabelText";
		}
		
		unit<UInt32> CombiK := combine(MaatK, HhModK, OVModK, OVIntK)
		, Descr = "Combinatie (Carthesiaans product) van MaatK, HhModK, OVModK en OVIntK"
		{
			attribute<String> MaatK_SrcRef := MaatK/SrcRef[first_rel];
			attribute<String> HhModK_SrcRef := HhModK/SrcRef[second_rel];
			attribute<String> OVModK_SrcRef := OVModK/SrcRef[third_rel];
			attribute<String> OVIntK_SrcRef := OVIntK/SrcRef[fourth_rel];
			attribute<String> MaatK_name := MaatK/name[first_rel];
			attribute<String> HhModK_name := HhModK/name[second_rel];
			attribute<String> OVModK_name := OVModK/name[third_rel];
			attribute<String> OVIntK_name := OVIntK/name[fourth_rel];
			attribute<String> name := MaatK_name+'/'+HhModK_name+'/'+OVModK_name+'/'+OVIntK_name;
			attribute<String> Label := replace(name, '/', '_'), DialogType = "LabelText";
		}
	}
}