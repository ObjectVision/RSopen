container Modellering : using = "Actor"
{
	unit<uint32> TIGRIS_Variants_IncludedList : NrOfRows = 3
	{
		attribute<string> name :['BAU','Verdichting', 'Uitleg'];
	}
	
	unit<uint32> TIGRIS_Variants_WLO_Laag := TIGRIS_Variants_IncludedList
	{
		attribute<string> Scenario_name  := const('WLO_laag', .);
		attribute<string> Variant_name   := TIGRIS_Variants_IncludedList/name;
	}
	
	unit<uint32> TIGRIS_Variants_WLO_Hoog := union_unit_uint32(VariantParameters/VariantK)
	{
		attribute<string> Scenario_name  := const('WLO_hoog', .);
		attribute<string> Variant_name   := union_data(., VariantParameters/VariantK/name);
	}
	
	unit<uint32> TIGRIS_Variants := union_unit(TIGRIS_Variants_WLO_Laag, TIGRIS_Variants_WLO_Hoog)
	{
		attribute<string> Scenario_name  := union_data(.,TIGRIS_Variants_WLO_Laag/Scenario_name, TIGRIS_Variants_WLO_Hoog/Scenario_name);
		attribute<string> Variant_name   := union_data(.,TIGRIS_Variants_WLO_Laag/Variant_name, TIGRIS_Variants_WLO_Hoog/Variant_name);
	}
	
	unit<uint32> Scenario_x_Variant_x_Zichtjaar := combine(TIGRIS_Variants, Time/Zichtjaar)
	{
		attribute<string> name           := Scenario_name + '_' + Variant_name + '_' + Zichtjaar_name;
		attribute<string> Scenario_name  := TIGRIS_Variants/Scenario_name[first_rel];
		attribute<string> Variant_name   := TIGRIS_Variants/Variant_name[first_rel];
		attribute<string> Zichtjaar_name := Time/Zichtjaar/name[second_rel];
	}

	unit<uint8> NoData: NrOfRows = 1
	{
		attribute<string> name: ['geen'];
		attribute<string> Label := name, DialogType = "LabelText";
		// attribute<uint32> Geen (AllocDomain) := const(0/0, AllocDomain);
	}
	
	unit<uint32> StandVar_Prep_wWerken_wWind := union_unit(Sector/xSubsector, Sector/xSubsectoren/Werken/Subsector, WindK)
	{
		attribute<string> name                 := union_data(., Sector/xSubsector/StandVar_name,      Sector/xSubsectoren/Werken/Subsector/StandVar_PF_name,      WindK/StandVar_Jaar_name);
		attribute<string> Subsector_name       := union_data(., Sector/xSubsector/Subsector_name,     Sector/xSubsectoren/Werken/Subsector/Subsector_name,        WindK/Subsector_name);
		attribute<string> path                 := union_data(., Sector/xSubsector/StandVar_path,      Sector/xSubsectoren/Werken/Subsector/StandVar_PF_path,      WindK/StandVar_Jaar_path);
		attribute<string> Sector_name          := union_data(., Sector/xSubsector/Sector_name,        Sector/xSubsectoren/Werken/Subsector/Sector_name,           WindK/Sector_name);
		attribute<string> unit_name            := union_data(., Sector/xSubsector/ValUnit_ref,        Sector/xSubsectoren/Werken/Subsector/StandVar_PF_unit,      WindK/StandVar_Jaar_unit);
		attribute<string> value_Type           := union_data(., Sector/xSubsector/StandVar_valueType, Sector/xSubsectoren/Werken/Subsector/StandVar_PF_valueType, WindK/StandVar_Jaar_valueType);
		
		attribute<bool>   HasClaims            := union_data(., Sector/xSubsector/HasClaims,           Sector/xSubsectoren/Werken/Subsector/HasClaims_PF,           const(FALSE, WindK));
		attribute<bool>   HasAlloc             := union_data(., Sector/xSubsector/HasAlloc,            Sector/xSubsectoren/Werken/Subsector/HasAlloc_PF,            const(FALSE, WindK));
		attribute<bool>   IsVastgoed           := union_data(., Sector/xSubsector/IsVastgoed,          Sector/xSubsectoren/Werken/Subsector/IsVastgoed_PF,          const(FALSE, WindK));
		attribute<bool>   UseForLeegstandCorr  := union_data(., Sector/xSubsector/UseForLeegstandCorr, Sector/xSubsectoren/Werken/Subsector/UseForLeegstandCorr_PF, const(FALSE, WindK));
	}
	
	unit<uint32> StandVar_Prep_wWerken_woWind  := union_unit(Sector/xSubsector, Sector/xSubsectoren/Werken/Subsector)
	{
		attribute<string> name                 := union_data(., Sector/xSubsector/StandVar_name,      Sector/xSubsectoren/Werken/Subsector/StandVar_PF_name);
		attribute<string> Subsector_name       := union_data(., Sector/xSubsector/Subsector_name,     Sector/xSubsectoren/Werken/Subsector/Subsector_name);
		attribute<string> path                 := union_data(., Sector/xSubsector/StandVar_path,      Sector/xSubsectoren/Werken/Subsector/StandVar_PF_path);
		attribute<string> Sector_name          := union_data(., Sector/xSubsector/Sector_name,        Sector/xSubsectoren/Werken/Subsector/Sector_name);
		attribute<string> unit_name            := union_data(., Sector/xSubsector/ValUnit_ref,        Sector/xSubsectoren/Werken/Subsector/StandVar_PF_unit);
		attribute<string> value_Type           := union_data(., Sector/xSubsector/StandVar_valueType, Sector/xSubsectoren/Werken/Subsector/StandVar_PF_valueType);
		
		attribute<bool>   HasClaims            := union_data(., Sector/xSubsector/HasClaims,           Sector/xSubsectoren/Werken/Subsector/HasClaims_PF);
		attribute<bool>   HasAlloc             := union_data(., Sector/xSubsector/HasAlloc,            Sector/xSubsectoren/Werken/Subsector/HasAlloc_PF);
		attribute<bool>   IsVastgoed           := union_data(., Sector/xSubsector/IsVastgoed,          Sector/xSubsectoren/Werken/Subsector/IsVastgoed_PF);
		attribute<bool>   UseForLeegstandCorr  := union_data(., Sector/xSubsector/UseForLeegstandCorr, Sector/xSubsectoren/Werken/Subsector/UseForLeegstandCorr_PF);
	}
	 
	unit<uint32> StandVar_Prep_woWerken_woWind := Sector/xSubsector
	{
		attribute<string> name                 := Sector/xSubsector/StandVar_name;
		attribute<string> Subsector_name       := Sector/xSubsector/Subsector_name;
		attribute<string> path                 := Sector/xSubsector/StandVar_path;
		attribute<string> Sector_name          := Sector/xSubsector/Sector_name;
		attribute<string> unit_name            := Sector/xSubsector/ValUnit_ref;
		attribute<string> value_Type           := Sector/xSubsector/StandVar_valueType;
		
		attribute<bool>   HasClaims            := Sector/xSubsector/HasClaims;
		attribute<bool>   HasAlloc             := Sector/xSubsector/HasAlloc;
		attribute<bool>   IsVastgoed           := Sector/xSubsector/IsVastgoed;
		attribute<bool>   UseForLeegstandCorr  := Sector/xSubsector/UseForLeegstandCorr;
	}
	
	unit<uint32> StandVar_Prep_woWerken_wWind := union_unit(Sector/xSubsector, WindK)
	{
		attribute<string> name                 := union_data(., Sector/xSubsector/StandVar_name,      WindK/StandVar_Jaar_name);
		attribute<string> Subsector_name       := union_data(., Sector/xSubsector/Subsector_name,     WindK/Subsector_name);
		attribute<string> path                 := union_data(., Sector/xSubsector/StandVar_path,      WindK/StandVar_Jaar_path);
		attribute<string> Sector_name          := union_data(., Sector/xSubsector/Sector_name,        WindK/Sector_name);
		attribute<string> unit_name            := union_data(., Sector/xSubsector/ValUnit_ref,        WindK/StandVar_Jaar_unit);
		attribute<string> value_Type           := union_data(., Sector/xSubsector/StandVar_valueType, WindK/StandVar_Jaar_valueType);
		
		attribute<bool>   HasClaims            := union_data(., Sector/xSubsector/HasClaims,           const(FALSE, WindK));
		attribute<bool>   HasAlloc             := union_data(., Sector/xSubsector/HasAlloc,            const(FALSE, WindK));
		attribute<bool>   IsVastgoed           := union_data(., Sector/xSubsector/IsVastgoed,          const(FALSE, WindK));
		attribute<bool>   UseForLeegstandCorr  := union_data(., Sector/xSubsector/UseForLeegstandCorr, const(FALSE, WindK));
	}
	
	unit<uint32> StandVar_Prep := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWerkenSector && /VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector
										? 'StandVar_Prep_wWerken_wWind' 
										: /VariantParameters/SectorAllocRegio/Uq_Sectors/HasWerkenSector && NOT(/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector)
											? 'StandVar_Prep_wWerken_woWind'
											: NOT(/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWerkenSector) && /VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector
												? 'StandVar_Prep_woWerken_wWind'
												: 'StandVar_Prep_woWerken_woWind'
	{
		unit<float32> Woning    := Units/Woning;
		unit<float32> Job       := Units/Job;
		unit<float32> meter2    := Units/meter2;
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		
		attribute<bool> DeltaVar_Prep_cond := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector ? 'id(.) <> V/jaartal_ingebruikname_Wind' : 'const(TRUE,.)';
	}
	
	unit<uint32> DeltaVar_Prep := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector 
										? 'select_with_org_rel(id(StandVar_Prep) <> StandVar_Prep/V/jaartal_ingebruikname_Wind)'
										: 'select_with_org_rel(IsDefined(id(StandVar_Prep)))'
	{
		attribute<string> name := StandVar_Prep/name[org_rel];
		attribute<string> path := StandVar_Prep/path[org_rel];
		attribute<string> Sector_name := StandVar_Prep/Sector_name[org_rel];
		attribute<string> unit_name := StandVar_Prep/unit_name[org_rel];
		attribute<string> value_Type := StandVar_Prep/value_Type[org_rel];
		
		unit<float32> Woning    := Units/Woning;
		unit<float32> Job       := Units/Job;
		unit<float32> meter2    := Units/meter2;
	}
	
	unit<uint32> AllCases := combine(ScenarioK, VariantParameters/VariantK)
	{
		attribute<string> Scenario_name := ScenarioK/name[first_rel];
		attribute<string> Variant_name  := VariantParameters/VariantK/name[second_rel];
		attribute<string> name          := Scenario_name+'/'+Variant_name;
		attribute<string> Label         := Scenario_name+'_'+Variant_name;
		attribute<bool>   IsBAU         := lowercase(Variant_name) == lowercase('BAU');
		
		unit<uint32> NotBAU := select_with_attr_by_cond(., !IsBAU);
	}
	
	unit<uint8> DomainK: NrOfRows = 2
	{
		attribute<string> name := union_data(., 'AllocDomain', 'AdminDomain');
	}
	
	unit<uint8> AllocRegiosK: NrOfRows = 4
	{
		attribute<string> name: ['NVM', 'COROP', 'Provincie', 'NL'];
		attribute<string> Label := name, DialogType = "LabelText";
		attribute<string> ref := name;
		attribute<string> path_src : ['','CBS/Last/','CBS/Last/',''];
		attribute<string> path     := 'RegioIndelingen/'+path_src+name;
		
		attribute<AllocRegiosK> Parent_rel := union_data(AllocRegiosK
		,	V/Provincie 
		,	V/Provincie
		,	V/NL
		,	uint8(0/0)
		);
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
	}
	
	unit<uint8> OrdeningsCriterium: NrOfRows = 2
	{
		attribute<string> name: ['Geschiktheid', 'Dichtheid'];
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
	}
	
	unit<uint8> ScenarioK: NrOfRows = 2
	{
		attribute<string> name : ['WLO_laag', 'WLO_hoog'];
		attribute<string> TableRef : ['laag', 'hoog'];
		attribute<bool> IsVoorExport: [FALSE, TRUE];
		
		container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
	}
	
	unit<uint8> VergelijkRegiosK: NrOfRows = 5
	{
		attribute<string> name: ['LMSSubzone', 'Gemeente', 'NVM', 'COROP', 'Provincie'];
		attribute<string> SrcRef:
		[
			'/Geography/RegioIndelingen/LMSSubzone',
			'/Geography/RegioIndelingen/Gemeente2019_single',
			'/Geography/RegioIndelingen/NVM',
			'/Geography/RegioIndelingen/COROP',
			'/Geography/RegioIndelingen/Provincie'
		];
		
		container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	unit<uint32> BBGK: NrOfRows = 2
	{
		attribute<string> name: ['BBG', 'Geen_BBG'];
		attribute<string> Label := name, DialogType = "LabelText";
		container V := for_each_nedv(name, string(id(.)) + '[..]', void, .);
	}
	
	unit<uint8> DichtheidRegio: NrOfRows = 6
	{
		attribute<string> name: ['Buurt', 'Wijk', 'Gemeente', 'NVM', 'COROP', 'Provincie'];
		attribute<string> SrcRef := 'RegioIndelingen/'+ union_data(., 'Buurt/Src_2019', 'Wijk/Src_2019', 'Gemeente2019_single', 'NVM_2011', 'COROP', 'Provincie');
	}
	
	unit<uint8> HardheidK: NrOfRows = 2
	{
		attribute<string> name: ['Hard', 'Zacht'];
	}
	
	unit<uint32> HardheidXDomainK := combine(HardheidK, domainK)
	{
		attribute<string> HardheidK_name := HardheidK/name[first_rel];
		attribute<string> domainK_name := domainK/name[second_rel];
		attribute<string> name := HardheidK_name+'/'+domainK_name;
	}
	
	unit<uint32> MaatregelK: NrOfRows = 3
	{
		attribute<string> name: ['Bouwen', 'Maatregelen', 'NietBouwen'];
		attribute<string> Label := name, DialogType = "LabelText";
		attribute<uint32> BrushColor : DialogType = "BrushColor",
		[
			rgb(127,127,127), rgb(188,184,217), rgb( 63,  0,125)
		];
		
		container V := for_each_nedv(name, string(id(.)) + '[..]', void, .);
	}
}