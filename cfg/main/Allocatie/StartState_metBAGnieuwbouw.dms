container StartState_metBAGnieuwbouw :=
	for_each_nedv(
		Sector/XSubsector/wo_landbouw/name
		, 'max_elem(MakeDefined(StartState/'+Sector/XSubsector/wo_landbouw/name+' + BaseData/StartState/BAG_nieuwbouw/'+Sector/XSubsector/wo_landbouw/name+', 0f), 0f)'
		, CompactedAdminDomain
		, float32
	), using = "CaseClassifications/Vastgoed/OP"
{
	// pas op: dat er nieuwbouw van bepaalde subsector heeft plaatsgevonden betekent niet dat betreffende subsector dominant is in een cel
	attribute<Sector/XSubsector> Subsector_rel_sw_case (CompactedAdminDomain) :=
		=#Sector/XSubsector/wo_landbouw > 0  
			? 'switch(
				'+AsList('case(BaseData/StartState/BAG_nieuwbouw/'+Sector/XSubsector/wo_landbouw/name+' > 0f, '+string(id(Sector/XSubsector/wo_landbouw))+'[Sector/XSubsector])', ',')+'
				, (0f/0f)[Sector/XSubsector]
				)'
			: 'const(null_f, CompactedAdminDomain, Sector/XSubsector)';
		
	attribute<Sector/XSubsector> Subsector_rel (CompactedAdminDomain) :=
		= #Sector/XSubsector/wo_landbouw > 0 
			? 'argmax_ifdefined(
					'+AsList('value(BaseData/StartState/BAG_nieuwbouw/'+Sector/XSubsector/wo_landbouw/name+', float32) != 0f 
								? value(BaseData/StartState/BAG_nieuwbouw/'+Sector/XSubsector/wo_landbouw/name+', float32) 
								: null_f'
						, ',')+'
				)'
			: 'const(null_f, CompactedAdminDomain, Sector/XSubsector)';
	
	attribute<CaseClassifications/Vastgoed/OP> OP_rel (CompactedAdminDomain) := const(0b/0b, CompactedAdminDomain, CaseClassifications/Vastgoed/OP);
	
	container PandFootprint :=
		for_each_nedv(
			Jobs6/name
			,'StartState/PandFootprint/'+Jobs6/name+' + BaseData/StartState/BAG_nieuwbouw/Pand_nieuwbouw/PerJobs6/'+Jobs6/name
			, CompactedAdminDomain
			, m2PandFootprint
		);
	
	container Wonen
	{
		attribute<woning> Totaal (CompactedAdminDomain) := ='add('+AsItemList(Classifications/Vastgoed/WP2xVSSH/name)+')';
	}
	
	container Wind
	{
		attribute<Yr>        jaartal_ingebruikname (CompactedAdminDomain) := IsDefined(StartState/Wind/jaartal_ingebruikname) ? StartState/Wind/jaartal_ingebruikname : BaseData/StartState/BAG_nieuwbouw/Wind/jaartal_ingebruikname;
	}
	
	container Landbouw := 
		for_each_nedv(
			LandbouwKlasses/name
			, 'const(null_f,CompactedAdminDomain,ha)'
			, CompactedAdminDomain
			, ha
		);
}