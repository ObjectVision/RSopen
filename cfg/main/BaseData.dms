////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Dit is RSOpen, de open source versie van het model RuimteScanner. Het scipt wordt uitgegeven onder GNU-GPL licentie.
//
// RSOpen is ontwikkeld door PBL Planbureau voor de Leefomgeving, i.s.m Object Vision B.V. en VU Vrije Universiteit Amsterdam.
// Opdrachtgever/ontwikkelaar PBL: Bart Rijken
// Contactpersoon/ontwikkelaar Object Vision B.V.: Jip Claassens (jclaassens@objectvision.nl)
// Contacpersoon PBL: Bas van Bemmel (Bas.vanBemmel@pbl.nl)
//
// Deze file bevat de zgn. basedata die het model nodig heeft voor de verschillende berekeningen/analyses. Basedata zijn altijd afgeleid van de SourceData en soms ontkoppeld (geexporteerd vanuit Sourcedata).
// De data worden on-the-fly gegenereerd of, indien reeds gegenereerd (ontkoppeld), ingelezen vanuit de LocalDataDir.
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

container BaseData: using = "Classifications;units;Geography"
{
	parameter<string> BAG_RecentYear := ModelParameters/BAG_RecentYear;
	parameter<string> BAG_ModelYear  := 'Y'+string(ModelParameters/Model_Startyear);
	
	container BAG            := /SourceData/Vastgoed/BAG/PerJaar;
	container BAG_ModelJaar  := = 'BAG/'+BAG_ModelYear;
	container BAG_RecentJaar := = 'BAG/'+BAG_RecentYear;
	
	attribute<bool> StudieGebied_AdminDomain (AdminDomain) := SourceData/RegioIndelingen/NL/IsNederland_AdminDomain;
	attribute<bool> StudieGebied_AllocDomain (AllocDomain) := SourceData/RegioIndelingen/NL/IsNederland_AllocDomain;
	
	attribute<Verblijfsobject> VBOs_ModelJaar (AdminDomain) := BAG_ModelJaar/VBO/count_vbo_alle[Verblijfsobject]
	{
		attribute<m2_vbo> Oppervlakte (AdminDomain) := sum(BAG_ModelJaar/VBO/oppervlakte_trunc, BAG_ModelJaar/VBO/AdminDomain_rel)
		{
			container Per_gebruiksdoel :=
				for_each_nedv(
					vastgoed/vbo_gebruiksdoel/name,
					'sum(
						BAG_ModelJaar/VBOs/GebruiksdoelSets/'+vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/oppervlakte_trunc,
						BAG_ModelJaar/VBOs/GebruiksdoelSets/'+vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/AdminDomain_rel 
					)',
					AdminDomain, m2_vbo
			);
		}
		
		container Per_gebruiksdoelSrc :=
			for_each_nedv(
				vastgoed/vbo_gebruiksdoel/name
				,'BAG_ModelJaar/VBOs/GebruiksdoelSets/'+vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/count_per_AdminDomain'
				, AdminDomain
				, Verblijfsobject
		);
		
		container Per_gebruiksdoel :=
			for_each_nedv(
				vastgoed/vbo_gebruiksdoel/name
				,'Per_gebruiksdoelSrc/'+vastgoed/vbo_gebruiksdoel/name+' / AdminDomain/NrHaPerCell'
				, AdminDomain
				, vbo_ha
		);
	}
	
	#include<StateBasisjaar.dms>
	#include<PandFootprint.dms>
	#include<PandFootprint_baan.dms>
	#include<Geschiktheden.dms>
	#include<BAG_nieuwbouw.dms>
	#include<Grondgebruik.dms>
	#include<Eigendom.dms>
	#include<Verdeling_VSSH.dms>
	#include<Afleiding_Winkelcentra.dms>
	#include<AlternatieveLISA.dms>
	#include<OV_Reistijd.dms>
	#include<Restricties.dms>
	#include<Stimuli.dms>
	#include<Zonneladder.dms>
	#include<Verblijfsrecreatie.dms>
	
	unit<uint32> RondBevolkingskern := SourceData/RegioIndelingen/Bevolkingskern_2011
	{
		attribute<bool> Is300kPlus (AllocDomain) := MetMeerdan300Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "300K";
		attribute<bool> Is100kPlus (AllocDomain) := MetMeerdan100Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "100K";
		attribute<bool> Is50kPlus  (AllocDomain) := MetMeerdan50Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "50K";
		attribute<bool> Is50kMin   (AllocDomain) := MetMinderdan50Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "50K";
		attribute<bool> Is20kPlus  (AllocDomain) := MetMeerdan20Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "25K";
		attribute<bool> Is20kMin   (AllocDomain) := MetMinderdan20Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "20K";
		attribute<bool> Is5kPlus   (AllocDomain) := MetMeerdan5Kinw/split_geometry/MetBuffer/Is_Buffer, Label = "5K";
	}
	
	attribute<float32> BelevingLandschap (AllocDomain) := /SourceData/Diversen/Per_AllocDomain/BelevingLandschap/NoMissing
	{
		attribute<bool> IsKleinerDanZesPunt5  (AllocDomain) := . < 6.5f;
		attribute<bool> IsGroterDanZeven      (AllocDomain) := . > 7.0f;
		attribute<bool> IsGroterDanZevenPunt3 (AllocDomain) := . > 7.3f;
		attribute<bool> IsZeerLaag            (AllocDomain) := IsKleinerDanZesPunt5;
		attribute<bool> IsLaag                (AllocDomain) := !IsHoog;
		attribute<bool> IsHoog                (AllocDomain) := IsGroterDanZeven;
		attribute<bool> IsZeerHoog            (AllocDomain) := IsGroterDanZevenPunt3;
		attribute<bool> IsZeerZeerHoog        (AllocDomain) := . > 7.6f;
		attribute<float32> Potential          (AllocDomain) := potential(., Distmatrices/impl_100m/pot2000m/potrange/rev_dist_scaled) / sum(Distmatrices/impl_100m/pot2000m/potrange/rev_dist_scaled)
		{
			attribute<bool> IsGroterDanZeven      (AllocDomain) := . >= 7.0f;
			attribute<bool> IsGroterDanZevenPunt3 (AllocDomain) := . >= 7.3f;
			attribute<bool> IsHoog                (AllocDomain) := IsGroterDanZeven
			{
				attribute<bool> EnBelevingIsNietHoog (AllocDomain) := IsHoog && !BelevingLandschap/IsHoog;
				attribute<bool> EnBelevingIsLaag     (AllocDomain) := IsHoog && BelevingLandschap/IsLaag;
			}
			
			attribute<bool> IsZeerHoog (AllocDomain) := IsGroterDanZevenPunt3
			{
				attribute<bool> EnBelevingIsNietHoog (AllocDomain) := IsZeerHoog && !BelevingLandschap/IsHoog;
			}
		}
	}
	
	attribute<bool> BBG (AllocDomain) := IsDefined(SourceData/RegioIndelingen/Bevolkingskern_2011/per_AllocDomain)
	{
		attribute<bool> per_AdminDomain (AdminDomain) := IsDefined(SourceData/RegioIndelingen/Bevolkingskern_2011/per_AdminDomain);
	}
	
	attribute<Classifications/overstromingsgevaarzonering/zoneringK> Overstromingsgevaarzonering (AdminDomain) := /SourceData/Water/overstromingsgevaarzonering;
	
	attribute<meter> Overstromingsdiepte              (AdminDomain) := value(SourceData/Water/dieptekaart == -9999f ? const(0f, AdminDomain) : SourceData/Water/dieptekaart, meter);
	attribute<bool> HogeGronden                       (AdminDomain) := !(Overstromingsdiepte > 0[Meter]);
	attribute<bool> Gebiedsreservering                (AdminDomain) := /SourceData/Omgevingsrecht/AMvB/NL_IMRO_0000_BZKamGCBarro_5025/Kaart4/gebiedsreservering/Multi/Result;
	attribute<bool> Stroomvoerend_deel_rivierbed      (AdminDomain) := /SourceData/Omgevingsrecht/AMvB/NL_IMRO_0000_BZKamGCBarro_5025/Kaart4/stroomvoerend_deel_rivierbed/Multi/Result;
	attribute<bool> N2K1000mBuffer0                   (AdminDomain) := ='potential(float32(/SourceData/Omgevingsrecht/EU/Natura2000/Result), /Geography/Distmatrices_'+AdminDomain_ref_short+'/pot1000m/Flat2) > 0f && !/SourceData/Omgevingsrecht/EU/Natura2000/Result';
	attribute<bool> GrootWater1500mBuffer             (AllocDomain) := ='potential(float32(Grondgebruik/per_AllocDomain/IsGrootWater),        /Geography/Distmatrices_'+AllocDomain_ref_short+'/pot1500m/Flat2) > 0f';
	attribute<bool> N2K1000mBuffer                    (AdminDomain) := N2K1000mBuffer0 && !GrootWater1500mBuffer[AdminDomain/AllocDomain_rel];
	// attribute<bool> Nationale_parken_van_wereldklasse (AllocDomain) := /SourceData/Landschap/Nationale_parken_van_wereldklasse/IsNationaalParkWereldklasse;
	// attribute<bool> VanGoghNationaalPark              (AllocDomain) := /SourceData/Landschap/VanGoghNationaalPark/IsVanGoghNationaalPark;
	attribute<bool> Pampus                            (AllocDomain) := /SourceData/Plancapaciteit/Wonen/NieuweNieuweKaart/Fijn/IsPampus/Per_AllocDomain;
}