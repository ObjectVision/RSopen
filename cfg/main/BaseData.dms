////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Dit is RSOpen, de open source versie van het model RuimteScanner. Het scipt wordt uitgegeven onder GNU-GPL licentie.
//
// RSOpen is ontwikkeld door PBL Planbureau voor de Leefomgeving, i.s.m Object Vision B.V. en VU Vrije Universiteit Amsterdam.
// Opdrachtgever/ontwikkelaar PBL: Bart Rijken
// Contactpersoon/ontwikkelaar Object Vision B.V.: Jip Claassens (jclaassens@objectvision.nl)
// Contacpersoon PBL: Bas van Bemmel (Bas.vanBemmel@pbl.nl)
//
// Deze file bevat de zgn. baseData die het model nodig heeft voor de verschillende berekeningen/analyses. BaseData zijn altijd afgeleid van de SourceData en soms ontkoppeld (geexporteerd vanuit SourceData).
// De Data worden on-the-fly gegenereerd of, indien reeds gegenereerd (ontkoppeld), ingelezen vanuit de LocalDataDir.
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

container BaseData: using = "Classifications;Units;Geography;ModelParameters"
{
	container BAG_ModelJaar  := = 'SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear), Descr = "Container voor de BAG in het model BAG jaar";
	container BAG_RecentJaar := = 'SourceData/Vastgoed/BAG/PerJaar/Y'+string(BAG_RecentYear), Descr = "Container voor de BAG in het meest recente BAG jaar";
	
	container StartState
	{
		#include<StateBasisjaar.dms> 
		#include<PandFootprint.dms>
		#include<BAG_nieuwbouw.dms> //nieuwbouw vanaf model jaar tot recent jaar
		#include<Grondgebruik.dms>
		#include<Eigendom.dms>
		#include<Verdeling_VSSH.dms>
		#include<AlternatieveLISA.dms>
		
		#include<VerblijfsrecreatieTrends.dms> //trends berekening tbv claims
		
		// container Landgebruikskaart := Templates/Landgebruikskaart/Make_Landgebruikskaart_Basisjaar_T('Basisjaar', StateBasisjaar/State, BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_basis);
		
		attribute<Verblijfsobject> VBOs_ModelJaar (AdminDomain) := count(BAG_ModelJaar/vbo/geometry, BAG_ModelJaar/vbo/AdminDomain_rel)[Verblijfsobject]
		, Descr = "Aantal VBOs in het model jaar per AdminDomain"
		{
			attribute<m2_vbo> Oppervlakte (AdminDomain) := sum(BAG_ModelJaar/VBO/oppervlakte_trunc, BAG_ModelJaar/VBO/AdminDomain_rel)
			, Descr = "Totale oppervlakte van VBOs in het model jaar per AdminDomain"
			{
				container Per_gebruiksdoel :=
					for_each_nedv(
						Vastgoed/vbo_gebruiksdoel/name,
						'sum(
							BAG_ModelJaar/VBOs/GebruiksdoelSets/'+Vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/oppervlakte_trunc,
							BAG_ModelJaar/VBOs/GebruiksdoelSets/'+Vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/AdminDomain_rel 
						)',
						AdminDomain, m2_vbo
				), Descr = "Oppervlakte van VBOs in het model jaar per AdminDomain, uitgesplitst naar gebruiksdoel";
			}
			
			container Per_gebruiksdoelSrc :=
				for_each_nedv(
					Vastgoed/vbo_gebruiksdoel/name
					,'BAG_ModelJaar/VBOs/GebruiksdoelSets/'+Vastgoed/vbo_gebruiksdoel/name+'/GebruiksdoelSet/count_Per_AdminDomain'
					, AdminDomain
					, Verblijfsobject
			), Descr = "Aantal VBOs in het model jaar per AdminDomain, uitgesplitst naar gebruiksdoel";
			
			container Per_gebruiksdoel :=
				for_each_nedv(
					Vastgoed/vbo_gebruiksdoel/name
					,'Per_gebruiksdoelSrc/'+Vastgoed/vbo_gebruiksdoel/name+' / AdminDomain/NrHaPerCell'
					, AdminDomain
					, vbo_ha
			), Descr = "Aantal VBOs per ha in het model jaar per AdminDomain, uitgesplitst naar gebruiksdoel";
		}
	}
	container Densities
	{
		#include<PandFootprint_baan.dms>
	}
	container Omgeving
	{
		#include<OV_Reistijd.dms>
		
		attribute <bool> BBGSamengesteld (AdminDomain) := /BaseData/Omgeving/BBG/Per_AdminDomain || /SourceData/Diversen/Per_AdminDomain/begrenzing_bebouwd_gebied || /BaseData/StartState/Grondgebruik/Per_AdminDomain/IsWoonlocatie || /BaseData/StartState/Grondgebruik/Per_AdminDomain/IsWerklocatie || /BaseData/StartState/Grondgebruik/Per_AdminDomain/IsZeehaven;
		
		unit<UInt32> RondBevolkingskern := SourceData/RegioIndelingen/Bevolkingskern_2011
		{
			// attribute<Bool> Is300kPlus (AllocDomain) := Maak_PopK_Buffers/isMeerdan300Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "300K", Descr = "Een 1km buffer rond bevolkingskernen groter dan 300k inwoners";
			attribute<Bool> Is100kPlus (AllocDomain) := Maak_PopK_Buffers/isMeerdan100Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "100K", Descr = "Een 1km buffer rond bevolkingskernen groter dan 100k inwoners";
			// attribute<Bool> Is50kPlus  (AllocDomain) := Maak_PopK_Buffers/isMeerdan50Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "50K", Descr = "Een 1km buffer rond bevolkingskernen groter dan 50k inwoners";
			// attribute<Bool> Is50kMin   (AllocDomain) := Maak_PopK_Buffers/isMinderdan50Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "50K", Descr = "Een 1km buffer rond bevolkingskernen kleiner dan 50k inwoners";
			attribute<Bool> Is20kPlus  (AllocDomain) := Maak_PopK_Buffers/isMeerdan20Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "25K", Descr = "Een 1km buffer rond bevolkingskernen groter dan 20k inwoners";
			attribute<Bool> Is20kMin   (AllocDomain) := Maak_PopK_Buffers/isMinderdan20Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "20K", Descr = "Een 1km buffer rond bevolkingskernen kleiner dan 20k inwoners";
			// attribute<Bool> Is5kPlus   (AllocDomain) := Maak_PopK_Buffers/isMeerdan5Kinw/select/split_geometry/MetBuffer/Is_Buffer, Label = "5K", Descr = "Een 1km buffer rond bevolkingskernen groter dan 5k inwoners";
		}
		
		attribute<Float32> BelevingLandschap (AllocDomain) := SourceData/Diversen/Per_AllocDomain/BelevingLandschap/NoMissing
		{
			attribute<Bool> IsKleinerDanZesPunt5  (AllocDomain) := . < 6.5f, Descr = "Beleving landschap score is lager dan 6.5";
			attribute<Bool> IsGroterDanZeven      (AllocDomain) := . > 7.0f, Descr = "Beleving landschap score is hoger dan 7.0";
			attribute<Bool> IsGroterDanZevenPunt3 (AllocDomain) := . > 7.3f, Descr = "Beleving landschap score is hoger dan 7.3";
			attribute<Bool> IsZeerLaag            (AllocDomain) := IsKleinerDanZesPunt5, Descr = "Beleving landschap score is zeer laag (lager dan 6.5)";
			attribute<Bool> IsLaag                (AllocDomain) := !IsHoog, Descr = "Beleving landschap score is laag (lager dan 7.0)";
			attribute<Bool> IsHoog                (AllocDomain) := IsGroterDanZeven, Descr = "Beleving landschap score is hoog (hoger dan 7.0)";
			attribute<Bool> IsZeerHoog            (AllocDomain) := IsGroterDanZevenPunt3, Descr = "Beleving landschap score is zeer hoog (hoger dan 7.3)";
			attribute<Bool> IsZeerZeerHoog        (AllocDomain) := . > 7.6f, Descr = "Beleving landschap score is zeer zeer hoog (hoger dan 7.6)";
			attribute<Float32> Potential          (AllocDomain) := potential(., Distmatrices/Impl_100m/pot2000m/PotRange/rev_dist_scaled) / sum(Distmatrices/Impl_100m/pot2000m/PotRange/rev_dist_scaled)
			, Descr = "Potentiaal van de beleving landschap score binnen 2km, gebaseerd op een omgekeerde afstandsvervalfunctie"
			{
				attribute<Bool> IsGroterDanZeven      (AllocDomain) := . >= 7.0f, Descr = "Beleving landschap score is groter dan of gelijk aan 7.0";
				attribute<Bool> IsGroterDanZevenPunt3 (AllocDomain) := . >= 7.3f, Descr = "Beleving landschap score is groter dan of gelijk aan 7.3";
				attribute<Bool> IsHoog                (AllocDomain) := IsGroterDanZeven, Descr = "Beleving landschap score is hoog (hoger dan of gelijk aan 7.0)"
				{
					attribute<Bool> EnBelevingIsNietHoog (AllocDomain) := IsHoog && !BelevingLandschap/IsHoog;
					attribute<Bool> EnBelevingIsLaag     (AllocDomain) := IsHoog && BelevingLandschap/IsLaag;
				}
				
				attribute<Bool> IsZeerHoog (AllocDomain) := IsGroterDanZevenPunt3, Descr = "Beleving landschap score is zeer hoog (hoger dan of gelijk aan 7.3)"
				{
					attribute<Bool> EnBelevingIsNietHoog (AllocDomain) := IsZeerHoog && !BelevingLandschap/IsHoog;
				}
			}
		}
		attribute<Classifications/Overstromingsgevaarzonering/zoneringK> Overstromingsgevaarzonering (AdminDomain) := SourceData/Water/Overstromingsgevaarzonering;
		
		attribute<meter> Overstromingsdiepte              (AdminDomain) := value(SourceData/Water/dieptekaart == -9999f ? const(0f, AdminDomain) : SourceData/Water/dieptekaart, meter), Descr = "Gemiddelde overstromingsdiepte in meters";
		attribute<Bool> HogeGronden                       (AdminDomain) := !(Overstromingsdiepte > 0[meter]), Descr = "Hoge gronden, niet overstromingsgevoelig";
		attribute<Bool> Gebiedsreservering                (AdminDomain) := SourceData/Omgevingsrecht/AMvB/NL_IMRO_0000_BZKamGCBarro_5025/Kaart4/Gebiedsreservering/Multi/Result, Descr = "Gebiedsreservering voor rivierbedverruiming";
		attribute<Bool> Stroomvoerend_deel_rivierbed      (AdminDomain) := SourceData/Omgevingsrecht/AMvB/NL_IMRO_0000_BZKamGCBarro_5025/Kaart4/Stroomvoerend_deel_rivierbed/Multi/Result, Descr = "Stroomvoerend deel rivierbed";
		attribute<Bool> N2K1000mBuffer0                   (AdminDomain) := ='potential(float32(SourceData/Omgevingsrecht/EU/Natura2000/Result_AdminDomain), /Geography/Distmatrices_'+AdminDomain_ref_short+'/pot1000m/Flat2) > 0f && !SourceData/Omgevingsrecht/EU/Natura2000/Result_AdminDomain', Descr = "1km buffer rond Natura2000 gebieden";
		attribute<Bool> GrootWater1500mBuffer             (AllocDomain) := ='potential(float32(StartState/Grondgebruik/Per_AllocDomain/IsGrootWater),        /Geography/Distmatrices_'+AllocDomain_ref_short+'/pot1500m/Flat2) > 0f', Descr = "1.5km buffer rond groot water (meren en grote rivieren)";
		attribute<Bool> N2K1000mBuffer                    (AdminDomain) := N2K1000mBuffer0 && !GrootWater1500mBuffer[AdminDomain/AllocDomain_rel], Descr = "1km buffer rond Natura2000 gebieden, exclusief groot water";
		// attribute<Bool> Nationale_parken_van_wereldklasse (AllocDomain) := SourceData/Landschap/Nationale_parken_van_wereldklasse/IsNationaalParkWereldklasse, Descr = "Nationale parken van wereldklasse";
		// attribute<Bool> VanGoghNationaalPark              (AllocDomain) := SourceData/Landschap/VanGoghNationaalPark/IsVanGoghNationaalPark, Descr = "Van Gogh Nationaal Park";
		// attribute<Bool> Pampus                            (AllocDomain) := SourceData/Plancapaciteit/Wonen/NieuweNieuweKaart/Fijn/IsPampus/Per_AllocDomain, Descr = "Pampus";
		
		attribute<Bool> BBG (AllocDomain) := IsDefined(SourceData/RegioIndelingen/Bevolkingskern_2011/Per_AllocDomain), Descr = "Begrenzing Bebouwd Gebied"
		{
			attribute<Bool> Per_AdminDomain (AdminDomain) := IsDefined(SourceData/RegioIndelingen/Bevolkingskern_2011/Per_AdminDomain);
		}
		
		container Fractie_VerhardGroen_PerBuurtType
		{
			attribute<meter2> GroenOpp   (AdminDomain) := /SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/GroenOpp_AdminDomain;
			attribute<meter2> BlauwOpp   (AdminDomain) := /SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/BlauwOpp_AdminDomain;
			attribute<meter2> BruinOpp   (AdminDomain) := /SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/BruinOpp_AdminDomain;
			attribute<meter2> VerhardOpp (AdminDomain) := /SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/VerhardOpp_AdminDomain;
			attribute<meter2> Check      (AdminDomain) := add(GroenOpp, BlauwOpp, BruinOpp, VerhardOpp) - AdminDomain/NrHaPerCell[meter2];
			
			unit<uint32> NettoBuurt := /SourceData/Vastgoed/Rudifun/nl2_04_Netto_Buurt
			{
				attribute<meter2>  GroenOpp                      := sum(../GroenOpp, Per_AdminDomain);
				attribute<meter2>  BlauwOpp                      := sum(../BlauwOpp, Per_AdminDomain);
				attribute<meter2>  BruinOpp                      := sum(../BruinOpp, Per_AdminDomain);
				attribute<meter2>  VerhardOpp                    := sum(../VerhardOpp, Per_AdminDomain);
				attribute<meter2>  TotalOpp                      := GroenOpp + BlauwOpp + BruinOpp + VerhardOpp;
				
				attribute<float32> FractieGroen                  := GroenOpp / TotalOpp;
				attribute<float32> FractieBlauw                  := BlauwOpp / TotalOpp;
				attribute<float32> FractieBruin                  := BruinOpp / TotalOpp;
				attribute<float32> FractieVerhard                := VerhardOpp / TotalOpp;
				
				attribute<.> Per_AdminDomain (AdminDomain) := poly2grid(geometry, AdminDomain);
			}
			
			unit<uint32> BrutoBuurt := /SourceData/Vastgoed/Rudifun/nl2_05_Bruto_Buurt
			{
				attribute<meter2>  GroenOpp                      := sum(../GroenOpp, Per_AdminDomain);
				attribute<meter2>  BlauwOpp                      := sum(../BlauwOpp, Per_AdminDomain);
				attribute<meter2>  BruinOpp                      := sum(../BruinOpp, Per_AdminDomain);
				attribute<meter2>  VerhardOpp                    := sum(../VerhardOpp, Per_AdminDomain);
				attribute<meter2>  TotalOpp                      := GroenOpp + BlauwOpp + BruinOpp + VerhardOpp;
				
				attribute<float32> FractieGroen                  := GroenOpp / TotalOpp;
				attribute<float32> FractieBlauw                  := BlauwOpp / TotalOpp;
				attribute<float32> FractieBruin                  := BruinOpp / TotalOpp;
				attribute<float32> FractieVerhard                := VerhardOpp / TotalOpp;
				
				attribute<.> Per_AdminDomain (AdminDomain) := poly2grid(geometry, AdminDomain);
			}
			
			unit<uint32> TarraBuurt := /SourceData/Vastgoed/Rudifun/nl2_05_Bruto_Buurt
			{
				attribute<meter2>     GroenOpp                   := max_elem(BrutoBuurt/GroenOpp - NettoBuurt/GroenOpp[NettoBuurt_rel], 0f);
				attribute<meter2>     BlauwOpp                   := max_elem(BrutoBuurt/BlauwOpp - NettoBuurt/BlauwOpp[NettoBuurt_rel], 0f);
				attribute<meter2>     BruinOpp                   := max_elem(BrutoBuurt/BruinOpp - NettoBuurt/BruinOpp[NettoBuurt_rel], 0f);
				attribute<meter2>     VerhardOpp                 := max_elem(BrutoBuurt/VerhardOpp - NettoBuurt/VerhardOpp[NettoBuurt_rel], 0f);
				attribute<meter2>     TotalOpp                   := GroenOpp + BlauwOpp + BruinOpp + VerhardOpp;
				
				attribute<float32>    FractieGroen               := GroenOpp / TotalOpp;
				attribute<float32>    FractieBlauw               := BlauwOpp / TotalOpp;
				attribute<float32>    FractieBruin               := BruinOpp / TotalOpp;
				attribute<float32>    FractieVerhard             := VerhardOpp / TotalOpp;
				
				attribute<.> Per_AdminDomain (AdminDomain) := poly2grid(geometry, AdminDomain);
				
				attribute<NettoBuurt> NettoBuurt_rel             := rlookup(BU_CODE, NettoBuurt/BU_CODE);
				attribute<Provincie>  Provincie_rel              := rlookup(PV_CODE, Provincie/statcode);
				
				attribute<float32>    Mean_FractieGroen_Prov     (Provincie) := mean(FractieGroen, Provincie_rel);
				attribute<float32>    Mean_FractieBlauw_Prov     (Provincie) := mean(FractieBlauw, Provincie_rel);
				attribute<float32>    Mean_FractieBruin_Prov     (Provincie) := mean(FractieBruin, Provincie_rel);
				attribute<float32>    Mean_FractieVerhard_Prov   (Provincie) := mean(FractieVerhard, Provincie_rel);
				// attribute<float32>    Median_FractieGroen_Prov   (Provincie) := rth_element(FractieGroen, 0.5f, Provincie_rel);
				// attribute<float32>    Median_FractieBlauw_Prov   (Provincie) := rth_element(FractieBlauw, 0.5f, Provincie_rel);
				// attribute<float32>    Median_FractieBruin_Prov   (Provincie) := rth_element(FractieBruin, 0.5f, Provincie_rel);
				// attribute<float32>    Median_FractieVerhard_Prov (Provincie) := rth_element(FractieVerhard, 0.5f, Provincie_rel);
				
				// attribute<float32_t> FractieVerhardInTarraBuurt:                       [0.01                              ,0.01                               ,0.01                               ], Descr = "Expert judgement PBL";
				// attribute<float32_t> FractieWaterInTarraBuurt:                         [0.33                              ,0.33                               ,0.33                               ], Descr = "Grove aanname binenn dit poject. Needs work";
				// attribute<float32_t> FractieGroenInTarraBuurt:                         [0.66                              ,0.66                               ,0.66                               ], Descr = "Grove aanname binenn dit poject. Needs work";
				
				// attribute<float32_t> FractieBoomInGroenInTarraBuurt:                   [0.20                              ,0.20                               ,0.20                               ], Descr = "Grove aanname binenn dit poject. Needs work";
				// attribute<float32_t> FractieStruikInGroenInTarraBuurt:                 [0.20                              ,0.20                               ,0.20                               ], Descr = "Grove aanname binenn dit poject. Needs work";
				// attribute<float32_t> FractieGrasInGroenInTarraBuurt:                   [0.60                              ,0.60                               ,0.60                               ], Descr = "Grove aanname binenn dit poject. Needs work";
	
				parameter<float32> Mean_FractieGroen   := mean(FractieGroen[Per_AdminDomain]);
				parameter<float32> Mean_FractieBlauw   := mean(FractieBlauw[Per_AdminDomain]);
				parameter<float32> Mean_FractieBruin   := mean(FractieBruin[Per_AdminDomain]);
				parameter<float32> Mean_FractieVerhard := mean(FractieVerhard[Per_AdminDomain]); // inclusief pandfootprint (voor zover relevant in tarra buurten)
				
				// parameter<float32>    Median_FractieGroen        := rth_element(FractieGroen, 0.5f);
				// parameter<float32>    Median_FractieBlauw        := rth_element(FractieBlauw, 0.5f);
				// parameter<float32>    Median_FractieBruin        := rth_element(FractieBruin, 0.5f);
				// parameter<float32>    Median_FractieVerhard      := rth_element(FractieVerhard, 0.5f);
			}
			
			container FractiesHoofdklassenPerBNSN := for_each_ne(Classifications/Grondgebruik/HoofdklasseK/Label, 'HoofdklasseT('+string(id(Classifications/Grondgebruik/HoofdklasseK))+'[Classifications/Grondgebruik/HoofdklasseK])');
			
			Template HoofdklasseT
			{
				//
				parameter<uint8> HoofdklasseK_id;
				//
				
				container FractiePerBNSN :=
					for_each_nedv(
						Landschap/BasiskaartNatuurlijkSysteemNederlandK/name,
						replace(
							'mean(
								SourceData/Landschap/BasiskaartNatuurlijkSysteemNederlandK/BNSN_rel_AdminDomain == Landschap/BasiskaartNatuurlijkSysteemNederlandK/v/'+Landschap/BasiskaartNatuurlijkSysteemNederlandK/name+' 
									? TarraBuurt/Fractie@[TarraBuurt/Per_AdminDomain]
									: null_f
							)',
							'@', Classifications/Grondgebruik/HoofdklasseK/Label[HoofdklasseK_id]
						)
						, void
						, float32
					)
				{
					attribute<float32> Map (Landschap/BasiskaartNatuurlijkSysteemNederlandK) := = 'union_data(Landschap/BasiskaartNatuurlijkSysteemNederlandK,'+AsItemList('FractiePerBNSN/'+Landschap/BasiskaartNatuurlijkSysteemNederlandK/name)+')';
				}
			}
		}
		
		// @Jip, hier komt een vreemde waarde uit die bovendien afwijkt van de 1,13 die PBL mailde. Graag berekenen obv netto en buurt geometry
		parameter<float32> BrutoTovNettoBuurt := sum(Fractie_VerhardGroen_PerBuurtType/BrutoBuurt/TotalOpp) / sum(Fractie_VerhardGroen_PerBuurtType/NettoBuurt/TotalOpp);
	}
	
	#include<Suitabilities.dms>
	
	container Restricties
	{
		#include<Afleiding_Winkelcentra.dms>
	}
	
	#include<Zonneladder.dms>
}