////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Dit is RSOpen, de open source versie van het model RuimteScanner. Het scipt wordt uitgegeven onder GNU-GPL licentie.
//
// RSOpen is ontwikkeld door PBL Planbureau voor de Leefomgeving, i.s.m Object Vision B.V. en VU Vrije Universiteit Amsterdam.
// Opdrachtgever/ontwikkelaar PBL: Bart Rijken
// Contactpersoon/ontwikkelaar Object Vision B.V.: Jip Claassens (jclaassens@objectvision.nl)
// Contacpersoon PBL: Bas van Bemmel (Bas.vanBemmel@pbl.nl)
//
// Deze file bevat de bronData (SourceData) die het model nodig heeft voor de verschillende berekeningen/analyses.De Data worden ingelezen vanuit de SourceRVF_DataDir.
// De bronnen zijn divers. Tenzij anders aangegeven is dat het PBL.
//
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

container PeilOptimalisatie: using = "Classifications;Classifications/Vastgoed;Classifications/Landbouw;Classifications/Actor;Classifications/Time;units;Geography"
{
	// To do:
		// absurd hoge NPVs landbouw eruit
		// checken waarom hogere npv landbouw in wbs dan in bau
		// afkappen op agrarisch grondgebruik in zichtjaar in beide varianten. Anders interfereert de post alloc analyse met de alloc van andere (stedelijke) klassen
		// resulatten aggregeren naar tabel, in logische combinaties, voor presentatie
		// co2 emiussie lokaal afkappen obv dikte veenpakket of (beter) hoeveenheid co2 in ondergrond
		// checken of de vooronderstellingen qua slootwaterpeil en griondweaterstand consistent zijn in bau en wbs (comsistent tussen CO2emissie berekening en opbrengstenderving bertekening van agrarisch landuse)
	
	container Impl
	{
		unit<uint32> ZichtjaarXEmissieXPrijsK := combine(ZichtjaarK, CO2/EmissieXPrijsK)
		{
			attribute<string> Zichtjaar_name := ZichtjaarK/name[first_rel];
			attribute<string> EmissieXPrijs_name := CO2/EmissieXPrijsK/name[second_rel];
			attribute<string> Emissie_name := CO2/EmissieXPrijsK/Emissie_name[second_rel];
			attribute<string> Prijs_name := CO2/EmissieXPrijsK/Prijs_name[second_rel];
			attribute<string> name := Zichtjaar_name+'/'+Emissie_name+'/'+Prijs_name;
		}
		
		container OntkoppelRelevantOutputFiles // de relevante te ontkoppelen data, per casus, per boervariant, per zichtjaar
		{
			parameter<string> Landgebruikskaart_basisjaar  := 'Ready', ExplicitSuppliers = "Indicatoren/Basisjaar/Landgebruikskaart/Write_Result_SA"; // gebruiken we deze hier?
			parameter<string> Landgebruikskaart_Zichtjaar  := 'Ready', ExplicitSuppliers = "=AsList('Indicatoren/'+ZichtjaarK/name+'/Landgebruikskaart/Write_Result_SA', ';')";
			parameter<string> NPVGrondgebruik              := 'Ready', ExplicitSuppliers = "=AsList('Indicatoren/Export/Impl/Zichtjaren/'+ZichtjaarK/name+'/NPV_perLUMT/Totaal', ';')";
			parameter<string> NPVCO2Emissie                := 'Ready', ExplicitSuppliers = "=AsList('Indicatoren/'+ZichtjaarXEmissieXPrijsK/Zichtjaar_name+'/SOMERS_CO2_laagveen/Kosten/'+ZichtjaarXEmissieXPrijsK/EmissieXPrijs_name+'/NPV', ';')";
		}
		
		unit<uint32> GrondgebruikContext := combine(ScenarioK, BoerVariantK, VariantK, ZichtjaarK) // De relevante onzekerheden achter Grondgebruik. Klopt.
		{
			attribute<string> Scenario_name    := ScenarioK/name[first_rel];
			attribute<string> Boervariant_name := BoerVariantK/name[second_rel];
			attribute<string> Variant_name     := VariantK/name[third_rel];
			attribute<string> Zichtjaar_name   := ZichtjaarK/name[fourth_rel];
			attribute<string> name             := Scenario_name+'/'+Boervariant_name+'/'+Variant_name+'/'+Zichtjaar_name;
			attribute<string> Label            := replace(name, '/', '_');
		}
		
		unit<uint32> AggregatieContext := combine(ScenarioK, BoerVariantK, VariantK, ZichtjaarK, CO2/EmissieK, CO2/PrijsK) // De relevante combinaties voor aggregatie van de NPV van landhgeruik en CO2emissie. Klopt.
		{
			attribute<string> Scenario_name    := ScenarioK/name[first_rel];
			attribute<string> Boervariant_name := BoerVariantK/name[second_rel];
			attribute<string> Variant_name     := VariantK/name[third_rel];
			attribute<string> Zichtjaar_name   := ZichtjaarK/name[fourth_rel];
			attribute<string> CO2Emissie_name := CO2/EmissieK/name[fifth_rel];
			attribute<string> CO2Prijs_name    := CO2/PrijsK/name[sixth_rel];
			attribute<string> name             := Scenario_name+'/'+Boervariant_name+'/'+Variant_name+'/'+Zichtjaar_name+'/'+CO2Emissie_name+'/'+CO2Prijs_name;
			attribute<string> Label            := replace(name, '/', '_');
		}
		
		unit<uint32> OptimalisatieContext := combine(ScenarioK, BoerVariantK, ZichtjaarK, CO2/EmissieK, CO2/PrijsK) // De relevante onzekerheden bij de optimalisatie van de variant, over co2 en Grondgebruik heen. Klopt
		{
			attribute<string> Scenario_name    := ScenarioK/name[first_rel];
			attribute<string> Boervariant_name := BoerVariantK/name[second_rel];
			attribute<string> Zichtjaar_name   := ZichtjaarK/name[third_rel];
			attribute<string> CO2Emissie_name  := CO2/EmissieK/name[fourth_rel];
			attribute<string> CO2Prijs_name    := CO2/PrijsK/name[fifth_rel];
			attribute<string> name             := Scenario_name+'/'+Boervariant_name+'/'+Zichtjaar_name+'/'+CO2Emissie_name+'/'+CO2Prijs_name;
			attribute<string> Label            := replace(name, '/', '_');
		}
	}
	
	unit<uint8> VariantK := /VariantParameters/VariantK;
	unit<uint8> ZichtjaarK := /Classifications/Time/Zichtjaar;
	unit<uint8> GrondgebruikK := /Classifications/Actor/LU_ModelType;
	
	container Grondgebruik := 
		for_each_ndvat(
			impl/GrondgebruikContext/name
			,AdminDomain
			,GrondgebruikK
			,'%LocalDataProjDir%/Indicatoren/Landgebruik/'+impl/GrondgebruikContext/Scenario_name+'_'+impl/GrondgebruikContext/variant_name+'/'+impl/GrondgebruikContext/Boervariant_name+'_'+impl/GrondgebruikContext/Zichtjaar_name+'_'+/ModelParameters/StudyArea+'.tif'
			,'gdal.grid'
		);
	
	container NPVGrondgebruik := 
		for_each_ndvat(
			impl/GrondgebruikContext/name
			,AdminDomain
			,EUR
			,'%LocalDataProjDir%/Indicatoren/NPV/'+impl/GrondgebruikContext/Scenario_name+'_'+impl/GrondgebruikContext/variant_name+'/'+impl/GrondgebruikContext/Boervariant_name+'_'+impl/GrondgebruikContext/Zichtjaar_name+'_'+/ModelParameters/StudyArea+'.tif'
			,'gdal.grid'
	);
	
	container NPVCO2Emissie := 
		for_each_ndvat(
			impl/AggregatieContext/name
			,AdminDomain
			,EUR
			,'%LocalDataProjDir%/Indicatoren/SOMERS_CO2/Kosten/'+impl/AggregatieContext/Scenario_name+'_'+impl/AggregatieContext/variant_name+'/'+impl/AggregatieContext/Boervariant_name+'_'+impl/AggregatieContext/Zichtjaar_name+'_'+/ModelParameters/StudyArea+'_'+impl/AggregatieContext/CO2Emissie_name+'_'+impl/AggregatieContext/CO2Prijs_name+'.tif'
			,'gdal.grid'
	);
	
	container NPVTotaal := 
		for_each_nedv(
			impl/AggregatieContext/name
			,replace(
				'NPVGrondgebruik/@S@/@B@/@V@/@Z@ - NPVCO2Emissie/@S@/@B@/@V@/@Z@/@E@/@P@'
				,'@S@', impl/AggregatieContext/Scenario_name
				,'@B@', impl/AggregatieContext/Boervariant_name
				,'@V@', impl/AggregatieContext/Variant_name
				,'@Z@', impl/AggregatieContext/Zichtjaar_name
				,'@E@', impl/AggregatieContext/CO2Emissie_name
				,'@P@', impl/AggregatieContext/CO2Prijs_name
			)
			,AdminDomain,
			EUR
		);
		
	container NPVTotaalPerPeilgebied :=
		for_each_nedv(
			impl/AggregatieContext/name
			,'sum(NPVTotaal/'+impl/AggregatieContext/name+', SourceData/Landbouw/Peilgebieden/per_grid)'
			,SourceData/Landbouw/Peilgebieden
			,EUR
		);
	
	container Optima
	{
		container Variant :=
			for_each_nedv(
				impl/OptimalisatieContext/name
				,replace(
					'ArgMax('+AsItemList('NPVTotaalPerPeilgebied/@S@/@B@/'+VariantK/name+'/@Z@/@E@/@P@')+')[VariantK]'
					,'@S@', impl/OptimalisatieContext/Scenario_name
					,'@B@', impl/OptimalisatieContext/Boervariant_name
					,'@Z@', impl/OptimalisatieContext/Zichtjaar_name
					,'@E@', impl/OptimalisatieContext/CO2Emissie_name
					,'@P@', impl/OptimalisatieContext/CO2Prijs_name
				)
				,SourceData/Landbouw/Peilgebieden
				,VariantK
			);
		
		container VariantPerGrid := 
			for_each_nedv(
				impl/OptimalisatieContext/name
				,'Variant/'+impl/OptimalisatieContext/name+'[SourceData/Landbouw/Peilgebieden/per_grid]'
				,AdminDomain
				,VariantK
			);
		
		container Grondgebruik :=
			for_each_nedv(
				impl/OptimalisatieContext/name
				,replace(
					'merge(uint16(VariantPerGrid/@O@), GrondgebruikK,'+AsItemList('PeilOptimalisatie/Grondgebruik/@S@/@B@/'+VariantK/name+'/@Z@')+')'
					,'@O@', impl/OptimalisatieContext/name
					,'@S@', impl/OptimalisatieContext/Scenario_name
					,'@B@', impl/OptimalisatieContext/Boervariant_name
					,'@Z@', impl/OptimalisatieContext/Zichtjaar_name
				)
				,AdminDomain
				,GrondgebruikK
			);
		
		container NPVGrondgebruik := 
			for_each_nedv(
				impl/OptimalisatieContext/name
				,replace(
					'merge(uint16(VariantPerGrid/@O@), EUR,'+AsItemList('PeilOptimalisatie/NPVGrondgebruik/@S@/@B@/'+VariantK/name+'/@Z@')+')'
					,'@O@', impl/OptimalisatieContext/name
					,'@S@', impl/OptimalisatieContext/Scenario_name
					,'@B@', impl/OptimalisatieContext/Boervariant_name
					,'@Z@', impl/OptimalisatieContext/Zichtjaar_name
				)
				,AdminDomain
				,EUR
			);
		
		container NPVCO2Emissie := 
			for_each_nedv(
				impl/OptimalisatieContext/name
				,replace(
					'merge(uint16(VariantPerGrid/@O@), EUR,'+AsItemList('PeilOptimalisatie/NPVCO2Emissie/NPVCO2Emissie/@S@/@B@/'+VariantK/name+'/@Z@/@E@/@P@')+')'
					,'@O@', impl/OptimalisatieContext/name
					,'@S@', impl/OptimalisatieContext/Scenario_name
					,'@B@', impl/OptimalisatieContext/Boervariant_name
					,'@Z@', impl/OptimalisatieContext/Zichtjaar_name
					,'@E@', impl/OptimalisatieContext/CO2Emissie_name
					,'@P@', impl/OptimalisatieContext/CO2Prijs_name
				)
				,AdminDomain
				,EUR
			);
		
		container NPVTotaal :=
			for_each_nedv(
				impl/OptimalisatieContext/name
				,replace(
					'merge(uint16(VariantPerGrid/@O@), EUR,'+AsItemList('PeilOptimalisatie/NPVTotaal/@S@/@B@/'+VariantK/name+'/@Z@/@E@/@P@')+')'
					,'@O@', impl/OptimalisatieContext/name
					,'@S@', impl/OptimalisatieContext/Scenario_name
					,'@B@', impl/OptimalisatieContext/Boervariant_name
					,'@Z@', impl/OptimalisatieContext/Zichtjaar_name
					,'@E@', impl/OptimalisatieContext/CO2Emissie_name
					,'@P@', impl/OptimalisatieContext/CO2Prijs_name
				)
				,AdminDomain
				,EUR
			);
	}
}