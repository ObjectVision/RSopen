container Landbouw
{
	container Impl
	{
		container OntkoppelRelevantOutputFiles // de relevante te ontkoppelen data, per casus, per boervariant, per zichtjaar
		{
			parameter<string> Landgebruikskaart_basisjaar  := 'Ready', ExplicitSuppliers = "Indicatoren/Basisjaar/Landgebruikskaart/Write_Result_SA"; // gebruiken we deze hier?
			parameter<string> Landgebruikskaart_Zichtjaar  := 'Ready', ExplicitSuppliers = "=AsList('Indicatoren/'+ZichtjaarK/name+'/Landgebruikskaart/Write_Result_SA', ';')";
			parameter<string> NPVGrondgebruik              := 'Ready', ExplicitSuppliers = "=AsList('Indicatoren/Export/Impl/Zichtjaren/'+ZichtjaarK/name+'/NPV_perLUMT/Totaal', ';')";
		}
		
		unit<uint32> GrondgebruikContext := combine(ScenarioK, VariantK, BoerVariantK, BeregeningScenarioK, ZichtjaarK) // Grondgebruik en de NVP ervan is afhankelijk van alles dat grondgebruik bepaalt, zowel niet-agrarisch als agrarisch
		{
			attribute<string> Scenario_name           := ScenarioK/name[first_rel];
			attribute<string> Variant_name            := VariantK/name[second_rel];
			attribute<string> Boervariant_name        := BoerVariantK/name[third_rel];
			attribute<string> BeregeningScenario_name := BeregeningScenarioK/name[fourth_rel];
			attribute<string> Zichtjaar_name          := ZichtjaarK/name[fifth_rel];
			attribute<string> name                    := Scenario_name+'/'+Variant_name+'/'+Boervariant_name+'/'+BeregeningScenario_name+'/'+Zichtjaar_name;
			attribute<string> Label                   := replace(name, '/', '_');
		}
	}
	
	unit<uint8> VariantK := /VariantParameters/VariantK;
	unit<uint8> ZichtjaarK := /Classifications/Time/Zichtjaar;
	unit<uint8> GrondgebruikK := /Classifications/Actor/LU_ModelType;
	unit<uint8> BeregeningScenarioK := /SourceData/Landbouw/Yield_Reduction/BeregeningScenarios;
	
	container Grondgebruik :=
		for_each_ndvat(
			impl/GrondgebruikContext/name
			,AdminDomain
			,GrondgebruikK
			,'%LocalDataProjDir%/Indicatoren/Landgebruik/'+impl/GrondgebruikContext/Scenario_name+'_'+impl/GrondgebruikContext/variant_name+'/'+impl/GrondgebruikContext/Boervariant_name+'/'+impl/GrondgebruikContext/BeregeningScenario_name+'/'+impl/GrondgebruikContext/Zichtjaar_name+'/'+/ModelParameters/StudyArea+'.tif'
			,'gdal.grid'
		);
	
	container TotaalGrondgebruikPerGrondgebruikK :=
		for_each_nedv(
			impl/GrondgebruikContext/name,
			'union_data(GrondgebruikK,'+
			replace(
				AsItemList('sum(float32(Grondgebruik/@GC@ == GrondgebruikK/V/'+GrondgebruikK/name+') * AdminDomain/NrHaPerCell)'),
				'@GC@', impl/GrondgebruikContext/name
			)+')',
			GrondgebruikK, Ha
		);
	
	container NPVGrondgebruik :=
		for_each_ndvat(
			impl/GrondgebruikContext/name
			,AdminDomain
			,EUR
			,'%LocalDataProjDir%/Indicatoren/NPV/'+impl/GrondgebruikContext/Scenario_name+'_'+impl/GrondgebruikContext/variant_name+'/'+impl/GrondgebruikContext/Boervariant_name+'/'+impl/GrondgebruikContext/BeregeningScenario_name+'/'+impl/GrondgebruikContext/Zichtjaar_name+'/'+/ModelParameters/StudyArea+'.tif'
			,'gdal.grid'
		);
	
	container GemiddeldeNPVGrondgebruikPerGrondgebruikK := 
		for_each_nedv(
			impl/GrondgebruikContext/name,
			'union_data(GrondgebruikK,'+
			replace(
				AsItemList('mean(NPVGrondgebruik/@GC@ / float32(Grondgebruik/@GC@ == GrondgebruikK/V/'+GrondgebruikK/name+') / AdminDomain/NrHaPerCell)'),
				'@GC@', impl/GrondgebruikContext/name
			)+')',
			GrondgebruikK, EUR_ha
		);
	
	#include <BeregeningScenarios.dms>
	#include <PeilOptimalisatie.dms>
}