container Density
: Descr = "In deze module wordt de maximale dichtheid (e.g. m2PandFootprint per job, of woning per ha) per subsector bepaald. Deze is immers variant specifiek."
{
	container Per_AllocDomain := 
		for_each_ne(Time/Zichtjaar/name
		, 'Templates/Densities/DichtheidZichtjaar_T('+string(id(Time/Zichtjaar))+'[Time/Zichtjaar])');
	
	container Per_CompactedAllocDomain := for_each_ne(Time/Zichtjaar/name, 'DichtheidZichtjaar_Compacted_T('+string(id(Time/Zichtjaar))+'[Time/Zichtjaar])');
	
	container MaxWoningDichtheid := 
		for_each_ne(
			Classifications/Vastgoed/WP2xVSSH/name
			, 'Templates/Densities/MaxWoningDichtheid_T('+string(id(Classifications/Vastgoed/WP2xVSSH))+'[Classifications/Vastgoed/WP2xVSSH], BaseData/StartState/Grondgebruik/Per_AllocDomain/IsWoonlocatie)'
		);
	
	
	Template DichtheidZichtjaar_Compacted_T
	{
		//
		parameter<Time/Zichtjaar> id;
		//
		
		parameter<String> Zichtjaar_name := Time/Zichtjaar/name[id];
		
		container SectorxSubsectoren := for_each_ne(Sector/xSubsector/name, 'SectorxSubsector_Compacted_T('+string(id(Sector/xSubsector))+'[Sector/xSubsector])');
		
		container Src
		{
			container Werken
			{
				container PandFootprint_perJob
				{
					container Subsectoren :=
						for_each_nedv(
							Actor/Jobs6/name
							,'collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, Per_AllocDomain/'+Zichtjaar_name+'/Src/Werken/PandFootprint_perJob/Subsectoren0/'+Actor/Jobs6/name+')'
							, CompactedAllocDomain
							, m2PandFootprint_job
						);
				}
			}
		}
		
		Template SectorxSubsector_Compacted_T
		{
			//
			parameter<Sector/xSubsector> id;
			//
			
			parameter<String> name           := Sector/xSubsector/name[id];
			parameter<String> Subsector_name := Sector/xSubsector/Subsector_name[id];
			parameter<String> Sector_name    := Sector/xSubsector/Sector_name[id];
			parameter<String> ValUnit_ref    := Sector/xSubsector/ValUnitDens_ref[id];
			
			unit<Float32> ValUnit := = ValUnit_ref;
			
			attribute<ValUnit> Zichtjaar (CompactedAllocDomain) := ='collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, Per_AllocDomain/'+Zichtjaar_name+'/SectorxSubsectoren/'+Sector_name+'/'+Subsector_name+'/Zichtjaar)';
		}
	}
}