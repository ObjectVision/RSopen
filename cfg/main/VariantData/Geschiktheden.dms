container src
{
	container Wonen
	: Descr = "In deze module wordt voorbereidende stap voor de geschiktheid van wonen berekend, i.e. potentiele opbrengsten en bouwkosten van een ontwikkelpakket (OP) per admin domain cel. OP's zijn immer variant specifiek." //dit zit staat hier omdat je niet wilt dat dit in elk zichtjaar opnieuw wordt berekend wat zou gebeuren als we het in de geschiktheden-template zouden stoppen.
	{
		unit<UInt8> OP := CaseClassifications/Vastgoed/OP;
		
		container Impl
		{
			container CalcOpbrengsten_perOP  := for_each_nedv(OP/name, 'Woningwaarde_perOP/'+OP/name+'/Prijs_OP', AdminDomain, Eur), Descr = "Extra container om gunstigere structuur te hebben om later aan te roepen.";
			container WriteOpbrengsten_perOP := 
				for_each_neidvat(
					OP/name
					, 'CalcOpbrengsten_perOP/'+OP/name
					, 'not(all(IsNull(this)))'
					, AdminDomain
					, Eur
					, '%LocalDataProjDir%/VariantData/Vastgoed/Opbrengsten_perOP/'+Variant_name+'/'+OP/name+'_'+/ModelParameters/StudyArea+'.tif'
					, 'gdalwrite.grid'
				), Descr = "Write opbrengsten per OP naar tif."
			{
				parameter<String> GenerateAll := 'Done', ExplicitSuppliers = "=asList(OP/name,';')";
			}
			
			container ReadOpbrengsten_perOP_UNCHECKED := 
				for_each_ndvat(
					OP/name
					, AdminDomain
					, Eur
					, '%LocalDataProjDir%/VariantData/Vastgoed/Opbrengsten_perOP/'+Variant_name+'/'+OP/name+'_'+/ModelParameters/StudyArea+'.tif'
					, 'tif'
				), StorageReadOnly = "True", Descr = "This tif file could potentially contain only zeros or undefined values. To check for this eventuality, the container following this one checks for this.";
				
			container ReadOpbrengsten_perOP  := 
				for_each_neidv(
					OP/name
					, 'ReadOpbrengsten_perOP_UNCHECKED/'+OP/name
					, 'not(all(IsNull(this)))'
					, AdminDomain
					, Eur
				), Descr = "Read opbrengsten per OP uit tif, nadet deze gecheckt zijn op undefined waarden.";
				
			container Woningwaarde_perOP := for_each_ne(OP/name, 'Templates/Suitabilities/Woningwaarde_OP_T('+string(id(OP))+'[OP])'), Descr = "Aanroep van template voor de berekening van de woningwaarde.";
		}
		
		container Opbrengsten_perOP_src  := = ModelParameters/VariantDataOntkoppeld ? 'Impl/ReadOpbrengsten_perOP' : 'Impl/WriteOpbrengsten_perOP', Descr = "Selecteer read of write obv parameter instelling.";
		container Opbrengsten_perOP      := 
			for_each_nedv(
				OP/name
				, 'collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, Opbrengsten_perOP_src/'+OP/name+')'
				, CompactedAdminDomain
				, Eur
			), Descr = "Vertaal naar compacted domain.";
		
		#include<Bouwkosten.dms>
	}
}