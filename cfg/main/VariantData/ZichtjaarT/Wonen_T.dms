Template Wonen_T
{
	//
	parameter<string> subsector_name;
	//
	
	unit<uint8> OP_sub := ='OP/perWP2xVSSH/'+subsector_name+'/OP_sub';
	
	attribute<Eur> MaxElem_Subsector (CompactedAllocDomain) := ='max_elem('+AsList('perOP/'+OP_sub/name+'/Totaal',',')+')';
	
	container perOP :=
		for_each_ne(
			OP_sub/name
			, 'perOP_T('+quote(OP_sub/name)+')'
		);
	
	Template perOP_T
	{
		//
		parameter<string> OP_name;
		//
		
		attribute<float32> Opbrengsten (CompactedAdminDomain) := ='src/Wonen/Opbrengsten_perOP/'+OP_name;
		
		attribute<Eur> Grondproductiekosten (CompactedAdminDomain) :=
			='collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Suitabilities/Wonen/Grondproductiekosten/Results/Totaal/'+/ModelParameters/grondproductie_kosten_variant+') 
			* float32(PotentieleState0/'+name+'/SectorXSubsectoren/Wonen/'+subsector_name+'/Beschikbaar) * AdminDomain/NrHaPerCell';
			
		attribute<Eur> Bouwkosten (CompactedAdminDomain) := ='src/Wonen/Bouwkosten/perOP/'+OP_name;
		
		container Verwervingskosten
		{
			attribute<Eur> Woningen      (CompactedAdminDomain) := = 'collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Suitabilities/Wonen/Verwervingskosten/Woningen/Totaal)      * float32(PotentieleState0/'+name+'/SectorXSubsectoren/Wonen/'+subsector_name+'/Beschikbaar)';
			attribute<Eur> Niet_woningen (CompactedAdminDomain) := = 'collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Suitabilities/Wonen/Verwervingskosten/Niet_woningen/Totaal) * float32(PotentieleState0/'+name+'/SectorXSubsectoren/Wonen/'+subsector_name+'/Beschikbaar)';
			attribute<Eur> Totaal        (CompactedAdminDomain) := Woningen + Niet_woningen;
		}
		
		attribute<Eur> ResidueleWaarde  (CompactedAdminDomain) := Opbrengsten - Bouwkosten - Grondproductiekosten;
		attribute<Eur> ExploitatieSaldo (CompactedAdminDomain) := ResidueleWaarde - Verwervingskosten/Totaal;
		attribute<Eur> Per_AdminDomain0 (CompactedAdminDomain) := ExploitatieSaldo;
		
		attribute<Eur> TotaalIJburg (CompactedAdminDomain) :=
			rth_element(Per_AdminDomain0, 0.5f, collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, SourceData/RegioIndelingen/LMSSubzone/Per_AdminDomain))[SourceData/RegioIndelingen/LMSSubzone/Ijburg2_proxy] //mediaan in IJburg en de rest van NL
			/ collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, float32(SourceData/RegioIndelingen/LMSSubzone/IsIjburg2)[SourceData/RegioIndelingen/LMSSubzone/per_AdminDomain])
			* rnd_uniform(0, CompactedAdminDomain, range(float32, 0f, 1f));
		
		attribute<Eur> TotaalAdminDomain (CompactedAdminDomain) := MakeDefined(Per_AdminDomain0, TotaalIjburg);
		attribute<Eur> CalcTotaal        (CompactedAllocDomain) := sum(TotaalAdminDomain, CompactedAdminDomain/CompactedAllocDomain_rel);
		attribute<Eur> Totaal            (CompactedAllocDomain) := CalcTotaal;
	}
}