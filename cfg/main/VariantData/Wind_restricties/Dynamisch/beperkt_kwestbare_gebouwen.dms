container beperkt_kwestbare_gebouwen
: Descr = "Gebouwen waar minder intensief of tijdelijk verblijf plaatsvindt. Voorbeelden: boerderijen met woning, kleine kantoren, sportclubs.
			Standaard formule: afstand = 1/2 * rotordiameter"
{
	attribute<Float32> woon_VBOs_perAllocDomain (AllocDomain) := sum((BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == vbo_gebruiksdoel_plus/V/woon)[BAG/ToedelingsMatrix_AllocDomain/Pand_rel][float32] * BAG/ToedelingsMatrix_AllocDomain/ToedelingPand, BAG/ToedelingsMatrix_AllocDomain/AllocDomain_rel);
	attribute<Float32> woon_VBOs_perAllocDomain_perPand (BAG) := woon_VBOs_perAllocDomain[BAG/ToedelingsMatrix_AllocDomain/AllocDomain_rel][invert(BAG/ToedelingsMatrix_AllocDomain/Pand_rel)];
		
	unit<UInt32> verspreide_Woningen := select_with_org_rel(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/woon
										&& BAG/WP4_rel == Classifications/Vastgoed/WP4/V/Vrijstaand
										&& woon_VBOs_perAllocDomain_perPand <= 2f
										)
	, Descr = "a. verspreid liggende Woningen van derden met een dichtheid van maximaal 2 per ha, en dienst- en bedrijfsWoningen van derden;"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> kantoren := select_with_org_rel(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor
										&& !(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor && BAG/Oppervlaktes/vbo_opp >= 1500[m2_pand])
										)
	, Descr = "b. Kantoorgebouwen, wanneer niet kwetsbaarobject c"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> hotels_restaurants := select_with_org_rel((BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/logies || BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/bijeenkomst)
												&& !(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor && BAG/Oppervlaktes/vbo_opp >= 1500[m2_pand])
												)
	, Descr = "c. hotels en restaurants, wanneer niet kwetsbaarobject c"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> winkels := select_with_org_rel((BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/winkel)
												&& !(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor && BAG/Oppervlaktes/vbo_opp >= 1500[m2_pand])
												)
	, Descr = "d. winkels, wanneer niet kwetsbaarobject c"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> sportlocaties := select_with_org_rel(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/sport)
	, Descr = "e. sporthallen, zwembaden en speeltuinen"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> sport_kampeer_recreatie := ='select_with_org_rel(SourceData/Grondgebruik/BBG/'+BBG_Bron+'/CBSKlasse_rel_'+string(ModelParameters/BBG_Year)+' == /Classifications/Grondgebruik/CBSKlasse/V/sportterreinen
													|| SourceData/Grondgebruik/BBG/'+BBG_Bron+'/CBSKlasse_rel_'+string(ModelParameters/BBG_Year)+' == /Classifications/Grondgebruik/CBSKlasse/V/dagrecreatieve_terreinen
													)'
	, Descr = "f. sport- en kampeerterreinen en terreinen bestemd voor recreatieve doeleinden, wanneer niet kwetsbaarobject d"
	{
		attribute<rdc_meter> geometry         (poly)        := ='SourceData/Grondgebruik/BBG/'+BBG_Bron+'/geometry_simpl[org_rel]';
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> bedrijfsgebouwen := select_with_org_rel((BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/industrie 
												||  BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Utiliteit_Combi
												||  BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Logistiek)
											&& !(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor && BAG/Oppervlaktes/vbo_opp >= 1500[m2_pand])
											)
	, Descr = "g. bedrijfsgebouwen, wanneer niet kwetsbaarobject c"
	{
		attribute<rdc_meter> geometry         (poly)        := BAG/geometry_simpl[org_rel];
		attribute<rdc_meter> geometry_buffer  (poly)        := geos_buffer_multi_polygon(geometry, (0.5f * CaseClassifications/WindTurbineType/RotorDiameter)[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// h. Objecten die met de onder a tm e en g genoemde gelijkgesteld kunnen worden uit hoofde van de gemiddelde tijd 
		// per dag gedurende welke personen daar verblijven, het aantal personen dat daarin doorgaans aanwezig is en 
		// de mogelijkheden voor zelfredzaamheid bij een ongeval, voorzover die Objecten geen kwetsbare Objecten zijn.
		
	// i. Objecten met een hoge infrastructurele waarde, zoals een telefoon- of elektriciteitcentrale of een 
		// gebouw met vluchtleidingsapparatuur, voor zover die Objecten Wegen de aard van de gevaarlijke stoffen 
		// die bij een ongeval kunnen vrijkomen, bescherming verdienen tegen de gevolgen van dat ongeval.
		
	attribute<Bool> IsNietToegestaan_basis (AdminDomain) := verspreide_Woningen/IsNietToegestaan_basis
		|| kantoren/IsNietToegestaan_basis
		|| hotels_restaurants/IsNietToegestaan_basis
		|| winkels/IsNietToegestaan_basis 
		|| sportlocaties/IsNietToegestaan_basis 
		|| sport_kampeer_recreatie/IsNietToegestaan_basis
		|| bedrijfsgebouwen/IsNietToegestaan_basis 
		;
}