container kwestbare_gebouwen 
: Descr = "Plaatsen waar veel mensen langdurig verblijven of waar kwetsbare groepen aanwezig zijn (kinderen, zieken, ouderen).
			Standaard formule: afstand = MaxElem(ashoogte + 1/2 * rotordiameter, max werpafstand bij nom. toerental) "
{
	unit<UInt32> Woningen := select_with_org_rel(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/woon)
	, Descr = "a. Woningen, niet zijnde Woningen als bedoeld in beperkt kwetsbare gebouwen"
	{
		attribute<rdc_meter> geometry (poly) := BAG/geometry_simpl[org_rel];
		
		parameter<meter>     Buffer_size     :=='max_elem(
													VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
													CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
													CaseClassifications/WindTurbineType/MaxWerpAfstandNom
												)';
		
		attribute<rdc_meter> geometry_buffer (poly) := geos_buffer_multi_polygon(geometry,Buffer_size[float64], 8b);
		
		attribute<Bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> ziekenhuizen_school_opvang := select_with_org_rel(BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Gezondheidszorg 
														|| BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/onderwijs
														|| BAG/Oppervlaktes/Fractie_VBOOpp_perGebruiksdoel/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/cel)
	, Descr = "b. gebouwen bestemd voor het verblijf, al dat  niet gedurende een gedeelte van de dag, van minderjarigen, ouderen, zieken of gehandicapten, zoals: 
				- ziekenhuizen, bejaardenhuizen, en verpleeghuizen
				- scholen
				- gebouwen of gedeelten daarvan, bestemd voor dagopvang van minderjarigen"
	{
		attribute<rdc_meter> geometry (poly) := BAG/geometry_simpl[org_rel];
		
		parameter<meter>     Buffer_size     :=='max_elem(
													VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
													CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
													CaseClassifications/WindTurbineType/MaxWerpAfstandNom
												)';
		attribute<rdc_meter> geometry_buffer (poly) := geos_buffer_multi_polygon(geometry,Buffer_size[float64], 8b);
			
		attribute<Bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> Kantoor_winkel_complexen := select_with_org_rel(
			(BAG_pand/BepalingGebruiksdoel/gebruiksdoel_plus_rel == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/Kantoor && BAG_pand/Oppervlaktes/vbo_opp >= 1500[m2_pand])
			|| (BAG_pand/BepalingGebruiksdoel/gebruiksdoel_plus_rel == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/logies && BAG_pand/Oppervlaktes/vbo_opp >= 1500[m2_pand])
			|| (IsDefined(point_in_polygon(centroid_or_mid(BAG_pand/geometry_simpl), BaseData/Restricties/Afleiding_Winkelcentra/WinkelCentra/WinkelComplexen/geometry)) 
					&&BAG_pand/BepalingGebruiksdoel/gebruiksdoel_plus_rel == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/winkel)
		)
	, Descr = "c. gebouwen waarin doorgaans grote aantallen personen gedurende een groot gedeelte van de dag aanwezig zijn, zoals:
				- Kantoorgebouwen en hotel met een bruto vloeroppervlak van meer dan 1500 m2 per Object
				- complexen waarin meer dan 5 winkels zijn gevestigd en waarvan het gezamenlijk bruto vloeroppervlak meer dan 1000 m2 bedraagt en winkels met een Totaal bruto vloeroppervlak van meer dan 2000 m2 per winkel, voorzover in die complexen of in de winkels een supermakrt, hypermarkt of warenhuis is gevestigd."
	{
		attribute<rdc_meter> geometry (poly) := BAG_pand/geometry_simpl[org_rel];
		
		parameter<meter>     Buffer_size     :=='max_elem(
													VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
													CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
													CaseClassifications/WindTurbineType/MaxWerpAfstandNom
												)';
		attribute<rdc_meter> geometry_buffer (poly) := geos_buffer_multi_polygon(geometry,Buffer_size[float64], 8b);
			
		attribute<Bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	unit<UInt32> kampeer_recreatie_groot := ='select_with_org_rel(SourceData/Grondgebruik/BBG/'+BBG_Bron+'/CBSKlasse_rel_'+string(ModelParameters/BBG_Year)+' == /Classifications/Grondgebruik/CBSKlasse/V/Verblijfsrecreatie)'
	, Descr = "d. kampeer- en andere recreatieterreinen bestemd voor het verblijf van meer dan 50 personen gedurende meerdere aaneengesloten dagen."
	{
		attribute<rdc_meter> geometry         (poly) := ='SourceData/Grondgebruik/BBG/'+BBG_Bron+'/geometry_simpl[org_rel]';
		
		parameter<meter>     Buffer_size     := ='VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+']';
		attribute<rdc_meter> geometry_buffer (poly) := geos_buffer_multi_polygon(geometry,Buffer_size[float64], 8b);
		attribute<Bool>      IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	attribute<Bool> IsNietToegestaan_basis (AdminDomain) := Woningen/IsNietToegestaan_basis 
		|| ziekenhuizen_school_opvang/IsNietToegestaan_basis
		|| Kantoor_winkel_complexen/IsNietToegestaan_basis 
		|| kampeer_recreatie_groot/IsNietToegestaan_basis 
		;
}