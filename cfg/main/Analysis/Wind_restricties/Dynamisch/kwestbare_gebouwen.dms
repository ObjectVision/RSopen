container kwestbare_gebouwen // 2.  kwestbare gebouwen --> afstand = MaxElem(ashoogte + 1/2 * rotordiameter, max werpafstand bij nom. toerental)
{
	// a. woningen, niet zijnde woningen als bedoeld in beperkt kwetsbare gebouwen
	unit<uint32> woningen := subset(BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/woon)
	{
		attribute<rdc_meter> geometry (poly) := BAG/geometry[nr_OrgEntity];
		
		attribute<rdc_meter> geometry_buffer (poly) :=
			= 'bg_buffer_polygon(
				geometry,
				max_elem(
					VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
					CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
					CaseClassifications/WindTurbineType/MaxWerpAfstandNom)[float64],
			8b)';
		
		attribute<bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// b. gebouwen bestemd voor het verblijf, al dat  niet gedurende een gedeelte van de dag, van minderjarigen, ouderen, zieken of gehandicapten, zoals:
		// - ziekenhuizen, bejaardenhuizen, en verpleeghuizen
		// - scholen
		// - gebouwen of gedeelten daarvan, bestemd voor dagopvang van minderjarigen
	unit<uint32> ziekenhuizen_school_opvang := subset(BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/gezondheidszorg 
														|| BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/onderwijs
														|| BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/cel)
	{
		attribute<rdc_meter> geometry (poly) := BAG/geometry[nr_OrgEntity];
		
		attribute<rdc_meter> geometry_buffer (poly) :=
			= 'bg_buffer_polygon(
				geometry,
				max_elem(
					VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
					CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
					CaseClassifications/WindTurbineType/MaxWerpAfstandNom)[float64],
			8b)';
			
		attribute<bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// c. gebouwen waarin doorgaans grote aantallen personen gedurende een groot gedeelte van de dag aanwezig zijn, zoals:
		// - kantoorgebouwen en hotel met een bruto vloeroppervlak van meer dan 1500 m2 per object
		// - complexen waarin meer dan 5 winkels zijn gevestigd en waarvan het gezamenlijk bruto vloeroppervlak meer dan 1000 m2 bedraagt en winkels met een totaal bruto vloeroppervlak van meer dan 2000 m2 per winkel, voorzover in die complexen of in de winkels een supermakrt, hypermarkt of warenhuis is gevestigd.
	unit<uint32> kantoor_winkel_complexen := subset(
			(BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/kantoor && BAG/pand_vbo_opp >= 1500[m2_pand])
			|| (BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/logies && BAG/pand_vbo_opp >= 1500[m2_pand])
			|| (IsDefined(point_in_polygon(centroid_or_mid(BAG/geometry), BaseData/Afleiding_Winkelcentra/WinkelCentra/WinkelComplexen/Geometry)) 
					&& BAG/Fractie_VBOOpp_per_vbo_functie/ArgMax == /Classifications/Vastgoed/vbo_gebruiksdoel_plus/V/winkel)
		)
	{
		attribute<rdc_meter> geometry (poly) := BAG/geometry[nr_OrgEntity];
		
		attribute<rdc_meter> geometry_buffer (poly) :=
			= 'bg_buffer_polygon(
				geometry,
				max_elem(
					VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'],
					CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter,
					CaseClassifications/WindTurbineType/MaxWerpAfstandNom)[float64],
			8b)';
			
		attribute<bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// d. kampeer- en andere recreatieterreinen bestemd voor het verblijf van meer dan 50 personen gedurende meerdere aaneengesloten dagen.
	unit<uint32> kampeer_recreatie_groot := subset(/SourceData/Grondgebruik/BBG/BBG_Reeks_1996_2017/CBSKlasse_rel_2017 == /Classifications/Grondgebruik/CBSKlasse/V/Verblijfsrecreatie)
	{
		attribute<rdc_meter> geometry         (poly) := SourceData/Grondgebruik/BBG/BBG_Reeks_1996_2017/geometry[nr_OrgEntity];
		attribute<rdc_meter> geometry_buffer  (poly) := = 'bg_buffer_polygon(geometry, VariantParameters/VariantK/AfstandGeluidsnorm[VariantParameters/VariantK/V/'+Variant_name+'][float64], 8b)';
		attribute<bool> IsNietToegestaan_basis (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	attribute<bool> IsNietToegestaan_basis (AdminDomain) := woningen/IsNietToegestaan_basis 
		|| ziekenhuizen_school_opvang/IsNietToegestaan_basis
		|| kantoor_winkel_complexen/IsNietToegestaan_basis 
		|| kampeer_recreatie_groot/IsNietToegestaan_basis 
		;
}