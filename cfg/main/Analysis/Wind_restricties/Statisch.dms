container Statisch
{
	// 3.  bestaande kassen
	unit<uint32> bestaande_kassen := OrgPath/Bestaande_kassen
	{
		attribute<rdc_meter> geometry (poly) := OrgPath/Bestaande_kassen/Geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry, AdminDomain));
	}
	
	// 5.  buisleidingen ondergronds --> afstand = MaxElem(ashoogte + 1/2 * rotordiameter, max werpafstand bij nom. toerental)
	// 5.  buisleidingen bovengronds --> afstand = max werpafstand bij nom. toerental
	unit<uint32> Buisleidingen := OrgPath/Buisleidingen_bestaand
	{
		attribute<rdc_meter> geometry_buffer (poly) := OrgPath/Buisleidingen_bestaand/geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 6.  hoogspanning --> afstand = MaxElem(ashoogte + 1/2 * rotordiameter, max werpafstand bij nom. toerental)
	unit<uint32> hoogspanning := subset(/SourceData/Energie/RES10_concept/Analysekaarten/RES_4_elektriciteit_infrastructuur/tennet_kabels_en_leidingen_v1/SPANNINGSN >= 100d)
	{
		attribute<rdc_meter> geometry        (arc)  := /SourceData/Energie/RES10_concept/Analysekaarten/RES_4_elektriciteit_infrastructuur/tennet_kabels_en_leidingen_v1/geometry[nr_OrgEntity];
		attribute<rdc_meter> geometry_buffer (dyna_points, poly) := Punten_Buffer/geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
		
		unit<uint32>         segments               := arc2segm(geometry);
		unit<uint32>         dyna_points            := dyna_point(segments/point, segments/nextpoint, 10f)
		{
			attribute<rdc_meter> geometry := point;
		}
		container Punten_Buffer := Templates/PointBuffer(dyna_points, max_elem(CaseClassifications/WindTurbineType/Ashoogte + 0.5f * CaseClassifications/WindTurbineType/RotorDiameter, CaseClassifications/WindTurbineType/MaxWerpAfstandNom));
	}
	
	// 8.  luchthaven
	unit<uint32> luchthaven := OrgPath/Luchthaven
	{
		attribute<rdc_meter> geometry_buffer (poly) := OrgPath/Luchthaven/geometry;
		
		attribute<bool> IsNietToegestaan  (AdminDomain) := IsNietToegestaan0 && !OR(
			IsNietToegestaan0/Rotterdam,
			IsNietToegestaan0/Eindhoven,
			IsNietToegestaan0/Lelystad,
			IsNietToegestaan0/Maastricht,
			IsNietToegestaan0/Groningen
		);
		
		attribute<bool> IsNietToegestaan0        (AdminDomain) := IsDefined(poly2grid(geometry_buffer, AdminDomain))
		{
			attribute<bool> Rotterdam (AdminDomain) := . && RegioIndelingen/Provincie/per_AdminDomain == RegioIndelingen/Provincie/V/Zuid_Holland && !(RegioIndelingen/RES/per_AdminDomain == RegioIndelingen/RES/V/Holland_Rijnland);
			attribute<bool> Eindhoven (AdminDomain) := . && OR(
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Bladel,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Eersel,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Veldhoven,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Oirschot,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Eindhoven,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Best,
				RegioIndelingen/Gemeente2019_single/per_AdminDomain == RegioIndelingen/Gemeente2019_single/V/Son_En_Breugel,
				RegioIndelingen/Wijk/src_2019/per_AdminDomain == RegioIndelingen/Wijk/src_2019/V/Sint_Oedenrode
			);
			
			attribute<bool> Lelystad   (AdminDomain) := . && RegioIndelingen/Provincie/per_AdminDomain == RegioIndelingen/Provincie/V/Flevoland;
			attribute<bool> Maastricht (AdminDomain) := . && RegioIndelingen/COROP/per_AdminDomain == RegioIndelingen/COROP/V/Zuid_Limburg;
			attribute<bool> Groningen  (AdminDomain) := . && OR(
				RegioIndelingen/NVM/per_AdminDomain == RegioIndelingen/NVM/V/Noord_Drenthe,
				RegioIndelingen/NVM/per_AdminDomain == RegioIndelingen/NVM/V/Stad_Groningen_EO,
				RegioIndelingen/NVM/per_AdminDomain == RegioIndelingen/NVM/V/Slochteren_EO
			);
		}
	}
	// 9.  primaire waterkering --> buiten kernzone
	unit<uint32> primaire_waterkering := OrgPath/Primaire_waterkering
	{
		attribute<rdc_meter> geometry_buffer (poly)       := OrgPath/primaire_waterkering/geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 10. risico inrichting 
	unit<uint32> Risico_inrichting := OrgPath/Risico_inrichting
	{
		attribute<rdc_meter> geometry_buffer (poly)       := OrgPath/Risico_inrichting/geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 11. spoor --> afstand = MaxElem(7.85m + 1/2 * rotordiameter, 30m)
	unit<uint32> spoor := SourceData/Grondgebruik/BGT/ReadCombine/IsSpoorbaan
	{
		attribute<rdc_meter> geometry (poly)        := SourceData/Grondgebruik/BGT/ReadCombine/IsSpoorbaan/Geometry;
		attribute<rdc_meter> geometry_buffer (poly) := bg_buffer_polygon(geometry, Max_Elem(7.85f + (0.5f * CaseClassifications/WindTurbineType/RotorDiameter), 30f)[float64], 8b);
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 12. waterwegen --> afstand = MaxElem(1/2 * rotordiameter, 50m)
	unit<uint32> waterwegen := SourceData/Grondgebruik/BRT/IsWaterweg
	{
		attribute<rdc_meter> geometry (poly)        := SourceData/Grondgebruik/BRT/IsWaterweg/Geometry;
		attribute<rdc_meter> geometry_buffer (poly) := bg_buffer_polygon(geometry, Max_Elem(0.5f * CaseClassifications/WindTurbineType/RotorDiameter, 50f)[float64], 8b);
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 13. wegen --> afstand = MaxElem(1/2 * rotordiameter, 30m)
	unit<uint32> wegen := SourceData/Grondgebruik/BRT/IsHoofdweg
	{
		attribute<rdc_meter> geometry (poly)        := SourceData/Grondgebruik/BRT/IsHoofdweg/Geometry;
		attribute<rdc_meter> geometry_buffer (poly) := bg_buffer_polygon(geometry, Max_Elem(0.5f * CaseClassifications/WindTurbineType/RotorDiameter, 30f)[float64], 8b);
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry_buffer, AdminDomain));
	}
	
	// 15. zee
	unit<uint32> zee := SourceData/Grondgebruik/BRT/IsZee
	{
		attribute<rdc_meter> geometry (poly)              := SourceData/Grondgebruik/BRT/IsZee/Geometry;
		attribute<bool>      IsNietToegestaan (AdminDomain)   := IsDefined(poly2grid(geometry, AdminDomain));
	}
}