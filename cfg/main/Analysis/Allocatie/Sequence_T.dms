Template Sequence_T : using = "Classifications/modellering"
{
	//
	parameter<Sequence> id;
	container StateVoorAllocatie;
	container Context0;
	parameter<bool> IsSeq0;
	unit<uint8> SectorAllocRegio;
	unit<uint8> Sequence;
	container Impl0;
	//
	
	parameter<string> name := Sequence/name[id];
	
	container Context := Context0
	{
		container ThisSequence := ..;
		parameter<string> ThisSequenceName := Sequence/name[id];
	}
	
	attribute<bool> ExtraRestrictieDoorWindAllocatie (CompactedAdminDomain) := =name == 'Seq_0' ? 'const(FALSE, CompactedAdminDomain)' : 'impl/ExtraRestrictieDoorWindAllocatie/IsNietToegestaan';
	
	container impl := impl0
	{
		container ExtraRestrictieDoorWindAllocatie
		{
			parameter<string> Gealloceerd_str := Context/ThisZichtjaar/name == first(Time/Zichtjaar/name)
				? 'const(FALSE, CompactedAdminDomain)'
				: 'Analysis/Allocatie/Zichtjaren/'+rjoin(Context/ThisZichtjaar/name, Time/Zichtjaar/name, Time/Zichtjaar/PrevName)+'/Sequences/Seq_0/StateNaAllocatie/Wind/Totaal > 0f';
			
			attribute<bool> Gealloceerd        (CompactedAdminDomain) := = Gealloceerd_str;
			attribute<bool> Gealloceerd_buffer (CompactedAdminDomain) := = Context/ThisZichtjaar/name == first(Time/Zichtjaar/name)
				? 'const(FALSE, CompactedAdminDomain)'
				: 'Buffer/proximity_res';
			
			container Buffer := Templates/Buffer_gridcel_T(Gealloceerd,  4f * CaseClassifications/WindTurbineType/RotorDiameter);
			
			attribute<bool> IsNietToegestaan (CompactedAdminDomain) := Gealloceerd_buffer;
		}
	}
	
	container SectorAllocRegios :=
		for_each_ne(
			SectorAllocRegio/name
			,'SectorAllocRegio_T(
				SectorAllocRegio
				,'+string(id(SectorAllocRegio))+'[SectorAllocRegio]
				,'+MakeDefined((SectorAllocRegio/name+'/StateNaAllocatie')[sub_or_null(ID(SectorAllocRegio),1b)], 'StateVoorAllocatie')+'
				, Context
				, '+string(SectorAllocRegio/AantalIters)+'[uint8]
				, impl
			)'
		);
	
	container StateNaAllocatie := = #SectorAllocRegio > 0 ? 'SectorAllocRegios/'+last(SectorAllocRegio/name)+'/StateNaAllocatie' : 'StateVoorAllocatie';
}