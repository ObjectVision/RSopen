Template Zeef_Wonen_perOP_T
: Descr = "Bepaal de zeef voor wonen per OP in de zichtjaren"
{
	parameter<String> Subsector_name;
	parameter<String> OP_name;
	parameter<String> WP2xVSSH_name;
	parameter<String> Zichtjaar_name;
	
	attribute<Bool> Beschikbaar_src (CompactedAdminDomain);
	parameter<Bool> HoogbouwOP;
	
	parameter<Woning_ha> Dichtheid_ha;
	
	container Dichtheid;
	container Geschiktheid;
	//
	
	parameter<Bool>      HoogbouwToegestaanInMooiLandschap     := ='VariantParameters/VariantK/HoogbouwToegestaanInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Bool>      HoogbouwToegestaanInZeerMooiLandschap := ='VariantParameters/VariantK/HoogbouwToegestaanInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Bool>      TinyHousesToegestaan                  := ='VariantParameters/VariantK/TinyHousesToegestaan[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Bool>      SuperStedelijkToegestaan              := ='VariantParameters/VariantK/SuperStedelijkToegestaan[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Woning_ha> MaxDichtheidInMooiLandschap           := ='VariantParameters/VariantK/MaxDichtheidInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Woning_ha> MaxDichtheidInZeerMooiLandschap       := ='VariantParameters/VariantK/MaxDichtheidInZeerMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Float32>   MinimumDichtheidToenameWonen          := ='VariantParameters/VariantK/MinimumDichtheidToenameWonen[VariantParameters/VariantK/V/'+Variant_name+']';
	parameter<Eur>       MinimumExploitatieSaldo               := ='VariantParameters/VariantK/MinimumExploitatieSaldo[VariantParameters/VariantK/V/'+Variant_name+']';
	
	attribute<Woning> PotentieleState (CompactedAllocDomain) := sum(Dichtheid_ha * AdminDomain/NrHaPerCell / float32(Beschikbaar_src), CompactedAdminDomain/CompactedAllocDomain_rel);
	attribute<Woning> StateBasisjaar  (CompactedAllocDomain) := sum(BaseData/StartState/StateBasisjaar/Src0/Wonen/WP1s/Totaal / float32(Beschikbaar_src), CompactedAdminDomain/CompactedAllocDomain_rel);
	
	container Impl
	{
		//resultaat van deze items is waar het NIET is toegestaan
		attribute<Bool> HoogbouwToegestaanInMooiLandschap     (CompactedAdminDomain) := HoogbouwOP && !../HoogbouwToegestaanInMooiLandschap 
																							? collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Omgeving/BelevingLandschap/IsHoog[AdminDomain/AllocDomain_rel]) 
																							: FALSE, Descr = "Is hoogbouw niet toegestaan in mooi landschap, dan mag je hier niet bouwen"; //hoogbouw in OP? && Is hoogbouw niet toegestaan? && Is mooilandschap?
		attribute<Bool> HoogbouwToegestaanInZeerMooiLandschap (CompactedAdminDomain) := HoogbouwOP && !../HoogbouwToegestaanInZeerMooiLandschap ? collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Omgeving/BelevingLandschap/IsZeerHoog[AdminDomain/AllocDomain_rel]) : FALSE, descr = "Is hoogbouw niet toegestaan in zeer mooi landschap, dan mag je hier niet bouwen";
		attribute<Bool> TinyHousesToegestaan                  (CompactedAdminDomain) := const(!../TinyHousesToegestaan     && (lowercase(OP_name) == lowercase('TinyHousesVS')     || lowercase(OP_name) == lowercase('TinyHousesLaagVS')), CompactedAdminDomain), Descr = "Als tiny house niet zijn toegestaan worden die OPs voor het hele gebied restrictief.";
		attribute<Bool> SuperStedelijkToegestaan              (CompactedAdminDomain) := const(!../SuperStedelijkToegestaan && (lowercase(OP_name) == lowercase('SuperStedelijkVS') || lowercase(OP_name) == lowercase('SuperStedelijkSH')), CompactedAdminDomain), Descr = "Als superstedelijk niet zijn toegestaan worden die OPs voor het hele gebied restrictief.";
		attribute<Bool> MaxDichtheid                          (CompactedAdminDomain) := = 'Dichtheid_ha > collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, VariantData/Density/MaxWoningDichtheid/'+WP2xVSSH_name+'/Result[AdminDomain/AllocDomain_rel])', Descr = "Is de dichtheid hoger dan de maximale dichtheid voor deze WP2xVSSH?";
		attribute<Bool> MaxDichtheidInMooiLandschap           (CompactedAdminDomain) := Dichtheid_ha > ../MaxDichtheidInMooiLandschap     && collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Omgeving/BelevingLandschap/IsHoog[AdminDomain/AllocDomain_rel]), Descr = "Is de dichtheid hoger dan de maximale dichtheid in mooi landschap en is het een mooi landschap?";
		attribute<Bool> MaxDichtheidInZeerMooiLandschap       (CompactedAdminDomain) := Dichtheid_ha > ../MaxDichtheidInZeerMooiLandschap && collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/Omgeving/BelevingLandschap/IsZeerHoog[AdminDomain/AllocDomain_rel]), Descr = "Is de dichtheid hoger dan de maximale dichtheid in zeer mooi landschap en is het een zeer mooi landschap?";
		
		attribute<Bool> MinimumDichtheidToenameWonen (CompactedAdminDomain) := ((PotentieleState - StateBasisjaar)[float32] / StateBasisjaar[float32] <= ../MinimumDichtheidToenameWonen)[CompactedAdminDomain/CompactedAllocDomain_rel], Descr = "Is de dichtheidstoename van wonen ten opzichte van de basisjaarstate lager dan de minimum dichtheidstoename voor wonen?";
		attribute<Bool> MinimumExploitatieSaldo      (CompactedAdminDomain) := = '(Geschiktheid/'+Zichtjaar_name+'/Wonen/'+Subsector_name+'/perOP/'+OP_name+'/Totaal < ../MinimumExploitatieSaldo[float32])[CompactedAdminDomain/CompactedAllocDomain_rel]', Descr = "Is het exploitatie saldo lager dan de minimum exploitatie saldo?";
	}
	
	attribute<Bool> MoetenWordenWeggezeefd (CompactedAdminDomain) := //fka Result
		OR(
			Impl/HoogbouwToegestaanInMooiLandschap,
			Impl/HoogbouwToegestaanInZeerMooiLandschap,
			Impl/TinyHousesToegestaan,
			Impl/SuperStedelijkToegestaan,
			Impl/MaxDichtheid,
			Impl/MaxDichtheidInMooiLandschap,
			Impl/MaxDichtheidInZeerMooiLandschap,
			Impl/MinimumDichtheidToenameWonen,
			Impl/MinimumExploitatieSaldo
		), Descr = "Moet de locatie worden weggezeefd voor deze OP?";
	
	attribute<Bool> Beschikbaar_voorOP (CompactedAdminDomain) := !MoetenWordenWeggezeefd && Beschikbaar_src, Descr = "Is de locatie beschikbaar voor deze OP?";
}