Template Zeef_T 
: Descr = "Bepaal de zeef in de zichtjaren"
{
	unit<uint32> VariantXSectorxSubsector;
	parameter<VariantXSectorxSubsector> id;
	container Analysis;
	parameter<string> Zichtjaar_name;
	//
	
	parameter<string> name := VariantXSectorxSubsector/combi_name[id];
	parameter<ha>      MinimalLandAvailability          := VariantK/MinimalLandAvailability[/Variant_rel];
	parameter<ha>      MinimalLandAvailability_Landbouw := VariantK/MinimalLandAvailability_Landbouw[/Variant_rel];
	parameter<float32> MinimumDichtheidToenameSector  := ='VariantK/MinimumDichtheidToename'+Sector_name+'[/Variant_rel]';
	parameter<float32> MinimumSubsectorShare          := VariantK/MinimumSubsectorShare[/Variant_rel];
	parameter<string>  Variant_name                   := VariantK/name[/Variant_rel];
	parameter<string>  Sector_name                    := VariantXSectorxSubsector/Sector_name[id];
	parameter<string>  Subsector_name                 := VariantXSectorxSubsector/Subsector_name[id];
	
	attribute<bool>    IsBeschikbaar                       (CompactedAdminDomain) := !IsRestrictief;
	attribute<bool>    IsRestrictief                       (CompactedAdminDomain) := = 
		'OR(
			 IsRestrictief_BuitenPlancapaciteit[CompactedAdminDomain/CompactedAllocDomain_rel]
			,IsRestrictief_OokBinnenPlancapaciteit[CompactedAdminDomain/CompactedAllocDomain_rel]
			,VariantData/Zeef/Basisjaar/'+Sector_name+'/'+Subsector_name+'/IsRestrictief
		)';
	
	//Geldt ook in plannen voor van betreffende sector
	attribute<bool> IsRestrictief_OokBinnenPlancapaciteit  (CompactedAllocDomain) := = Sector_name = 'Wind' || Sector_name = 'Zon'  || Sector_name = 'Landbouw'  ? 'Impl/HeeftPandenInCelInBasisjaar' : 'Impl/TeKleineDichtheidsToenameSubsector';
	
	//Geldt alleen buiten plannen van betreffende sector
	attribute<bool> IsRestrictief_BuitenPlancapaciteit     (CompactedAllocDomain) := IsRestrictief_BuitenPlancapaciteit0 && !Src/HeeftPlannenDezeSector;
	attribute<bool> IsRestrictief_BuitenPlancapaciteit0    (CompactedAllocDomain) := = Sector_name = 'Wind' || Sector_name = 'Zon' ? 'const(FALSE, CompactedAllocDomain)' : 'OR(Impl/TeLageSubsectorShare, Impl/GeeftOnvoldoendeRuimte)';
	
	container Impl
	{
		attribute<bool>    HeeftPandenInCelInBasisjaar         (CompactedAllocDomain) := (Sector_name == 'Zon' || Sector_name == 'Wind' || Sector_name == 'Verblijfsrecreatie') && src/HeeftPandenInCelInBasisjaar;
		attribute<bool>    TeKleineDichtheidsToenameSubsector  (CompactedAllocDomain) := Sector_name == 'Werken' && src/PotentieleDichtheidToenameSubsector < MinimumDichtheidToenameSector;
		attribute<bool>    TeLageSubsectorShare                (CompactedAllocDomain) := Sector_name == 'Werken' && src/SubsectorShare < MinimumSubsectorShare;
		attribute<bool>    GeeftOnvoldoendeRuimte              (CompactedAllocDomain) := Src/PotentieleRuimte < MinimalLandAvailability;
	}
	
	container Src
	{
		attribute<bool>    HeeftPandenInCelInBasisjaar         (CompactedAllocDomain) := = 'collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, SourceData/Vastgoed/BAG/PerJaar/Y'+string(/ModelParameters/BAG_RecentYear)+'/pand/counts/Per_AllocDomain)[float32] > 0f';
		attribute<bool>    HeeftPlannenDezeSector              (CompactedAllocDomain) := = Sector_name == 'Landbouw' ? 'const(FALSE, CompactedAllocDomain)' : 'Plannen/Per_CompactedAllocDomain/Hard/Result/'+Sector_name+' || Plannen/Per_CompactedAllocDomain/Zacht/Result/'+Sector_name;
		attribute<float32> PotentieleDichtheidToenameSubsector (CompactedAllocDomain) := ='VariantData/PotentieleState/'+Zichtjaar_name+'/Per_SectorxSubsectoren/'+Sector_name+'/'+Subsector_name+'/TovStateVoorAllocatie', Descr = "gebruikmakend van de zeef in basisjaar (vanwege invalid recursions)";
		attribute<float32> SubsectorShare                      (CompactedAllocDomain) := ='VariantData/PotentieleState/'+Zichtjaar_name+'/Per_SectorxSubsectoren/'+Sector_name+'/'+Subsector_name+'/Basisjaar/SubsectorShare';
		attribute<ha>      PotentieleRuimte                    (CompactedAllocDomain) := = 'sum(float32(VariantData/Zeef/Basisjaar/'+Sector_name+'/'+Subsector_name+'/IsBeschikbaar) * AdminDomain/NrHaPerCell, CompactedAdminDomain/CompactedAllocDomain_rel)';
		attribute<bool>    GeeftOnvoldoendeRuimte_Landbouw     (CompactedAdminDomain) := (PotentieleRuimte < MinimalLandAvailability_Landbouw)[CompactedAdminDomain/CompactedAllocDomain_rel];
	}
}
