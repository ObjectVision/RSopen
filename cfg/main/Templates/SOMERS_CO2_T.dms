Template SOMERS_CO2_T 
: using = "/Classifications/Landbouw;SourceData/Landbouw/SOMERS"
, Descr = "Het registratiesysteem SOMERS (Subsurface Organic Matter Emission RegistRation System) is ontwikkeld om de landelijke CO2-uitstoot reductie in het veenweidegebied jaarlijks bij te houden. Met SOMERS 2.0 zijn rekenregels opgesteld om de toekomstige uitstoot te bepalen onder gestandaardiseerde omstandigheden."
, URL = "https://www.nobveenweiden.nl/bevindingen-rekenregels/"
{
	parameter<string>  Zichtjaar_name;
	//
	
	parameter<bool>    IsStartYear           := lowercase(Zichtjaar_name) == lowercase('Basisjaar');
	parameter<int16>   PrevZichtjaar_value   := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/PrevYearRange_rel);
	parameter<int16>   Zichtjaar_value       := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/YearRange_rel);
	parameter<bool>    IsFirstAllocYear      := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/IsFirstZichtjaar); 
	
	unit<uint32> Huidige_Percelen := Veenweide_percelen
	{
		parameter<WinterdroogleggingK>     WinterdroogleggingK_rel      := = 'WinterdroogleggingK/v/'+/VariantParameters/VariantK/WinterdroogleggingK_ref[/Variant_rel];
		parameter<ZomerdroogleggingK>      ZomerdroogleggingK_rel       := = 'ZomerdroogleggingK/v/'+/VariantParameters/VariantK/ZomerdroogleggingK_ref[/Variant_rel];
		parameter<InfiltratieMaatregelK>   InfiltratieMaatregelK_rel    := = 'InfiltratieMaatregelK/v/'+/VariantParameters/VariantK/InfiltratieMaatregelK_ref[/Variant_rel];

		attribute<CombinatieTabel>         CombinatieTabel_rel          := combine_Data(CombinatieTabel, WeerregioK_rel, KwelsituatieK_rel, PerceelsbreedteK_rel, BodemK_rel, InfiltratieMaatregelK_rel, WinterdroogleggingK_rel, ZomerdroogleggingK_rel);
		attribute<CombinatieTabel>         CT_rel_perGrid (AdminDomain) := CombinatieTabel_rel[AdminDomain_rel];
	}
	
	attribute<ton_ha_jaarlijks>            Min_Uitstoot            (AdminDomain) := rjoin(Huidige_Percelen/CT_rel_perGrid, Datasheet/CombinatieTabel_rel, value(Datasheet/Minimum, ton_ha_jaarlijks));
	attribute<ton_ha_jaarlijks>            Max_Uitstoot            (AdminDomain) := rjoin(Huidige_Percelen/CT_rel_perGrid, Datasheet/CombinatieTabel_rel, value(Datasheet/Maximum, ton_ha_jaarlijks));
	attribute<ton_ha_jaarlijks>            Mediaan_Uitstoot        (AdminDomain) := rjoin(Huidige_Percelen/CT_rel_perGrid, Datasheet/CombinatieTabel_rel, value(Datasheet/Mediaan, ton_ha_jaarlijks));

	attribute<ton_ha_jaarlijks>            Mediaan_Uitstoot_naLUC  (AdminDomain) := LU_ModelType/IsStedelijk[ThisYear/Landgebruikskaart/Result_SA] 
																						? ModelParameters/Landbouw/CO2_Uitstoot_StedelijkGebruik 
																						: ThisYear/Landgebruikskaart/Result_SA == LU_ModelType/v/Water_Totaal 
																							? ModelParameters/Landbouw/CO2_Uitstoot_Water 
																							: Mediaan_Uitstoot;
	//aanname vanuit Deltares: de veenlaag is oneindig dik.
	attribute<ton_ha>                      Cumulatief_Mediaan_overZichtjaar       (AdminDomain) := Mediaan_Uitstoot_naLUC * float32(Zichtjaar_value - PrevZichtjaar_value);
	attribute<ton_ha>                      Cumulatief_Mediaan_sindsStartYear      (AdminDomain) := =IsFirstAllocYear 
																								? 'Cumulatief_Mediaan_overZichtjaar' 
																								: 'PrevIndicatoren/SOMERS_CO2/Cumulatief_Mediaan_sindsStartYear + Cumulatief_Mediaan_overZichtjaar';
	
}