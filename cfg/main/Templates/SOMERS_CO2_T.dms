Template SOMERS_CO2_T 
: using = "/Classifications/Landbouw;SourceData/Landbouw/SOMERS"
, Descr = "Het registratiesysteem SOMERS (Subsurface Organic Matter Emission RegistRation System) is ontwikkeld om de landelijke CO2-uitstoot reductie in het veenweidegebied jaarlijks bij te houden. Met SOMERS 2.0 zijn rekenregels opgesteld om de toekomstige uitstoot te bepalen onder gestandaardiseerde omstandigheden."
, URL = "https://www.nobveenweiden.nl/bevindingen-rekenregels/"
{
	//
	parameter<String>  Zichtjaar_name;
	//
	
	parameter<Bool>  IsStartYear         := lowercase(Zichtjaar_name) == lowercase('Basisjaar');
	parameter<Int16> PrevZichtjaar_value := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/PrevYearRange_rel);
	parameter<Int16> Zichtjaar_value     := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/YearRange_rel);
	parameter<Bool>  IsFirstAllocYear    := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/IsFirstZichtjaar); 
	
	unit<UInt32> Huidige_Percelen := Veenweide_percelen
	{
		parameter<WinterdroogleggingK>   WinterdroogleggingK_rel   := = 'WinterdroogleggingK/v/'+/VariantParameters/VariantK/WinterdroogleggingK_ref[/Variant_rel];
		parameter<ZomerdroogleggingK>    ZomerdroogleggingK_rel    := = 'ZomerdroogleggingK/v/'+/VariantParameters/VariantK/ZomerdroogleggingK_ref[/Variant_rel];
		parameter<InfiltratieMaatregelK> InfiltratieMaatregelK_rel := = 'InfiltratieMaatregelK/v/'+/VariantParameters/VariantK/InfiltratieMaatregelK_ref[/Variant_rel];
		
		attribute<CombinatieTabel> CombinatieTabel_rel          := combine_Data(CombinatieTabel, WeerregioK_rel, KwelsituatieK_rel, PerceelsbreedteK_rel, BodemK_rel, InfiltratieMaatregelK_rel, WinterdroogleggingK_rel, ZomerdroogleggingK_rel);
		attribute<CombinatieTabel> CT_rel_perGrid (AdminDomain) := CombinatieTabel_rel[AdminDomain_rel];
	}
	
	// attribute<EUR> NPV (AdminDomain) := PerZichtjaar * (1f - (1[jaarlijks] + Rentevoet)^-Periode / Rentevoet);
	
	container Minimum_uitstoot := UitstootT('Minimum');
	container Maximum_uitstoot := UitstootT('Maximum');
	container Mediaan_uitstoot := UitstootT('Mediaan');
	
	Template UitstootT
	{
		//
		parameter<string> Stat_ref;
		//
		
		attribute<kg_jaarlijks> Uitstoot (AdminDomain) := = 'rjoin(Huidige_Percelen/CT_rel_perGrid, Datasheet/CombinatieTabel_rel, value(Datasheet/'+Stat_ref+', kg_ha_jaarlijks)) * AdminDomain/NrHaPerCell';
		
		attribute<kg_jaarlijks> Uitstoot_naLUC (AdminDomain) :=
			LU_ModelType/IsStedelijk[ThisYear/Landgebruikskaart/Result_SA]
				? ModelParameters/Landbouw/CO2_Uitstoot_StedelijkGebruik * AdminDomain/NrHaPerCell
				: ThisYear/Landgebruikskaart/Result_SA == LU_ModelType/v/Water_Totaal
					? ModelParameters/Landbouw/CO2_Uitstoot_Water * AdminDomain/NrHaPerCell
					: Uitstoot;
		
		parameter<jaar> Periode := Zichtjaar_Value[jaar] - PrevZichtjaar_Value[jaar];
		
		// Voorlopige aanname: de jaarlijkse uitstoot is constant en de veenlaag is oneindig dik. Needs work.
		attribute<kg> Cumulatief_overZichtjaar  (AdminDomain) := Uitstoot_naLUC * Periode, StorageName = "='%LocalDataProjDir%/Indicatoren/CO2_Uitstoot/'+Casus_name +'/'+BoerVariantK/name[ModelParameters/Landbouw/BoerVariant]+'_'+Zichtjaar_name+'_'+/ModelParameters/StudyArea+'_'+Stat_ref+'.tif'", StorageType = "gdalwrite.grid";
		attribute<kg> Cumulatief_sindsStartyear (AdminDomain) := =IsFirstAllocYear ? 'Cumulatief_overZichtjaar' : 'PrevIndicatoren/SOMERS_CO2/'+Stat_ref+'_uitstoot/Cumulatief_sindsStartyear + Cumulatief_overZichtjaar';
		
		parameter<mton> Cumulatief_sindsStartyear_StGebied := sum(Cumulatief_sindsStartyear)[mton];
		
		container Kosten_minimum := KostenT('Minimum', Periode, Uitstoot_naLUC);
		container Kosten_maximum := KostenT('Maximum', Periode, Uitstoot_naLUC);
	}
	
	Template KostenT
	{
		//
		parameter<string> Stat_ref;
		parameter<jaar> Periode;
		
		attribute<kg_jaarlijks> Uitstoot_naLUC (AdminDomain);
		//
		
		parameter<Eur_ton>       CO2_prijs                  := = 'ModelParameters/Landbouw/CO2_prijs_'+Stat_ref;
		parameter<jaarlijks>     Rentevoet                  := ModelParameters/Landbouw/Rentevoet;// TO DO: check vooronderstelde waarde
		
		attribute<EUR_jaarlijks> PerZichtjaar (AdminDomain) := Uitstoot_naLUC[ton_jaarlijks] * CO2_prijs;
		attribute<EUR>           NPV          (AdminDomain) := PerZichtjaar * (1f - (1f + Rentevoet / 1[jaarlijks])^(-Periode / 1[jaar])) / Rentevoet,  Descr = "NPV = C * [1-(1+r)^-r] / r";
		
		parameter<EUR>           NPV_StGebied := sum(NPV);
	}
}
