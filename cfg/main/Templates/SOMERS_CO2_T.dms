Template SOMERS_CO2_T 
: using = "/Classifications/Landbouw;SourceData/Landbouw/SOMERS"
, Descr = "Het registratiesysteem SOMERS (Subsurface Organic Matter Emission RegistRation System) is ontwikkeld om de landelijke CO2-uitstoot reductie in het veenweidegebied jaarlijks bij te houden. Met SOMERS 2.0 zijn rekenregels opgesteld om de toekomstige uitstoot te bepalen onder gestandaardiseerde omstandigheden."
, URL = "https://www.nobveenweiden.nl/bevindingen-rekenregels/"
{
	//
	parameter<String>  Zichtjaar_name;
	//
	
	parameter<Bool>  IsStartYear         := lowercase(Zichtjaar_name) == lowercase('Basisjaar');
	parameter<Int16> PrevZichtjaar_value := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/PrevYearRange_rel);
	parameter<Int16> Zichtjaar_value     := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/YearRange_rel);
	parameter<Bool>  IsFirstAllocYear    := rjoin(lowercase(Zichtjaar_name), lowercase(/Classifications/Time/Zichtjaar/name), /Classifications/Time/Zichtjaar/IsFirstZichtjaar);
	parameter<jaar>  Periode := Zichtjaar_Value[jaar] - PrevZichtjaar_Value[jaar];
	parameter<jaarlijks> Rentevoet := ModelParameters/Landbouw/Rentevoet;// TO DO: check vooronderstelde waarde
	
	unit<UInt32> Huidige_Percelen := Veenweide_percelen
	{
		parameter<WinterdroogleggingK>   WinterdroogleggingK_rel   := = 'WinterdroogleggingK/v/'+/VariantParameters/VariantK/WinterdroogleggingK_ref[/Variant_rel];
		parameter<ZomerdroogleggingK>    ZomerdroogleggingK_rel    := = 'ZomerdroogleggingK/v/'+/VariantParameters/VariantK/ZomerdroogleggingK_ref[/Variant_rel];
		parameter<InfiltratieMaatregelK> InfiltratieMaatregelK_rel := = 'InfiltratieMaatregelK/v/'+/VariantParameters/VariantK/InfiltratieMaatregelK_ref[/Variant_rel];
		
		attribute<CombinatieTabel> CombinatieTabel_rel          := combine_Data(CombinatieTabel, WeerregioK_rel, KwelsituatieK_rel, PerceelsbreedteK_rel, BodemK_rel, InfiltratieMaatregelK_rel, WinterdroogleggingK_rel, ZomerdroogleggingK_rel);
		attribute<CombinatieTabel> CT_rel_perGrid (AdminDomain) := CombinatieTabel_rel[AdminDomain_rel];
	}
	
	container Emissies := for_each_ne(CO2/EmissieK/name, 'EmissieT('+quote(CO2/EmissieK/name)+')');
	
	container Kosten := for_each_ne(CO2/EmissieXPrijsK/name, 'EmissieXPrijsT('+string(id(CO2/EmissieXPrijsK))+'[CO2/EmissieXPrijsK])');
	
	Template EmissieT
	{
		//
		parameter<string> Emissie_name;
		//
		
		attribute<kg_jaarlijks> Jaarlijks (AdminDomain) := = 'rjoin(Huidige_Percelen/CT_rel_perGrid, Datasheet/CombinatieTabel_rel, value(Datasheet/'+Emissie_name+', kg_ha_jaarlijks)) * AdminDomain/NrHaPerCell';
		
		attribute<kg_jaarlijks> NaLUC (AdminDomain) :=
			LU_ModelType/IsStedelijk[ThisYear/Landgebruikskaart/Result_SA]
				? ModelParameters/Landbouw/CO2_Uitstoot_StedelijkGebruik * AdminDomain/NrHaPerCell
				: ThisYear/Landgebruikskaart/Result_SA == LU_ModelType/V/Water_Totaal
					? ModelParameters/Landbouw/CO2_Uitstoot_Water * AdminDomain/NrHaPerCell
					: Jaarlijks;
		
		// Voorlopige aanname: de jaarlijkse uitstoot is constant en de veenlaag is oneindig dik. Needs work.
		attribute<kg> Cumulatief_overZichtjaar  (AdminDomain) := NaLUC * Periode;
		attribute<kg> Cumulatief_sindsStartyear (AdminDomain) := = IsFirstAllocYear ? 'Cumulatief_overZichtjaar' : 'PrevIndicatoren/SOMERS_CO2/Emissies/'+Emissie_name+'/Cumulatief_sindsStartyear + Cumulatief_overZichtjaar';
		
		parameter<mton> Cumulatief_sindsStartyear_StGebied := sum(Cumulatief_sindsStartyear)[mton];
	}
	
	Template EmissieXPrijsT
	{
		//
		parameter<CO2/EmissieXPrijsK> id;
		//
		
		parameter<string> Emissie_name := CO2/EmissieXPrijsK/Emissie_name[id];
		parameter<string> Prijs_name := CO2/EmissieXPrijsK/Prijs_name[id];
		parameter<Eur_ton> Prijs := CO2/EmissieXPrijsK/Prijs[id];
		
		attribute<EUR_jaarlijks> Jaarlijks (AdminDomain) := = 'Emissies/'+Emissie_name+'/NaLUC[ton_jaarlijks] * Prijs';
		
		container Kapitalisatie := Kapitalisatie_T(null_b, Rentevoet, Periode);
		
		attribute<EUR> NPV (AdminDomain) := Jaarlijks * Kapitalisatie/Factor, StorageName = "='%LocalDataProjDir%/Indicatoren/SOMERS_CO2/Kosten/'+Casus_name +'/'+BoerVariant_name+'_'+Zichtjaar_name+'_'+/ModelParameters/StudyArea+'_'+Emissie_name+'_'+Prijs_name+'.tif'", StorageType = "gdalwrite.grid";
		
		parameter<EUR> NPV_StGebied := sum(NPV);
	}
}
