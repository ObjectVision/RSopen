container Densities
{
	#include<DichtheidZichtjaar_T.dms>
	#include<MaxWoningDichtheid_T.dms>

	Template Werken_T
	{
		//
		unit<UInt8> TXLJobs6;
		parameter<TXLJobs6> id;
		container Basisjaar;
		unit<IPoint> domain;
		//
		
		parameter<String> domain_str := domain/name;
		parameter<String> domain_size := string(domain/gridsize);
		parameter<String> name := TXLJobs6/name[id];
		
		parameter<Bool> KanLokaalHogereDichtheidKrijgen := TXLJobs6/KanLokaalHogereDichtheidKrijgen[id];
		
		parameter<Float32> DichtheidFactorOV              := CaseClassifications/VariantK/DichtheidFactorOV_NietWonen[0b];
		parameter<Float32> DichtheidFactorWater           := CaseClassifications/VariantK/DichtheidFactorWater_NietWonen[0b];
		parameter<Float32> DichtheidFactorBevKern100kPlus := = 'CaseClassifications/VariantK/DichtheidFactorBevKern100kPlus_NietWonen'+/Scenario_name+'[0b]';
		parameter<Float32> DichtheidFactorBevKern20kPlus  := = 'CaseClassifications/VariantK/DichtheidFactorBevKern20kPlus_NietWonen'+/Scenario_name+'[0b]';
		parameter<Float32> DichtheidFactorBevKern20kMin   := = 'CaseClassifications/VariantK/DichtheidFactorBevKern20kMin_NietWonen'+/Scenario_name+'[0b]';
		
		unit<UInt32> RegioUnit1 := SourceData/RegioIndelingen/CBS/Last/Gemeente
		{
			attribute<.> per_domain (domain) := = 'per_'+domain_str;
		}
		
		unit<UInt32> RegioUnit2 := SourceData/RegioIndelingen/NVM
		{
			attribute<.> per_domain (domain) := = 'per_'+domain_str;
		}
		
		unit<UInt32> RegioUnit3 := SourceData/RegioIndelingen/CBS/Last/Provincie
		{
			attribute<.> per_domain (domain) := = 'per_'+domain_str;
		}
		
		unit<UInt8> Tellersubset := = 'TXLJobs6/CBSKlassen/'+Grondgebruik/CBSKlasse/Aggr/name[TXLJobs6/CBSKlasse_Aggr_rel[id]]+'/subset';
		
		parameter<String> DichtheidFactorStr := 'mul(
				add(
					float32(BaseData/Omgeving/OV_Reistijd/TotRailHalte/Smoothed)          * (DichtheidFactorOV - 1f),
					float32(BaseData/StartState/Grondgebruik/NabijGrootWaterInBBG/Smoothed) * (DichtheidFactorWater - 1f),
					Max_Elem(
						float32(BaseData/Omgeving/RondBevolkingskern/Is100kPlus) * (DichtheidFactorBevKern100kPlus - 1f),
						float32(BaseData/Omgeving/RondBevolkingskern/Is20kPlus)  * (DichtheidFactorBevKern20kPlus - 1f),
						float32(BaseData/Omgeving/RondBevolkingskern/Is20kMin)   * (DichtheidFactorBevKern20kMin - 1f)
					)
					// (DichtheidFactorGeneriek - 1f),
				),
				float32(KanLokaalHogereDichtheidKrijgen)
		) + 1f';
		
		attribute<Float32> DichtheidFactor (domain) := = '('+DichtheidFactorStr+')[domain/AllocDomain_rel]';

		attribute<m2PandFootprint> TellerBasisjaar0 (domain) := = 'add('+asItemList('Basisjaar/PerYear_AdminDomain/y'+string(ModelParameters/LISA_StartYear)+'/pand_met_vbo/Footprint_perTXLJobs6_'+domain_str+'/'+Tellersubset/name)+')';
		attribute<m2PandFootprint> TellerBasisjaar  (domain) := TellerBasisjaar0 / float32(NoemerBasisjaar > 0[ha]);
		
		attribute<ha> NoemerBasisjaar (domain) := ='(Grondgebruik/CBSKlasse/Aggr_rel[BaseData/StartState/Grondgebruik/per_'+domain_str+'/Basisjaar] == TXLJobs6/CBSKlasse_Aggr_rel[id])[float32] * domain/NrHaPerCell';

		attribute<m2PandFootprint_ha> PerRegioUnit1 (domain) := (sum(TellerBasisjaar, RegioUnit1/per_domain) / sum(NoemerBasisjaar, RegioUnit1/per_domain))[RegioUnit1/per_domain];
		attribute<m2PandFootprint_ha> PerRegioUnit2 (domain) := (sum(TellerBasisjaar, RegioUnit2/per_domain) / sum(NoemerBasisjaar, RegioUnit2/per_domain))[RegioUnit2/per_domain];
		attribute<m2PandFootprint_ha> PerRegioUnit3 (domain) := (sum(TellerBasisjaar, RegioUnit3/per_domain) / sum(NoemerBasisjaar, RegioUnit3/per_domain))[RegioUnit3/per_domain];
		attribute<m2PandFootprint_ha> ResultOud     (domain) := max_elem(PerRegioUnit1, PerRegioUnit2, PerRegioUnit3) * DichtheidFactor;
		
		//
		attribute<m2PandFootprint_ha> per_domain  (domain) := TellerBasisjaar / NoemerBasisjaar;
		attribute<m2PandFootprint_ha> Buurt       (domain) := = 'mean(per_domain, RegioIndelingen/CBS/Last/Buurt/per_'+domain_str+')[RegioIndelingen/CBS/Last/Buurt/per_'+domain_str+']';
		attribute<m2PandFootprint_ha> Wijk        (domain) := = 'mean(per_domain, RegioIndelingen/CBS/Last/Wijk/per_'+domain_str+')[RegioIndelingen/CBS/Last/Wijk/per_'+domain_str+']';
		attribute<m2PandFootprint_ha> Gemeente    (domain) := = 'mean(per_domain, RegioIndelingen/CBS/Last/Gemeente/per_'+domain_str+')[RegioIndelingen/CBS/Last/Gemeente/per_'+domain_str+']';
		attribute<m2PandFootprint_ha> NVM         (domain) := = 'mean(per_domain, RegioIndelingen/NVM/per_'+domain_str+')[RegioIndelingen/NVM/per_'+domain_str+']';
		attribute<m2PandFootprint_ha> COROP       (domain) := = 'mean(per_domain, RegioIndelingen/CBS/Last/COROP/per_'+domain_str+')[RegioIndelingen/CBS/Last/COROP/per_'+domain_str+']';
		attribute<m2PandFootprint_ha> Provincie   (domain) := = 'mean(per_domain, RegioIndelingen/CBS/Last/Provincie/per_'+domain_str+')[RegioIndelingen/CBS/Last/Provincie/per_'+domain_str+']';
		
		attribute<m2PandFootprint_ha> Max                  (domain) := max_elem(per_domain, Buurt, Wijk, Gemeente, NVM, COROP, Provincie);
		attribute<m2PandFootprint_ha> MaalFActor           (domain) := Max * DichtheidFactor;
		attribute<m2PandFootprint_ha> ResultMaalFActor     (domain) := value(potential(MaalFActor, Geography/Distmatrices_100m/pot2000m/rev_dist_scaled / sum(Geography/Distmatrices_100m/pot2000m/rev_dist_scaled)), m2PandFootprint_ha);
		attribute<m2PandFootprint_ha> ResultNietMaalFActor (domain) := value(potential(Max, Geography/Distmatrices_100m/pot2000m/rev_dist_scaled / sum(Geography/Distmatrices_100m/pot2000m/rev_dist_scaled)), m2PandFootprint_ha);
		attribute<m2PandFootprint_ha> Result               (domain) := ResultMaalFActor;
	}	
	
	Template SectorxSubsector_T
	{
		parameter<Sector/xSubsector> id;
		unit<IPoint> domain;
		//
		
		parameter<String> name           := Sector/xSubsector/name[id];
		parameter<String> Subsector_name := Sector/xSubsector/Subsector_name[id];
		parameter<String> Sector_name    := Sector/xSubsector/Sector_name[id];
		parameter<String> ValUnit_ref    := Sector/xSubsector/ValUnitDens_ref[id];
		
		unit<Float32> ValUnit := = ValUnit_ref;
		
		attribute<ValUnit> Zichtjaar (domain) :=
			= lowercase(Sector_name) == lowercase('Verblijfsrecreatie')
				? 'MakeDefined(VariantData/Claims/'+Zichtjaar_name+'/AllocRegios/'+VariantParameters/Recreatie_AllocRegio_name+'/Verblijfsrecreatie/Object_perHa[RegioIndelingen/CBS/Last/'+VariantParameters/Recreatie_AllocRegio_name+'/Per_AllocDomain],0f)'
				: lowercase(Sector_name) == lowercase('Wonen') || lowercase(Sector_name) == lowercase('Wind' )
					? 'const((0/0)[ValUnit], domain)'
					: lowercase(Sector_name) == lowercase('Landbouw')
						? 'const(domain/NrHaPerCell / 1[Ha], domain)'
						: 'Src/'+Sector_name+'/Subsectoren/'+Subsector_name;
	}
}