Template Trede_T
: Descr = "In deze module wordt per subsector de eventuele voorkeurslocaties ('treden') beschreven, van hoog naar laag. Welke er beschouwd worden is bepaald in de variant parameters in Trede_Variant in VariantK"
{
	parameter<Sector/xSubsector> id : Descr = "Identifier for the subsector";
	container Src : Descr = "Source container for additional data";
	//
	
	parameter<String> SectorxSubsector_name := Sector/xSubsector/name[id], Descr = "Name of the subsector";
	parameter<String> Sector_name           := Sector/xSubsector/Sector_name[id], Descr = "Name of the sector";
	parameter<String> Subsector_name        := Sector/xSubsector/Subsector_name[id], Descr = "Name of the sector";
	
	container Klassen := 
		for_each_nedv(
			Impl/Klasse/Label
			,replace(
				Impl/Klasse/RefSrc+' ? Impl/Klasse/V/'+Impl/Klasse/name+' : null_u[Impl/Klasse]'
				,'@S@', 'Src/'
				,'@X@', Sector_name
				,'@SS@', Subsector_name
			)
			, AllocDomain
			, Impl/Klasse
		)
	{
		attribute<uint32>            per_domain_brushcolor        (AllocDomain) := per_domain_grid_visualisatie -> BrushColor, DialogType = "BrushColor", Descr = "Visualization per allocdomain";
		attribute<Impl/Visualisatie> per_domain_grid_visualisatie (AllocDomain) := = 'ArgMin_ifdefined('+asItemList(Impl/Klasse/Label)+')[Impl/Visualisatie]', Descr = "Visualization per allocdomain";
	}
	
	
	container Impl
	{
		unit<UInt32> Visualisatie := union_unit(Classifications/Actor/NoData, Visualisatie_src)
		{
			attribute<String> name       := union_data(., Classifications/Actor/NoData/name, Visualisatie_src/Label);
			attribute<String> Label      := union_data(., Classifications/Actor/NoData/Label, Visualisatie_src/Label), DialogType = "LabelText";
			attribute<UInt8>  r          := union_data(., const(null_b, Classifications/Actor/NoData), Visualisatie_src/r), Descr = "Red color component";
			attribute<UInt8>  g          := union_data(., const(null_b, Classifications/Actor/NoData), Visualisatie_src/g), Descr = "Green color component";
			attribute<UInt8>  b          := union_data(., const(null_b, Classifications/Actor/NoData), Visualisatie_src/b), Descr = "Blue color component";
			attribute<UInt32> BrushColor := rgb(r,g,b), DialogType = "BrushColor", Descr = "Combined RGB color for brush";
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
		
		unit<UInt32> Visualisatie_src := range(uint32,0, #Klasse)
		{
			attribute<String> Label := Klasse/Label[id(.)[Klasse]], DialogType = "LabelText";
			attribute<UInt32> BrushColor := rgb(r,g,b), DialogType = "BrushColor";
			attribute<UInt8>  r := id(.) <= middle_record ? (0f + change)[uint8] : 254b;
			attribute<UInt8>  g := id(.) <= middle_record ? 254b : (254f - change)[uint8];
			attribute<UInt8>  b := const(0b, .);
			
			attribute<Float32> change :=
				id(.) <= middle_record 
					? (id(.)[float32] * factor) > 254f ? 254f : (id(.)[float32] * factor)
					: ((id(.)[float32] - middle_record[float32]) * factor), Descr = "Change factor for color gradient";
			
			parameter<uint32> middle_record := (#.[float32] / 2f)[uint32], Descr = "Middle record index for color calculation";
			parameter<Float32> factor := 255f / #.[float32] * 2f, Descr = "Factor for color scaling";
		}
		
		unit<UInt32> Klasse := = 'VariantParameters/Tredes/'+SectorxSubsector_name+'/'+/VariantParameters/VariantK/Trede_Variant[Variant_rel]
		{
			attribute<String> Label := '_'+name, DialogType = "LabelText", Descr = "Label for the class";
			attribute<String> name_short := 'Trede_' + string(id(.)), Descr = "Short name for the class";
			attribute<String> PrevName := MakeDefined(name[sub_or_null(id(.),1[.])], ''), Descr = "Name of the previous class";
			attribute<String> PrevName_short := MakeDefined(name_short[sub_or_null(id(.),1[.])], ''), Descr = "Short name of the previous class";
			attribute<.>      Per_AllocDomain    (AllocDomain) := = #. = 1[uint32] ? 'const(0[.], AllocDomain)' : 'ArgMin('+asItemList('Klassen/'+Label)+')[.]', Descr = "Value per allocdomain";
			attribute<.>      Per_AdminDomain    (AdminDomain) := Per_AllocDomain[AdminDomain/AllocDomain_rel], Descr = "Value per admindomain";
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
	}
}