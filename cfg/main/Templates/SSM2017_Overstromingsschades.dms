Template SSM2017_Overstromingsschades_T : using = "Classifications/Vastgoed"
, Descr = "SSM Model van Deltares nagebouwd in RuimteScanner; Slager, K. (2016). Technische documentatie SSM2015: functionele en technische ontwerpkeuzen. Deltares rapportage 1230095-004-HYE-0009 dd. november 2016 "
{
	//
	parameter<String> Zichtjaar_name;
	//
	
	parameter<Bool>   IsStartYear := lowercase(Zichtjaar_name) == lowercase('Basisjaar');
	
	#include<Parameters.dms>
	#include<Grondgebruik.dms>
	
	attribute<Int32> Diepte (AdminDomain) := SourceData/Water/dieptekaart == -9999f || SourceData/Water/dieptekaart > 10f ? 0i/0i : (SourceData/Water/dieptekaart * 100f)[int32]; //convert naar int van cm
	
	container Schadefuncties
	{
		unit<UInt32> FileList
		: StorageName = "%RSo_DataDir%/Omgeving/Water/Schade/SSM_Schadefuncties/filelist.csv"
		, StorageReadOnly = "TRUE"
		, StorageType = "gdal.vect"
		{
			unit<UInt32> Clean := select_with_org_rel(Filename != 'evacuatie' && Filename != 'filelist.csv')
			{
				attribute<String> Filename := ../Filename[org_rel];
				attribute<String> name := 'Function_'+ left(Filename, strlen(Filename) - 4);
			}
		}
		
		container Read_DamageFunctions := for_each_ne(FileList/Clean/name, 'Read_DamageFunctions_T('+quote(FileList/Clean/Filename)+')');
		
		Template Read_DamageFunctions_T
		{
			//
			parameter<String> Filename;
			//
			
			unit<UInt32> Read: StorageName = "='%RSo_DataDir%/Omgeving/Water/Schade/SSM_Schadefuncties/'+Filename", StorageReadOnly = "TRUE", StorageType = "gdal.vect"
			{
				attribute<Int32>   Waterdiepte  := int32(float32(wd_m_) * 100f);
				attribute<Float32> SchadeFActor := float32(factor);
			}
		}
	}
	
	container Resultaat : using = "Parameters"
	{
		container Hoofdwatersysteem_Binnendijks := for_each_ne(Binnendijks/name, 'PerCategorie_T('+quote(Binnendijks/name)+', ''Binnendijks'')')
		{
			attribute<Eur> Totaal (AdminDomain) := ='Add('+asItemList(Binnendijks/name+'/Totaal')+')';
		}
		
		container Hoofdwatersysteem_Buitendijks := for_each_ne(Buitendijks/name, 'PerCategorie_T('+quote(Buitendijks/name)+', ''Buitendijks'')')
		{
			attribute<Eur> Totaal (AdminDomain) := ='Add('+asItemList(Buitendijks/name+'/Totaal')+')';
		}
		
		container Regionaalwatersysteem_Binnendijks := for_each_ne(RegionaalBinnendijks/name, 'PerCategorie_T('+quote(RegionaalBinnendijks/name)+', ''RegionaalBinnendijks'')')
		{
			attribute<Eur> Totaal (AdminDomain) := ='Add('+asItemList(RegionaalBinnendijks/name+'/Totaal')+')';
		}
		
		attribute<Eur>  Result_Hoofdwatersysteem_Binnendijks     (AdminDomain) := Hoofdwatersysteem_Binnendijks/Totaal, StorageName = "='%LocalDataProjDir%/Output/SSM/'+Casus_name+'/'+Zichtjaar_name+'/Hoofdwatersysteem_Binnendijks.tif'", StorageType = "gdalwrite.grid";
		attribute<Eur>  Result_Hoofdwatersysteem_Buitendijks     (AdminDomain) := Hoofdwatersysteem_Buitendijks/Totaal, StorageName = "='%LocalDataProjDir%/Output/SSM/'+Casus_name+'/'+Zichtjaar_name+'/Hoofdwatersysteem_Buitendijks.tif'", StorageType = "gdalwrite.grid";
		attribute<Eur>  Result_Regionaalwatersysteem_Binnendijks (AdminDomain) := Regionaalwatersysteem_Binnendijks/Totaal, StorageName = "='%LocalDataProjDir%/Output/SSM/'+Casus_name+'/'+Zichtjaar_name+'/Regionaalwatersysteem_Binnendijks.tif'", StorageType = "gdalwrite.grid";
		attribute<Eur>  Result_Totaal                            (AdminDomain) := Result_Hoofdwatersysteem_Binnendijks, StorageName = "='%LocalDataProjDir%/Output/SSM/'+Casus_name+'/'+Zichtjaar_name+'/Totaal.tif'", StorageType = "gdalwrite.grid";
		
		
		attribute<Eur>   Result_Dijkring (SourceData/Water/dijkringen/dijkring2020) := sum(Result_Totaal,SourceData/Water/dijkringen/dijkring2020/per_rdc_25m);
		attribute<Bool>  Waterdiepte_buiten_dijkring  (AdminDomain)                  := IsDefined(Diepte) && !IsDefined(SourceData/Water/dijkringen/dijkring2020/per_rdc_25m);
		attribute<Eur>   Result_buiten_dijkring       (AdminDomain)                  := Waterdiepte_buiten_dijkring ? Result_Totaal : 0f/0f;//tbv check
		attribute<Eur>   Read_Result_Totaal           (AdminDomain) : StorageName = "='%LocalDataProjDir%/Output/SSM/'+Casus_name+'/'+Zichtjaar_name+'/Totaal.tif'", StorageReadOnly = "TRUE", StorageType = "tif"
		{
			attribute<Eur>  PerProvincie (/SourceData/RegioIndelingen/Provincie) := sum(., /SourceData/RegioIndelingen/Provincie/Per_AdminDomain);
			parameter<Eur>  PerNL := sum(.);
		}
		
		attribute<Eur>  Result_Totaal_tov_basisjaar (AdminDomain) := =IsStartYear ? 'const(null_f, AdminDomain, Eur)' : 'Result_Totaal - Basisjaar/Overstromingsschade/Resultaat/Result_Totaal'
		{
			attribute<Eur>  PerProvincie (/SourceData/RegioIndelingen/Provincie) := sum(., /SourceData/RegioIndelingen/Provincie/Per_AdminDomain);
			parameter<Eur>  PerNL := sum(.);
		}
	}
	
	Template PerCategorie_T
	{
		//
		parameter<String> Categorie;
		parameter<String> TypeDijk;
		//
		
		parameter<Bool> HeeftBU := =''+TypeDijk+'/HeeftBU['+TypeDijk+'/V/'+Categorie+']';
		
		parameter<String> Direct_DamageFunction      := ='string('+TypeDijk+'/Function['+TypeDijk+'/V/'+Categorie+'])';
		parameter<String> Indirect_BU_DamageFunction := ='string('+TypeDijk+'/BU_Function['+TypeDijk+'/V/'+Categorie+'])';
		parameter<String> Unit_str                   := =''+TypeDijk+'/Unit['+TypeDijk+'/V/'+Categorie+']';
		parameter<String> DamageUnit_str             := =''+TypeDijk+'/DamageUnit['+TypeDijk+'/V/'+Categorie+']';
		parameter<String> Path_str                   := =''+TypeDijk+'/Path['+TypeDijk+'/V/'+Categorie+']';
		
		unit<Float32> Unit := =Unit_str;
		unit<Float32> DamageUnit := =DamageUnit_str;
		
		parameter<DamageUnit> s_i  := =''+TypeDijk+'/MaxDamage_Direct['+TypeDijk+'/V/'+Categorie+'][DamageUnit]';  //maximale schade euro per eenheid in Categorie i
		parameter<DamageUnit> SN_i := =HeeftBU ? ''+TypeDijk+'/MaxDamage_Indirect_Netto['+TypeDijk+'/V/'+Categorie+'][DamageUnit]' : '0[DamageUnit]'; //maximale netto schade per eenheid in Categorie i
		parameter<DamageUnit> SB_i := =HeeftBU ? ''+TypeDijk+'/MaxDamage_Indirect_Bruto['+TypeDijk+'/V/'+Categorie+'][DamageUnit]' : '0[DamageUnit]'; //maximale bruto schade per eenheid in Categorie i
		
		parameter<Float32> id   := 1f;   //1 jaar; maximale bedrijfsuitvalduur van getroffen Object
		parameter<Float32> SF_i := 0f;   //substitutiefactor van Categorie i
		parameter<Float32> M_i  := 1f;   //multiplier voor indirecte schade voor Categorie i
		parameter<Float32> n    := ='sum_uint32('+TypeDijk+'/Use)[float32]';    //Totaal aantal Categorieen
		
		attribute<Float32> α_i (AdminDomain) := ='rjoin(Diepte, Schadefuncties/Read_DamageFunctions/Function_'+Direct_DamageFunction+'/Read/waterdiepte, Schadefuncties/Read_DamageFunctions/Function_'+Direct_DamageFunction+'/Read/SchadeFActor)';  //schadefactor direct in Categorie i
		attribute<Float32> β_i (AdminDomain) := =HeeftBU ? 'rjoin(Diepte, Schadefuncties/Read_DamageFunctions/Function_'+Indirect_BU_DamageFunction+'/Read/waterdiepte, Schadefuncties/Read_DamageFunctions/Function_'+Indirect_BU_DamageFunction+'/Read/SchadeFActor)' : 'const(0f, AdminDomain)';  //schadefactor bedrijfsuitval in Categorie i
		
		attribute<unit> η_i (AdminDomain) := =Path_str;  //aantal eenheden in Categorie i
		
		attribute<Eur> Direct   (AdminDomain) :=  α_i * η_i * s_i;
		attribute<Eur> Indirect (AdminDomain) := ((α_i * η_i * SN_i + (1f - α_i) * SB_i * 1[unit]) * β_i * id * (1f - SF_i) * M_i) * float32(η_i > 0f);
		attribute<Eur> Totaal   (AdminDomain) := Direct + Indirect;
	}
}