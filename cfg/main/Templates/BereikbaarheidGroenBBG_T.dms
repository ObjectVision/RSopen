Template BereikbaarheidGroenBBG_T : using = "Classifications/Actor"
{
	container SrcSrc;
	// parameter<String> Ref;
	//
	container Src := .;
	attribute<GroenK>    Groen                (AdminDomain) := SrcSrc/Landgebruikskaart/GroenInZichtjaar; // Groen obv CBS BBG
	
	container PerRegios := for_each_ne(Regio/name, 'Templates/Indicatoren/BereikbaarheidGroen_Per_Regio_T(Regio/per_AdminDomain == Regio/V/'+Regio/name+', Woningen, Src)')
	{
		container Totaal := Templates/Indicatoren/BereikbaarheidGroen_Per_Regio_T(const(true, AdminDomain), Woningen, Src);
	}
	
	Template Per_Afstand_T 
	{
		parameter<Bereikbaarheid/AfstandGroen> id;
		attribute<bool>                        IsRegio    (AdminDomain);
		attribute<Woning>                      Woningen (AdminDomain);
		container                              Src;
		//
		
		container AggrGroenKlassen := 
			for_each_ne(
				GroenK/name
				, 'Per_GroenK_T(
					Groen == GroenK/V/'+GroenK/name+'
					, id
					, IsRegio
					, Woningen
					, Src
				)'
			)
		{
			container InDezeStudie := TotaalExAgrarisch;
			container Totaal            := Per_GroenK_T(IsDefined(Groen), id, IsRegio, Woningen, Src);
			container TotaalExAgrarisch := Per_GroenK_T(IsDefined(Src/Groen) && !(Src/Groen == GroenK/V/overige_agrarisch_gebruik), id, IsRegio, Woningen, Src);
		}
	}
	
	Template Per_GroenK_T 
	{ 
		attribute<bool>                        IsGroenKlasse                 (AdminDomain) : Descr = "Indicator of een cel tot een groenklasse behoort.";
		parameter<Bereikbaarheid/AfstandGroen> AfstandGroen_id;
		attribute<bool>                        IsRegio                       (AdminDomain) : Descr = "Indicator of een cel binnen de afgebakende studie- of regiogrens ligt.";
		attribute<Woning>                      Woningen                      (AdminDomain) : Descr = "Aantal woningen per cel.";
		container                              Src;
		//
		
		parameter<meter>                       Bovengrens                                  := Bereikbaarheid/AfstandGroen/Bovengrens[AfstandGroen_id], Descr = "Bovengrens afstandsklasse, i.e. tot hoever kijken we?";
		attribute<Woning>                      WoningenInThisStRegio         (AdminDomain) := Woningen * IsRegio[float32], Descr = "Aantal woningen per cel, geclipt op de geselecteerde regio.";
		attribute<bool>                        Extent0                       (AdminDomain) := Woningen <= MaxWoningenInGroen * AdminDomain/NrHaPerCell && IsGroenKlasse && IsRegio, Descr = "Selectie van groencellen binnen de regio die voldoen aan de maximale woningdichtheid.";
		
		unit<uint32> ExtentDistrict := district_8(uint32(Extent0))
		, Descr = "Identificatie van aaneengesloten groengebieden op basis van 8-connectiviteit."
		{
			attribute<ha> Oppervlakte := pcount(Districts)[float32] * AdminDomain/NrHaPerCell, Descr = "Oppervlakte van elk aaneengesloten groengebied (in hectare).";
		}
		
		attribute<bool>                        Extent                        (AdminDomain) := Extent0 && ExtentDistrict/Oppervlakte[ExtentDistrict/Districts] >= MinimumAaneengeslotenOppervlakte, Descr = "Groencellen die deel uitmaken van een aaneengesloten groengebied met een minimale oppervlakte.";
		attribute<meter2>                      RelevantGroen                 (AdminDomain) := float32(IsGroenKlasse) * float32(Extent) * AdminDomain/NrMeter2PerCell, Descr = "Oppervlakte (m²) van groen in cellen die voldoen aan alle selectiecriteria.";
		
		attribute<Woning>                      Som_WoningenInOmgeving        (AdminDomain) := ='potential(WoningenInThisStRegio, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[Woning]', Descr = "Totale som van woningen binnen de gegeven afstand rond elke cel.";
		attribute<meter2>                      Som_GroenInOmgeving           (AdminDomain) := ='potential(RelevantGroen, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[meter2]', Descr = "Totale oppervlakte groen (m²) binnen de gegeven afstand rond elke cel.";
		attribute<meter2>                      Som_GroenInOmgeving_metWoning (AdminDomain) := Som_GroenInOmgeving * float32(WoningenInThisStRegio > 0[woning]), Descr = "Totale oppervlakte groen binnen de gegeven afstand, alleen voor cellen waar woningen aanwezig zijn.";
		
		attribute<m2_woning>                   GroenAanwezigPerWoning        (AdminDomain) := RelevantGroen / Som_WoningenInOmgeving, Descr = "Oppervlakte groen per woning aan de aanbodzijde, berekend als het aanwezige groen in een cel gedeeld door het aantal omliggende woningen.";
		attribute<m2_woning>                   Som_AanwezigGroenPerWoning    (AdminDomain) := ='potential(GroenAanwezigPerWoning, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[m2_woning]', Descr = "Som van per-woning groenbijdragen van alle groencellen binnen de gegeven afstand (drukte-gecorrigeerd groenaanbod).";
		
		container Indicatoren := for_each_ne(Indicator/name, 'Templates/Indicatoren/BereikbaarGroen_Indicatoren_T('+quote(Indicator/IndicatorExpr)+','+quote(Indicator/ValuesUnit)+', ..)');
	}
}
