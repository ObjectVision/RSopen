container Claims
{
	#include<Basisjaar.dms>
	#include<ZichtjaarXScenarioxAllocRegios.dms>
	#include<Claims_TargetUnits_T.dms>
	
	Template Maximum_T
	: Descr = ""
	{
		parameter<AllocatieK> id;
		container Claims;
		//
		
		parameter<string> name := AllocatieK/name[id];
		
		attribute<float32> Result (Claims/AllocRegio) := = replace('max_elem('+AsItemList('Claims/ZichtjaarXCases/'+Time/Zichtjaren/name+'/@A@[float32]')+')', '@A@', name);
	}
	
	Template TigrisXLTable_T
	: Descr = ""
	{
		parameter<Classifications/TigrisXL/Table> table_id;
		container Src;
		//
		
		parameter<string> FileNm := Classifications/TigrisXL/Table/ModuleNm[table_id] + '/' + Classifications/TigrisXL/Table/name[table_id] + '.dat';
		
		container metainfo := Src
			, StorageName = "=FileNm"
			, StorageType = "cfg"
			, StorageReadOnly = "True";
			
		container Scenario := for_each_ne(MetaInfo/GenericSrc/ScenarioTimeCombiUnit/name, 'Case('+string(id(MetaInfo/GenericSrc/ScenarioTimeCombiUnit))+'[MetaInfo/GenericSrc/ScenarioTimeCombiUnit], Metainfo)');
	}
	
	Template Case_T
	: Descr = ""
	{
		//
		parameter<Classifications/TigrisXL/Table> table_id;
		parameter<YearRange> Jaar;
		parameter<string> Scenario;
		parameter<string> Variant;
		//
		
		parameter<string> Filename      := Classifications/TigrisXL/Table/name[table_id]+'.dat';
		parameter<string> Filename_bare := Classifications/TigrisXL/Table/name[table_id];
		
		parameter<string>  File
		:	StorageName = "=replace(ClaimSrcDir, '@S@', Scenario)+'_LMS/'+string(Jaar)+'/'+Variant+'_'+Filename"
		,	StorageType = "str";
		
		unit<uint32> SrcDomain := range(uint32,0,StrCount(File, '\n'))
		{
			attribute<string> FullLine (SrcDomain) := ReadLines(File, .,0);
			attribute<uint32> EK := ReadElems(FullLine, ., const(0, .));
		}
		
		parameter<string> Validator := '\\s*-?\\d+(\\.\\d+)?';
		
		unit<uint32> SrcAttribute := ='Classifications/TigrisXL/MetaInfo/'+Classifications/TigrisXL/Table/name[table_id] +'/SrcAttribute' 
		{
			attribute<bool> is_oke := ='union_data(., '+AsItemList('all(regex_match(substr(SrcDomain/FullLine,'+string(SrcAttribute/StartPos)+','+string(SrcAttribute/Length)+'), Validator))')+')';
		}
		
		unit<uint32> TargetDomain := Geography/RegioIndelingen/LMSSubzone;
		unit<float32> TargetValues;
		
		container SrcAttributes_strings := 
			for_each_nedv(
				SrcAttribute/name
				, 'substr(SrcDomain/FullLine,' + string(SrcAttribute/StartPos) +',' + string(SrcAttribute/Length) + ')'
				, SrcDomain 
				, string
			)
		{
			attribute<uint32> EK(SrcDomain) := uint32(LmsSubzone);
		}
		
		container SrcAttributes_oke := 
			for_each_nedv(
				SrcAttribute/name
				, 'regex_match(SrcAttributes_strings/'+SrcAttribute/name+', Validator)'
				, SrcDomain
				, bool
			)
		{
			attribute<uint32> EK (SrcDomain) := uint32(LmsSubzone);
		}
		
		container SrcAttributes := 
			for_each_neidv(
				SrcAttribute/name
				, 'value(SrcAttributes_strings/'+SrcAttribute/name+', TargetValues)'
				, 'SrcAttributes_oke/'+SrcAttribute/name
				, SrcDomain
				, TargetValues
			)
		{
			attribute<uint32> EK(SrcDomain) := uint32(LmsSubzone);
		}
		
		container Attributes := 
			for_each_nedv(
				SrcAttribute/name
				, SrcAttribute/is_oke
					? 'rjoin(TargetDomain/LmsSubF, SrcAttributes/EK, SrcAttributes/'+ SrcAttribute/name +')'
					: 'Formatting Error: MetaInfo defined a column with an unexpected number format: '
				, TargetDomain
				, TargetValues
			);
		
		container WritePerNVM :=
			for_each_nedv(
				SrcAttribute/name,
				'sum(Attributes/'+SrcAttribute/name+', TargetDomain/Relaties/NVM_2011)',
				RegioIndelingen/NVM, TargetValues
			), StorageName = "='%LocalDataProjDir%/TigrisXLClaimsPerNVM/'+string(Jaar)+'/'+Scenario+'/'+Variant+'/'+Filename_bare+'.csv'", StorageType = "gdalwrite.vect";
	}
	
	Template MetaInfo_T
	: Descr = ""
	{
		parameter<string> Filename;
		//
		
		unit<uint32> FileData:
			StorageName = "=Filename",
			StorageType = "gdal.vect",
			StorageReadOnly = "True";
			
		parameter<string> Filenamefss := substr(Filename, 0, strlen(Filename) - 3) + 'fss';
		
		unit<uint32> MakeFileDatafss := FileData
		, StorageName = "=Filenamefss"
		{
			attribute<string> Variable := FileData/Variable;
			attribute<string> Length   := FileData/Length;
		}
		
		unit<uint32> ReadFileDatafss
		: StorageName = "=Filenamefss"
		, StorageReadOnly = "True"
		{
			attribute<string> Variable;
			attribute<string> Length;
		}
		
		unit<uint32> SrcAttribute := ReadFileDatafss
		{
			attribute<string> name:      = ReadFileDatafss/variable;
			attribute<uint32> Length:    = ReadFileDatafss/Length[uint32];
			attribute<uint32> EndPos:    = cumulate(Length);
			attribute<uint32> Startpos:  = EndPos - Length;
		}
	}
}