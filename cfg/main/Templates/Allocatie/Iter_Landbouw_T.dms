Template Iter_Landbouw_T
{
	//
	parameter<Iter> id;
	container StateVoorAllocatie0;
	container Context0;
	unit<uint8> Iter; //IterK
	container Impl0;
	parameter<bool> IsEnkelIter;
	//
	
	parameter<string> name := Iter/name[id];
	
	container Context := Context0
	{
		container ThisIter := ..;
		parameter<string> ThisIterName := Iter/name[id];
		parameter<string> PrevIterName := Iter/prevname[id];
	}	
	
	container StateVoorAllocatie := =ModelParameters/OntkoppelStedelijkeKlasses 
										? '/Allocatie/Zichtjaren/'+Context/ThisZichtjaar_name+'/Impl/ReadStand_Compacted'
										: 'StateVoorAllocatie0'
	, Descr = "Om ontkoppelde stedelijke functies te gebruiken wanneer dit zo ingesteld wordt.";
	
	container Subsectoren :=
		for_each_ne(
			 Allocatie_Prep/SubSector/name
			, 'IterSubsector_T_Landbouw('+string(id(Allocatie_Prep/SubSector))+'[Allocatie_Prep/SubSector],Allocatie_Prep/SubSector,StateVoorAllocatie,Context)'
	);
	
	container Allocatie_Prep 
	{
		// unit<uint8>  SubSector  := = /ModelParameters/Landbouw/BoerVariant == BoerVariantK/V/IntermediateBoer ? 'LandbouwK_alloc/wo_Exotisch' : 'LandbouwK_Alloc';
		unit<uint8>  SubSector  := = 'LandbouwK_Alloc';
		unit<uint32> AllocRegio := Context/ThisSectorAllocRegio/AllocRegio
		{
			attribute<.> id := id(.);
		}
		
		container ExogeenOpleggen
		{
			attribute<LU_ModelType> Totaal          (AdminDomain) := VariantData/ExogeenOpleggen/Totaal;
			attribute<bool>         Beschikbaar     (AdminDomain) := IsNull(Totaal);
		}
		container Suitabilities :=
			for_each_nedv(
				SubSector/Name
				, 'Subsectoren/'+SubSector/Name+'/CompactedAdminDomain/Beschikbaar 
					&& collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, ExogeenOpleggen/Beschikbaar) 
						? value(collect_by_cond(CompactedAdminDomain, AdminDomain/IsCompactedDomain, VariantData/Suitability/'+Context/ThisZichtjaar_name+'/Landbouw/per_ModelType/'+SubSector/Name+'/Suitability) * 100f, int32) 
						: null_i'
				, CompactedAdminDomain
				, int32
			) // voor discr_alloc moet het int32 zijn, dus floats * 100 om precisie te behouden.
		{
			attribute<SubSector> ArgMax (CompactedAdminDomain) := ='argmax_ifdefined('+AsItemList(SubSector/name)+')[SubSector]';
		}

		attribute<AllocRegio> AtomicRegionMap (CompactedAdminDomain) := AllocRegio/per_CompactedAdminDomain;
		
		unit<uint32> AtomicRegions := AllocRegio
		{
			attribute<.> regio := id(.);
		}	
		
		container Claims
		{
			parameter<Ha> AreaalStudyGebied := area(rdc_meter/geometry[0], meter2)[ha];
			
			container MaxClaims := 
				for_each_nedv(
					  SubSector/name
					, replace('uint32(VariantData/Claims/@Z@/SectorAllocRegios/@AS@/MaxClaims/'+SubSector/name+AllocRegio_StudyArea_orgrel+' / AdminDomain/NrHaPerCell)'
							,'@Z@' , Context/ThisZichtjaar/name
							,'@AS@', Context/ThisSectorAllocRegio/name)
					, AllocRegio
					, uint32
				);
			
			container MinClaims := 
				for_each_nedv(
					  SubSector/name
					, replace('uint32(VariantData/Claims/@Z@/SectorAllocRegios/@AS@/MinClaims/'+SubSector/name+AllocRegio_StudyArea_orgrel+' / AdminDomain/NrHaPerCell)'
							,'@Z@' , Context/ThisZichtjaar/name
							,'@AS@', Context/ThisSectorAllocRegio/name)
					, AllocRegio
					, uint32
				);
		}
		
		parameter<int32> threshold := -999999i; 	//This threshold determines above which point the suitablilty is considered.
	}
	
	container Allocatie := 
		discrete_alloc_sp(
			  Allocatie_Prep/SubSector/name										// 1 string 	attribute	TypeNames
			, CompactedAdminDomain												// 2 uint32		unit
			, Allocatie_Prep/Suitabilities 										// 3 int32		container	SuitabilityMaps
			, Allocatie_Prep/AtomicRegions										// 6 uint16		unit		Regions
			, Allocatie_Prep/AtomicRegionMap									// 7 uint16		attribute	RegionMap
			, Allocatie_Prep/Claims/minclaims									// 8 uint32		container	MinClaims
			, Allocatie_Prep/Claims/maxclaims									// 9 uint32		container	MaxClaims
			, Allocatie_Prep/threshold											// 10 int32		parameter	Threshold
		)
	{
		parameter<bool> IsConversatieveBoer := /ModelParameters/Landbouw/BoerVariant == BoerVariantK/V/ConservatieveBoer;
	
		attribute<SubSector>                        Alloc_Result         (CompactedAdminDomain) := =IsConversatieveBoer ? 'const(null_b, CompactedAdminDomain, SubSector)' : 'Allocatie_Prep/SubSector/LandbouwK_rel[landuse]'; 
		attribute<Sector/XSubsector>                per_SectorXSubSector (CompactedAdminDomain) := rlookup(Alloc_Result, Sector/XSubsector/LandbouwK_rel); // TO DO weer insschakelen, IntegrityCheck = "statusFlag";
	}

	attribute<bool> IsNuGealloceerd (CompactedAdminDomain) := IsDefined(Allocatie/Alloc_Result);
	
	container impl := impl0
	{
		parameter<bool> StopCriterium       := StaticStopCriterium;
		parameter<bool> StaticStopCriterium := IsEnkelIter ? TRUE : id == (ModelParameters/Default_NrOfIters - 1b)[Iter];
	}
	
	container StateNaAllocatie0 := 
		for_each_ne(
			Sector/XSubsector/name
			, 'StatePrep_T('+string(id(Sector/XSubsector))+'[Sector/XSubsector], Context/ThisIter)'
		)
	{
		container PandFootprint :=
			for_each_nedv(
				Jobs6/name
				, sector_name == 'Werken' ? replace(impl/Update_Pandfootprint_expr,'@Y@', Context/ThisZichtjaar/name, '@SS@', Jobs6/name): replace(impl/Verdring_expr, '@S@', 'PandFootprint', '@SS@', Jobs6/name)
				, CompactedAdminDomain
				, m2PandFootprint
			);
	}
	
	container StateNaAllocatie1 :=
		for_each_neidv(
			Sector/XSubsector/name
			, 'StateNaAllocatie0/'+Sector/XSubsector/name+'/Result'
			, 'IsDefined(StateNaAllocatie0/'+Sector/XSubsector/name+'/Result)'
			, CompactedAdminDomain
			, float32
		)
	{
		attribute<Sector/XSubsector> Subsector_rel0 (CompactedAdminDomain) := switch(
																				case(Sector/XSubsector/IsLandbouw[StateVoorAllocatie/Subsector_rel], null_u)
																				, StateVoorAllocatie/Subsector_rel
																				); 
		attribute<Sector/XSubsector> Subsector_rel (CompactedAdminDomain) := =ModelParameters/OntkoppelStedelijkeKlasses ? 'MakeDefined(Subsector_rel0, Allocatie/per_SectorXSubSector)' : 'MakeDefined(StateVoorAllocatie/Subsector_rel, Allocatie/per_SectorXSubSector)';
		attribute<OP>                OP_rel        (CompactedAdminDomain) := StateVoorAllocatie/OP_rel;
		
		container PandFootprint :=
			for_each_nedv(
				Jobs6/name
				, 'StateNaAllocatie0/PandFootprint/'+Jobs6/name
				, CompactedAdminDomain
				, m2PandFootprint
			);
		
		container Wind
		{
			attribute<Yr> jaartal_ingebruikname (CompactedAdminDomain) := Allocatie/per_SectorXSubSector == Classifications/Actor/Sector/XSubsector/V/Wind_Totaal ? uint16(right(Context/ThisZichtjaar/name,4))[Yr] : StateVoorAllocatie/Wind/jaartal_ingebruikname;
		}
	}

	container StateNaAllocatie2 :=
		for_each_nedvn(
			CaseClassifications/StandVar/Path
			, 'value(StateNaAllocatie1/'+CaseClassifications/StandVar/Path + ', scope(CaseClassifications/StandVar, '+CaseClassifications/StandVar/unit_name+'))'
			, CompactedAdminDomain
			, CaseClassifications/StandVar
			, CaseClassifications/StandVar/unit_name
		)
	{
		container Wonen
		{
			attribute<Woning> Aantal_Woningen_Totaal (CompactedAdminDomain) := = 'add('+AsItemList(Classifications/Vastgoed/WP2xVSSH/name)+')';
		}
	}
	
	container StateNaAllocatie := PhaseContainer(StateNaAllocatie2, 'Results for '+Context/ThisSequenceName+', '+Context/ThisSectorAllocRegioName+', '+Context/ThisIterName+' are finished calculating');
}