container Allocatie
: Descr = "Container waar de daadwerkelijke allocatie in deze iteratie wordt gekozen."
{
	parameter<String> sector_domain_name := lowercase(Sector_name) == lowercase('Wind') ? 'CompactedAdminDomain' : 'CompactedAllocDomain';
	unit<uint64>      sector_domain      := =sector_domain_name;
	
	container GeschikthedenLiggenBovenAfkapgrenzen :=
		for_each_nedv(
			Subsector/name
			, 'Subsectoren/'+Subsector/name+'/'+sector_domain_name+'/Geschiktheid[uint64] > uint64(Subsectoren/'+Subsector/name+'/AllocRegio/Afkapgrens)[AllocRegio/per_'+sector_domain_name+']'
			, sector_domain
			, bool
		), Descr = "Per subsector per cel of de geschiktheid boven de afkapgrens ligt.";
		
	container GeschikthedenBovenAfkapgrenzen :=
		for_each_nedv(
			Subsector/name
			, 'GeschikthedenLiggenBovenAfkapgrenzen/'+Subsector/name+' 
				? Subsectoren/'+Subsector/name+'/'+sector_domain_name+'/Geschiktheid_Cleaned 
				: null_f'
			, sector_domain
			, float32
		), Descr = "Per subsector per cel de geschiktheid, maar dan 0 als die onder de afkapgrens ligt."
	{
		attribute<Bool> AnyDefined (sector_domain) := = 'OR('+asItemList('IsDefined('+Subsector/name+')')+')';
	}
	
	attribute<Subsector> Resultaat (sector_domain) := = 'GeschikthedenBovenAfkapgrenzen/AnyDefined 
																	? ArgMax('+asItemList('GeschikthedenBovenAfkapgrenzen/'+Subsector/name)+')[Subsector] 
																	: (0/0)[Subsector]', Descr = "De subsector met de hoogste geschiktheid, of undefined als er geen geschiktheid boven de afkapgrens ligt.";
	
	attribute<OP> Resultaat_OP (CompactedAdminDomain) := = lowercase(Sector_name) == lowercase('Wonen')
		? 'merge(value(Resultaat[CompactedAdminDomain/'+sector_domain_name+'_rel], UInt16), OP ,'+asItemList('Subsectoren/'+Subsector/name+'/CompactedAdminDomain/Geschiktheid_ArgMax_OP')+')'
		: 'const(null_b, CompactedAdminDomain, OP)', Descr = "De OP van de subsector die in deze cel het hoogst scoort, of undefined als er geen subsector is.";
	
	attribute<Bool>              Beschikbaar           (CompactedAdminDomain) := = 'merge(value(Resultaat[CompactedAdminDomain/'+sector_domain_name+'_rel], UInt16), bool,'+asItemList('Subsectoren/'+Subsector/name+'/CompactedAdminDomain/Beschikbaar')+')', Descr = "Is deze cel ook daadwerkelijk beschikbaar voor allocatie.";
	attribute<OP>                per_OP                (CompactedAdminDomain) := Beschikbaar ? Resultaat_OP : (0/0)[OP], Descr = "De OP van de gealloceerde subsector, of undefined als deze toch niet beschikbaar bleek te zijn.";
	attribute<Sector/xSubsector> per_SectorxSubsector0 (CompactedAdminDomain) := ='(Subsector/SectorxSubSector_rel[Resultaat])[CompactedAdminDomain/'+sector_domain_name+'_rel]', Descr = "De SectorxSubsector van de gealloceerde subsector.";
	attribute<Sector/xSubsector> per_SectorxSubsector  (CompactedAdminDomain) := Beschikbaar ? per_SectorxSubsector0 : (0/0)[Sector/xSubsector], Descr = "De SectorxSubsector van de gealloceerde subsector, of undefined als deze toch niet beschikbaar bleek te zijn.";
}