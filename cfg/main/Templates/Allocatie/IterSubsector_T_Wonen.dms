Template IterSubsector_T_Wonen
{
	//
	parameter<SubSector> id;
	unit<uint8> SubSector;
	container StateVoorAllocatie;
	container Context0;
	//
	
	container Context := Context0
	{
		container ThisSubsector := ..;
	}
	
	parameter<string> name               := SubSector/name[id];
	parameter<string> SubSector_pathname := SubSector/Sector_name_ThisName[id];
	parameter<string> Zichtjaar_name     := Context/ThisZichtjaar/name;
	parameter<string> AllocRegio_name    := Context/ThisSectorAllocRegio/allocregio_name;
	
	unit<uint32> TredeK        := ='VariantData/Trede/SectorXSubSectoren/'+SubSector_pathname+'/Klasse';
	unit<uint8>  OP            := CaseClassifications/Vastgoed/OP;
	unit<uint8>  OP_subdomain  := ='OP/perWP2xVSSH/'+Context/ThisSubsector/name+'/OP_sub';
	unit<uint8>  OP_sub_NoData := ='OP/perWP2xVSSH/'+Context/ThisSubsector/name+'/OP_sub_NoData';
	
	unit<uint64> CompactedAdminDomain := Geography/CompactedAdminDomain
	{
		attribute<OP_subdomain> Geschiktheid_ArgMax_OP_sub :=  
			='argmax_ifdefined('+AsItemList('VariantData/Zeef/'+Zichtjaar_name+'/'+SubSector_pathname+'/Zeef_perOP/'+OP/name[OP_subdomain/org_rel]+'/Beschikbaar_voorOP'
				'? Suitability/'+Zichtjaar_name+'/'+SubSector_pathname+'/perOP/'+OP/name[OP_subdomain/org_rel]+'/TotaalAdminDomain'
				': (0f/0f)'
			)+')[OP_subdomain]';
			
		attribute<float32> Geschiktheid_MaxElem_OP_sub :=
			='max_elem('+AsItemList('VariantData/Zeef/'+Zichtjaar_name+'/'+SubSector_pathname+'/Zeef_perOP/'+OP/name[OP_subdomain/org_rel]+'/Beschikbaar_voorOP'
				'? Suitability/'+Zichtjaar_name+'/'+SubSector_pathname+'/perOP/'+OP/name[OP_subdomain/org_rel]+'/TotaalAdminDomain'
				': (0f/0f)'
			)+')';
		
		attribute<OP>           Geschiktheid_ArgMax_OP     := Geschiktheid_ArgMax_OP_sub -> org_rel;
		attribute<bool>         Beschikbaar_OPs            := = 'OR('+AsList('VariantData/Zeef/'+Zichtjaar_name+'/'+SubSector_pathname+'/Zeef_perOP/'+OP_subdomain/name+'/Beschikbaar_voorOP', ',')+')';
		
		attribute<bool> Beschikbaar := Beschikbaar_OPs && !(IsDefined(StateVoorAllocatie/Subsector_rel)) && !ExtraRestrictieDoorWindAllocatie;
		attribute<float32> PotentieleStateNaAllocatie := MakeDefined(OP/Dichtheid[Geschiktheid_ArgMax_OP], 0f) * float32(Beschikbaar) * AdminDomain/NrHaPerCell, IntegrityCheck = "Check_of_IsBestOPOokDaarBeschikbaar";
		attribute<bool> Check_of_IsBestOPOokDaarBeschikbaar := ='NOT(IsDefined(Geschiktheid_ArgMax_OP_sub)) || OR('+AsItemList('Geschiktheid_ArgMax_OP_sub == '+string(id(OP_subdomain))+'[OP_subdomain] && VariantData/Zeef/'+Zichtjaar_name+'/'+SubSector_pathname+'/Zeef_perOP/'+OP_subdomain/name+'/Beschikbaar_voorOP')+')';
	}
	
	unit<uint64> CompactedAllocDomain := Geography/CompactedAllocDomain
	{
		attribute<float32> Geschiktheid_srcsrc      := sum(CompactedAdminDomain/Geschiktheid_MaxElem_OP_sub, CompactedAdminDomain/CompactedAllocDomain_rel);
		attribute<float32> Geschiktheid_src         := Geschiktheid_srcsrc == 0f ? 0f/0f : Geschiktheid_srcsrc;
		attribute<Ha>      HectarenBeschikbaar      := sum(float32(CompactedAdminDomain/Beschikbaar) * CompactedAdminDomain/NrHaPerCell, CompactedAdminDomain/CompactedAllocDomain_rel);
		attribute<uint32>  Geschiktheid_src_ordered := invert(index(Geschiktheid_src))[uint32];
		parameter<uint32>  Geschiktheid_src_range   := max(Geschiktheid_src_ordered)+100;
		attribute<uint32>  TredeScore               := (#TredeK - 1) - collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, TredeK/per_AllocDomain), IntegrityCheck = "max(collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, TredeK/per_AllocDomain)) < #TredeK";
		attribute<uint32>  Geschiktheid0            := TredeScore * Geschiktheid_src_range + Geschiktheid_src_ordered;
		attribute<uint32>  Geschiktheid             := = 'HectarenBeschikbaar > VariantParameters/VariantK/MinimalLandAvailability[VariantParameters/VariantK/V/'+Variant_name+'] ? Geschiktheid0 : 0/0';
	
		attribute<float32> PotentieleStateNaAllocatie := sum(CompactedAdminDomain/PotentieleStateNaAllocatie, CompactedAdminDomain/CompactedAllocDomain_rel);
	}
	
	unit<uint32> AllocRegio := Context/ThisSectorAllocRegio/AllocRegio
	{
		attribute<float32> RestClaim := = replace( 
			'VariantData/Claims/@Z@/SectorAllocRegios/@AS@/@S@'+AllocRegio_StudyArea_orgrel+' - sum(StateVoorAllocatie/@SP@, per_CompactedAdminDomain)',
			'@Z@' , Zichtjaar_name,
			'@AS@', Context/ThisSectorAllocRegio/name,
			'@S@' , Context/ThisSubsector/name,
			'@SP@',Subsector_pathname
		);
		
		attribute<Ha> Beschikbaar := sum_uint32(CompactedAdminDomain/Beschikbaar, per_CompactedAdminDomain)[float32] * AdminDomain/NrHaPerCell;
		attribute<bool>   GeenRuimteOfRestclaim := Beschikbaar == 0[ha] || RestClaim <= 0[float32];
		attribute<uint32> MaxGeschiktheid       := max(CompactedAllocDomain/Geschiktheid, collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, AllocRegio/per_AllocDomain))[uint32];
		
		parameter<uint32> MaxGeschiktheid_value := max(CompactedAllocDomain/Geschiktheid)[uint32];
		
		attribute<uint32> Afkapgrens0 := MaxGeschiktheid_value - nth_element_weighted(
				MaxGeschiktheid_value - CompactedAllocDomain/Geschiktheid[uint32], // rank
				max_elem(RestClaim, 0f),                                           // target
				CompactedAllocDomain/PotentieleStateNaAllocatie,                   // weight, hier geen potentiele delta gebruikt; evt sloop van deze subsector heralloceren we in volgende iteraties
				collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, per_AllocDomain)
		);
		
		attribute<uint32> Afkapgrens := GeenRuimteOfRestclaim ? (0/0)[uint32] : MakeDefined(Afkapgrens0, MaxGeschiktheid);
	}
}