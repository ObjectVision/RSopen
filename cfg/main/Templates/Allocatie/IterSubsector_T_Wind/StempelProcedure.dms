container StempelProcedure
{
	container reprojectie_windrichting
	{
		parameter<rdc_meter> StudyArea  (poly) := geos_union_polygon(rdc_meter/geometry);
		
		parameter<meter>     ZuidOost_afstand  := (4f * CaseClassifications/WindTurbineType/RotorDiameter);
		parameter<meter>     NoordOost_afstand := (6f * CaseClassifications/WindTurbineType/RotorDiameter);
		
		parameter<meter>     Xc_aligned        := ZuidOost_afstand  * sqrt(2f) / 2f;
		parameter<meter>     Yc_aligned        :=-ZuidOost_afstand  * sqrt(2f) / 2f;
		parameter<meter>     Xr_aligned        := NoordOost_afstand * sqrt(2f) / 2f;
		parameter<meter>     Yr_aligned        := NoordOost_afstand * sqrt(2f) / 2f;
		
		parameter<meter_f64>     Xc                := Xc_aligned[meter_f64];
		parameter<meter_f64>     Yc                := Yc_aligned[meter_f64];
		parameter<meter_f64>     Xr                := Xr_aligned[meter_f64] - 0.5d * Xc;
		parameter<meter_f64>     Yr                := Yr_aligned[meter_f64] - 0.5d * Yc;
		
		parameter<meter2_f64>    Determinant       := (Xc * Yr - Xr * Yc);
		parameter<Float64>   f_inv             := 1d / Determinant;
		parameter<Float64>   Cx                :=  Yr * f_inv;
		parameter<Float64>   Cy                := -Xr * f_inv;
		parameter<Float64>   Rx                := -Yc * f_inv;
		parameter<Float64>   Ry                :=  Xc * f_inv;
		
		unit<UInt32> SA_Coords := sequence2points(StudyArea)
		{
			attribute<SPoint>    RC_coord  := spoint(point_xy(PointCol(Point) * Cx +  PointRow(Point) * Cy,PointCol(Point) * Rx +  PointRow(Point) * Ry));  
			attribute<rdc_meter> rdc_coord := point_xy(float64(PointCol(RC_coord)) * Xc +  float64(PointRow(RC_coord)) * Xr ,float64(PointCol(RC_coord)) * Yc +  float64(PointRow(RC_coord)) * Yr)[rdc_meter];  
		}
		
		parameter<SPoint>    RC_coords  (poly) := points2sequence(SA_Coords/RC_coord);
		parameter<SPoint>    RC_org            := spoint(Lower_Bound(RC_coords));
		parameter<SPoint>    RC_end            := spoint(Upper_Bound(RC_coords));
		
		parameter<meter_f64>     X_org             :=  (float64(PointCol(RC_org))*Xc + float64(PointRow(RC_org))*Xr)[meter_f64];
		parameter<meter_f64>     Y_org             :=  (float64(PointCol(RC_org))*Yc + float64(PointRow(RC_org))*Yr)[meter_f64];
		parameter<meter_f64>     X_end             :=  (float64(PointCol(RC_end))*Xc + float64(PointRow(RC_end))*Xr)[meter_f64];
		parameter<meter_f64>     Y_end             :=  (float64(PointCol(RC_end))*Yc + float64(PointRow(RC_end))*Yr)[meter_f64];
		parameter<rdc_meter> XY_org            := point_xy(X_org, Y_org)[rdc_meter];
		parameter<rdc_meter> XY_end            := point_xy(X_end, Y_End)[rdc_meter];
		
		parameter<Int16>     aantalrijen       := int16(PointRow(RC_end) - PointRow(RC_org))+4s;
		parameter<Int16>     aantalkolommen    := int16(PointCol(RC_end) - PointCol(RC_org))+4s;
	}
	
	container StempelSettings
	{
		unit<UInt32> OffSet := range(uint32, 0, ModelParameters/Wind/aantal_stempel_in1richting * /ModelParameters/Wind/aantal_stempel_in1richting)
		{
			attribute<Float64>       X  := float64(mod(id(.) , /ModelParameters/Wind/aantal_stempel_in1richting)) * /ModelParameters/Wind/afstand_offset_stempel[float64];
			attribute<Float64>       Y  := float64(    id(.) / /ModelParameters/Wind/aantal_stempel_in1richting)  * /ModelParameters/Wind/afstand_offset_stempel[float64];
			attribute<rdc_meter>     XY := point_xy(x,y,rdc_meter);
		}
		
		unit<UInt32> EersteRijKolomLeeg := range(uint32, 0, /ModelParameters/Wind/max_aantal_rijen_zonder_tussenruimte + /ModelParameters/Wind/rijkolom_tussenruimte);
		// unit<UInt32> LijnOrientatie     := range(uint32,0, strcount(/ModelParameters/Wind/LijnOrientatie,',') + 1) //dit hele template is al hardcoded op deze orientatie. Dus parameter is irrelevant...
		unit<UInt32> LijnOrientatie     := range(uint32,0, 1)
		{
			// attribute<String> name := ReadArray(/ModelParameters/Wind/LijnOrientatie, ., string, 0, 0);
			attribute<String> name := const('NW_ZO', .); //dit hele template is al hardcoded op deze orientatie. Dus parameter is irrelevant...
		}
	}
	
	unit<UInt32> Stempel := combine(StempelSettings/LijnOrientatie, StempelSettings/EersteRijKolomLeeg, StempelSettings/OffSet)
	{
		attribute<String>    orientatie          := StempelSettings/LijnOrientatie/name[first_rel];
		attribute<UInt32>    eersteRijKolomLeeg  := id(StempelSettings/EersteRijKolomLeeg)[second_rel];
		attribute<Float64>   offsetX             := StempelSettings/OffSet/X[third_rel];
		attribute<Float64>   offsetY             := StempelSettings/OffSet/Y[third_rel];
		attribute<rdc_meter> XY                  := point_xy(offsetX, offsetY, rdc_meter);
		attribute<String>    name                := 'st_' + orientatie + '_' + string(eersteRijKolomLeeg) + '_' + string(offsetX) + '_' + string(offsetY);
		
		container V := for_each_nedv(name, string(id(.)) + '[..]', void, .);
	}
	
	container Stempels :=
		for_each_ne(
			 Stempel/name
			,'Templates/Beschikbaarheden/Wind_BepaalGeschikteCellenPerStempel_T(' + quote(Stempel/orientatie) + ', ' + string(Stempel/EersteRijKolomLeeg) + ', ' + string(Stempel/offSetX) + '[meter], '+ string(Stempel/offSetY) + '[meter])'
		);
}