container StempelProcedure
{
	container reprojectie_windrichting
	{
		parameter<rdc_meter> StudyArea  (poly) := fpolygon(bp_union_polygon(ipolygon(rdc_meter/geometry)));
		
		parameter<meter>     ZuidOost_afstand  := (4f * CaseClassifications/WindTurbineType/RotorDiameter);
		parameter<meter>     NoordOost_afstand := (6f * CaseClassifications/WindTurbineType/RotorDiameter);
		
		parameter<meter>     Xc_aligned        := ZuidOost_afstand  * sqrt(2f) / 2f;
		parameter<meter>     Yc_aligned        :=-ZuidOost_afstand  * sqrt(2f) / 2f;
		parameter<meter>     Xr_aligned        := NoordOost_afstand * sqrt(2f) / 2f;
		parameter<meter>     Yr_aligned        := NoordOost_afstand * sqrt(2f) / 2f;
		
		parameter<meter>     Xc                := Xc_aligned;
		parameter<meter>     Yc                := Yc_aligned;
		parameter<meter>     Xr                := Xr_aligned - 0.5f * Xc;
		parameter<meter>     Yr                := Yr_aligned - 0.5f * Yc;
		
		parameter<meter2>    Determinant       := (Xc * Yr - Xr * Yc);
		parameter<float32>   f_inv             := 1f / Determinant;
		parameter<float32>   Cx                :=  Yr * f_inv;
		parameter<float32>   Cy                := -Xr * f_inv;
		parameter<float32>   Rx                := -Yc * f_inv;
		parameter<float32>   Ry                :=  Xc * f_inv;
		
		unit<uint32> SA_Coords := sequence2points(StudyArea)
		{
			attribute<spoint>    RC_coord  := spoint(point_xy(PointCol(Point) * Cx +  PointRow(Point) * Cy,PointCol(Point) * Rx +  PointRow(Point) * Ry));  
			attribute<rdc_meter> rdc_coord := point_xy(float32(PointCol(RC_coord)) * Xc +  float32(PointRow(RC_coord)) * Xr ,float32(PointCol(RC_coord)) * Yc +  float32(PointRow(RC_coord)) * Yr)[rdc_meter];  
		}
		
		parameter<spoint>    RC_coords  (poly) := points2sequence(SA_Coords/RC_coord);
		parameter<spoint>    RC_org            := spoint(Lower_Bound(RC_coords));
		parameter<spoint>    RC_end            := spoint(Upper_Bound(RC_coords));
		
		parameter<meter>     X_org             :=  (float32(PointCol(RC_org))*Xc + float32(PointRow(RC_org))*Xr)[meter];
		parameter<meter>     Y_org             :=  (float32(PointCol(RC_org))*Yc + float32(PointRow(RC_org))*Yr)[meter];
		parameter<meter>     X_end             :=  (float32(PointCol(RC_end))*Xc + float32(PointRow(RC_end))*Xr)[meter];
		parameter<meter>     Y_end             :=  (float32(PointCol(RC_end))*Yc + float32(PointRow(RC_end))*Yr)[meter];
		parameter<rdc_meter> XY_org            := point_xy(X_org, Y_org)[rdc_meter];
		parameter<rdc_meter> XY_end            := point_xy(X_end, Y_End)[rdc_meter];
		
		parameter<int16>     aantalrijen       := int16(pointrow(RC_end) - pointrow(RC_org))+4s;
		parameter<int16>     aantalkolommen    := int16(pointcol(RC_end) - pointcol(RC_org))+4s;
	}
	
	container StempelSettings
	{
		unit<uint32> OffSet := range(uint32, 0, ModelParameters/Wind/aantal_stempel_in1richting * /ModelParameters/Wind/aantal_stempel_in1richting)
		{
			attribute<meter>     X  := float32(mod(id(.) , /ModelParameters/Wind/aantal_stempel_in1richting)) * /ModelParameters/Wind/afstand_offset_stempel;
			attribute<meter>     Y  := float32(    id(.) / /ModelParameters/Wind/aantal_stempel_in1richting)  * /ModelParameters/Wind/afstand_offset_stempel;
			attribute<rdc_meter> XY := point_xy(x,y,rdc_meter);
		}
		
		unit<uint32> EersteRijKolomLeeg := range(uint32, 0, /ModelParameters/Wind/max_aantal_rijen_zonder_tussenruimte + /ModelParameters/Wind/rijkolom_tussenruimte);
		unit<uint32> LijnOrientatie     := range(uint32,0, strcount(/ModelParameters/Wind/LijnOrientatie,',') + 1)
		{
			attribute<string> name := ReadArray(/ModelParameters/Wind/LijnOrientatie, ., string, 0, 0);
		}
	}
	
	unit<uint32> Stempel := combine(StempelSettings/LijnOrientatie, StempelSettings/EersteRijKolomLeeg, StempelSettings/OffSet)
	{
		attribute<string>    orientatie          := StempelSettings/LijnOrientatie/name[first_rel];
		attribute<uint32>    eersteRijKolomLeeg  := id(StempelSettings/EersteRijKolomLeeg)[second_rel];
		attribute<meter>     offsetX             := StempelSettings/OffSet/X[third_rel];
		attribute<meter>     offsetY             := StempelSettings/OffSet/Y[third_rel];
		attribute<rdc_meter> XY                  := point_xy(offsetX, offsetY, rdc_meter);
		attribute<string>    name                := 'st_' + orientatie + '_' + string(eersteRijKolomLeeg) + '_' + string(offsetX) + '_' + string(offsetY);
		
		container V := for_each_nedv(name, string(id(.)) + '[..]', void, .);
	}
	
	container Stempels :=
		for_each_ne(
			 Stempel/name
			,'Templates/Beschikbaarheden/Wind_BepaalGeschikteCellenPerStempel_T(' + quote(Stempel/orientatie) + ', ' + string(Stempel/eersteRijKolomLeeg) + ', ' + string(Stempel/offSetX) + '[meter], '+ string(Stempel/offSetY) + '[meter])'
		);
}