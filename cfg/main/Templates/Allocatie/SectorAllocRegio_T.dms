Template SectorAllocRegio_T
{
	//
	unit<uint8>                 SectorAllocRegio;
	parameter<SectorAllocRegio> id;
	container                   StateVoorAllocatie;
	container                   Context0;
	parameter<uint8>            AantalIters;
	container                   Impl;
	//
	
	parameter<string> name            := SectorAllocRegio/name[id];
	parameter<string> Sector_name     := SectorAllocRegio/Sector_name[id];
	parameter<string> AllocRegio_name := SectorAllocRegio/AllocRegio_name[id];
	parameter<string> AllocRegio_path := SectorAllocRegio/AllocRegio_path[id];
	
	unit<uint32>      AllocRegio                  := =ModelParameters/StudyArea == 'Friesland' ? AllocRegio_path+'/StudyArea' : AllocRegio_path;
	parameter<string> AllocRegio_StudyArea_orgrel := ModelParameters/StudyArea == 'Friesland' ? '[AllocRegio/org_rel]' : '';
	unit<uint8>       Subsector                   := = SectorAllocRegio/Subsector_ref[id];
	
	container Context := Context0
	{
		container ThisSectorAllocRegio := ..;
		parameter<string> ThisSectorAllocRegioname := ThisSectorAllocRegio/name;
		parameter<string> Prev_SectorAllocRegio_name := rjoin(ThisSectorAllocRegio/name, SectorAllocRegio/name, SectorAllocRegio/PrevName);
	}
	
	unit<uint8> Iter := range(uint8, 0[uint8], AantalIters)
	{
		attribute<string> name := 'Iter_'+string(id(.));
		attribute<bool>   IsFirstIter := id(.) == 0b;
		
		attribute<string> PrevName   := IsFirstIter ? 'Start' : name[id(.) - min_elem( id(.), 1b)];
		attribute<string> PrevState := IsFirstIter ? 'StateVoorAllocatie' : Iter/PrevName+'/StateNaAllocatie';
		attribute<string> Label := name, DialogType = "LabelText";
	}
	
	container Iters_Normaal :=
		for_each_ne(
			Iter/name,
			'Iter_T(
					'+string(id(Iter))+'[Iter]
					, '+Iter/PrevState+'
					, Context
					, Iter
					, Impl
					, FALSE
				)'
		), Descr = "Iteraties zijn nodig om niet vervulde restclaims in een iteratie in een volgende iteratie te vervullen. "
	{
		container LastIter := =last(Iter/name);
	}
	
	container Iters_Enkel : Descr = "Zon en Verblijfsrecreatie hebben niet meerdere iteraties nodig, dus gaan verder in een enkele iteratie." 
	{
		container Iter_0 := = 
			 'Iter_T(
					0[Iter]
					,StateVoorAllocatie
					,Context
					,Iter
					,Impl
					,TRUE
				)';
		
		container LastIter := ='Iter_0';
	}
	container Iters_Landbouw : Descr = "Landbouw gebruiks discrete allocatie, en behoeft dus een ander mechanisme." 
	{
		container Iter_0 := = 
			 'Iter_Landbouw_T(
					0[Iter]
					,StateVoorAllocatie
					,Context
					,Iter
					,Impl
					,TRUE
				)';
		
		container LastIter := ='Iter_0';
	}
	
	// parameter<string> Iter_str := Sector_name == 'Zon' || Sector_name == 'Verblijfsrecreatie' ? 'Iters_Enkel' : Sector_name == 'Landbouw' ? 'Iters_Landbouw' : 'Iters_Normaal';
	parameter<string> Iter_str := Sector_name == 'Zon' ? 'Iters_Enkel' : Sector_name == 'Landbouw' ? 'Iters_Landbouw' : 'Iters_Normaal';
	// parameter<string> Iter_str := Sector_name == 'Zon' ? 'Iters_Enkel' : 'Iters_Normaal';
	
	container Iters := =Iter_str
	{
	
		parameter<bool> HasEnoughIteraties_Normaal := ='Iters_Normaal/'+last(Iter/name)+'/Impl/StopCriterium';
		parameter<bool> HasEnoughIteraties_Enkel   := ='Iters_Enkel/Iter_0/Impl/StopCriterium';
		parameter<bool> HasEnoughIteraties         := =Iter_str == 'Iters_Enkel' ? 'HasEnoughIteraties_Enkel' : 'HasEnoughIteraties_Normaal';
		
		parameter<bool> HasNotEnoughIteraties := NOT(HasEnoughIteraties);
	}
	
	#include<Restricties_Dynamisch_Wind.dms>
	
	container StateNaAllocatie := ='Iters/LastIter/StateNaAllocatie', IntegrityCheck = "Iters/HasEnoughIteraties";
}