container ClaimRealisatie : using = "impl"
{
	container Per_SectorAllocRegio :=
		for_each_ne(
			VariantParameters/SectorAllocRegio/name
			, 'Per_AllocRegio/'+VariantParameters/SectorAllocRegio/AllocRegio_name+'/Claim_Realisatie/'+VariantParameters/SectorAllocRegio/Sector_name
		);
		
	container Per_AllocRegio := 
		for_each_ne(
			VariantParameters/SectorAllocRegio/Uq_AllocRegios/AllocRegio_name
			, 'ClaimRealisatie_T('+quote(VariantParameters/SectorAllocRegio/Uq_AllocRegios/AllocRegio_name)+','+quote(VariantParameters/SectorAllocRegio/Uq_AllocRegios/AllocRegio_path)+')'
		);
	
	Template ClaimRealisatie_T
	{
		//
		parameter<string> ClaimRegio_name;
		parameter<string> ClaimRegio_path;
		//
		
		unit<uint32> ClaimRegio := =ClaimRegio_path;
		
		container Claims := // Voor het gemak pakken we hier voor elke sector de nationale claim, om te checken of deze uiteindelijk ergens wordt gealloceerd (de vraag tav claimrealisatie in in evt kleinere allocregio'sblijft dus over)
			for_each_nedvn(
				Standvar/Claims_sub/name,
				'Impl/Claims/AllocRegios/'+ClaimRegio_Name+'/'+Standvar/Claims_sub/path
				, ClaimRegio
				, StandVar/Claims_sub
				, StandVar/Claims_sub/unit_name
			);
	
		container MinClaims :=  // /VariantData/Claims/Y2050/AllocRegios/Provincie/Landbouw_min
			for_each_nedvn(
				Standvar/Claims_sub/name
				, Standvar/Claims_sub/IsLandbouw 
					? 'Impl/Claims/AllocRegios/'+ClaimRegio_Name+'/Landbouw_min/'+Standvar/Claims_sub/subsector_name 
					: 'min_elem(Claims/'+Standvar/Claims_sub/name+', sum(PrevStand/'+Standvar/Claims_sub/name+', ClaimRegio/per_AdminDomain))'
				, ClaimRegio
				, StandVar/Claims_sub
				, StandVar/Claims_sub/unit_name
			);
		
		container MaxClaims := 
			for_each_nedvn(
				Standvar/Claims_sub/name
				, Standvar/Claims_sub/IsLandbouw 
					? 'Impl/Claims/AllocRegios/'+ClaimRegio_Name+'/Landbouw_max/'+Standvar/Claims_sub/subsector_name 
					: '	max_elem(Claims/'+Standvar/Claims_sub/name+', sum(PrevStand/'+Standvar/Claims_sub/name+', ClaimRegio/per_AdminDomain))'
				, ClaimRegio
				, StandVar/Claims_sub
				, StandVar/Claims_sub/unit_name
			);
		
		container Stand_PerAllocRegio := 
			for_each_nedvn(
				Standvar/Alloc_sub/name
				, 'sum((Stand/'+Standvar/Alloc_sub/name+'), ClaimRegio/per_AdminDomain)'
				, ClaimRegio
				, StandVar/Alloc_sub
				, StandVar/Alloc_sub/unit_name
			);
		
		container Claims_oke := 
			for_each_nedv(
				Standvar/Claims_sub/name
				, 'Stand_PerAllocRegio/'+Standvar/Claims_sub/name+' >= MinClaims/'+Standvar/Claims_sub/name+' && Stand_PerAllocRegio/'+Standvar/Claims_sub/name+' <= MaxClaims/'+Standvar/Claims_sub/name
				, ClaimRegio
				, bool
			);
		
		container MinClaim_realisatie := 
			for_each_nedv(
				Standvar/Claims_sub/name
				, 'Stand_PerAllocRegio/'+Standvar/Claims_sub/name+' / MinClaims/'+Standvar/Claims_sub/name
				, ClaimRegio
				, float32
			);
		container MaxClaim_realisatie := 
			for_each_nedv(
				Standvar/Claims_sub/name
				, 'Stand_PerAllocRegio/'+Standvar/Claims_sub/name+' / MaxClaims/'+Standvar/Claims_sub/name
				, ClaimRegio
				, float32
			);
		
		container Claim_realisatie
		{
			container Wonen	:= 
				for_each_nedv(
					Classifications/Vastgoed/WP2xVSSH/name
					, 'Stand_PerAllocRegio/Aantal_woningen_'+/Classifications/Vastgoed/WP2xVSSH/name+' / Claims/Aantal_woningen_'+/Classifications/Vastgoed/WP2xVSSH/name
					, ClaimRegio
					, float32
				);
			
			container Werken := 
				for_each_nedv(
					Classifications/Actor/Jobs6/name
					, 'Stand_PerAllocRegio/Banen_'+Classifications/Actor/Jobs6/name+' / Claims/Banen_'+Classifications/Actor/Jobs6/name
					, ClaimRegio
					, float32
				);
			
			container Verblijfsrecreatie := 
				for_each_nedv(
					Classifications/Vastgoed/VerblijfsrecreatieK/sector_name
					, 'Stand_PerAllocRegio/Objecten_'+Classifications/Vastgoed/VerblijfsrecreatieK/sector_name+' / Claims/Objecten_'+Classifications/Vastgoed/VerblijfsrecreatieK/sector_name
					, ClaimRegio
					, float32
				);
			
			container Wind := 
				for_each_nedv(
					Classifications/Actor/WindK/sector_name
					, 'Stand_PerAllocRegio/Vermogen_'+Classifications/Actor/WindK/sector_name+' / Claims/Vermogen_'+Classifications/Actor/WindK/sector_name
					, ClaimRegio
					, float32
				);
			
			container Zon := 
				for_each_nedv(
					Classifications/Actor/ZonK/sector_name
					, 'Stand_PerAllocRegio/Vermogen_'+Classifications/Actor/ZonK/sector_name+' / Claims/Vermogen_'+Classifications/Actor/ZonK/sector_name
					, ClaimRegio
					, float32
				);
				
			container Landbouw_min := 
				for_each_nedv(
					Classifications/Actor/LandbouwK/name
					, 'Stand_PerAllocRegio/ha_'+Classifications/Actor/LandbouwK/name+' / minclaims/ha_'+Classifications/Actor/LandbouwK/name
					, ClaimRegio
					, float32
				);
			container Landbouw_max := 
				for_each_nedv(
					Classifications/Actor/LandbouwK/name
					, 'Stand_PerAllocRegio/ha_'+Classifications/Actor/LandbouwK/name+' / maxclaims/ha_'+Classifications/Actor/LandbouwK/name
					, ClaimRegio
					, float32
				);
		}
	}
}