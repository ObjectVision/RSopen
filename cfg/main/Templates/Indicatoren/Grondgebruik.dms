container Grondgebruik : using = "SourceData/Grondgebruik"
{
	container Verandering
	{
		unit<UInt32> Regio := SourceData/RegioIndelingen/Provincie;
		
		unit<UInt8> VeranderingK := combine_uint8(Classifications/Grondgebruik/CBSKlasse/Aggr2, /Classifications/Actor/Sector)
		{
			attribute<String> Van_name       := Classifications/Grondgebruik/CBSKlasse/Aggr2/name[first_rel];
			attribute<String> Naar_name      := /Classifications/Actor/Sector/name[second_rel];
			attribute<String> name           := 'Van_'+Van_name+'_Naar_'+Naar_name;
			attribute<String> Label          := name, DialogType = "LabelText";
			
			attribute<UInt8>  from_TransCode := uint8(first_rel) + 1b;
			attribute<UInt8>  to_TransCode   := (uint8(second_rel) + 1b) * 10b;
			attribute<UInt8>  TransCode      := from_TransCode + to_TransCode;
			
			attribute<UInt32> count          := pcount(LU_Mutatie)[uint32];
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}

		attribute<Classifications/Grondgebruik/CBSKlasse/Aggr2>       Basisjaar   (AdminDomain) := Classifications/Grondgebruik/CBSKlasse/Aggr2_rel[/BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_CBS];
		attribute<Classifications/Actor/Sector>                       Zichtjaar   (AdminDomain) := Classifications/Actor/Sector/xSubsector/Sector_rel[Stand/SubSector_rel];
		
		attribute<UInt8>        LU_Basisjaar (rdc_25m) := uint8(basisjaar) + 1b;
		attribute<UInt8>        LU_Y2050     (rdc_25m) := (uint8(Zichtjaar) + 1b)  * 10b;
		attribute<VeranderingK> LU_Mutatie   (rdc_25m) := rlookup(LU_Basisjaar + LU_Y2050, VeranderingK/TransCode);
		
		container Per_NL0 := 
			for_each_nedv(
				VeranderingK/name
				,'sum_uint32(LU_Mutatie == VeranderingK/V/'+VeranderingK/name+')[float32] * AdminDomain/NrHaPerCell'
				, void
				, ha
			);
		
		attribute<ha> Per_NL (VeranderingK) := = 'union_data(VeranderingK, '+asItemList('Per_NL0/'+VeranderingK/name)+')';
		
		container Per_Regio := 
			for_each_nedv(
				VeranderingK/name
				,'sum_uint32(LU_Mutatie == VeranderingK/V/'+VeranderingK/name+', Regio/Per_AdminDomain)[float32] * AdminDomain/NrHaPerCell'
				, Regio
				, ha
			);
			
		attribute<LU_ModelType> LU_MT_Basisjaar          (AdminDomain) := Indicatoren/Basisjaar/Landgebruikskaart/Result_SA;
		attribute<LU_ModelType> LU_MT_Zichtjaar          (AdminDomain) := Indicatoren/Y2050/Landgebruikskaart/Result_SA;
		
		attribute<Sector> LU_Sector_Basisjaar          (AdminDomain) := LU_ModelType/Sector_rel[Indicatoren/Basisjaar/Landgebruikskaart/Result_SA];
		attribute<Sector> LU_Sector_Zichtjaar          (AdminDomain) := LU_ModelType/Sector_rel[Indicatoren/Y2050/Landgebruikskaart/Result_SA];
			
		// container Per_ABCD_klasse := 
			// for_each_nedv(
				// LU_ModelType/name
				// ,'sum_uint32(LU_MT_Basisjaar == LU_ModelType/v/'+LU_ModelType/name+' && LU_MT_Zichtjaar != LU_ModelType/v/'+LU_ModelType/name+', ABCD/ABCD_sub_rel_25m)[float32] * AdminDomain/NrHaPerCell'
				// , ABCD/ABCD_subK
				// , ha
			// );
		container Per_ABCD_klasse := 
			for_each_nedv(
				Sector/name
				,'sum_uint32(LU_Sector_Basisjaar == Sector/v/'+Sector/name+' && LU_Sector_Zichtjaar != Sector/v/'+Sector/name+', ABCD/ABCD_sub_rel_25m)[float32] * AdminDomain/NrHaPerCell'
				, ABCD/ABCD_subK
				, ha
			);
		
		unit<UInt32> LU_ModelType_x_ResHardheidK_x_Sector := combine(LU_ModelType,Sector_x_ResHardheidK)
		{
			attribute<String> LU_ModelType_name          := LU_ModelType/name[first_rel];
			attribute<String> Sector_x_ResHardheid_name  := Sector_x_ResHardheidK/name[second_rel];
			attribute<String> HardheidK_name             := rjoin(Sector_x_ResHardheid_name, Sector_x_ResHardheidK/name, Sector_x_ResHardheidK/HardheidK_name);
			attribute<String> Sector_name                := rjoin(Sector_x_ResHardheid_name, Sector_x_ResHardheidK/name, Sector_x_ResHardheidK/Sector_name);
			attribute<String> label                      := LU_ModelType_name+'_'+HardheidK_name+'_'+Sector_name;
			attribute<String> Sector_HardheidK_name      := Sector_name+'_'+HardheidK_name;
			attribute<String> Sector_HardheidK_path      := Sector_name+'/'+HardheidK_name;
		}
		
		container Per_LU_ModelType_x_ResHardheidK_x_Sector := 
			for_each_nedv(
				LU_ModelType_x_ResHardheidK_x_Sector/label
				,'sum_uint32(
						LU_MT_Basisjaar == LU_ModelType/v/'+LU_ModelType_x_ResHardheidK_x_Sector/LU_ModelType_name+' 
						&& LU_MT_Zichtjaar != LU_ModelType/v/'+LU_ModelType_x_ResHardheidK_x_Sector/LU_ModelType_name+'
						&& SourceData/Restricties/'+LU_ModelType_x_ResHardheidK_x_Sector/Sector_HardheidK_path+')[float32] * AdminDomain/NrHaPerCell'
				, void
				, ha
			);
			
		container Per_LU_ModelType_x_ResHardheidK_Wonen := 
			for_each_nedv(
				Restrictie_HardheidK/name
				, replace('union_data(LU_ModelType, '+AsItemList('Per_LU_ModelType_x_ResHardheidK_x_Sector/'+LU_ModelType/name+'_@HK@_Wonen')+')', '@HK@', Restrictie_HardheidK/name)
				, LU_ModelType
				, ha
			);
		container Per_LU_ModelType_x_ResHardheidK_Werken := 
			for_each_nedv(
				Restrictie_HardheidK/name
				, replace('union_data(LU_ModelType, '+AsItemList('Per_LU_ModelType_x_ResHardheidK_x_Sector/'+LU_ModelType/name+'_@HK@_Werken')+')', '@HK@', Restrictie_HardheidK/name)
				, LU_ModelType
				, ha
			);
		
	}
	
	// container Groen
	// {
		// attribute</Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> InBasisjaar (AdminDomain) := Basisjaar/Grondgebruik/Groen;
		// attribute</Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> InZichtjaar (AdminDomain) := (InBasisjaar[uint8] / (!Stand/TovStandBasisjaar/toevoeging/AnyVastgoed)[uint8])[/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen];
		
		// container Toename :=
			// for_each_nedv(
				// /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name,
				// '(float32(InZichtjaar ==  /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/V/'+/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name+') -
				  // float32(InBasisjaar ==  /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/V/'+/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name+')) *
				  // AdminDomain/NrHaPerCell',
				// AdminDomain, ha
			// )
		// {
			// attribute<ha> Totaal (AdminDomain) := = 'add('+asItemList(/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name)+')';
		// }
	// }
}