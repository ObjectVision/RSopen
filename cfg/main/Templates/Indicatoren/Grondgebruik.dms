container Grondgebruik
{
	container Verandering
	{
		unit<uint32> Regio := SourceData/RegioIndelingen/Provincie;
		
		unit<uint32> VeranderingK := combine_uint32(Classifications/Grondgebruik/CBSKlasse/Aggr2, /Classifications/Actor/Sector)
		{
			attribute<string> Van_name := Classifications/Grondgebruik/CBSKlasse/Aggr2/name[first_rel];
			attribute<string> Naar_name := /Classifications/Actor/Sector/name[second_rel];
			attribute<string> name := 'Van_'+Van_name+'_Naar_'+Naar_name;
			attribute<string> label := name, DialogType = "Labeltext";
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
		
		attribute<Classifications/Grondgebruik/CBSKlasse/Aggr2> Basisjaar (AdminDomain) := Classifications/Grondgebruik/CBSKlasse/Aggr2_rel[BaseData/StartState/Grondgebruik/Per_AdminDomain/Basisjaar];
		attribute</Classifications/Actor/Sector>                      Zichtjaar (AdminDomain) := Classifications/Actor/Sector/XSubsector/Sector_rel[Stand/Subsector_rel];
		
		attribute<VeranderingK> Verandering (AdminDomain) := (Basisjaar[uint32] * #Classifications/Grondgebruik/CBSKlasse/Aggr2 + Zichtjaar[uint32])[VeranderingK];
		
		container Per_NL0 := for_each_nedv(
			VeranderingK/name,
			'sum(float32(Verandering == VeranderingK/V/'+VeranderingK/name+') * AdminDomain/NrHaPerCell)',
			void, ha
		);
		
		attribute<ha> Per_NL (VeranderingK) := = 'union_data(VeranderingK, '+AsItemList('Per_NL0/'+VeranderingK/name)+')';
		
		container Per_Regio := 
			for_each_nedv(
				VeranderingK/name,
				'sum(float32(Verandering == VeranderingK/V/'+VeranderingK/name+') * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain)',
				Regio, ha
			);
	}
	
	container Groen
	{
		attribute</Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> InBasisjaar (AdminDomain) := Basisjaar/Grondgebruik/Groen;
		attribute</Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> InZichtjaar (AdminDomain) := (InBasisjaar[uint8] / (!Stand/TovStandBasisjaar/toevoeging/AnyVastgoed)[uint8])[/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen];
		
		container Toename :=
			for_each_nedv(
				/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name,
				'(float32(InZichtjaar ==  /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/V/'+/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name+') -
				  float32(InBasisjaar ==  /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/V/'+/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name+')) *
				  AdminDomain/NrHaPerCell',
				AdminDomain, ha
			)
		{
			attribute<ha> Totaal (AdminDomain) := = 'add('+AsItemList(/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/name)+')';
		}
	}
}