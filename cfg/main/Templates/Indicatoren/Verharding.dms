container Verharding
{
	unit<uint32> Regio := /geography/RegioIndelingen/Provincie;
	
	container Wonen
	{
		unit<uint8> OP := Impl/StandVar/OP;
		attribute<OP> OP_rel (AdminDomain) := Stand/OP_rel;
		attribute<bool> AllocatieHeeftPlaatsgevonden (AdminDomain) := IsDefined(OP_rel);
		attribute<Meter2> Verhard (AdminDomain) := = 'VariantParameters/Ontwikkelpakketten/'+Variant_name+'/InNettoBuurt/Verhard[OP_rel] * AdminDomain/NrHaPerCell[float32]';
		
		parameter <m2_ha> Per_NL := sum(Verhard) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell);
		
		attribute<m2_ha> Per_Regio (Regio) := sum(Verhard, Regio/Per_AdminDomain) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain);
	}
	
	container Werken
	{
		attribute<bool> AllocatieHeeftPlaatsgevonden (AdminDomain) := Impl/StandVar/SubSector/sector_rel[Stand/Subsector_rel] == /Classifications/Actor/Sector/V/Werken;
		attribute<Meter2> Verhard                    (AdminDomain) := = 'Stand/m2PandFootprint_Totaal + (AdminDomain/NrHaPerCell[Meter2] - Stand/m2PandFootprint_Totaal) * VariantParameters/FractiesGroenInWerken/'+Variant_name+'/Verhard / float32(AllocatieHeeftPlaatsgevonden)';
		
		parameter <m2_ha> Per_NL := sum(Verhard) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell);
		
		attribute<m2_ha> Per_Regio (Regio) := sum(Verhard, Regio/Per_AdminDomain) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain);
	}
	
	attribute<bool> AllocatieHeeftPlaatsgevonden (AdminDomain) := Wonen/AllocatieHeeftPlaatsgevonden || Werken/AllocatieHeeftPlaatsgevonden;
	
	attribute<Meter2> Totaal (AdminDomain) := MakeDefined(Wonen/Verhard, Werken/Verhard);
	
	parameter <m2_ha> Per_NL := sum(Totaal) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell)
	{
		parameter<float32> Fractie := . / (1[ha])[meter2];
	}
	
	attribute<m2_ha> Per_Regio (Regio) := sum(Totaal, Regio/Per_AdminDomain) / sum(float32(AllocatieHeeftPlaatsgevonden) * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain)
	{
		attribute<float32> Fractie (Regio) := . / (1[ha])[meter2];
	}
}