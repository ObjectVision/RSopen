template CasusT : using = "units"
{
	parameter<string> Scenario_name;
	parameter<string> Variant_name ;
	parameter<string> casus_name  := Scenario_name + '_' + Variant_name;
	parameter<bool>   DoCalculate := false;
	
	container    CaseClassifications := Templates/CaseClassificationsT(Scenario_name, Variant_name);
	unit<uint8>  Sector := Classifications/Actor/Sector;
	unit<uint32> SubSector := Sector/xSubSector;
	unit<uint8>  OP := CaseClassifications/Vastgoed/OP ;
	
	#include<../../VariantParameters.dms>
	
	container StartState
	{
		attribute<SubSector>         Subsector_rel (AdminDomain) := const(value(0/0, SubSector), AdminDomain);
		attribute<OP>                OP_rel        (AdminDomain) := const(0b/0b, AdminDomain, OP);
	
		container Wonen := 
			for_each_nedv(
				WP2xVSSH/name
				, '/BaseData/StateBasisjaar/State/Wonen/'+WP2xVSSH/name
				, AdminDomain
				, Woning
			);
			
		container Werken := 
			for_each_nedv(
				Jobs6/name
				, 'BaseData/StateBasisjaar/State/Werken/'+Jobs6/name
				, AdminDomain
				, job
			);
		
		container Wind
		{
			attribute<MW> Totaal (AdminDomain) := BaseData/StateBasisjaar/State/Wind/Totaal;
			attribute<Yr> jaartal_ingebruikname (AdminDomain) := BaseData/StateBasisjaar/State/Wind/jaartal_ingebruikname;
		}
		
		container Zon
		{
			attribute<MW> Totaal (AdminDomain) := BaseData/StateBasisjaar/State/Zon/Totaal;
		}
		
		container Verblijfsrecreatie
		{
			attribute<Verblijfsobject> Totaal (AdminDomain) := BaseData/StateBasisjaar/State/Verblijfsrecreatie/Totaal;
		}
		
		container PandFootprint := 
			for_each_nedv(
				Jobs6/name
				,'BaseData/StateBasisjaar/State/PandFootprint/'+Jobs6/name
				, AdminDomain
				, m2PandFootprint
			);
			
	}
		
	container Indicatoren := 
		for_each_ne(
			Classifications/Time/Zichtjaar/name
			,'Templates/Indicatoren_T('+quote(Classifications/Time/Zichtjaar/name)+','+MakeDefined(Classifications/Time/Zichtjaar/name[ID(Classifications/Time/Zichtjaar)-1b], 'Basisjaar')+', Scenario_name, Variant_name)'
		), using = "CaseClassifications"
	{
		container Basisjaar
		{
			container Stand :=  
				for_each_nedvn( 
					CaseClassifications/Standvar/name
					, 'StartState/'+CaseClassifications/Standvar/path
					, AdminDomain
					, CaseClassifications/StandVar
					, CaseClassifications/StandVar/unit_name
				)
			{
				attribute<Woning> Aantal_Woningen_Totaal               (AdminDomain) := = 'add('+AsItemList('Aantal_Woningen_'+/Classifications/Vastgoed/WP2xVSSH/name)+')';
				attribute<Job>    Aantal_Banen_Totaal                  (AdminDomain) := = 'add('+AsItemList('Banen_'+/Classifications/Actor/Jobs6/name)+')';
				
				attribute<meter2> Gebruiksopp_Eengezins                (AdminDomain) := Gebruiksoppervlak/per_WP2_perAdminDomain/Eengezins;
				attribute<meter2> Gebruiksopp_Meergezins               (AdminDomain) := Gebruiksoppervlak/per_WP2_perAdminDomain/Meergezins;
				attribute<meter2> Gebruiksopp_Nijverheid               (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Nijverheid;
				attribute<meter2> Gebruiksopp_Logistiek                (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Logistiek;
				attribute<meter2> Gebruiksopp_Detailhandel             (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Detailhandel;
				attribute<meter2> Gebruiksopp_Ov_consumentendiensten   (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Ov_consumentendiensten;
				attribute<meter2> Gebruiksopp_Zak_dienstverlening      (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Zak_dienstverlening;
				attribute<meter2> Gebruiksopp_Overheid_kw_diensten     (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Overheid_kw_diensten;
				
				container Aantal_Woningen_WP4 := /BaseData/StateBasisjaar/Src0/woningen/WP4;
				container Gebruiksoppervlak_WP4 := /BaseData/StateBasisjaar/Src0/woningen/WP4_opp;
			}
			
			container Gebruiksoppervlak
			{
				unit<uint32> pand_src := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/Panden/src';
				unit<uint32> pand     := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/Panden/pand';
				
				container per_Gebruiksdoel_perAdminDomain :=
					for_each_nedv(
						vbo_gebruiksdoel_plus/name
						, 'sum(recollect_by_cond(pand_src/pand_selection_condition, pand/VBOopp_inPand_perFunctie/'+vbo_gebruiksdoel_plus/name+', 0[meter2])[pand_src/ToedelingsMatrix/pand_rel] * pand_src/ToedelingsMatrix/ToedelingPand, pand_src/ToedelingsMatrix/AdminDomain_rel)'
						, AdminDomain
						, meter2
					);
				
				container per_Jobs6_perAdminDomain := 
					for_each_nedv(
						Actor/Jobs6/name
						, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'per_Gebruiksdoel_perAdminDomain/','#', '')
						, AdminDomain
						, meter2
					);
				
				container per_WP2_perAdminDomain := /BaseData/StateBasisjaar/Src0/Wonen/WP2_opp; 
				container per_WP4_perAdminDomain := /BaseData/StateBasisjaar/Src0/Wonen/WP4_opp; 
				
				container per_WP2xVSSH_perAdminDomain := 
					for_each_nedv(
						WP2xVSSH/name
						, '/BaseData/Verdeling_VSSH/Uitsmeren_WP2_'+WP2xVSSH/VSSH_name+'/'+WP2xVSSH/WP_name+'/VBOopp_per_AdminDomain'
						, AdminDomain
						, meter2
					);
			}
			
			container Grondgebruik
			{
				attribute<Classifications/Actor/sector> sector_rel(AdminDomain) := Classifications/Grondgebruik/CBSKlasse/Aggr/sector_rel[Per_Aggr/CbsStand];
				attribute<Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> Groen (AdminDomain) := invert(/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/Nr_OrgEntity)[/BaseData/Grondgebruik/per_AdminDomain/Basisjaar/per_Aggr];
				
				container Per_Aggr
				{
					unit<uint8>     Aggr                   := Classifications/Grondgebruik/CBSKlasse/Aggr;
					attribute<Aggr> CbsStand (AdminDomain) := /BaseData/Grondgebruik/per_AdminDomain/Basisjaar/per_Aggr;
				}
			}
			container Banen 
			{
				unit<uint8> Jobs6 := Classifications/Actor/Jobs6;
				
				container Stand := StartState/Werken;
			}
			
			container SSM2017_Overstromingsschades := Templates/SSM2017_Overstromingsschades('Basisjaar');
		}
	}
	
	container Analysis
	{
		container PotentieleState  := for_each_ne(Time/Zichtjaar/name, 'Templates/PotentieleStateT('+quote(Time/Zichtjaar/name)+', Zeef/'+Time/Zichtjaar/name+'/SectorXSubsectoren, Dichtheid)');
		#include<../../VariantData/Zeef.dms>
		#include<../../VariantData/Geschiktheid.dms>
		container PotentieleState0 := for_each_ne(Time/Zichtjaar/name, 'Templates/PotentieleStateT('+quote(Time/Zichtjaar/name)+', Zeef_AdminDomain, Dichtheid)');
		#include<../../VariantData/Dichtheid.dms>
		#include<../../VariantData/Zeef_SrcDomain_grid.dms>
		#include<../../VariantData/Plannen.dms>
		#include<../../VariantData/StateBasisjaar.dms>
		#include<../../VariantData/Claims.dms>
		#include<../../VariantData/Geschiktheden.dms>
		#include<../../VariantData/Wind_restricties.dms>
		#include<../../VariantData/NietBouwen.dms>
		#include<../../VariantData/Maatregelen.dms>
	
	
	}
}