Template Woningwaarde_perWoningType_T //voor verwervingskosten
{
	//
	parameter<WP4> WP4_id;
	//
	
	parameter<string> WP4_name       := WP4/name[WP4_id];
	parameter<string> WP2_name       := WP2/name[WP4/WP2_rel[WP4_id]];
	parameter<bool>   IsDetached     := WP4/IsDetached[WP4_id];
	parameter<bool>   IsSemiDetached := WP4/IsSemiDetached[WP4_id];
	
	parameter<string> CalcStr0: ['(@X@)[Eur_woning]'];
		
	parameter<string> CalcStr :=
		'	Pand/@T@ > 0[EUR_woning]
				? exp(
					Constant
					+ Pand/Generiek
					+ Pand/@T@
					+ Locatie
					)[EUR_woning]
				: 0[EUR_woning]';
			
	
	attribute<Eur_woning> Basisjaar (AdminDomain) := = replace(CalcStr, '@T@', 'Basisjaar');
	
	attribute<Eur_woning> Locatie (AdminDomain) := = replace(CalcStr0, '@X@',
		'add(
			log(Src/Locatie/tt_500kinhab_min_2020)      * Src/Prijs/lnreistijd_500k_inw_2020_NL,
			Src/Locatie/uai_2021                        * Src/Prijs/uai_2021,
			log(Src/Locatie/tt_Trainstation_2006_min)   * Src/Prijs/reistijd_stations_2006,
			Src/Locatie/bbg_groen_100mbuff[float32] * Src/Prijs/groennabij_2015
		)'
	);
	
	container Pand
	{
		attribute<Eur_woning> Generiek (AdminDomain) := = replace(CalcStr0, '@X@',
			'add(
				log(Src/Pand/'+WP4_name+'_lotsize[float32])     * MakeDefined(Src/Prijs/lnlotsize,0f),
				Src/Pand/'+WP4_name+'_d_highrise[float32]       * MakeDefined(Src/Prijs/d_highrise,0f),
				Src/Pand/'+WP4_name+'_nrooms[float32]           * Src/Prijs/nrooms,
				Src/Pand/'+WP4_name+'_nbathrooms[float32]       * Src/Prijs/nbathrooms,
				Src/Pand/'+WP4_name+'_d_maintgood[float32]      * Src/Prijs/d_maintgood,
				Src/Pand/'+WP4_name+'_d_kk[float32]             * Src/Prijs/d_kk,
				Src/Pand/'+WP4_name+'_d_centralheating[float32] * Src/Prijs/d_centralheating,
				Src/Pand/'+WP4_name+'_d_erfpacht[float32]       * Src/Prijs/d_erfpacht,
				Src/Pand/'+WP4_name+'_d_isol_compl[float32]     * Src/Prijs/d_isol_compl,
				Src/Pand/'+WP4_name+'_d_privpark[float32]       * Src/Prijs/d_privpark,
				1[Eur]                                          * Src/Prijs/Y'+ModelParameters/NVM_coeff_year+'
			) / 1[woning]'
		);
		attribute<Eur_woning> Basisjaar (AdminDomain) := = replace(CalcStr0, '@X@', 'log(MakeDefined(BAG/'+BAG_RecentYear+'/VBO/Per_Gebruiksdoel/woon/Select/Oppervlaktes/per_WP4_AdminDomain/'+WP4_name+',1f)) * Src/Prijs/lnsize');
	}
	
	parameter<Eur_woning> Constant := = replace(CalcStr0, '@X@', 'Src/Prijs/constant');
	
	container Src
	{
		container Prijs := = 'BaseData/Geschiktheden/Wonen/PrijsIndex/ReadCoefficients_WP4/'+WP4_name+'/Result';
		container Pand    := BaseData/Geschiktheden/Wonen/RegionalAvgCharacteristics/perWP4/RegionalAvgCharacteristics;
		container Locatie := /SourceData/Diversen/per_AdminDomain;
	}
}