Template Make_Landgebruikskaart_Basisjaar_T : using = "Classifications/Actor;Classifications/Grondgebruik"
{
	parameter<string>            Zichtjaar_name;
	container                    Stand;
	///
	container                    Parent              := .;
	container                    Prev_Stand          := =IsStartJaar ? 'Parent' : prev_zichtjaar_name+'/Stand';
	parameter<string>            Prev_Zichtjaar_name := rjoin(Zichtjaar_name, Time/Zichtjaar/name, Time/Zichtjaar/prevname_triv);
	parameter<bool>              IsStartJaar         := Zichtjaar_name == 'Basisjaar';
	
	attribute<LU_ModelType>      gg_basis                      (AdminDomain) := BaseData/StartState/Grondgebruik/per_AdminDomain/gg_basis;
	attribute<CBSKlasse>         gg_CBS                        (AdminDomain) := BaseData/StartState/Grondgebruik/per_AdminDomain/gg_CBS;
	
	// attribute<bool>              IsWonen_StartState              (AdminDomain) := Stand/Wonen/Aantal_Woningen_Totaal >= MinimumWoningAmount * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<bool>              IsWerken_StartState             (AdminDomain) := Stand/PandFootprint/Totaal >= MinimumPandfootprint * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<bool>              IsVerblijfsrecreatie_StartState (AdminDomain) := Stand/Verblijfsrecreatie/Totaal >= MinimumVerblijfsobjectAmount * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<bool>              IsWind_StartState               (AdminDomain) := Stand/Wind/Totaal > 0[MW];
	// attribute<bool>              IsZon_StartState                (AdminDomain) := Stand/Zon/Totaal > 0[MW];
	
	attribute<bool>              IsWonen_StartState              (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWonenSector ? 'Stand/Aantal_Woningen_Totaal >= ModelParameters/Advanced/MinimumWoningAmount * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<bool>              IsWerken_StartState             (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWerkenSector ? 'Stand/PandFootprint_Werken >= ModelParameters/Advanced/MinimumPandfootprint * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<bool>              IsVerblijfsrecreatie_StartState (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasVerblijfsrecreatieSector ? 'Stand/Objecten_Verblijfsrecreatie >= ModelParameters/Advanced/MinimumVerblijfsobjectAmount * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<bool>              IsWind_StartState               (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector ? 'Stand/Vermogen_Wind > 0[MW]' : 'const(FALSE, AdminDomain)';
	attribute<bool>              IsZon_StartState                (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasZonSector ? 'Stand/Vermogen_Zon > 0[MW]' : 'const(FALSE, AdminDomain)';
	
	
	attribute<LU_ModelType>      gg_prev                         (AdminDomain) := =IsStartJaar ? 'gg_basis' : prev_zichtjaar_name+'/Landgebruikskaart/Result';
	
	attribute<bool>              IsWonen_Zichtjaar               (AdminDomain) := =AsList('Stand/subsector_rel == CaseClassifications/StandVar/SubSector/v/Wonen_'+Sector/XSubSectoren/Wonen/Subsector/SubSector_name,  '||');
	attribute<bool>              IsWerken_Zichtjaar              (AdminDomain) := =AsList('Stand/subsector_rel == CaseClassifications/StandVar/SubSector/v/Werken_'+Sector/XSubSectoren/Werken/Subsector/SubSector_name,  '||');
	attribute<bool>              IsVerblijfsrecreatie_Zichtjaar  (AdminDomain) := Stand/subsector_rel == CaseClassifications/StandVar/SubSector/v/Verblijfsrecreatie_Totaal;
	
	
	attribute<bool>              IsLU_NoData                   (AdminDomain) := IsNull(gg_prev);
	attribute<bool>              IsLU_exogenous                (AdminDomain) := CBSKlasse/IsExogeen[gg_CBS];
	attribute<bool>              IsLU_Water                    (AdminDomain) := gg_prev == LU_ModelType/v/water_totaal;
	attribute<bool>              IsLU_Wonen                    (AdminDomain) := gg_prev == LU_ModelType/V/wonen_totaal;
	attribute<bool>              IsLU_Werken                   (AdminDomain) := gg_prev == LU_ModelType/V/werken_totaal;
	attribute<bool>              IsLU_Verblijfsrecreatie       (AdminDomain) := gg_prev == LU_ModelType/V/verblijfsrecreatie_totaal;
	attribute<bool>              IsLU_Landbouw                 (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasLandbouwSector ? 'LU_ModelType/Sector_rel[gg_prev] == Actor/Sector/V/Landbouw' : 'const(FALSE, AdminDomain)';
	
	attribute<LU_ModelType>      ExogeenOpleggen               (AdminDomain) := VariantData/ExogeenOpleggen/Totaal;

	
	attribute<meter2>            Wonen_Filtered                (AdminDomain) := =IsStartJaar ? 'Group_Wonen_Start/IsNieuwGrouped ? Stand/PandFootprint_Wonen : null_f[meter2]' : 'Group_Wonen_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';
	attribute<meter2>            Werken_Filtered               (AdminDomain) := =IsStartJaar ? 'Group_Werken_Start/IsNieuwGrouped ? Stand/PandFootprint_Werken : null_f[meter2]' : 'Group_Werken_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';
	attribute<meter2>            Verblijfrecreatie_Filtered    (AdminDomain) := =IsStartJaar ? 'Group_Verblijfsrecreatie_Start/IsNieuwGrouped ? Stand/PandFootprint_Werken : null_f[meter2]' : 'Group_Verblijfsrecreatie_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';

	attribute<LU_ModelType>      Allocatie                     (AdminDomain) := CaseClassifications/StandVar/SubSector/LU_ModelType_rel[Stand/Subsector_rel];
	attribute<Typen>             Dominant_BebouwdeOmgeving     (AdminDomain) := IsLU_Wonen || IsLU_NoData ? null_b[Typen] : argmax_ifdefined(Wonen_Filtered, Werken_Filtered, Verblijfrecreatie_Filtered)[Typen];

	container Group_Wonen_Start                  := Templates/Landgebruikskaart/GetNieuwGrouped_T('Wonen','Wonen_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');
	container Group_Werken_Start                 := Templates/Landgebruikskaart/GetNieuwGrouped_T('Werken','Werken_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');
	container Group_Verblijfsrecreatie_Start     := Templates/Landgebruikskaart/GetNieuwGrouped_T('Verblijfsrecreatie','Verblijfsrecreatie_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');

	container Group_Wonen_Zichtjaar              := Templates/Landgebruikskaart/GetNieuwGrouped_T('Wonen','Wonen_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');
	container Group_Werken_Zichtjaar             := Templates/Landgebruikskaart/GetNieuwGrouped_T('Werken','Werken_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');
	container Group_Verblijfsrecreatie_Zichtjaar := Templates/Landgebruikskaart/GetNieuwGrouped_T('Verblijfsrecreatie','Verblijfsrecreatie_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');

	unit<uint8> Typen : nrofrows = 3
	{
		attribute<string> name : ['Wonen', 'Werken', 'Verblijfsrecreatie'];
		attribute<string> label := name;
		
		container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	attribute<LU_ModelType> Result             (AdminDomain) := switch(
																	 case(IsLU_exogenous || IsLU_NoData || IsLU_Water || IsLU_Wonen || IsLU_Verblijfsrecreatie, gg_prev)
																	,case(Zichtjaar_name != 'Basisjaar' && IsDefined(ExogeenOpleggen), ExogeenOpleggen)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Wonen, LU_ModelType/V/Wonen_totaal)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Werken, LU_ModelType/V/Werken_totaal)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Verblijfsrecreatie, LU_ModelType/V/Verblijfsrecreatie_totaal)
																	,case(IsDefined(Allocatie), Allocatie)
																	// ,case(IsLU_Landbouw && IsDefined(Allocatie), Allocatie)
																	, gg_prev
																);
																
	parameter<string> StorageStr := IsStartJaar 
										? '%LocalDataProjDir%/Indicatoren/Landgebruik/'+Zichtjaar_name+'_'+/ModelParameters/StudyArea+'.tif'
										: '%LocalDataProjDir%/Indicatoren/Landgebruik/'+Casus_name +'/'+BoerVariantK/name[ModelParameters/Landbouw/BoerVariant]+'_'+Zichtjaar_name+'_'+/ModelParameters/StudyArea+'.tif';
	attribute<LU_ModelType> Write_Result_SA          (AdminDomain) := Geography/rdc_meter/IsStudyArea_AdminDomain ? Result : null_b, StorageName = "=StorageStr", StorageType = "gdalwrite.grid";
	attribute<LU_ModelType> Result_SA                (AdminDomain) : StorageName = "=PropValue(Write_Result_SA, 'StorageName')", StorageType = "gdal.grid";

	parameter<string> Result_SA_prev_str := 'Result_SA != PrevIndicatoren/Landgebruikskaart/Result_SA';
	
	attribute<LU_ModelType> Result_bij_tov_PrevYear (AdminDomain) := =IsStartJaar ? 'const(null_b,AdminDomain)' : Result_SA_prev_str +' ? Result_SA : null_b';
	attribute<LU_ModelType> Result_af_tov_PrevYear  (AdminDomain) := =IsStartJaar ? 'const(null_b,AdminDomain)' : Result_SA_prev_str +' ? PrevIndicatoren/Landgebruikskaart/Result_SA : null_b';
	
	attribute<bool> IsLandbouwInAllocEnNietInResultLU (AdminDomain) := =AsList('(Allocatie == LU_ModelType/v/Landbouw_'+/Classifications/Actor/LandbouwKlasses/name+' && Result_SA != LU_ModelType/v/Landbouw_'+/Classifications/Actor/LandbouwKlasses/name+')', '||');
}
