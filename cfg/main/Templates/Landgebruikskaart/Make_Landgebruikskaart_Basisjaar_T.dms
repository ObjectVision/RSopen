Template Make_Landgebruikskaart_T : using = "Classifications/Actor;Classifications/Grondgebruik"
{
	parameter<String>            Zichtjaar_name;
	container                    Stand;
	///
	container                    Parent              := .;
	container                    Prev_Stand          := =IsStartJaar ? 'Parent' : Prev_Zichtjaar_name+'/Stand';
	parameter<String>            Prev_Zichtjaar_name := rjoin(lowercase(Zichtjaar_name), lowercase(Time/Zichtjaar/name), Time/Zichtjaar/PrevName_triv);
	parameter<Bool>              IsStartJaar         := lowercase(Zichtjaar_name) == lowercase('Basisjaar');
	
	attribute<LU_ModelType>      gg_basis                      (AdminDomain) := BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_basis;
	attribute<CBSKlasse>         gg_CBS                        (AdminDomain) := BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_CBS;
	
	// attribute<Bool>              IsWonen_StartState              (AdminDomain) := Stand/Wonen/Aantal_Woningen_Totaal >= MinimumWoningAmount * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<Bool>              IsWerken_StartState             (AdminDomain) := Stand/PandFootprint/Totaal >= MinimumPandFootprint * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<Bool>              IsVerblijfsrecreatie_StartState (AdminDomain) := Stand/Verblijfsrecreatie/Totaal >= MinimumVerblijfsobjectAmount * AdminDomain/NrHaPerCell / 1[ha];
	// attribute<Bool>              IsWind_StartState               (AdminDomain) := Stand/Wind/Totaal > 0[MW];
	// attribute<Bool>              IsZon_StartState                (AdminDomain) := Stand/Zon/Totaal > 0[MW];
	
	attribute<Bool>              IsWonen_StartState              (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWonenSector ? 'Stand/Aantal_Woningen_Totaal >= ModelParameters/Advanced/MinimumWoningAmount * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<Bool>              IsWerken_StartState             (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWerkenSector ? 'Stand/PandFootprint_Werken >= ModelParameters/Advanced/MinimumPandFootprint * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<Bool>              IsVerblijfsrecreatie_StartState (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasVerblijfsrecreatieSector ? 'Stand/Objecten_Verblijfsrecreatie >= ModelParameters/Advanced/MinimumVerblijfsobjectAmount * AdminDomain/NrHaPerCell / 1[ha]' : 'const(FALSE, AdminDomain)';
	attribute<Bool>              IsWind_StartState               (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector ? 'Stand/Vermogen_Wind > 0[MW]' : 'const(FALSE, AdminDomain)';
	attribute<Bool>              IsZon_StartState                (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasZonSector ? 'Stand/Vermogen_Zon > 0[MW]' : 'const(FALSE, AdminDomain)';
	
	
	attribute<LU_ModelType>      gg_prev                         (AdminDomain) := =IsStartJaar ? 'gg_basis' : Prev_Zichtjaar_name+'/Landgebruikskaart/Result';
	
	attribute<Bool>              IsWonen_Zichtjaar               (AdminDomain) := =asList('Stand/subSector_rel == CaseClassifications/StandVar/Subsector/v/Wonen_'+Sector/xSubsectoren/Wonen/Subsector/Subsector_name,  '||');
	attribute<Bool>              IsWerken_Zichtjaar              (AdminDomain) := =asList('Stand/subSector_rel == CaseClassifications/StandVar/Subsector/v/Werken_'+Sector/xSubsectoren/Werken/Subsector/Subsector_name,  '||');
	attribute<Bool>              IsVerblijfsrecreatie_Zichtjaar  (AdminDomain) := Stand/subSector_rel == CaseClassifications/StandVar/Subsector/v/Verblijfsrecreatie_Totaal;
	
	
	attribute<Bool>              IsLU_NoData                   (AdminDomain) := IsNull(gg_prev);
	attribute<Bool>              IsLU_exogenous                (AdminDomain) := CBSKlasse/IsExogeen[gg_CBS];
	attribute<Bool>              IsLU_Water                    (AdminDomain) := gg_prev == LU_ModelType/v/water_Totaal;
	attribute<Bool>              IsLU_Wonen                    (AdminDomain) := gg_prev == LU_ModelType/V/wonen_Totaal;
	attribute<Bool>              IsLU_Werken                   (AdminDomain) := gg_prev == LU_ModelType/V/werken_Totaal;
	attribute<Bool>              IsLU_Verblijfsrecreatie       (AdminDomain) := gg_prev == LU_ModelType/V/Verblijfsrecreatie_Totaal;
	attribute<Bool>              IsLU_Landbouw                 (AdminDomain) := =/VariantParameters/SectorAllocRegio/Uq_Sectors/HasLandbouwSector ? 'LU_ModelType/Sector_rel[gg_prev] == Actor/Sector/V/Landbouw' : 'const(FALSE, AdminDomain)';
	
	attribute<LU_ModelType>      ExogeenOpleggen               (AdminDomain) := VariantData/ExogeenOpleggen/Totaal;

	
	attribute<meter2>            Wonen_Filtered                (AdminDomain) := =IsStartJaar ? 'Group_Wonen_Start/IsNieuwGrouped ? Stand/PandFootprint_Wonen : null_f[meter2]' : 'Group_Wonen_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';
	attribute<meter2>            Werken_Filtered               (AdminDomain) := =IsStartJaar ? 'Group_Werken_Start/IsNieuwGrouped ? Stand/PandFootprint_Werken : null_f[meter2]' : 'Group_Werken_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';
	attribute<meter2>            Verblijfrecreatie_Filtered    (AdminDomain) := =IsStartJaar ? 'Group_Verblijfsrecreatie_Start/IsNieuwGrouped ? Stand/PandFootprint_Werken : null_f[meter2]' : 'Group_Verblijfsrecreatie_Zichtjaar/IsNieuwGrouped ? AdminDomain/NrMeter2PerCell : null_f[meter2]';

	attribute<LU_ModelType>      Allocatie                     (AdminDomain) := CaseClassifications/StandVar/Subsector/LU_ModelType_rel[Stand/SubSector_rel];
	attribute<Typen>             Dominant_BebouwdeOmgeving     (AdminDomain) := IsLU_Wonen || IsLU_NoData ? null_b[Typen] : ArgMax_ifdefined(Wonen_Filtered, Werken_Filtered, Verblijfrecreatie_Filtered)[Typen];

	container Group_Wonen_Start                  := Templates/Landgebruikskaart/GetNieuwGrouped_T('Wonen','Wonen_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');
	container Group_Werken_Start                 := Templates/Landgebruikskaart/GetNieuwGrouped_T('Werken','Werken_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');
	container Group_Verblijfsrecreatie_Start     := Templates/Landgebruikskaart/GetNieuwGrouped_T('Verblijfsrecreatie','Verblijfsrecreatie_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_basis, 'StartState');

	container Group_Wonen_Zichtjaar              := Templates/Landgebruikskaart/GetNieuwGrouped_T('Wonen','Wonen_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');
	container Group_Werken_Zichtjaar             := Templates/Landgebruikskaart/GetNieuwGrouped_T('Werken','Werken_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');
	container Group_Verblijfsrecreatie_Zichtjaar := Templates/Landgebruikskaart/GetNieuwGrouped_T('Verblijfsrecreatie','Verblijfsrecreatie_Totaal', ModelParameters/Advanced/MinimumGroupSize, gg_prev, 'Zichtjaar');

	unit<UInt8> Typen : NrOfRows = 3
	{
		attribute<String> name : ['Wonen', 'Werken', 'Verblijfsrecreatie'];
		attribute<String> Label := name;
		
		container V := for_each_nedv(name, 'value('+string(id(.))+', ..)', void, .);
	}
	
	attribute<LU_ModelType> Result             (AdminDomain) := switch(
																	 case(IsLU_exogenous || IsLU_NoData || IsLU_Water || IsLU_Wonen || IsLU_Verblijfsrecreatie, gg_prev)
																	,case(lowercase(Zichtjaar_name) != lowercase('Basisjaar') && IsDefined(ExogeenOpleggen), ExogeenOpleggen)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Wonen, LU_ModelType/V/Wonen_Totaal)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Werken, LU_ModelType/V/Werken_Totaal)
																	,case(Dominant_BebouwdeOmgeving == Typen/V/Verblijfsrecreatie, LU_ModelType/V/Verblijfsrecreatie_Totaal)
																	,case(IsDefined(Allocatie), Allocatie)
																	// ,case(IsLU_Landbouw && IsDefined(Allocatie), Allocatie)
																	, gg_prev
																);
	attribute<LU_ModelType> Result_SA          (AdminDomain) := Geography/rdc_meter/IsStudyArea_AdminDomain ? Result : null_u
																	, StorageName = "='%LocalDataProjDir%/Output/'+Casus_name +'/LU_'+Zichtjaar_name+'_'+/ModelParameters/StudyArea+'_LUMT-'+string(#LU_ModelType)+'.tif'", StorageType = "gdalwrite.grid"
																	, Descr = "De cardinaliteit van LU_ModelType wordt meegeschreven in de naam. Want deze wijzigt met de gealloceerde sectoren. Dit is niet fool proof, maar geeft enige indicatie wanneer je dit later inleest. Je kunt immers geen legenda meeschrijven. Het zit wel in de export xml!";

	parameter<String> Result_SA_prev_str := 'Result_SA != PrevIndicatoren/Landgebruikskaart/Result_SA';
	
	attribute<LU_ModelType> Result_bij_tov_PrevYear (AdminDomain) := =IsStartJaar ? 'const(null_u,AdminDomain)' : Result_SA_prev_str +' ? Result_SA : null_u';
	attribute<LU_ModelType> Result_af_tov_PrevYear  (AdminDomain) := =IsStartJaar ? 'const(null_u,AdminDomain)' : Result_SA_prev_str +' ? PrevIndicatoren/Landgebruikskaart/Result_SA : null_u';
	
	attribute<Bool> IsLandbouwInAllocEnNietInResultLU (AdminDomain) := =asList('(Allocatie == LU_ModelType/v/Landbouw_'+/Classifications/Actor/LandbouwKlasses/name+' && Result_SA != LU_ModelType/v/Landbouw_'+/Classifications/Actor/LandbouwKlasses/name+')', '||');
}
