Template Woningwaarde_perWoningType_T //voor verwervingskosten
{
	//
	parameter<WP4> WP4_id;
	//
	
	parameter<string> WP4_name       := WP4/name[WP4_id];
	parameter<string> WP2_name       := WP2/name[WP4/WP2_rel[WP4_id]];
	parameter<bool>   IsDetached     := WP4/IsDetached[WP4_id];
	parameter<bool>   IsSemiDetached := WP4/IsSemiDetached[WP4_id];
	
	parameter<string> CalcStr0: ['(@X@)'];
		
	parameter<string> CalcStr :=
		'	Pand/@T@ > 0[EUR_woning]
				? exp(
					Constant
					+ Pand/Generiek
					+ Pand/@T@
					+ Locatie
					)[EUR_woning]
				: 0[EUR_woning]';
			
	
	attribute<float32> Basisjaar (AdminDomain) := = replace(CalcStr, '@T@', 'Basisjaar');
	
	attribute<float32> Locatie (AdminDomain) := = replace(CalcStr0, '@X@',
		'add(
			log(Src/Locatie/tt_500kinhab_min_2020 / 1[min])      * Src/Prijs/lnreistijd_500k_inw_2020_NL,
			Src/Locatie/uai_2021                        * Src/Prijs/uai_2021,
			log(Src/Locatie/tt_Trainstation_2006_min / 1[min])   * Src/Prijs/reistijd_stations_2006,
			Src/Locatie/bbg_groen_100mbuff[float32] * Src/Prijs/groennabij_2015
		)'
	);
	
	container Pand
	{
		attribute<float32> Generiek (AdminDomain) := = replace(CalcStr0, '@X@',
			'add(
				log(Src/Pand/'+WP4_name+'_lotsize[float32])     * MakeDefined(Src/Prijs/lnlotsize,0f),
				Src/Pand/'+WP4_name+'_d_highrise[float32]       * MakeDefined(Src/Prijs/d_highrise,0f),
				Src/Pand/'+WP4_name+'_nrooms[float32]           * Src/Prijs/nrooms,
				Src/Pand/'+WP4_name+'_nbathrooms[float32]       * Src/Prijs/nbathrooms,
				Src/Pand/'+WP4_name+'_d_maintgood[float32]      * Src/Prijs/d_maintgood,
				Src/Pand/'+WP4_name+'_d_kk[float32]             * Src/Prijs/d_kk,
				Src/Pand/'+WP4_name+'_d_centralheating[float32] * Src/Prijs/d_centralheating,
				Src/Pand/'+WP4_name+'_d_erfpacht[float32]       * Src/Prijs/d_erfpacht,
				Src/Pand/'+WP4_name+'_d_isol_compl[float32]     * Src/Prijs/d_isol_compl,
				Src/Pand/'+WP4_name+'_d_privpark[float32]       * Src/Prijs/d_privpark,
				1f                                          * Src/Prijs/Y'+ModelParameters/NVM_coeff_year+'
			)'
		);
		attribute<float32> Basisjaar (AdminDomain) := = replace(CalcStr0, '@X@', 'log(MakeDefined(SourceData/Vastgoed/BAG/PerJaar/Y'+string(/ModelParameters/BAG_RecentYear)+'/VBO/Per_Gebruiksdoel/woon/Select/Oppervlaktes/per_WP4_AdminDomain/'+WP4_name+' / 1[m2_vbo],1f)) * Src/Prijs/lnsize');
	}
	
	parameter<float32> Constant := = replace(CalcStr0, '@X@', 'Src/Prijs/constant');
	
	container Src
	{
		container Prijs := = 'BaseData/Suitabilities/Wonen/PrijsIndex/ReadCoefficients_WP4/'+WP4_name+'/Result';
		container Pand    := BaseData/Suitabilities/Wonen/RegionalAvgCharacteristics/perWP4/RegionalAvgCharacteristics;
		container Locatie := SourceData/Diversen/per_AdminDomain;
	}
}