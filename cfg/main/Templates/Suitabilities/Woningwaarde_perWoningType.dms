Template Woningwaarde_perWoningType_T 
: Descr = "Bepaal de woningwaarde per woningtype (i.e. WP4) in het basisjaar ten behoeve van de verwervingskosten"
{
	parameter<WP4> WP4_id;
	//
	
	parameter<String> WP4_name       := WP4/name[WP4_id];
	parameter<String> WP2_name       := WP2/name[WP4/WP2_rel[WP4_id]];
	parameter<Bool>   IsDetached     := WP4/IsDetached[WP4_id];
	parameter<Bool>   IsSemiDetached := WP4/IsSemiDetached[WP4_id];
	
	parameter<String> CalcStr0: ['(@X@)'];
		
	parameter<String> CalcStr :=
		'	Pand/@T@ > 0[Eur_Woning]
				? exp(
					Constant
					+ Pand/Generiek
					+ Pand/@T@
					+ Locatie
					)[Eur_Woning]
				: 0[Eur_Woning]';
			
	
	attribute<Float32> Basisjaar (AdminDomain) := = replace(CalcStr, '@T@', 'Basisjaar'), Descr = "De totale bijdrage aan de woningprijs in het basisjaar";
	
	attribute<Float32> Locatie (AdminDomain) := = replace(CalcStr0, '@X@',
		'add(
			log(Src/Locatie/tt_500kinhab_min_2020 / 1[min])      * Src/Prijs/lntt_500k_inw,
			Src/Locatie/UAI_2021                        * Src/Prijs/UAI_2021,
			log(Src/Locatie/tt_Trainstation_2006_min / 1[min])   * Src/Prijs/lntt_stations,
			Src/Locatie/BBG_groen_100mbuff[float32] * Src/Prijs/d_groennabij
		)'
	), Descr = "De locatiegebonden bijdrage aan de woningprijs";
	
	container Pand
	{
		attribute<Float32> Generiek (AdminDomain) := = replace(CalcStr0, '@X@',
			'add(
				log(Src/Pand/'+WP4_name+'_lotsize[float32])     * MakeDefined(Src/Prijs/lnlotsize,0f),
				Src/Pand/'+WP4_name+'_d_highrise[float32]       * MakeDefined(Src/Prijs/d_highrise,0f),
				Src/Pand/'+WP4_name+'_nrooms[float32]            * Src/Prijs/nrooms,
				Src/Pand/'+WP4_name+'_d_maintgood[float32]      * Src/Prijs/d_maintgood,
				1f                                              * Src/Prijs/Y'+ModelParameters/NVM_coeff_Year+'
			)'
		), Descr = "De generieke bijdrage aan de woningprijs op basis van de kenmerken van het woonpand";
		attribute<Float32> Basisjaar (AdminDomain) := = replace(CalcStr0, '@X@', 'log(MakeDefined(SourceData/Vastgoed/BAG/PerJaar/Y'+string(/ModelParameters/BAG_RecentYear)+'/VBO/Per_Gebruiksdoel/woon/Select/Oppervlaktes/per_WP4_AdminDomain/'+WP4_name+' / 1[m2_vbo],1f)) * Src/Prijs/lnsize'), Descr = "De bijdrage aan de woningprijs op basis van de woonoppervlakte van het woonpand";
	}
	
	parameter<Float32> Constant := = replace(CalcStr0, '@X@', 'Src/Prijs/Constant'), Descr = "De constante term in de woningprijsformule";
	
	container Src
	{
		container Prijs := = 'BaseData/Suitabilities/Wonen/PrijsIndex/ReadCoefficients_WP4/'+WP4_name+'/Result', Descr = "De coëfficiënten voor de woningprijsformule voor dit WP4";
		container Pand    := BaseData/Suitabilities/Wonen/RegionalAvgCharacteristics/perWP4/RegionalAvgCharacteristics, Descr = "De gemiddelde kenmerken van woningen per regio voor dit WP4";
		container Locatie := SourceData/Diversen/Per_AdminDomain, Descr = "De locatiekenmerken per AdminDomain";
	}
}