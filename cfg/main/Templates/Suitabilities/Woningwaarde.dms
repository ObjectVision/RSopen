Template Woningwaarde_OP_T
: Descr = "Bepaal de woningwaarde per OP (i.e. nieuwbouw)"
{
	parameter<OP> OP_id;
	//
	
	unit<UInt8> OP := CaseClassifications/Vastgoed/OP;  
	
	parameter<String>     WP2_name                        := OP/WP2_name[OP_id];
	parameter<String>     VSSH_name                       := OP/VSSH_name[OP_id];
	parameter<String>     WP4_name                        := OP/WP4_name[OP_id];
	
	attribute<Eur>        Prijs_OP          (AdminDomain) := OP/Dichtheid[OP_id] * Prijs_perWoning * AdminDomain/NrHaPerCell, Descr = "De totale prijs van alle woningen in deze AdminDomain voor deze OP";
	
	attribute<Eur_Woning> Prijs_perWoning   (AdminDomain) := ='Prijs_perWoning0 * ModelParameters/Wonen/WoningPrijsVerandering_'+ModelParameters/NVM_coeff_Year+'_naar_'+string(ModelParameters/Model_StartYear);
	attribute<Eur_Woning> Prijs_perWoning0  (AdminDomain) := MakeDefined(Prijs_perWoning00, max(Prijs_perWoning00))[Eur_Woning], Descr = "De prijs per woning in deze AdminDomain voor deze OP, met een minimum van de gemiddelde prijs per woning in Nederland voor deze WP4";
	attribute<Float32>    Prijs_perWoning00 (AdminDomain) := =
		replace(
			'(exp('
				'@C@/Constant'
				
//				Woonpand
				'+ '+string(OP/IsHoogbouw[OP_id])+'[float32]                  * MakeDefined(@C@/d_highrise, 0f)' // kenmerk uit ontwikkelpakket
				'+ log('+string(OP/Woonoppervlakte_woning[OP_id])+'[float32]) * @C@/lnsize'                      // kenmerk uit ontwikkelpakket             SIZE == WOONOPP
				'+ log('+string(OP/InDezeBuurt/Tuinoppervlakte[OP_id])+'[float32]) * MakeDefined(@C@/lnlotsize, 0f)'  // kenmerk uit ontwikkelpakket. LOTSIZE == TUIN OPP
				'+ '+string(OP/Aant_kamers[OP_id])+'[float32]                 * @C@/nrooms'                       // idem
				'+ 1f                                                         * @C@/d_maintgood'                 // wordt 100% voorondersteld bij nieuw te bouwen Woningen
				'+ 1f                                                         * @C@/Y'+ModelParameters/NVM_coeff_Year+ // in alle gevallen gaat het om nieuwbouw
				'+ 1f                                                         * @C@/d_constrgt2001'              // in alle gevallen gaat het om nieuwbouw
				
//				Locatie
				'+ '+string(OP/Weging_Amenities[OP_id])+'[float32]  * @LOC@/UAI_2021                                     * @C@/UAI_2021' //wegingsfactor voor amenities uit ontwikkelpakket maal de UAI van de locatie mail de coëfficiënt voor UAI
				'+ '+string(OP/Weging_NatNiv[OP_id])+'[float32]     * log(value(@LOC@/tt_500kinhab_min,float32))    * @C@/lntt_500k_inw' //wegingsfactor voor nationale niveaus (proxy Randstad-NietRandstad) uit ontwikkelpakket maal de reistijd naar 500k inwoners maal de coëfficiënt voor reistijd naar 500k inwoners
				'+ '+string(OP/Weging_NatOV[OP_id])+'[float32]      * log(value(@LOC@/tt_Trainstation_2006_min,float32)) * @C@/lntt_stations' //wegingsfactor voor nationale OV (proxy voor grotere steden) uit ontwikkelpakket maal de reistijd naar het dichtstbijzijnde station maal de coëfficiënt voor reistijd naar station
				'+ '+string(OP/Weging_GroenNabij[OP_id])+'[float32] * value(@LOC@/BBG_groen_100mbuff,float32)            * @C@/d_groennabij' // wegingsfactor voor groen nabij uit ontwikkelpakket maal de hoeveelheid groen binnen 100m buffer maal de coëfficiënt voor groen nabij
				
//				Diff mean (correctie weging aanpassing)
				'- sum((@LOC@/UAI_2021                                     * @C@/UAI_2021                    *('+string(OP/Weging_Amenities[OP_id])+'[float32]  - 1f)) * VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32]) / sum(VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32])'  //diff_mean_amenities
				'- sum((log(value(@LOC@/tt_500kinhab_min,float32))    * @C@/lntt_500k_inw               *('+string(OP/Weging_NatNiv[OP_id])+'[float32]     - 1f)) * VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32]) / sum(VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32])'  //diff_mean_natniv
				'- sum((log(value(@LOC@/tt_Trainstation_2006_min,float32)) * @C@/lntt_stations          *('+string(OP/Weging_NatOV[OP_id])+'[float32]      - 1f)) * VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32]) / sum(VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32])'  //diff_mean_natov
				'- sum((value(@LOC@/BBG_groen_100mbuff,float32)            * @C@/d_groennabij                *('+string(OP/Weging_GroenNabij[OP_id])+'[float32] - 1f)) * VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32]) / sum(VariantData/Zeef/Basisjaar/Wonen/'+WP2_name+'_'+VSSH_name+'/IsBeschikbaar_AdminDomain[float32])'  //diff_mean_groennabij
			'))'
			
			,'@C@',   'BaseData/Suitabilities/Wonen/PrijsIndex/ReadCoefficients_WP4/'+WP4_name+'/Result'
			,'@RAT@', 'BaseData/Suitabilities/Wonen/RegionalAvgCharacteristics/perWP4/RegionalAvgCharacteristics'
			,'@LOC@', 'SourceData/Diversen/Per_AdminDomain'
		);
	
	attribute<Float32> Prijs_perWoning_zWeging  (AdminDomain) := ='Prijs_perWoning_zWeging0 * ModelParameters/Wonen/WoningPrijsVerandering_'+ModelParameters/NVM_coeff_Year+'_naar_'+string(ModelParameters/Model_StartYear);
	attribute<Float32> Prijs_perWoning_zWeging0 (AdminDomain) := =
		replace(
			'(exp('
				'@C@/Constant'
				
//				Woonpand
				'+ '+string(OP/IsHoogbouw[OP_id])+'[float32]                  * MakeDefined(@C@/d_highrise, 0f)' // kenmerk uit ontwikkelpakket
				'+ log('+string(OP/Woonoppervlakte_woning[OP_id])+'[float32])        * @C@/lnsize'     // kenmerk uit ontwikkelpakket
				'+ log('+string(OP/InDezeBuurt/Tuinoppervlakte[OP_id])+'[float32])     * MakeDefined(@C@/lnlotsize, 0f)'  // perceeloppervlak gemiddeld in NL voor betreffend WoningType
				'+ '+string(OP/Aant_kamers[OP_id])+'[float32]                 * @C@/nrooms'     // idem voor aantal kamers etc.
				'+ 1f                                                         * @C@/d_maintgood' // wordt 100% voorondersteld bij nieuw te bouwen Woningen
				'+ 1f                                                         * @C@/Y'+ModelParameters/NVM_coeff_Year+ // in alle gevallen gaat het om nieuwbouw
				'+ 1f                                                         * @C@/d_constrgt2001' // in alle gevallen gaat het om nieuwbouw
				
//				Locatie
				'+ @LOC@/UAI_2021                                             * @C@/UAI_2021'
				'+ log(value(@LOC@/tt_500kinhab_min,float32))            * @C@/lntt_500k_inw'
				'+ log(value(@LOC@/tt_Trainstation_2006_min,float32))         * @C@/lntt_stations'
				'+ value(@LOC@/BBG_groen_100mbuff,float32)                    * @C@/d_groennabij'
				
			'))'
			
			,'@C@',   'BaseData/Suitabilities/Wonen/PrijsIndex/ReadCoefficients_WP4/'+WP4_name+'/Result'
			,'@RAT@', 'BaseData/Suitabilities/Wonen/RegionalAvgCharacteristics/perWP4/RegionalAvgCharacteristics'
			,'@LOC@', 'SourceData/Diversen/Per_AdminDomain'
		);
}