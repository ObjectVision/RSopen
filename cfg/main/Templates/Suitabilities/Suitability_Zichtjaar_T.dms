Template Suitability_Zichtjaar_T
: Descr = "Bepaal de geschiktheid in de zichtjaren voor alle sectoren/subsectoren"
{
	parameter<String> name;
	//
	
	unit<UInt8> OP := CaseClassifications/Vastgoed/OP;
	
	container Wonen := 
		for_each_ne(
			Vastgoed/WP2xVSSH/name
			, 'Suitability_Wonen_perSubsector_T('+quote(Vastgoed/WP2xVSSH/name)+')'
		), Descr = "Bepaal de geschiktheid voor wonen per subsector in de zichtjaren, op basis van de geschiktheid per OP"
	{
		container perOP := 
			for_each_nedv(
				OP/name
				, 'Wonen/'+OP/WP2xVSSH_name+'/perOP/'+OP/name+'/Totaal'
				, CompactedAllocDomain
				, float32
			), Descr = "De geschiktheid voor wonen per OP";
	}
	
	container Werken :=
		for_each_nedv(
			Actor/Jobs6/name
			, 'collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, BaseData/Suitabilities/Werken/ProbabilityMap_perSector/'+Actor/Jobs6/name+'/exponentiated)'
			, CompactedAllocDomain
			, float32
		), Descr = "Geschiktheid voor werken per sector";
	
	container Verblijfsrecreatie
	{
		attribute<Float32> Totaal (CompactedAllocDomain):= collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, BaseData/Suitabilities/Verblijfsrecreatie/ProbabilityMap_Verblijfsrecreatie/Variant_Dist_1000m_Neighbourhood/exponentiated), Descr = "Geschiktheid voor verblijfsrecreatie in compacted alloc dommain. Obv probability map uit studie VU.";
	}
	
	container Wind
	{
		attribute<Float32> Totaal (CompactedAllocDomain):= collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, SourceData/Diversen/Per_AllocDomain/BelevingLandschap/Inverse), Descr = "Geschiktheid voor wind op basis van beleving landschap. Inverse, dus hoe lager de beleving hoe hoger de geschiktheid.";
	}
	
	container Zon
	{
		attribute<Float32> Totaal (CompactedAllocDomain):= collect_by_cond(CompactedAllocDomain, AllocDomain/IsCompactedDomain, SourceData/Diversen/Per_AllocDomain/BelevingLandschap/Inverse), Descr = "Geschiktheid voor zon op basis van beleving landschap. Inverse, dus hoe lager de beleving hoe hoger de geschiktheid.";
	}
	
	container Landbouw := ='Templates/Landbouw/Suitability_T('+quote(name)+', /VariantParameters/VariantK/Waterbeheeroptie[/Variant_rel])', Descr = "Bepaal de geschiktheid voor landbouw in de zichtjaren";
}