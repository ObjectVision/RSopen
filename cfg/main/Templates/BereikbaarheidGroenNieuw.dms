Template BereikbaarheidGroenNieuw : using = "Classifications/Actor"
{
	//
	container SrcSrc;
	parameter<String> Ref;
	//
	
	container Src := .;
	
	parameter<Woning_ha> MaxWoningenInGroen                 := 5[Woning_ha];
	parameter<ha>        MinimumAaneengeslotenOppervlakte   := 1[Ha];
	parameter<meter2>    MinimumOppervlakteInCelOmMeeTeDoen := 100[meter2];

	attribute<GroenK> Groen           (AdminDomain) := SrcSrc/Landgebruikskaart/GroenInZichtjaar; // Groen obv CBS BBG
	attribute<bool>   Range           (AdminDomain) := =  Ref == 'Groen/InZichtjaar' ? 'IsDefined(Stand/subsector_rel)' : 'const(true, AdminDomain)';
	attribute<Woning> Woningen        (AdminDomain) := SrcSrc/Stand/Aantal_Woningen_Totaal[Woning];
	attribute<Woning> Woningen_range  (AdminDomain) := Woningen * float32(Range);

	unit<UInt32> Regio  := SourceData/RegioIndelingen/Provincie;
	unit<uint8>  GroenK := /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen;
	
	unit<UInt8> Indicator : NrOfRows = 3
	{
		attribute<String> name:
		[
			  'Aanwezig'    //Oppervlakte groen waar groen aanwezig is
			 ,'Beschikbaar' //Oppervlakte groen nabij woningen
			 ,'Bereikbaar'  //
		];
		
		attribute<String> GroenSrc:
		[
			  '1f *  Src/OppGroenBinnenExtent'                    // Cellen waar groen aanwezig is
			, '(AantalWoningenInOmgeving > 0[Woning])[float32] * AdminDomain/NrHaPerCell[meter2]'   // Groen nabij woningen
			,'1f * OppGroenInOmgeving'                            // 
		];
	}
	
	container PerRegios := for_each_ne(Regio/name, 'Per_Regio_T(Regio/per_AdminDomain == Regio/V/'+Regio/name+', Woningen_range, Src)')
	{
		container Totaal := Per_Regio_T(const(true, AdminDomain), Woningen_range, Src);
	}
	
	Template Per_Regio_T // fka T1
	{
		//
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		container Afstanden :=
			for_each_ne(
				Bereikbaarheid/AfstandGroen/name
				, 'Per_Afstand_T(
						'+string(id(Bereikbaarheid/AfstandGroen))+'[Bereikbaarheid/AfstandGroen]
						, Regio
						, Woningen
						, Src
					)'
			);
	}
	
	Template Per_Afstand_T
	{
		//
		parameter<Bereikbaarheid/AfstandGroen> id;
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		container AggrGroenKlassen := for_each_ne(GroenK/name, 'Per_GroenK_T(Groen == GroenK/V/'+GroenK/name+', id, Regio, Woningen, Src)'), Descr = "Nieuwe indicator obv BGT"
		{
			container Totaal := Per_GroenK_T(IsDefined(Groen), id, Regio, Woningen, Src);
			container TotaalExAgrarisch := Per_GroenK_T(IsDefined(Src/Groen) && !(Src/Groen == GroenK/V/overige_agrarisch_gebruik), id, Regio, Woningen, Src);
		}
	}
	
	Template Per_GroenK_T 
	{
		//
		attribute<bool> IsGroenKlasse (AdminDomain);
		parameter<Bereikbaarheid/AfstandGroen> AfstandGroen_id;
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		parameter<meter> AfstandGroen_Bovengrens := Bereikbaarheid/AfstandGroen/Bovengrens[AfstandGroen_id];
		attribute<Bool>  Extent0 (AdminDomain) := Woningen <= Src/MaxWoningenInGroen * AdminDomain/NrHaPerCell 
													&& float32(IsGroenKlasse) * AdminDomain/NrHaPerCell[meter2] > MinimumOppervlakteInCelOmMeeTeDoen, Descr = "Initiele groen extent: opp groen per cell boven grens EN minder woningen in cel dan grens";
		
		unit<UInt32> ExtentDistrict := district(uint32(Extent0)), Descr = "Zoek aaneengelsoten stukken groen"
		{
			attribute<meter2> Oppervlakte := sum(float32(Extent0) *AdminDomain/NrHaPerCell[meter2], Districts);
		}
		
		attribute<Bool>   Extent               (AdminDomain) := Extent0 && ExtentDistrict/Oppervlakte[ExtentDistrict/Districts] >= Src/MinimumAaneengeslotenOppervlakte[meter2], Descr = "Groen extent: Initiele extent EN aangesloten groen opp boven grens.";
		attribute<meter2> OppGroenBinnenExtent (AdminDomain) := float32(Extent) * AdminDomain/NrHaPerCell[meter2];
		
		attribute<Woning> AantalWoningenInOmgeving (AdminDomain) := //sommeert aantal woningen binnen range
			= 'potential(
				Woningen
				,Geography/Distmatrices/Impl_25m/pot'+string(AfstandGroen_Bovengrens)+'m/PotRange/Flat2
			)[Woning]';
		
		attribute<Meter2> OppGroenInOmgeving (AdminDomain) := //sommeert oppervlakte groen binnen range
			= 'potential(
				OppGroenBinnenExtent
				,Geography/Distmatrices/Impl_25m/pot'+string(AfstandGroen_Bovengrens)+'m/PotRange/Flat2
			)[meter2]';
			
		
		attribute<m2_woning> OppGroen_perWoning (AdminDomain) := OppGroenInOmgeving / Woningen;
		
		container Indicatoren := for_each_ne(Indicator/name, 'Indicatoren_T('+quote(Indicator/GroenSrc)+', ..)');
	}
	
	Template Indicatoren_T // fka T4
	{
		//
		parameter<String> GroenSrc;
		container Src;
		//
		
		attribute<Meter2> Groen (AdminDomain) := = GroenSrc
		{
			parameter<Meter2> Aggregated := sum(.)
			{
				parameter<m2_Woning> PerWoning := . / sum(Src/Woningen);
			}
		}
	}
}