Template BereikbaarheidGroen : using = "Classifications/Actor"
{
	//
	container SrcSrc;
	parameter<String> Ref;
	//
	
	container Src := .;
	
	parameter<Woning_ha> MaxWoningenInGroen                 := 5[Woning_ha];
	parameter<ha>        MinimumAaneengeslotenOppervlakte   := 1[Ha];
	attribute<GroenK>    Groen                (AdminDomain) := SrcSrc/Landgebruikskaart/GroenInZichtjaar; // Groen obv CBS BBG
	attribute<bool>      Range                (AdminDomain) := =Ref == 'Groen/InZichtjaar' ? 'IsDefined(Stand/subsector_rel)' : 'const(true, AdminDomain)';
	attribute<Woning>    Woningen             (AdminDomain) := SrcSrc/Stand/Aantal_Woningen_Totaal[Woning];
	attribute<Woning>    Woningen_range       (AdminDomain) := Woningen * float32(Range);
	
	unit<UInt32> Regio := SourceData/RegioIndelingen/Provincie;
	unit<uint8> GroenK := /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen;
	
	unit<uint8> Indicator : NrOfRows = 2 
	{
		attribute<string> name: 
		[
			 'Beschikbaar'  // Gemiddelde oppervlakte groen binnen gegeven afstand van een woning
			,'Bruikbaar'    // Gemiddelde oppervlakte groen binnen gegeven afstand van een woning, gedeeld door het aantal woningen. Dus rekeninghoudend met 'drukte'.
		];
		
		attribute<string> IndicatorExpr : 
		[
			 'Som_GroenInOmgeving_metWoning'                          //Hoeveelheid m2 groenklasse (e.g. Park) binnen x meter op locaties waar woningen zijn.
			,'Som_GroenInOmgeving_metWoning / Som_WoningenInOmgeving' //Hoeveelheid m2 groenklasse (e.g. Park) per woning binnen x meter 
		];
		
		attribute<string> ValuesUnit : 
		[
			 'meter2' 
			,'m2_Woning' 
		];
	}
	
	container PerRegios := for_each_ne(Regio/name, 'Per_Regio_T(Regio/per_AdminDomain == Regio/V/'+Regio/name+', Woningen_range, Src)')
	{
		container Totaal := Per_Regio_T(const(true, AdminDomain), Woningen_range, Src);
	}
	
	Template Per_Regio_T 
	{
		attribute<bool>   IsRegio  (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container         Src;
		//
		
		container Afstanden := 
			for_each_ne(
				Bereikbaarheid/AfstandGroen/name
				, 'Per_Afstand_T(
						'+string(id(Bereikbaarheid/AfstandGroen))+'[Bereikbaarheid/AfstandGroen]
						, IsRegio
						, Woningen
						, Src
					)'
			);
	}
	
	Template Per_Afstand_T 
	{
		parameter<Bereikbaarheid/AfstandGroen> id;
		attribute<bool>                        IsRegio    (AdminDomain);
		attribute<Woning>                      Woningen (AdminDomain);
		container                              Src;
		//
		
		container AggrGroenKlassen := 
			for_each_ne(
				GroenK/name
				, 'Per_GroenK_T(
					Groen == GroenK/V/'+GroenK/name+'
					, id
					, IsRegio
					, Woningen
					, Src
				)'
			)
		{
			container Totaal            := Per_GroenK_T(IsDefined(Groen), id, IsRegio, Woningen, Src);
			container TotaalExAgrarisch := Per_GroenK_T(IsDefined(Src/Groen) && !(Src/Groen == GroenK/V/overige_agrarisch_gebruik), id, IsRegio, Woningen, Src);
		}
	}
	
	Template Per_GroenK_T 
	{ 
		attribute<bool>                        IsGroenKlasse                 (AdminDomain);
		parameter<Bereikbaarheid/AfstandGroen> AfstandGroen_id;
		attribute<bool>                        IsRegio                       (AdminDomain);
		attribute<Woning>                      Woningen                      (AdminDomain);
		container                              Src;
		//
		
		parameter<meter>                       Bovengrens                                  := Bereikbaarheid/AfstandGroen/Bovengrens[AfstandGroen_id], Descr = "Bovengrens afstandsklasse, i.e. tot hoever kijken we?";
		attribute<Woning>                      WoningenInThisStRegio         (AdminDomain) := Woningen * IsRegio[float32], Descr = "Aantal woningen per cel, geclipt op regiogrenzen.";
		attribute<bool>                        Extent0                       (AdminDomain) := Woningen <= Src/MaxWoningenInGroen * AdminDomain/NrHaPerCell && IsGroenKlasse && IsRegio, Descr = "Cellen groen, die ook niet teveel woningen bevatten, en in de regio liggen.";
		
		unit<uint32> ExtentDistrict := district_8(uint32(Extent0))
		, Descr = "vind aaneengesloten cellen groen."
		{
			attribute<ha> Oppervlakte := pcount(Districts)[float32] * AdminDomain/NrHaPerCell, Descr = "Wat is de oppervlakte van zo'n aaneengesloten groep groencellen?";
		}
		
		attribute<bool>                        Extent                        (AdminDomain) := Extent0 && ExtentDistrict/Oppervlakte[ExtentDistrict/Districts] >= Src/MinimumAaneengeslotenOppervlakte, Descr = "Cellen groen, die ook een minimale aaneengesloten oppervlakte hebben.";
		attribute<meter2>                      RelevantGroen                 (AdminDomain) := float32(IsGroenKlasse) * float32(Extent) * AdminDomain/NrMeter2PerCell, Descr = "Oppervlakte groen van alleen relevante groencellen.";
		
		attribute<Woning>                      Som_WoningenInOmgeving        (AdminDomain) := ='potential(WoningenInThisStRegio, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[Woning]', Descr = "Per cel de sommatie van het aantal woningen binnen de gegeven afstandsgrens.";
		attribute<meter2>                      Som_GroenInOmgeving           (AdminDomain) := ='potential(RelevantGroen, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[meter2]', Descr = "Per cel de sommatie van de hoeveelheid groen binnen de gegeven afstandsgrens.";
		attribute<meter2>                      Som_GroenInOmgeving_metWoning (AdminDomain) := Som_GroenInOmgeving * float32(WoningenInThisStRegio > 0[woning]), Descr = "Alleen cellen waar ook daadwerkelijk woningen staan, zijn relevant voor de indicator berekeningen.";
		
		container Indicatoren := for_each_ne(Indicator/name, 'Indicatoren_T('+quote(Indicator/IndicatorExpr)+','+quote(Indicator/ValuesUnit)+', ..)');
	}

	Template Indicatoren_T 
	{
		parameter<string> IndicatorExpr;
		parameter<string> ValuesUnit0;
		container         Src;
		//
		unit<float32> ValuesUnit :=  =ValuesUnit0;
		
		attribute<ValuesUnit>   Indicator        (AdminDomain) := =IndicatorExpr, Descr = "Berekende indicator waarde";
		parameter<ValuesUnit>   Indicator_Aggregated           := mean(Indicator > 0[ValuesUnit] ? Indicator : null_f), Descr = "Geaggregeerde indicator over studiegebied.";
	}
}
