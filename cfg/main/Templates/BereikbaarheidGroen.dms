Template BereikbaarheidGroen : using = "Classifications/Actor"
{
	//
	container SrcSrc;
	parameter<String> Ref;
	//
	
	container Src := .;
	
	parameter<Woning_ha> MaxWoningenInGroen                 := 5[Woning_ha];
	parameter<ha>        MinimumAaneengeslotenOppervlakte   := 1[Ha];
	attribute<GroenK> Groen           (AdminDomain) := SrcSrc/Landgebruikskaart/GroenInZichtjaar; // Groen obv CBS BBG
	attribute<bool>   Range           (AdminDomain) := =  Ref == 'Groen/InZichtjaar' ? 'IsDefined(Stand/subsector_rel)' : 'const(true, AdminDomain)';
	attribute<Woning> Woningen        (AdminDomain) := SrcSrc/Stand/Aantal_Woningen_Totaal[Woning];
	attribute<Woning> Woningen_range  (AdminDomain) := Woningen * float32(Range);
	
	unit<UInt32> Regio := SourceData/RegioIndelingen/Provincie;
	unit<uint8> GroenK := /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen;
	
	unit<uint8> Indicator : NrOfRows = 3  //uit RVNL
	{
		attribute<string> name: 
		[
			 'Aanwezig'     // Opprvlakte groen (per woning) in totale regio. Dus simpelweg het totale areaal groen delen op het totale aantal woningen in de regio
			,'Beschikbaar'  // Zelfde maar dan telt alleen groen binnen gegeven afstand van tenminste 1 woning mee. Perifeer groen telt dus niet mee.
			,'Bereikbaar'   // Gemiddelde oppervlakte groen binnen gegeven afstand van een woning
		];
		
		attribute<string> GroenSrc : 
		[
			'float32(Src/Regio)'
			,'(Potentiaal > 0[Woning])[float32]'
			,'Potentiaal[float32]'
		];
	}
	
	container PerRegios := for_each_ne(Regio/name, 'Per_Regio_T(Regio/per_AdminDomain == Regio/V/'+Regio/name+', Woningen_range, Src)')
	{
		container Totaal := Per_Regio_T(const(true, AdminDomain), Woningen_range, Src);
	}
	
	Template Per_Regio_T // fka T1
	{
		//
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		container Afstanden := 
			for_each_ne(
				Bereikbaarheid/AfstandGroen/name
				, 'Per_Afstand_T_oud(
						'+string(id(Bereikbaarheid/AfstandGroen))+'[Bereikbaarheid/AfstandGroen]
						, Regio
						, Woningen
						, Src
					)'
			);
	}
	
	Template Per_Afstand_T_oud //fka T2
	{
		//
		parameter<Bereikbaarheid/AfstandGroen> id;
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		container AggrGroenKlassen := for_each_ne(GroenK/name, 'Per_GroenK_T(Groen == GroenK/V/'+GroenK/name+', id, Regio, Woningen, Src)')
		{
			container Totaal := Per_GroenK_T(IsDefined(Groen), id, Regio, Woningen, Src);
			container TotaalExAgrarisch := Per_GroenK_T(IsDefined(Src/Groen) && !(Src/Groen == GroenK/V/overige_agrarisch_gebruik), id, Regio, Woningen, Src);
		}
	}
	
	Template Per_GroenK_T  // fka T3
	{
		//
		attribute<bool> IsGroenKlasse (AdminDomain);
		parameter<Bereikbaarheid/AfstandGroen> AfstandGroen_id;
		attribute<bool> Regio (AdminDomain);
		attribute<Woning> Woningen (AdminDomain);
		container Src;
		//
		
		parameter<meter> Bovengrens := Bereikbaarheid/AfstandGroen/Bovengrens[AfstandGroen_id];
		
		attribute<Woning> WoningenInThisStRegio (AdminDomain) := Woningen * Regio[float32]
		{
			parameter<Woning> Aggregated := sum(.);
		}
		
		attribute<bool> Extent0 (AdminDomain) := Woningen <= Src/MaxWoningenInGroen * AdminDomain/NrHaPerCell && IsGroenKlasse;
		
		unit<uint32> ExtentDistrict := district(uint32(Extent0))
		{
			attribute<.> per_AdminDomain (AdminDomain):= Districts;
			attribute<ha> Oppervlakte := pcount(per_AdminDomain)[float32] * AdminDomain/NrHaPerCell;
		}
		
		attribute<bool> Extent (AdminDomain) := Extent0 && ExtentDistrict/Oppervlakte[ExtentDistrict/per_AdminDomain] >= Src/MinimumAaneengeslotenOppervlakte;
		
		attribute<Woning> Potentiaal (AdminDomain) := = 'potential(WoningenInThisStRegio, geography/Distmatrices/Impl_25m/pot'+string(Bovengrens)+'m/potrange/Flat2)[Woning]';
		
		container Indicatoren := for_each_ne(Indicator/name, 'Indicatoren_T_oud('+quote(Indicator/GroenSrc)+', ..)');
	}

	Template Indicatoren_T_oud //fka T4
	{
		//
		parameter<string> GroenSrc;
		container Src;
		//
		
		attribute<ha> Groen (AdminDomain) := = GroenSrc+' * Src/Extent[float32] * AdminDomain/NrHaPerCell'
		{
			parameter<ha> Aggregated := sum(.)
			{
				parameter<m2_Woning> PerWoning := .[meter2] / Src/WoningenInThisStRegio/Aggregated;
			}
		}
	}
}