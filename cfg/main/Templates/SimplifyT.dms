Template simplifyT
{
	unit<uint32> Iteratie_domain;
	parameter<uint32> nrIteRations;
	
	unit<uint32> Iter := range(Uint32, 0, nrIteRations)
	{
		attribute<string> name := 'I'+string(id(.));
		attribute<bool>   IsFirstIter := id(.) == 0;
		
		attribute<string> PrevName   := IsFirstIter ? 'StartingState' : name[id(.) - min_elem( id(.), 1)];
		attribute<string> PrevState  := IsFirstIter ? 'StateVoorAllocatie' : PrevName+'/StateNaAllocatie';
	}
	
	container StartingState 
	{
		unit<uint32> NextValue := sequence2points(Iteratie_domain/geometry);
	}
	
	container Iters := for_each_ne(Iter/name, 'IterT('+Iter/PrevName+', Iteratie_domain)');
	container LastIter := =last('Iters/'+Iter/name);
	
	template IterT
	{
		container PrevIterator;
		unit<uint32> domain;
		
		unit<uint32>  PrevValue := PrevIterator/NextValue;
		
		container _ConvexHullT := MakeConvexHullT(domain, PrevValue);
		unit<uint32>  NextValue := _ConvexHullT/sequence/Convex_hull_points;
	}
	
	container MakeFinal 
	{
		unit<uint32> Domain_with_endpoints := union_unit(Iteratie_domain, LastIter/NextValue)
		{
			attribute<rdc_meter>  point      := union_data(., LastIter/NextValue/point, first(LastIter/NextValue/point, LastIter/NextValue/Sequence_rel));
			attribute<uint32>     Sequence_rel := union_data(., LastIter/NextValue/Sequence_rel, id(Iteratie_domain));
		}
		attribute<rdc_meter>  convex_hull (Iteratie_domain, poly) := points2sequence(Domain_with_endpoints/point, Domain_with_endpoints/Sequence_rel);
	}
	
	Template MakeConvexHullT
	{
		//
		unit<uint32> domain;
		unit<uint32> seq;
		//
		
		unit<uint32> sequence := seq
		{
			attribute<rdc_meter> point := seq/point;
			attribute<uint32> Sequence_rel := seq/Sequence_rel;
			
			attribute<uint32> min_index (domain) := min_index(id(.), Sequence_rel);
			attribute<uint32> max_index (domain) := max_index(id(.), Sequence_rel);
			
			attribute<bool> IsFirst := id(.) == min_index[Sequence_rel];
			attribute<bool> IsLast  := id(.) == max_index[Sequence_rel];
			attribute<uint32> prev_id := IsFirst ? rjoin(Sequence_rel, id(domain), max_index) : id(.) - min_elem( id(.), 1);   
			attribute<uint32> next_id := Islast  ? rjoin(Sequence_rel, id(domain), min_index) : id(.) + 1;
			attribute<rdc_meter> A := point[prev_id];
			attribute<rdc_meter> B := point;
			attribute<rdc_meter> C := point[next_id];
			attribute<rdc_meter> p := B - A;
			attribute<rdc_meter> q := C - B;
			
			attribute<float64> Px := PointCol(P);
			attribute<float64> Py := PointRow(P);
			attribute<float64> Qx := PointCol(Q);
			attribute<float64> Qy := PointRow(Q);
			
			attribute<float64> det := Px*Qy - Py*Qx;
			
			unit<uint32> Convex_hull_points := select_with_org_rel(det <= 0d && (point != rjoin(prev_id, id(.), point)))
			{
				attribute<rdc_meter> point := ../point[org_rel];
				attribute<uint32> Sequence_rel := ../Sequence_rel[org_rel];
			}
		}
	}
}