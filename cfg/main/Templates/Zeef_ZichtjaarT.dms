Template Zeef_ZichtjaarT
{
	//
	parameter<string> Zichtjaar_name;
	container Zeef;
	container Dichtheid;
	container Geschiktheid;
	//
	
	container SectorxSubsectoren :=
		for_each_ne(
			CaseClassifications/Actor/VariantXSectorXSubsector/combi_name,
			'Templates/Zeef(
				CaseClassifications/Actor/VariantXSectorXSubsector,'+
				string(id(CaseClassifications/Actor/VariantXSectorXSubsector))+'[CaseClassifications/Actor/VariantXSectorXSubsector],
				Analysis,
				Zichtjaar_name
			)'
		);
		
	container Wonen := for_each_ne(
		/Classifications/Actor/Sector/XSubSectoren/Wonen/subsector/subsector_name
		, 'Wonen_perOP_Zeef_T('+
			quote(/Classifications/Actor/Sector/XSubSectoren/Wonen/subsector/subsector_name)+', '+
			quote(Zichtjaar_name)+',
			Zeef,
			Dichtheid,
			Geschiktheid
		)'
	);
	
	Template Wonen_perOP_Zeef_T
	{
		parameter<string> Subsector_name;
		parameter<string> Zichtjaar_name;
		container Zeef;
		container Dichtheid;
		container Geschiktheid;
		//
		
		unit<uint8> OP_sub := ='CaseClassifications/Vastgoed/OP/perWP2xVSSH/'+Subsector_name+'/OP_sub';
		
		container Zeef_perOP := for_each_ne(
			OP_sub/name
			,'Zeef_perOP_T(
				  Subsector_name
				, '+quote(OP_sub/name)+'
				, '+quote(OP_sub/WP2xVSSH_name)+'
				, Zichtjaar_name
				, Zeef/'+Zichtjaar_name+'/SectorXSubsectoren/Wonen/'+Subsector_name+'/Beschikbaar
				, '+string(uint32(OP_sub/IsHoogbouw))+'[bool]
				, OP_sub/Dichtheid['+string(id(OP_sub))+'[OP_sub]]
				, Dichtheid
				, Geschiktheid
			)'
		);
		
		Template Zeef_perOP_T
		{
			//
			parameter<string> Subsector_name;
			parameter<string> OP_name;
			parameter<string> WP2xVSSH_name;
			parameter<string> Zichtjaar_name;
			
			attribute<bool> Beschikbaar_src (CompactedAdminDomain);
			parameter<bool> HoogbouwOP;
			
			parameter<Woning_ha> Dichtheid_ha;
			
			container Dichtheid;
			container Geschiktheid;
			//
			
			parameter<bool>      HoogbouwToegestaanInMooiLandschap     := ='VariantParameters/VariantK/HoogbouwToegestaanInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<bool>      HoogbouwToegestaanInZeerMooiLandschap := ='VariantParameters/VariantK/HoogbouwToegestaanInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<bool>      TinyHousesToegestaan                  := ='VariantParameters/VariantK/TinyHousesToegestaan[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<bool>      SuperStedelijkToegestaan              := ='VariantParameters/VariantK/SuperStedelijkToegestaan[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<Woning_ha> MaxDichtheidInMooiLandschap           := ='VariantParameters/VariantK/MaxDichtheidInMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<Woning_ha> MaxDichtheidInZeerMooiLandschap       := ='VariantParameters/VariantK/MaxDichtheidInZeerMooiLandschap[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<float32>   MinimumDichtheidToenameWonen          := ='VariantParameters/VariantK/MinimumDichtheidToenameWonen[VariantParameters/VariantK/V/'+Variant_name+']';
			parameter<Eur>       MinimumExploitatieSaldo               := ='VariantParameters/VariantK/MinimumExploitatieSaldo[VariantParameters/VariantK/V/'+Variant_name+']';
			
			attribute<Woning> PotentieleState (CompactedAllocDomain) := sum(Dichtheid_ha * AdminDomain/NrHaPerCell / float32(Beschikbaar_src), CompactedAdminDomain/CompactedAllocDomain_rel);
			attribute<Woning> StateBasisjaar  (CompactedAllocDomain) := sum(select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/StateBasisjaar/Src0/Wonen/WP1s/Totaal) / float32(Beschikbaar_src), CompactedAdminDomain/CompactedAllocDomain_rel);
			
			container Impl
			{
				//resultaat van deze items is waar het NIET is toegestaan
				attribute<bool> HoogbouwToegestaanInMooiLandschap     (CompactedAdminDomain) := HoogbouwOP && !../HoogbouwToegestaanInMooiLandschap ? select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/BelevingLandschap/IsHoog[/AdminDomain/AllocDomain_rel]) : FALSE; //hoogbouw in OP? && Is toegestaan? && Is mooilandschap?
				attribute<bool> HoogbouwToegestaanInZeerMooiLandschap (CompactedAdminDomain) := HoogbouwOP && !../HoogbouwToegestaanInZeerMooiLandschap ? select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/BelevingLandschap/IsZeerHoog[/AdminDomain/AllocDomain_rel]) : FALSE; //hoogbouw in OP? && Is toegestaan? && Is mooilandschap?
				attribute<bool> TinyHousesToegestaan                  (CompactedAdminDomain) := const(!../TinyHousesToegestaan     && (OP_name == 'TinyHousesVS'     || OP_name == 'TinyHousesLaagVS'), CompactedAdminDomain);
				attribute<bool> SuperStedelijkToegestaan              (CompactedAdminDomain) := const(!../SuperStedelijkToegestaan && (OP_name == 'SuperStedelijkVS' || OP_name == 'SuperStedelijkSH'), CompactedAdminDomain);
				attribute<bool> MaxDichtheid                          (CompactedAdminDomain) := = 'Dichtheid_ha > select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, Analysis/MaxWoningDichtheid/'+WP2xVSSH_name+'/Result[AdminDomain/AllocDomain_rel])';
				attribute<bool> MaxDichtheidInMooiLandschap           (CompactedAdminDomain) := Dichtheid_ha > ../MaxDichtheidInMooiLandschap     && select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/BelevingLandschap/IsHoog[AdminDomain/AllocDomain_rel]);
				attribute<bool> MaxDichtheidInZeerMooiLandschap       (CompactedAdminDomain) := Dichtheid_ha > ../MaxDichtheidInZeerMooiLandschap && select_data(CompactedAdminDomain, AdminDomain/IsCompactedDomain, BaseData/BelevingLandschap/IsZeerHoog[AdminDomain/AllocDomain_rel]);
				
				attribute<bool> MinimumDichtheidToenameWonen (CompactedAdminDomain) := ((PotentieleState - StateBasisjaar)[float32] / StateBasisjaar[float32] <= ../MinimumDichtheidToenameWonen)[CompactedAdminDomain/CompactedAllocDomain_rel];
				attribute<bool> MinimumExploitatieSaldo      (CompactedAdminDomain) := = '(Geschiktheid/'+Zichtjaar_name+'/Wonen/'+Subsector_name+'/perOP/'+OP_name+'/Totaal < ../MinimumExploitatieSaldo[float32])[CompactedAdminDomain/CompactedAllocDomain_rel]';
			}
			
			attribute<bool> Result (CompactedAdminDomain) :=
				OR(
					Impl/HoogbouwToegestaanInMooiLandschap,
					Impl/HoogbouwToegestaanInZeerMooiLandschap,
					Impl/TinyHousesToegestaan,
					Impl/SuperStedelijkToegestaan,
					Impl/MaxDichtheid,
					Impl/MaxDichtheidInMooiLandschap,
					Impl/MaxDichtheidInZeerMooiLandschap,
					Impl/MinimumDichtheidToenameWonen,
					Impl/MinimumExploitatieSaldo
				);
			
			attribute<bool> Beschikbaar_voorOP (CompactedAdminDomain) := !Result && Beschikbaar_src;
		}
	}
}