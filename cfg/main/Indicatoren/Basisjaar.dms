container Basisjaar 
{
	parameter<string> Zichtjaar             := 'Basisjaar';
	
	container         ThisYear              := .;
	
	unit<UInt32>      Subsector             := Classifications/Actor/Sector/xSubsector;
	unit<UInt8>       OP                    := CaseClassifications/Vastgoed/OP;
	unit<UInt8>       WP2                   := Classifications/Vastgoed/WP2;
	unit<UInt8>       WP4                   := Classifications/Vastgoed/WP4;
	unit<UInt8>       WP2xVSSH              := Classifications/Vastgoed/WP2xVSSH;
	unit<UInt8>       vbo_gebruiksdoel_plus := Classifications/Vastgoed/vbo_gebruiksdoel_plus;
	
	unit<UInt32> StandVar  := CaseClassifications/StandVar
	{
		attribute<String> quantity_name := is_quantity ? name : '';
		unit<UInt8> OP := CaseClassifications/StandVar/OP;
	}
	
	container Stand :=
		for_each_nedvn(
			CaseClassifications/StandVar_Prep/name
			, 'BaseData/StartState/StateBasisjaar/State/'+CaseClassifications/StandVar_Prep/path
			, AdminDomain
			, CaseClassifications/StandVar_Prep
			, CaseClassifications/StandVar_Prep/unit_name
		)
	{
		attribute<StandVar/OP>             OP_rel            (AdminDomain) := const(value(0/0, StandVar/OP), AdminDomain);
		attribute<Actor/Sector/xSubsector> SubSector_rel     (AdminDomain) := const(value(0/0, Actor/Sector/xSubsector), AdminDomain);
		attribute<Actor/Sector>            Sector_rel        (AdminDomain) := Actor/Sector/XSubsector/Sector_rel[Subsector_rel];
		attribute<Woning> Aantal_Woningen_Totaal             (AdminDomain) := = 'add('+asItemList('Aantal_Woningen_'+/Classifications/Vastgoed/WP2xVSSH/name)+')';
		attribute<Job>    Aantal_Banen_Totaal                (AdminDomain) := = 'add('+asItemList('Banen_'+/Classifications/Actor/Jobs6/name)+')';
		attribute<meter2> m2PandFootprint_Totaal             (AdminDomain) := ='add('+asItemList('m2PandFootprint_'+/Classifications/Actor/Jobs6/name)+')';
		
		attribute<meter2> Gebruiksopp_Eengezins              (AdminDomain) := recollect_by_cond(AdminDomain/IsCompactedDomain, Gebruiksoppervlak/per_WP2_perAdminDomain/Eengezins);
		attribute<meter2> Gebruiksopp_Meergezins             (AdminDomain) := recollect_by_cond(AdminDomain/IsCompactedDomain, Gebruiksoppervlak/per_WP2_perAdminDomain/Meergezins);
		attribute<meter2> Gebruiksopp_Nijverheid             (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Nijverheid;
		attribute<meter2> Gebruiksopp_Logistiek              (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Logistiek;
		attribute<meter2> Gebruiksopp_Detailhandel           (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Detailhandel;
		attribute<meter2> Gebruiksopp_Ov_consumentendiensten (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Ov_consumentendiensten;
		attribute<meter2> Gebruiksopp_Zak_dienstverlening    (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Zak_dienstverlening;
		attribute<meter2> Gebruiksopp_Overheid_kw_diensten   (AdminDomain) := Gebruiksoppervlak/per_Jobs6_perAdminDomain/Overheid_kw_diensten;
		
		attribute<meter2> PandFootprint_Wonen                (AdminDomain) := BaseData/StartState/StateBasisjaar/State/Wonen/Footprint;
		attribute<meter2> PandFootprint_Werken               (AdminDomain) := BaseData/StartState/StateBasisjaar/State/PandFootprint/Totaal;
		attribute<meter2> PandFootprint_Verblijfsrecreatie   (AdminDomain) := BaseData/StartState/StateBasisjaar/State/Verblijfsrecreatie/Footprint;
		
		container Aantal_Woningen_WP4   := BaseData/StartState/StateBasisjaar/Src0/Wonen/WP4;
		container Gebruiksoppervlak_WP4 := BaseData/StartState/StateBasisjaar/Src0/Wonen/WP4_opp;
	}
	
	container Stand_PerAllocRegio :=
		for_each_nedvn(
			CaseClassifications/StandVar_Prep/name
			, 'sum((Stand/'+CaseClassifications/StandVar_Prep/name+'), SourceData/RegioIndelingen/NVM/Per_AdminDomain)'
			, SourceData/RegioIndelingen/NVM
			, CaseClassifications/StandVar_Prep
			, CaseClassifications/StandVar_Prep/unit_name
		);
	container Stand_metBAGnb_PerAllocRegio :=
		for_each_nedvn(
			CaseClassifications/StandVar_Prep/name
			, 'sum((/Allocatie/StartState_metBAGnieuwbouw/'+CaseClassifications/StandVar_Prep/path+'), SourceData/RegioIndelingen/NVM/Per_CompactedAdminDomain)'
			, SourceData/RegioIndelingen/NVM
			, CaseClassifications/StandVar_Prep
			, CaseClassifications/StandVar_Prep/unit_name
		);
	
	container Gebruiksoppervlak
	{
		unit<UInt32> pand := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_StartYear)+'/pand';
		
		container per_Gebruiksdoel_perAdminDomain :=
			for_each_nedv(
				vbo_gebruiksdoel_plus/name
				, 'sum(pand/Oppervlaktes/VBOopp_perGebruiksdoel/'+vbo_gebruiksdoel_plus/name+'[pand/ToedelingsMatrix/Pand_rel] * pand/ToedelingsMatrix/ToedelingPand, pand/ToedelingsMatrix/AdminDomain_rel)'
				, AdminDomain
				, meter2
			);
		
		container per_Jobs6_perAdminDomain := 
			for_each_nedv(
				Actor/Jobs6/name
				, replace(Actor/Jobs6/vbo_gebruiksdoel_plus_ref, '@', 'per_Gebruiksdoel_perAdminDomain/','#', '')
				, AdminDomain
				, meter2
			);
		
		container per_WP2_perAdminDomain := BaseData/StartState/StateBasisjaar/Src0/Wonen/WP2_opp; 
		container per_WP4_perAdminDomain := BaseData/StartState/StateBasisjaar/Src0/Wonen/WP4_opp; 
		
		container per_WP2xVSSH_perAdminDomain := 
			for_each_nedv(
				WP2xVSSH/name
				, 'recollect_by_cond(AdminDomain/IsCompactedDomain, BaseData/StartState/Verdeling_VSSH/Uitsmeren_WP2_'+WP2xVSSH/VSSH_name+'/'+WP2xVSSH/WP_name+'/VBOopp_Per_CompactedAdminDomain)'
				, AdminDomain
				, meter2
			);
	}
	
	#include<Dichtheid.dms>
	
	container Grondgebruik
	{
		attribute<Classifications/Grondgebruik/CBSKlasse/Aggr/Groen> Groen      (AdminDomain) := invert(/Classifications/Grondgebruik/CBSKlasse/Aggr/Groen/org_rel)[/BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_CBS/Per_Aggr];
		attribute<Classifications/Actor/Sector>                      Sector_rel (AllocDomain) := Classifications/Grondgebruik/CBSKlasse/Sector_rel[BaseData/StartState/Grondgebruik/Per_AllocDomain/Basisjaar];
		
		attribute<Classifications/Actor/LU_ModelType>                gg_basis   (AdminDomain) := BaseData/StartState/Grondgebruik/Per_AdminDomain/gg_basis;
		
		container Verandering
		{
			unit<UInt32> Regio := SourceData/RegioIndelingen/Provincie;
			
			attribute<bool> Verstedelijking (AdminDomain) := const(false, AdminDomain)
			{
				attribute<ha> Per_Regio (Regio) := sum(float32(.) * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain);
			}
			
			attribute</SourceData/Grondgebruik/ABCD/ABCD_subK> VerstedelijkingInABCD (AdminDomain) := const(null_b, AdminDomain);
			
			attribute<bool> VerstedelijkingOpVruchtbareLandbouwgronden (AdminDomain) := VerstedelijkingInABCD == /SourceData/Grondgebruik/ABCD/ABCD_subK/V/A
			{
				attribute<ha> Per_Regio (Regio) := sum(float32(.) * AdminDomain/NrHaPerCell, Regio/Per_AdminDomain);
			}
		}
	}
	
	container Landgebruikskaart := Templates/Landgebruikskaart/Make_Landgebruikskaart_T('Basisjaar', Stand);
	
	// N.B. Dit is de potentiële schade bij een diepte vanaf kleine kans (= 1/1000 jaar of hoger). Zie: https://www.klimaateffectatlas.nl/nl/overstromingsdiepte
	container Overstromingsschade := Templates/SSM2017_Overstromingsschades_T('Basisjaar'), Descr = "SSM Model van Deltares nagebouwd in RuimteScanner; Slager, K. (2016). Technische documentatie SSM2015: functionele en technische ontwerpkeuzen. Deltares rapportage 1230095-004-HYE-0009 dd. november 2016 ";
	
	// container SOMERS_CO2_laagveen := Templates/SOMERS_CO2_T(Zichtjaar), Descr = "Het registratiesysteem SOMERS (Subsurface Organic Matter Emission RegistRation System) is ontwikkeld om de landelijke CO2-uitstoot reductie in het veenweidegebied jaarlijks bij te houden. Met SOMERS 2.0 zijn rekenregels opgesteld om de toekomstige uitstoot te bepalen onder gestandaardiseerde omstandigheden.", URL = "https://www.nobveenweiden.nl/bevindingen-rekenregels/";
	
	container BereikbaarheidGroen
	{
		parameter<Woning_ha> MaxWoningenInGroen                 := 5[Woning_ha];
		parameter<ha>        MinimumAaneengeslotenOppervlakte   := 1[Ha];
		attribute<Woning>    Woningen0             (AdminDomain) := Stand/Aantal_Woningen_Totaal[Woning];
		attribute<Woning>    Woningen              (AdminDomain) := Woningen0;
		
		parameter<meter2>    MinimumOppervlakteInCelOmMeeTeDoen := 100[meter2];
		parameter<float32>   FractieGroenWerken                 := VariantParameters/FractiesGroenInWerken/DezeVariant/GroenTotaal;
		parameter<float32>   FractieGroenVerblijfsrecreatie     := VariantParameters/FractiesGroenInVerblijfsrecreatie/DezeVariant/GroenTotaal;
		
		unit<UInt32> Regio  := SourceData/RegioIndelingen/Provincie;
		unit<uint8>  GroenK := /Classifications/Grondgebruik/CBSKlasse/Aggr/Groen;
		
		unit<uint8> Indicator : NrOfRows = 3 
		, Descr = "Groenaanbod = Totale oppervlakte groen (m²) binnen de gegeven straal rond cellen waar woningen aanwezig zijn.
					Cumulatief_Groenaanbod_over_woningen = Totale oppervlakte groen binnen de gegeven straal maal het aantal woningen in die cel (m²). In dichtgebouwd gebied nieuw groen toevoegen, maak je veel mensen gelukkig mee. Echter, ze realiseren zich dan niet dat je het moet delen met al die andere mensen (indicator 3).
					Drukte_gecorrigeerd_groenaanbod_per_woning = Som van drukte-gecorrigeerde groenbijdragen binnen de gegeven straal, waarbij per groencel het aanwezige groen wordt gewogen naar het aantal omliggende woningen (m² per woning)."
		{
			attribute<string> name: 
			[
				 'Groenaanbod'                                  // Totale oppervlakte groen (m²) binnen de gegeven straal rond cellen waar woningen aanwezig zijn.
				,'Cumulatief_Groenaanbod_over_woningen'         // Totale oppervlakte groen binnen de gegeven straal maal het aantal woningen in die cel (m²). In dichtgebouwd gebied nieuw groen toevoegen, maak je veel mensen gelukkig mee. Echter, ze realiseren zich dan niet dat je het moet delen met al die andere mensen (indicator 3).
				,'Drukte_gecorrigeerd_groenaanbod_per_woning'   // Som van drukte-gecorrigeerde groenbijdragen binnen de gegeven straal, waarbij per groencel het aanwezige groen wordt gewogen naar het aantal omliggende woningen (m² per woning).
			];
			
			attribute<string> IndicatorExpr : 
			[
				 'Som_GroenInOmgeving_metWoning'                          
				,'Som_GroenInOmgeving_metWoning * WoningenInThisStRegio_ge1 / 1[woning]' 
				,'Som_AanwezigGroenPerWoning' 
			];
			
			attribute<string> ValuesUnit : 
			[
				 'meter2' 
				,'meter2' 
				,'m2_Woning' 
			];
		}
		
		container InDezeStudie := obv_BBG;
		
		container obv_BBG := Templates/BereikbaarheidGroenBBG_T(..);
		container obv_BGT := Templates/BereikbaarheidGroenBGT_T(..);
	}
	
	container Verharding
	{
		attribute<meter2>   VerhardOpp_CopernicusBGTcombi   (AdminDomain) := SourceData/Grondgebruik/BGT/IsDefinedAndNotNoData/VerhardOpp_AdminDomain;
		attribute<meter2>   VerhardOpp_Copernicus           (AdminDomain) := SourceData/Grondgebruik/Imperviousness_Map/InProcent[Ratio] * AdminDomain/NrMeter2PerCell;
		attribute<meter2>   Diff                            (AdminDomain) := VerhardOpp_Copernicus - VerhardOpp_CopernicusBGTcombi;
		attribute<float32>  Result                          (AdminDomain) := VerhardOpp_CopernicusBGTcombi / AdminDomain/NrMeter2PerCell;
	}
	
	container Waterberging
	{
		parameter<string> Grid_ref := '500m'; // 100m, 500m
		parameter<string> Kans_ref := '100'; // 100, 1000
		
		unit<IPoint> Grid  := = '/Geography/rdc_'+Grid_ref;
		
		attribute<Meter>  WaterdiepteBijHevigeNeerslag (AdminDomain) := = 'SourceData/Water/WaterdiepteBijHevigeNeerslag_T'+Kans_ref;
		attribute<bool> BestaandBebouwdGebied          (AdminDomain) := /BaseData/Omgeving/BBGSamengesteld; // @Jip, waarom nergens BBG woongebied? 
		attribute<Meter3> Vraag                        (AdminDomain) := WaterdiepteBijHevigeNeerslag * AdminDomain/NrMeter2PerCell * float32(BestaandBebouwdGebied);
		attribute<Meter3> Restvraag                    (AdminDomain) := Vraag; // We gaan ervan uit dat evt bestaande capaciteit al 'verbruikt' is. Er is dus geen (extra) 'aanbod'
		
		container PerGrid
		{
			attribute<Meter3> Vraag             (Grid) := ='sum(../Vraag, AdminDomain/rdc_'+Grid_ref+'_rel)';
			attribute<Meter3> RestVraag         (Grid) := ='sum(../RestVraag, AdminDomain/rdc_'+Grid_ref+'_rel)';
			attribute<Meter3> RestvraagPositief (Grid) := max_elem(Restvraag, 0[meter3]);
		}
		
		container PerRegio
		{
			unit<UInt32> Regio := SourceData/RegioIndelingen/Provincie
			{
				attribute<.> Per_Grid (Grid) := poly2grid(geometry, Grid);
			}
			
			attribute<Meter3> Vraag             (Regio) := sum(PerGrid/Vraag, Regio/Per_Grid);
			attribute<Meter3> RestVraag         (Regio) := sum(PerGrid/RestVraag, Regio/Per_Grid);
			attribute<Meter3> RestvraagPositief (Regio) := sum(PerGrid/RestvraagPositief, Regio/Per_Grid);
		}
	}
}