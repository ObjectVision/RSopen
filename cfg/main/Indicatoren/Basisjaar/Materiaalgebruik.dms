container Materiaalgebruik : using = "Classifications/MateriaalStromen"
{
	unit<uint32> pand_src     := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_Startyear)+'/Panden/src';
	unit<uint32> pand         := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_Startyear)+'/Panden/pand';
	unit<uint32> pand_met_vbo := ='SourceData/Vastgoed/BAG/PerJaar/Y'+string(ModelParameters/Model_Startyear)+'/Panden/pand/pand_met_vbo';
	
	unit<uint32> per_Pand := pand_met_vbo
	{
		attribute<m2_pand>               vbo_opp       := pand_met_vbo/pand_vbo_opp;
		attribute<int16>                 bouwjaar      := pand_met_vbo/bouwjaar_trunc;
		attribute<vbo_gebruiksdoel_plus> dominant_doel := pand_met_vbo/Fractie_VBOOpp_per_vbo_functie/argmax;
		attribute<BouwjaarCohort>        BJCohort_rel  := classify(bouwjaar, BouwjaarCohort/ClassBreaks);
		attribute<BuildingTypes>         BT_rel        := WP4_rel == WP4/V/Vrijstaand
			? BuildingTypes/V/detached_house
			: WP4_rel == WP4/V/Twee_onder_1_kap
				? BuildingTypes/V/row_house
				: WP4_rel == WP4/V/Rijtjeswoning
					? BuildingTypes/V/row_house
					: WP4_rel == WP4/V/Appartement
						? BuildingTypes/V/apartment
						: dominant_doel == vbo_gebruiksdoel_plus/V/winkel
							? BuildingTypes/V/shop
							: dominant_doel == vbo_gebruiksdoel_plus/V/kantoor
								? BuildingTypes/V/office
								: dominant_doel == vbo_gebruiksdoel_plus/V/industrie
									? BuildingTypes/V/industry
									: dominant_doel == vbo_gebruiksdoel_plus/V/bijeenkomst 
										|| dominant_doel == vbo_gebruiksdoel_plus/V/cel 
										|| dominant_doel == vbo_gebruiksdoel_plus/V/gezondheidszorg 
										|| dominant_doel == vbo_gebruiksdoel_plus/V/logies 
										|| dominant_doel == vbo_gebruiksdoel_plus/V/onderwijs 
										|| dominant_doel == vbo_gebruiksdoel_plus/V/sport
											? BuildingTypes/V/services
											: (0b/0b)[BuildingTypes];
		
		attribute<BT_BC> BT_BC_rel := rlookup(BuildingTypes/name[BT_rel] + '_' + BouwjaarCohort/name[BJCohort_rel], BT_BC/name);
		attribute<string> BT_BC_name := BT_BC/name[BT_BC_rel];
		
		container per_Materiaal :=
			for_each_nedv(
				Materialen/name
				, replace(AsList('(BT_BC_name == '+quote(BT_BC/name)+' ? SourceData/Vastgoed/MateriaalStromen/MaterialIntensity_Historical/Per_BT_BC/'+BT_BC/name+'[Materialen/V/@M@[Materialen]] * vbo_opp : 0f)', ' + ')
					,'@M@', Materialen/name)
				, per_Pand
				, kg_pand
			);
	}
	
	container per_Materiaal_perAdminDomain :=
		for_each_nedv(
			Materialen/name
			, 'sum('
				'recollect_by_cond('
					'pand_src/pand_selection_condition, per_pand/per_Materiaal/'+Materialen/name+'[invert(per_Pand/org_rel)], 0[kg_pand]'
				')[pand_src/ToedelingsMatrix/pand_rel]'
			 '* pand_src/ToedelingsMatrix/ToedelingPand, pand_src/ToedelingsMatrix/AdminDomain_rel'
			 ') * 1[PandUnit]'
			, AdminDomain
			, kg
		);
	
	unit<uint32> Stock_perProv :=
		for_each_nedv(
			Materialen/name
			, 'sum(per_Materiaal_perAdminDomain/'+Materialen/name+', geography/RegioIndelingen/Provincie/Per_AdminDomain)'
			, geography/RegioIndelingen/Provincie
			, kg
		), StorageName = "='%LocalDataProjDir%/Export/Materialen/Stock_perProv_Basisjaar.csv'", StorageType = "gdalwrite.vect";
}