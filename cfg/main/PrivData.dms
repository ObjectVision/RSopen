container PrivData: using = "Classifications;units;Geography;Classifications/Vastgoed;Classifications/actor"
{
	unit<uint32> Grondposities
	:	StorageName     = "%PrivDataDir%/Grondposities/K_Perceel_31dec2016_Nederland_DIS_nov_2017_correctie_vastgoed.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	SyncMode        = "Attr"
	,	Source          = "Bron2 - 16 november 2018, Y:\Project\M500399_Vinext\Data\Data_Bronnen\K_Perceel_31dec2016_KadGEM_ALLES_gedraaid_GEBIEDEN_plat.gdb K_Perceel_31dec2016_Nederland_DIS_nov_2017_correctie_vastgoed"
	{
		attribute<rdc_meter>  Geometry (poly);
		attribute<string>     Categorie2;
		attribute<string>     Categorie0 := replace(Categorie2, ' ', '_', '(', '_', ')', '_');
		attribute<CategorieK> Categorie  := rjoin(Categorie0, CategorieK/Label, id(CategorieK))[CategorieK];
		
		attribute<bool> CommercieleBouwgrond := CategorieK/CommercieleBouwgrond[Categorie];
		attribute<bool> VanWoningcooperatie  := CategorieK/VanWoningcooperatie[Categorie];
		attribute<bool> VanGemeente          := CategorieK/VanGemeente[Categorie];
		attribute<bool> VanWaterschap        := CategorieK/VanWaterschap[Categorie];
		attribute<bool> VanProvincie         := CategorieK/VanProvincie[Categorie];
		attribute<bool> VanRijk              := CategorieK/VanRijk[Categorie];
		
		attribute<bool> VanGemeenteOfCommercieel := CommercieleBouwgrond || VanGemeente;
		
		attribute<EigendomK> EigendomK_rel := CategorieK/EigendomK_rel[Categorie];
		
		attribute<EigendomK> Write_Per_AllocDomain_EigendomK_rel (AllocDomain) := EigendomK_rel[poly2grid(Geometry, AllocDomain)], StorageName = "%RSo_DataDir%/Actoren/Eigendom_per_AllocDomain.tif", StorageType = "gdalwrite.grid", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))";
		attribute<EigendomK> Write_Per_AdminDomain_EigendomK_rel (AdminDomain) := EigendomK_rel[poly2grid(Geometry, AdminDomain)], StorageName = "%RSo_DataDir%/Actoren/Eigendom_per_AdminDomain.tif", StorageType = "gdalwrite.grid", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))";
		
		unit<uint32> CategorieK := unique(Categorie0)
		{
			attribute<string> Label := values, DialogType = "LabelText";
			attribute<bool> CommercieleBouwgrond := // Obv input Maarten v schie (PBL), 181127
				Label == 'Bedrijf__Bouw_' || Label == 'Bedrijf__NEPROM_' || Label == 'Bedrijf__NVB_Bouw_' || Label == 'Bedrijf__Projectontwikkelaar_';
			
			attribute<bool> VanWoningcooperatie := Label == 'Woningbouw';// Obv input Maarten v schie (PBL), 181127,
			attribute<bool> VanGemeente := Label == 'Publiek__Gemeente_';// Obv input Maarten v schie (PBL), 181127,
			attribute<bool> VanWaterschap := Label == 'Publiek__Waterschap_';// Obv input Maarten v schie (PBL), 181127,
			attribute<bool> VanProvincie := Label == 'Publiek__Provincie_';// Obv input Maarten v schie (PBL), 181127,
			attribute<bool> VanRijk := Label == 'Publiek__Rijk_';// Obv input Maarten v schie (PBL), 181127,
			attribute<bool> VanNatuurOrganisatie := Label == 'Natuurorganisatie__niet-publiek_' || Label == 'Natuurorganisatie';
			
			attribute<EigendomK> EigendomK_rel :=
				CommercieleBouwgrond ? EigendomK/V/Commercieel :
				VanWoningcooperatie  ? EigendomK/V/Corporatie :
				VanGemeente ? EigendomK/V/Gemeente :
				VanWaterschap ? EigendomK/V/Waterschap :
				VanProvincie ? EigendomK/V/Provincie :
				VanRijk ? EigendomK/V/Rijk :
				VanNatuurOrganisatie ? EigendomK/V/NatuurOrganisatie :
				EigendomK/V/Overigen;
		}
	}
	
	#include<Claims.dms>
}