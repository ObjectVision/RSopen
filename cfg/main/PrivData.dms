container PrivData: using = "Classifications;Units;Geography;Classifications/Vastgoed;Classifications/Actor"
, Descr = "Bevat alle privé data die niet publiekelijk beschikbaar is, maar wel gebruikt wordt in het model. Deze data is niet opgenomen in de repository vanwege licentie restricties."
{
	unit<UInt32> Grondposities
	:	StorageName     = "%PrivDataDir%/Grondposities/K_Perceel_31dec2016_Nederland_DIS_nov_2017_correctie_Vastgoed.shp"
	,	StorageType     = "gdal.vect"
	,	StorageReadOnly = "True"
	,	SyncMode        = "Attr"
	,	Source          = "Bron2 - 16 november 2018, Y:\Project\M500399_Vinext\Data\Data_Bronnen\K_Perceel_31dec2016_KadGEM_ALLES_gedraaid_GEBIEDEN_plat.gdb K_Perceel_31dec2016_Nederland_DIS_nov_2017_correctie_Vastgoed"
	,	Descr 	        = "Grondposities van percelen in Nederland, met eigendomscategorieën."
	{
		attribute<rdc_meter>  geometry (poly);
		attribute<String>     Categorie2;
		attribute<String>     Categorie0 := replace(Categorie2, ' ', '_', '(', '_', ')', '_');
		attribute<CategorieK> Categorie  := rjoin(lowercase(Categorie0), lowercase(CategorieK/Label), id(CategorieK))[CategorieK];
		
		attribute<Bool> CommercieleBouwgrond := CategorieK/CommercieleBouwgrond[Categorie], Descr = "Commerciele bouwgrond betreft grond in eigendom van commerciële partijen zoals projectontwikkelaars, bouwbedrijven en beleggers.";
		attribute<Bool> VanWoningcooperatie  := CategorieK/VanWoningcooperatie[Categorie], Descr = "Grond in eigendom van woningcorporaties.";
		attribute<Bool> VanGemeente          := CategorieK/VanGemeente[Categorie], Descr = "Grond in eigendom van gemeenten.";
		attribute<Bool> VanWaterschap        := CategorieK/VanWaterschap[Categorie], Descr = "Grond in eigendom van waterschappen.";
		attribute<Bool> VanProvincie         := CategorieK/VanProvincie[Categorie], Descr = "Grond in eigendom van provincies.";
		attribute<Bool> VanRijk              := CategorieK/VanRijk[Categorie], Descr = "Grond in eigendom van het Rijk.";
		
		attribute<Bool> VanGemeenteOfCommercieel := CommercieleBouwgrond || VanGemeente, Descr = "Grond in eigendom van gemeenten of commerciële partijen.";
		
		attribute<EigendomK> EigendomK_rel := CategorieK/EigendomK_rel[Categorie], Descr = "Hergebruik van de eigendom categorieën, maar nu gegroepeerd naar relevante groepen voor de woningbouw vraag en aanbod analyse.";
		
		attribute<EigendomK> Write_Per_AllocDomain_EigendomK_rel (AllocDomain) := EigendomK_rel[poly2grid(geometry, AllocDomain)], StorageName = "%RSo_DataDir%/Actoren/Eigendom_Per_AllocDomain.tif", StorageType = "gdalwrite.grid", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))", Descr = "Check om ervoor te zorgen dat niet de hele kaart undefined of zero is. Is dit wel zo, ga dan terug tracen naar waar het probleem zit.";
		attribute<EigendomK> Write_Per_AdminDomain_EigendomK_rel (AdminDomain) := EigendomK_rel[poly2grid(geometry, AdminDomain)], StorageName = "%RSo_DataDir%/Actoren/Eigendom_Per_AdminDomain.tif", StorageType = "gdalwrite.grid", IntegrityCheck = "not(all(IsNull(this))) && not(all(IsZero(this)))", Descr = "Check om ervoor te zorgen dat niet de hele kaart undefined of zero is. Is dit wel zo, ga dan terug tracen naar waar het probleem zit.";
		
		unit<UInt32> CategorieK := unique(Categorie0)
		, Descr = "Unieke lijst van eigendomscategorieën, met daaraan gekoppeld de relevante eigenschappen."
		{
			attribute<String> Label := lowercase(values), DialogType = "LabelText";
			attribute<Bool> CommercieleBouwgrond := Label == lowercase('Bedrijf__Bouw_') || Label == lowercase('Bedrijf__NEPROM_') || Label == lowercase('Bedrijf__NVB_Bouw_') || Label == lowercase('Bedrijf__Projectontwikkelaar_');// Obv input Maarten v schie (PBL), 181127
			attribute<Bool> VanWoningcooperatie  := Label == lowercase('Woningbouw');// Obv input Maarten v schie (PBL), 181127,
			attribute<Bool> VanGemeente          := Label == lowercase('Publiek__Gemeente_');// Obv input Maarten v schie (PBL), 181127,
			attribute<Bool> VanWaterschap        := Label == lowercase('Publiek__Waterschap_');// Obv input Maarten v schie (PBL), 181127,
			attribute<Bool> VanProvincie         := Label == lowercase('Publiek__Provincie_');// Obv input Maarten v schie (PBL), 181127,
			attribute<Bool> VanRijk              := Label == lowercase('Publiek__Rijk_');// Obv input Maarten v schie (PBL), 181127,
			attribute<Bool> VanNatuurOrganisatie := Label == lowercase('Natuurorganisatie__niet-publiek_') || lowercase(Label) == lowercase('Natuurorganisatie');
			
			attribute<EigendomK> EigendomK_rel :=
				CommercieleBouwgrond ? EigendomK/V/Commercieel :
				VanWoningcooperatie  ? EigendomK/V/Corporatie :
				VanGemeente ? EigendomK/V/Gemeente :
				VanWaterschap ? EigendomK/V/Waterschap :
				VanProvincie ? EigendomK/V/Provincie :
				VanRijk ? EigendomK/V/Rijk :
				VanNatuurOrganisatie ? EigendomK/V/NatuurOrganisatie :
				EigendomK/V/Overigen;
		}
	}
	
	#include<Claims.dms>
}