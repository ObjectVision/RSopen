container PostAllocAnalyses : using = "Geography;Classifications/Actor;VariantData;CaseClassifications/Vastgoed;Classifications/modellering;units"
{
	container PerZichtjaar := 
		for_each_ne(
			Time/Zichtjaar/name
			,'PerZichtjaar_T('+quote(Time/Zichtjaar/name)+')'
		);
		
	#include <Vergelijk.dms>
	#include <RuimtelijkeVerkenning2023.dms>
	#include <PeilOptimalisatie.dms>
	#include <Output.dms>
	
	container AdHoc // Runnen vanuit WBS variant
	{
		unit<uint8> RestrictieK: NrOfRows = 4
		{
			attribute<string> name: ['Restrictief_hard','WBS', 'Restrictief_zacht', 'Geen'];
			attribute<String> Label := name, DialogType = "LabelText";
			attribute<UInt32> BrushColor : DialogType = "BrushColor",
			[
				rgb(110,178,134), rgb(149,201,226), rgb(205,238,210), rgb(255,255,255)
			];
			
			container V := for_each_nedv(name, string(id(.))+'[..]', void, .);
		}
		
		attribute<Bool> Restrictief_hard  (AdminDomain) := OR(/SourceData/Restricties/Wonen/ZeerHard, /SourceData/Restricties/Wonen/Hard, /SourceData/Restricties/Wonen/Middel);
		attribute<Bool> Restrictief_zacht (AdminDomain) := /SourceData/Restricties/Wonen/Zacht;
		attribute<Bool> WBS               (AdminDomain) := OR(
			/BaseData/Omgeving/Overstromingsgevaarzonering == /Classifications/Overstromingsgevaarzonering/zoneringK/V/Zeer_gevaarlijk || /BaseData/Omgeving/Overstromingsgevaarzonering == /Classifications/Overstromingsgevaarzonering/zoneringK/V/Gevaarlijk,
			/VariantData/Zeef/Basisjaar/Wonen/eengezins_VrijeSector/Src/TeZettingsgevoelig[/Geography/CompactedAdminDomain/Per_AdminDomain],
			/VariantData/Zeef/Basisjaar/Wonen/eengezins_VrijeSector/Src/TeSlap[/Geography/CompactedAdminDomain/Per_AdminDomain],
			/VariantData/Zeef/Basisjaar/Wonen/eengezins_VrijeSector/Src/TeNat[/Geography/CompactedAdminDomain/Per_AdminDomain],
			/VariantData/Zeef/Basisjaar/Wonen/eengezins_VrijeSector/Src/ReserverenVoorRivierAfvoer_Krap[/Geography/CompactedAdminDomain/Per_AdminDomain]
		);
		
		attribute<RestrictieK> Resultaat (AdminDomain) := switch(
			case(Restrictief_hard, RestrictieK/V/Restrictief_hard),
			case(WBS, RestrictieK/V/WBS),
			case(Restrictief_zacht, RestrictieK/V/Restrictief_zacht),
			RestrictieK/V/Geen
		);
	}
	
	Template PerZichtjaar_T
	{
		parameter<string> ZichtjaarName;
		///
		container Stand           := ='Allocatie/Zichtjaren/'+ZichtjaarName+'/impl/ReadStand';
		container Stand_Compacted := ='Allocatie/Zichtjaren/'+ZichtjaarName+'/impl/ReadStand_Compacted';
		
		container Oppervlakte_km2_PerSubsector_rel :=
			for_each_nedv(
				Sector/xSubsector/Label
				, 'sum_uint32(Stand/SubSector_rel == Sector/xSubsector/V/' + Sector/xSubsector/Label + ')[float32] * AdminDomain/NrHaPerCell[km2]'
				, void
				, km2
			);
			
		container Wind := = VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector  
							? 'Stand_Compacted/Wind'
							: 'PlaceHolder', Descr = "De windmolen data, indien aanwezig in de stand bestanden"
		{
			attribute<Bool>  IsWind    (AdminDomain) := = VariantParameters/SectorAllocRegio/Uq_Sectors/HasWindSector ? 'Stand/SubSector_rel == Sector/xSubsector/V/Wind_Totaal' : 'const(FALSE, AdminDomain)';
			attribute<UInt8> WindUint8 (AdminDomain) := IsWind ? 1b :  null_b;
			
			#include <WindPolygoon.dms>
			
			unit<uint64> Objects := select_with_attr_by_cond(., ./Totaal > 0f)
			{
				attribute<rdc_meter> geometry :=  collect_by_cond(., ../Totaal > 0f, CompactedAdminDomain/point)
				{
					parameter<meter> SymbolWorldSize   := 10[meter], DialogType = "SymbolWorldSize  ";
					parameter<Int16> SymbolIndex := 83s, DialogType = "SymbolIndex";
				}
			}
		}
	}
}